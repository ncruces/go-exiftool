use 5.008;

package fields;

use strict;
no strict 'refs';
unless ( eval { require warnings::register; warnings::register->import; 1 } ) {
    *warnings::warnif = sub {
        require Carp;
        Carp::carp(@_);
    }
}
our %attr;

our $VERSION = '2.27';
$VERSION =~ tr/_//d;

sub PUBLIC ()    { 2**0 }
sub PRIVATE ()   { 2**1 }
sub INHERITED () { 2**2 }
sub PROTECTED () { 2**3 }

sub import {
    my $class = shift;
    return unless @_;
    my $package = caller(0);
    %{"$package\::FIELDS"} = () unless %{"$package\::FIELDS"};
    my $fields = \%{"$package\::FIELDS"};
    my $fattr  = ( $attr{$package} ||= [1] );
    my $next   = @$fattr;

    bless \%{"$package\::FIELDS"}, 'pseudohash';

    if ( $next > $fattr->[0]
        and ( $fields->{ $_[0] } || 0 ) >= $fattr->[0] )
    {
        $next = $fattr->[0];
    }
    foreach my $f (@_) {
        my $fno = $fields->{$f};

        if ( $fno and $fno != $next ) {
            require Carp;
            if ( $fno < $fattr->[0] ) {
                if ( $] < 5.006001 ) {
                    warn("Hides field '$f' in base class") if $^W;
                }
                else {
                    warnings::warnif("Hides field '$f' in base class");
                }
            }
            else {
                Carp::croak("Field name '$f' already in use");
            }
        }
        $fields->{$f} = $next;
        $fattr->[$next] = ( $f =~ /^_/ ) ? PRIVATE : PUBLIC;
        $next += 1;
    }
    if ( @$fattr > $next ) {
        require Carp;
        Carp::croak("Reloaded module must declare all fields at once");
    }
}

sub inherit {
    require base;
    goto &base::inherit_fields;
}

sub _dump {
    for my $pkg ( sort keys %attr ) {
        print "\n$pkg";
        if ( @{"$pkg\::ISA"} ) {
            print " (", join( ", ", @{"$pkg\::ISA"} ), ")";
        }
        print "\n";
        my $fields = \%{"$pkg\::FIELDS"};
        for my $f ( sort { $fields->{$a} <=> $fields->{$b} } keys %$fields ) {
            my $no = $fields->{$f};
            print "   $no: $f";
            my $fattr = $attr{$pkg}[$no];
            if ( defined $fattr ) {
                my @a;
                push( @a, "public" )    if $fattr & PUBLIC;
                push( @a, "private" )   if $fattr & PRIVATE;
                push( @a, "inherited" ) if $fattr & INHERITED;
                print "\t(", join( ", ", @a ), ")";
            }
            print "\n";
        }
    }
}

if ( $] < 5.009 ) {
    *new = sub {
        my $class = shift;
        $class = ref $class if ref $class;
        return bless [ \%{ $class . "::FIELDS" } ], $class;
    }
}
else {
    *new = sub {
        my $class = shift;
        $class = ref $class if ref $class;
        require Hash::Util;
        my $self = bless {}, $class;

        &Hash::Util::lock_keys( \%$self, _accessible_keys($class) );
        return $self;
    }
}

sub _accessible_keys {
    my ($class) = @_;
    return (
        keys %{ $class . '::FIELDS' },
        map( _accessible_keys($_), @{ $class . '::ISA' } ),
    );
}

sub phash {
    die "Pseudo-hashes have been removed from Perl" if $] >= 5.009;
    my $h;
    my $v;
    if (@_) {
        if ( ref $_[0] eq 'ARRAY' ) {
            my $a = shift;
            @$h{@$a} = 1 .. @$a;
            if (@_) {
                $v = shift;
                unless ( !@_ and ref $v eq 'ARRAY' ) {
                    require Carp;
                    Carp::croak("Expected at most two array refs\n");
                }
            }
        }
        else {
            if ( @_ % 2 ) {
                require Carp;
                Carp::croak(
                    "Odd number of elements initializing pseudo-hash\n");
            }
            my $i = 0;
            @$h{ grep ++$i % 2, @_ } = 1 .. @_ / 2;
            $i = 0;
            $v = [ grep $i++ % 2, @_ ];
        }
    }
    else {
        $h = {};
        $v = [];
    }
    [ $h, @$v ];

}

1;

__END__

