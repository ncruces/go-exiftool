package File::Temp;

our $VERSION = '0.2311';

use 5.006;
use strict;
use Carp;
use File::Spec 0.8;
use Cwd ();
use File::Path 2.06 qw/ rmtree /;
use Fcntl 1.03;
use IO::Seekable;
use Errno;
use Scalar::Util 'refaddr';
require VMS::Stdio if $^O eq 'VMS';

eval { require Carp::Heavy; };

require Symbol if $] < 5.006;

use parent 0.221 qw/ IO::Handle IO::Seekable /;
use overload
  '""'     => "STRINGIFY",
  '0+'     => "NUMIFY",
  fallback => 1;

our $DEBUG    = 0;
our $KEEP_ALL = 0;

use Exporter 5.57 'import';

our @EXPORT_OK = qw{
  tempfile
  tempdir
  tmpnam
  tmpfile
  mktemp
  mkstemp
  mkstemps
  mkdtemp
  unlink0
  cleanup
  SEEK_SET
  SEEK_CUR
  SEEK_END
};

our %EXPORT_TAGS = (
    'POSIX'    => [qw/ tmpnam tmpfile /],
    'mktemp'   => [qw/ mktemp mkstemp mkstemps mkdtemp/],
    'seekable' => [qw/ SEEK_SET SEEK_CUR SEEK_END /],
);

Exporter::export_tags( 'POSIX', 'mktemp', 'seekable' );

my @CHARS = (
    qw/ A B C D E F G H I J K L M N O P Q R S T U V W X Y Z
      a b c d e f g h i j k l m n o p q r s t u v w x y z
      0 1 2 3 4 5 6 7 8 9 _
      /
);

use constant MAX_TRIES => 1000;

use constant MINX => 4;

use constant TEMPXXX => 'X' x 10;

use constant STANDARD => 0;
use constant MEDIUM   => 1;
use constant HIGH     => 2;

my $OPENFLAGS = O_CREAT | O_EXCL | O_RDWR;
my $LOCKFLAG;

unless ( $^O eq 'MacOS' ) {
    for my $oflag (qw/ NOFOLLOW BINARY LARGEFILE NOINHERIT /) {
        my ( $bit, $func ) = ( 0, "Fcntl::O_" . $oflag );
        no strict 'refs';
        $OPENFLAGS |= $bit if eval {
            local $SIG{__DIE__}  = sub { };
            local $SIG{__WARN__} = sub { };
            $bit = &$func();
            1;
        };
    }
    $LOCKFLAG = eval {
        local $SIG{__DIE__}  = sub { };
        local $SIG{__WARN__} = sub { };
        &Fcntl::O_EXLOCK();
    };
}

my $OPENTEMPFLAGS = $OPENFLAGS;
unless ( $^O eq 'MacOS' ) {
    for my $oflag (qw/ TEMPORARY /) {
        my ( $bit, $func ) = ( 0, "Fcntl::O_" . $oflag );
        local ($@);
        no strict 'refs';
        $OPENTEMPFLAGS |= $bit if eval {
            local $SIG{__DIE__}  = sub { };
            local $SIG{__WARN__} = sub { };
            $bit = &$func();
            1;
        };
    }
}

my %FILES_CREATED_BY_OBJECT;

sub _gettemp {

    croak 'Usage: ($fh, $name) = _gettemp($template, OPTIONS);'
      unless scalar(@_) >= 1;

    my $tempErrStr;

    my %options = (
        "open"             => 0,
        "mkdir"            => 0,
        "suffixlen"        => 0,
        "unlink_on_close"  => 0,
        "use_exlock"       => 0,
        "ErrStr"           => \$tempErrStr,
        "file_permissions" => undef,
    );

    my $template = shift;
    if ( ref($template) ) {
        carp "File::Temp::_gettemp: template must not be a reference";
        return ();
    }

    if ( scalar(@_) % 2 != 0 ) {
        carp "File::Temp::_gettemp: Must have even number of options";
        return ();
    }

    %options = ( %options, @_ ) if @_;

    ${ $options{ErrStr} } = undef;

    if ( $options{"open"} && $options{"mkdir"} ) {
        ${ $options{ErrStr} } = "doopen and domkdir can not both be true\n";
        return ();
    }

    my $start = length($template) - 1 - $options{"suffixlen"};

    if ( substr( $template, $start - MINX + 1, MINX ) ne 'X' x MINX ) {
        ${ $options{ErrStr} } =
          "The template must end with at least " . MINX . " 'X' characters\n";
        return ();
    }

    my $path = _replace_XX( $template, $options{"suffixlen"} );

    my ( $volume, $directories, $file );
    my $parent;
    if ( $options{"mkdir"} ) {
        ( $volume, $directories, $file ) = File::Spec->splitpath( $path, 1 );

        my @dirs = File::Spec->splitdir($directories);

        if ( $#dirs == 0 ) {
            $parent = File::Spec->curdir;
        }
        else {

            if ( $^O eq 'VMS' ) {
                $parent =
                  File::Spec->catdir( $volume, @dirs[ 0 .. $#dirs - 1 ] );
                $parent = 'sys$disk:[]' if $parent eq '';
            }
            else {

                $parent = File::Spec->catdir( @dirs[ 0 .. $#dirs - 1 ] );

                $parent = File::Spec->catpath( $volume, $parent, '' );
            }

        }

    }
    else {

        ( $volume, $directories, $file ) = File::Spec->splitpath($path);

        $parent = File::Spec->catpath( $volume, $directories, '' );

        $parent = File::Spec->curdir
          unless $directories ne '';

    }

    unless ( -e $parent ) {
        ${ $options{ErrStr} } = "Parent directory ($parent) does not exist";
        return ();
    }
    unless ( -d $parent ) {
        ${ $options{ErrStr} } = "Parent directory ($parent) is not a directory";
        return ();
    }

    if ( File::Temp->safe_level == MEDIUM ) {
        my $safeerr;
        unless ( _is_safe( $parent, \$safeerr ) ) {
            ${ $options{ErrStr} } =
              "Parent directory ($parent) is not safe ($safeerr)";
            return ();
        }
    }
    elsif ( File::Temp->safe_level == HIGH ) {
        my $safeerr;
        unless ( _is_verysafe( $parent, \$safeerr ) ) {
            ${ $options{ErrStr} } =
              "Parent directory ($parent) is not safe ($safeerr)";
            return ();
        }
    }

    my $perms     = $options{file_permissions};
    my $has_perms = defined $perms;
    $perms = 0600 unless $has_perms;

    for ( my $i = 0 ; $i < MAX_TRIES ; $i++ ) {

        if ( $options{"open"} ) {
            my $fh;

            if ( $] < 5.006 ) {
                $fh = &Symbol::gensym;
            }

            local $^F = 2;

            my $open_success = undef;
            if ( $^O eq 'VMS' and $options{"unlink_on_close"} && !$KEEP_ALL ) {
                $fh = VMS::Stdio::vmssysopen( $path, $OPENFLAGS, $perms,
                    'fop=dlt' );
                $open_success = $fh;
            }
            else {
                my $flags = (
                    ( $options{"unlink_on_close"} && !$KEEP_ALL )
                    ? $OPENTEMPFLAGS
                    : $OPENFLAGS
                );
                $flags |= $LOCKFLAG
                  if ( defined $LOCKFLAG && $options{use_exlock} );
                $open_success = sysopen( $fh, $path, $flags, $perms );
            }
            if ($open_success) {

                chmod( $perms, $path ) unless $has_perms;

                return ( $fh, $path );

            }
            else {

                unless ( $!{EEXIST} ) {
                    ${ $options{ErrStr} } =
                      "Could not create temp file $path: $!";
                    return ();
                }

            }
        }
        elsif ( $options{"mkdir"} ) {

            if ( mkdir( $path, 0700 ) ) {
                chmod( 0700, $path );

                return undef, $path;
            }
            else {

                unless ( $!{EEXIST} ) {
                    ${ $options{ErrStr} } =
                      "Could not create directory $path: $!";
                    return ();
                }

            }

        }
        else {

            return ( undef, $path ) unless -e $path;

        }

        my $original  = $path;
        my $counter   = 0;
        my $MAX_GUESS = 50;

        do {

            $path = _replace_XX( $template, $options{"suffixlen"} );

            $counter++;

        } until ( $path ne $original || $counter > $MAX_GUESS );

        if ( $counter > $MAX_GUESS ) {
            ${ $options{ErrStr} } =
"Tried to get a new temp name different to the previous value $MAX_GUESS times.\nSomething wrong with template?? ($template)";
            return ();
        }

    }

    ${ $options{ErrStr} } =
        "Have exceeded the maximum number of attempts ("
      . MAX_TRIES
      . ") to open temp file/dir";

    return ();

}

sub _replace_XX {

    croak 'Usage: _replace_XX($template, $ignore)'
      unless scalar(@_) == 2;

    my ( $path, $ignore ) = @_;

    my $end = ( $] >= 5.006 ? "\\z" : "\\Z" );

    if ($ignore) {
        substr( $path, 0, -$ignore ) =~
          s/X(?=X*$end)/$CHARS[ int( rand( @CHARS ) ) ]/ge;
    }
    else {
        $path =~ s/X(?=X*$end)/$CHARS[ int( rand( @CHARS ) ) ]/ge;
    }
    return $path;
}

sub _force_writable {
    my $file = shift;
    chmod 0600, $file;
}

sub _is_safe {

    my $path    = shift;
    my $err_ref = shift;

    my @info = stat($path);
    unless ( scalar(@info) ) {
        $$err_ref = "stat(path) returned no values";
        return 0;
    }

    return 1 if $^O eq 'VMS';

    if ( $info[4] > File::Temp->top_system_uid() && $info[4] != $> ) {

        Carp::cluck( sprintf "uid=$info[4] topuid=%s euid=$> path='$path'",
            File::Temp->top_system_uid() );

        $$err_ref = "Directory owned neither by root nor the current user"
          if ref($err_ref);
        return 0;
    }

    if (   ( $info[2] & &Fcntl::S_IWGRP )
        || ( $info[2] & &Fcntl::S_IWOTH ) )
    {

        unless ( -d $path ) {
            $$err_ref = "Path ($path) is not a directory"
              if ref($err_ref);
            return 0;
        }
        unless ( -k $path ) {
            $$err_ref =
              "Sticky bit not set on $path when dir is group|world writable"
              if ref($err_ref);
            return 0;
        }
    }

    return 1;
}

sub _is_verysafe {

    require POSIX;

    my $path = shift;
    print "_is_verysafe testing $path\n" if $DEBUG;
    return 1                             if $^O eq 'VMS';

    my $err_ref = shift;

    local ($@);
    my $chown_restricted;
    $chown_restricted = &POSIX::_PC_CHOWN_RESTRICTED()
      if eval { &POSIX::_PC_CHOWN_RESTRICTED(); 1 };

    if ( defined $chown_restricted ) {

        return _is_safe( $path, $err_ref ) if POSIX::sysconf($chown_restricted);

    }

    unless ( File::Spec->file_name_is_absolute($path) ) {
        $path = File::Spec->rel2abs($path);
    }

    my ( $volume, $directories, undef ) = File::Spec->splitpath( $path, 1 );

    my @dirs = File::Spec->splitdir($directories);

    foreach my $pos ( 0 .. $#dirs ) {
        my $dir = File::Spec->catpath( $volume,
            File::Spec->catdir( @dirs[ 0 .. $#dirs - $pos ] ), '' );

        print "TESTING DIR $dir\n" if $DEBUG;

        return 0 unless _is_safe( $dir, $err_ref );

    }

    return 1;
}

sub _can_unlink_opened_file {

    if ( grep $^O eq $_, qw/MSWin32 os2 VMS dos MacOS haiku/ ) {
        return 0;
    }
    else {
        return 1;
    }

}

sub _can_do_level {

    my $level = shift;

    return 1 if $level == STANDARD;

    if (   $^O eq 'MSWin32'
        || $^O eq 'os2'
        || $^O eq 'cygwin'
        || $^O eq 'dos'
        || $^O eq 'MacOS'
        || $^O eq 'mpeix' )
    {
        return 0;
    }
    else {
        return 1;
    }

}

{

    my ( %files_to_unlink, %dirs_to_unlink );

    END {
        local ( $., $@, $!, $^E, $? );
        cleanup( at_exit => 1 );
    }

    sub cleanup {
        my %h       = @_;
        my $at_exit = delete $h{at_exit};
        $at_exit = 0 if not defined $at_exit;
        { my @k = sort keys %h; die "unrecognized parameters: @k" if @k }

        if ( !$KEEP_ALL ) {
            my @files =
              ( exists $files_to_unlink{$$} ? @{ $files_to_unlink{$$} } : () );
            foreach my $file (@files) {
                close( $file->[0] );

                if ( -f $file->[1] ) {
                    _force_writable( $file->[1] );
                    unlink $file->[1] or warn "Error removing " . $file->[1];
                }
            }
            my @dirs =
              ( exists $dirs_to_unlink{$$} ? @{ $dirs_to_unlink{$$} } : () );
            my ( $cwd, $cwd_to_remove );
            foreach my $dir (@dirs) {
                if ( -d $dir ) {
                    if ($at_exit) {
                        $cwd = Cwd::abs_path( File::Spec->curdir )
                          if not defined $cwd;
                        my $abs = Cwd::abs_path($dir);
                        if ( $abs eq $cwd ) {
                            $cwd_to_remove = $dir;
                            next;
                        }
                    }
                    eval { rmtree( $dir, $DEBUG, 0 ); };
                    warn $@ if ( $@ && $^W );
                }
            }

            if ( defined $cwd_to_remove ) {
                chdir $cwd_to_remove
                  or die "cannot chdir to $cwd_to_remove: $!";
                my $updir = File::Spec->updir;
                chdir $updir or die "cannot chdir to $updir: $!";
                eval { rmtree( $cwd_to_remove, $DEBUG, 0 ); };
                warn $@ if ( $@ && $^W );
            }

            @{ $files_to_unlink{$$} } = ()
              if exists $files_to_unlink{$$};
            @{ $dirs_to_unlink{$$} } = ()
              if exists $dirs_to_unlink{$$};
        }
    }

    sub _deferred_unlink {

        croak 'Usage:  _deferred_unlink($fh, $fname, $isdir)'
          unless scalar(@_) == 3;

        my ( $fh, $fname, $isdir ) = @_;

        warn "Setting up deferred removal of $fname\n"
          if $DEBUG;

        $fname = Cwd::abs_path($fname);
        ($fname) = $fname =~ /^(.*)$/;

        if ($isdir) {

            if ( -d $fname ) {

                $fname = VMS::Filespec::vmspath($fname) if $^O eq 'VMS';
                $dirs_to_unlink{$$} = []
                  unless exists $dirs_to_unlink{$$};
                push( @{ $dirs_to_unlink{$$} }, $fname );

            }
            else {
                carp
"Request to remove directory $fname could not be completed since it does not exist!\n"
                  if $^W;
            }

        }
        else {

            if ( -f $fname ) {

                $files_to_unlink{$$} = []
                  unless exists $files_to_unlink{$$};
                push( @{ $files_to_unlink{$$} }, [ $fh, $fname ] );

            }
            else {
                carp
"Request to remove file $fname could not be completed since it is not there!\n"
                  if $^W;
            }

        }

    }

}

sub _parse_args {
    my $leading_template = ( scalar(@_) % 2 == 1 ? shift(@_) : '' );
    my %args             = @_;
    %args = map +( uc($_) => $args{$_} ), keys %args;

    my @template = (
          exists $args{TEMPLATE} ? $args{TEMPLATE}
        : $leading_template      ? $leading_template
        :                          ()
    );
    delete $args{TEMPLATE};

    return ( \@template, \%args );
}

sub new {
    my $proto = shift;
    my $class = ref($proto) || $proto;

    my ( $maybe_template, $args ) = _parse_args(@_);

    my $unlink = ( exists $args->{UNLINK} ? $args->{UNLINK} : 1 );
    delete $args->{UNLINK};

    delete $args->{OPEN};

    my ( $fh, $path ) = tempfile( @$maybe_template, %$args );

    print "Tmp: $fh - $path\n" if $DEBUG;

    ${*$fh} = $path;

    $FILES_CREATED_BY_OBJECT{$$}{$path} = 1;

    %{*$fh} = %$args;

    bless $fh, $class;

    $fh->unlink_on_destroy($unlink);

    return $fh;
}

sub newdir {
    my $self = shift;

    my ( $maybe_template, $args ) = _parse_args(@_);

    my $cleanup = ( exists $args->{CLEANUP} ? $args->{CLEANUP} : 1 );
    delete $args->{CLEANUP};

    my $tempdir = tempdir( @$maybe_template, %$args );

    my $real_dir = Cwd::abs_path($tempdir);
    ($real_dir) = $real_dir =~ /^(.*)$/;

    return bless {
        DIRNAME   => $tempdir,
        REALNAME  => $real_dir,
        CLEANUP   => $cleanup,
        LAUNCHPID => $$,
      },
      "File::Temp::Dir";
}

sub filename {
    my $self = shift;
    return ${*$self};
}

sub STRINGIFY {
    my $self = shift;
    return $self->filename;
}

sub NUMIFY {
    return refaddr( $_[0] );
}

sub unlink_on_destroy {
    my $self = shift;
    if (@_) {
        ${*$self}{UNLINK} = shift;
    }
    return ${*$self}{UNLINK};
}

sub DESTROY {
    local ( $., $@, $!, $^E, $? );
    my $self = shift;

    my $file = $self->filename;
    my $was_created_by_proc;
    if ( exists $FILES_CREATED_BY_OBJECT{$$}{$file} ) {
        $was_created_by_proc = 1;
        delete $FILES_CREATED_BY_OBJECT{$$}{$file};
    }

    if ( ${*$self}{UNLINK} && !$KEEP_ALL ) {
        print "# --------->   Unlinking $self\n" if $DEBUG;

        return unless $was_created_by_proc;

        _force_writable($file);
        unlink1( $self, $file )
          or unlink($file);
    }
}

sub tempfile {
    if ( @_ && $_[0] eq 'File::Temp' ) {
        croak "'tempfile' can't be called as a method";
    }

    my %options = (
        "DIR"    => undef,
        "SUFFIX" => '',
        "UNLINK" => 0,
        "OPEN"   => 1,
        "TMPDIR" => 0,
        "EXLOCK" => 0,
        "PERMS"  => undef,
    );

    my ( $maybe_template, $args ) = _parse_args(@_);
    my $template = @$maybe_template ? $maybe_template->[0] : undef;

    %options = ( %options, %$args );

    if ( !$options{"OPEN"} ) {

        warn
"tempfile(): temporary filename requested but not opened.\nPossibly unsafe, consider using tempfile() with OPEN set to true\n"
          if $^W;

    }

    if ( $options{"DIR"} and $^O eq 'VMS' ) {

        $options{"DIR"} = VMS::Filespec::vmspath( $options{"DIR"} );
    }

    if ( defined $template ) {
        if ( $options{"DIR"} ) {

            $template = File::Spec->catfile( $options{"DIR"}, $template );

        }
        elsif ( $options{TMPDIR} ) {

            $template =
              File::Spec->catfile( _wrap_file_spec_tmpdir(), $template );

        }

    }
    else {

        if ( $options{"DIR"} ) {

            $template = File::Spec->catfile( $options{"DIR"}, TEMPXXX );

        }
        else {

            $template =
              File::Spec->catfile( _wrap_file_spec_tmpdir(), TEMPXXX );

        }

    }

    $template .= $options{"SUFFIX"};

    my $unlink_on_close = ( wantarray ? 0 : 1 );

    my ( $fh, $path, $errstr );
    croak "Error in tempfile() using template $template: $errstr"
      unless (
        ( $fh, $path ) = _gettemp(
            $template,
            "open"             => $options{OPEN},
            "mkdir"            => 0,
            "unlink_on_close"  => $unlink_on_close,
            "suffixlen"        => length( $options{SUFFIX} ),
            "ErrStr"           => \$errstr,
            "use_exlock"       => $options{EXLOCK},
            "file_permissions" => $options{PERMS},
        )
      );

    _deferred_unlink( $fh, $path, 0 ) if $options{"UNLINK"};

    if ( wantarray() ) {

        if ( $options{'OPEN'} ) {
            return ( $fh, $path );
        }
        else {
            return ( undef, $path );
        }

    }
    else {

        unlink0( $fh, $path )
          or croak "Error unlinking file $path using unlink0";

        return $fh;
    }

}

{
    my ( $alt_tmpdir, $checked );

    sub _wrap_file_spec_tmpdir {
        return File::Spec->tmpdir unless $^O eq "MSWin32" && ${^TAINT};

        if ($checked) {
            return $alt_tmpdir ? $alt_tmpdir : File::Spec->tmpdir;
        }

        my $xxpath = _replace_XX( "X" x 10, 0 );

        my $tmpdir   = File::Spec->tmpdir;
        my $testpath = File::Spec->catdir( $tmpdir, $xxpath );
        if ( mkdir( $testpath, 0700 ) ) {
            $checked = 1;
            rmdir $testpath;
            return $tmpdir;
        }

        require Win32;
        my $local_app = File::Spec->catdir(
            Win32::GetFolderPath( Win32::CSIDL_LOCAL_APPDATA() ), 'Temp' );
        $testpath = File::Spec->catdir( $local_app, $xxpath );
        if ( -e $local_app or mkdir( $local_app, 0700 ) ) {
            if ( mkdir( $testpath, 0700 ) ) {
                $checked = 1;
                rmdir $testpath;
                return $alt_tmpdir = $local_app;
            }
        }

        croak <<"HERE";
Couldn't find a writable temp directory in taint mode. Tried:
  $tmpdir
  $local_app

Try setting and untainting the TMPDIR environment variable.
HERE

    }
}

sub tempdir {
    if ( @_ && $_[0] eq 'File::Temp' ) {
        croak "'tempdir' can't be called as a method";
    }

    my %options = (
        "CLEANUP" => 0,
        "DIR"     => '',
        "TMPDIR"  => 0,
    );

    my ( $maybe_template, $args ) = _parse_args(@_);
    my $template = @$maybe_template ? $maybe_template->[0] : undef;

    %options = ( %options, %$args );

    if ( defined $template ) {

        if ( $options{'TMPDIR'} || $options{'DIR'} ) {

            $template = VMS::Filespec::vmspath($template) if $^O eq 'VMS';
            my ( $volume, $directories, undef ) =
              File::Spec->splitpath( $template, 1 );

            $template = ( File::Spec->splitdir($directories) )[-1];

            if ( $options{"DIR"} ) {

                $template = File::Spec->catdir( $options{"DIR"}, $template );

            }
            elsif ( $options{TMPDIR} ) {

                $template =
                  File::Spec->catdir( _wrap_file_spec_tmpdir(), $template );

            }

        }

    }
    else {

        if ( $options{"DIR"} ) {

            $template = File::Spec->catdir( $options{"DIR"}, TEMPXXX );

        }
        else {

            $template = File::Spec->catdir( _wrap_file_spec_tmpdir(), TEMPXXX );

        }

    }

    my $tempdir;
    my $suffixlen = 0;
    if ( $^O eq 'VMS' ) {
        $template =~ m/([\.\]:>]+)$/;
        $suffixlen = length($1);
    }
    if ( ( $^O eq 'MacOS' ) && ( substr( $template, -1 ) eq ':' ) ) {
        ++$suffixlen;
    }

    my $errstr;
    croak "Error in tempdir() using $template: $errstr"
      unless (
        ( undef, $tempdir ) = _gettemp(
            $template,
            "open"      => 0,
            "mkdir"     => 1,
            "suffixlen" => $suffixlen,
            "ErrStr"    => \$errstr,
        )
      );

    if ( $options{'CLEANUP'} && -d $tempdir ) {
        _deferred_unlink( undef, $tempdir, 1 );
    }

    return $tempdir;

}

sub mkstemp {

    croak "Usage: mkstemp(template)"
      if scalar(@_) != 1;

    my $template = shift;

    my ( $fh, $path, $errstr );
    croak "Error in mkstemp using $template: $errstr"
      unless (
        ( $fh, $path ) = _gettemp(
            $template,
            "open"      => 1,
            "mkdir"     => 0,
            "suffixlen" => 0,
            "ErrStr"    => \$errstr,
        )
      );

    if ( wantarray() ) {
        return ( $fh, $path );
    }
    else {
        return $fh;
    }

}

sub mkstemps {

    croak "Usage: mkstemps(template, suffix)"
      if scalar(@_) != 2;

    my $template = shift;
    my $suffix   = shift;

    $template .= $suffix;

    my ( $fh, $path, $errstr );
    croak "Error in mkstemps using $template: $errstr"
      unless (
        ( $fh, $path ) = _gettemp(
            $template,
            "open"      => 1,
            "mkdir"     => 0,
            "suffixlen" => length($suffix),
            "ErrStr"    => \$errstr,
        )
      );

    if ( wantarray() ) {
        return ( $fh, $path );
    }
    else {
        return $fh;
    }

}

sub mkdtemp {

    croak "Usage: mkdtemp(template)"
      if scalar(@_) != 1;

    my $template  = shift;
    my $suffixlen = 0;
    if ( $^O eq 'VMS' ) {
        $template =~ m/([\.\]:>]+)$/;
        $suffixlen = length($1);
    }
    if ( ( $^O eq 'MacOS' ) && ( substr( $template, -1 ) eq ':' ) ) {
        ++$suffixlen;
    }
    my ( $junk, $tmpdir, $errstr );
    croak "Error creating temp directory from template $template\: $errstr"
      unless (
        ( $junk, $tmpdir ) = _gettemp(
            $template,
            "open"      => 0,
            "mkdir"     => 1,
            "suffixlen" => $suffixlen,
            "ErrStr"    => \$errstr,
        )
      );

    return $tmpdir;

}

sub mktemp {

    croak "Usage: mktemp(template)"
      if scalar(@_) != 1;

    my $template = shift;

    my ( $tmpname, $junk, $errstr );
    croak "Error getting name to temp file from template $template: $errstr"
      unless (
        ( $junk, $tmpname ) = _gettemp(
            $template,
            "open"      => 0,
            "mkdir"     => 0,
            "suffixlen" => 0,
            "ErrStr"    => \$errstr,
        )
      );

    return $tmpname;
}

sub tmpnam {

    my $tmpdir = _wrap_file_spec_tmpdir();

    croak "Error temporary directory is not writable"
      if $tmpdir eq '';

    my $template = File::Spec->catfile( $tmpdir, TEMPXXX );

    if ( wantarray() ) {
        return mkstemp($template);
    }
    else {
        return mktemp($template);
    }

}

sub tmpfile {

    my ( $fh, $file ) = tmpnam();

    unlink0( $fh, $file )
      or return undef;

    return $fh;

}

sub tempnam {

    croak 'Usage tempnam($dir, $prefix)' unless scalar(@_) == 2;

    my ( $dir, $prefix ) = @_;

    $prefix .= 'XXXXXXXX';

    my $template = File::Spec->catfile( $dir, $prefix );

    return mktemp($template);

}

sub unlink0 {

    croak 'Usage: unlink0(filehandle, filename)'
      unless scalar(@_) == 2;

    my ( $fh, $path ) = @_;

    cmpstat( $fh, $path ) or return 0;

    if ( _can_unlink_opened_file() ) {

        return 1 if $KEEP_ALL;

        croak "unlink0: $path has become a directory!" if -d $path;
        unlink($path) or return 0;

        my @fh = stat $fh;

        print "Link count = $fh[3] \n" if $DEBUG;

        return 1 if $fh[3] == 0 || $^O eq 'cygwin';
    }
    _deferred_unlink( $fh, $path, 0 );
    return 1;
}

sub cmpstat {

    croak 'Usage: cmpstat(filehandle, filename)'
      unless scalar(@_) == 2;

    my ( $fh, $path ) = @_;

    warn "Comparing stat\n"
      if $DEBUG;

    my @fh;
    {
        local ($^W) = 0;
        @fh = stat $fh;
    }
    return unless @fh;

    if ( $fh[3] > 1 && $^W ) {
        carp "unlink0: fstat found too many links; SB=@fh" if $^W;
    }

    my @path = stat $path;

    unless (@path) {
        carp "unlink0: $path is gone already" if $^W;
        return;
    }

    unless ( -f $path ) {
        confess "panic: $path is no longer a file: SB=@fh";
    }

    my @okstat = ( 0 .. $#fh );
    if ( $^O eq 'MSWin32' ) {
        @okstat = ( 1, 2, 3, 4, 5, 7, 8, 9, 10 );
    }
    elsif ( $^O eq 'os2' ) {
        @okstat = ( 0, 2 .. $#fh );
    }
    elsif ( $^O eq 'VMS' ) {
        @okstat = ( 0, 1 );
    }
    elsif ( $^O eq 'dos' ) {
        @okstat = ( 0, 2 .. 7, 11 .. $#fh );
    }
    elsif ( $^O eq 'mpeix' ) {
        @okstat = ( 0 .. 4, 8 .. 10 );
    }

    for (@okstat) {
        print "Comparing: $_ : $fh[$_] and $path[$_]\n" if $DEBUG;
        unless ( $fh[$_] eq $path[$_] ) {
            warn "Did not match $_ element of stat\n" if $DEBUG;
            return 0;
        }
    }

    return 1;
}

sub unlink1 {
    croak 'Usage: unlink1(filehandle, filename)'
      unless scalar(@_) == 2;

    my ( $fh, $path ) = @_;

    cmpstat( $fh, $path ) or return 0;

    close($fh) or return 0;

    _force_writable($path);

    return 1 if $KEEP_ALL;

    return unlink($path);
}

{
    my $LEVEL = STANDARD;

    sub safe_level {
        my $self = shift;
        if (@_) {
            my $level = shift;
            if (   ( $level != STANDARD )
                && ( $level != MEDIUM )
                && ( $level != HIGH ) )
            {
                carp
"safe_level: Specified level ($level) not STANDARD, MEDIUM or HIGH - ignoring\n"
                  if $^W;
            }
            else {
                if ( $] < 5.006 && $level != STANDARD ) {
                    croak
"Currently requires perl 5.006 or newer to do the safe checks";
                }
                $LEVEL = $level if _can_do_level($level);
            }
        }
        return $LEVEL;
    }
}

{
    my $TopSystemUID = 10;
    $TopSystemUID = 197108 if $^O eq 'interix';

    sub top_system_uid {
        my $self = shift;
        if (@_) {
            my $newuid = shift;
            croak "top_system_uid: UIDs should be numeric"
              unless $newuid =~ /^\d+$/s;
            $TopSystemUID = $newuid;
        }
        return $TopSystemUID;
    }
}

package File::Temp::Dir;

our $VERSION = '0.2311';

use File::Path qw/ rmtree /;
use strict;
use overload
  '""'     => "STRINGIFY",
  '0+'     => \&File::Temp::NUMIFY,
  fallback => 1;

sub dirname {
    my $self = shift;
    return $self->{DIRNAME};
}

sub STRINGIFY {
    my $self = shift;
    return $self->dirname;
}

sub unlink_on_destroy {
    my $self = shift;
    if (@_) {
        $self->{CLEANUP} = shift;
    }
    return $self->{CLEANUP};
}

sub DESTROY {
    my $self = shift;
    local ( $., $@, $!, $^E, $? );
    if (   $self->unlink_on_destroy
        && $$ == $self->{LAUNCHPID}
        && !$File::Temp::KEEP_ALL )
    {
        if ( -d $self->{REALNAME} ) {
            eval { rmtree( $self->{REALNAME}, $File::Temp::DEBUG, 0 ); };
            warn $@ if ( $@ && $^W );
        }
    }
}

1;

__END__

