package File::stat 1.14;
use v5.38;

use warnings::register;
use Carp;
use constant _IS_CYGWIN => $^O eq "cygwin";

BEGIN { *warnif = \&warnings::warnif }

our (
    $st_dev,   $st_ino,     $st_mode, $st_nlink, $st_uid,
    $st_gid,   $st_rdev,    $st_size, $st_atime, $st_mtime,
    $st_ctime, $st_blksize, $st_blocks
);

use Exporter 'import';
our @EXPORT = qw(stat lstat);
our @fields = qw( $st_dev	   $st_ino    $st_mode
  $st_nlink   $st_uid    $st_gid
  $st_rdev    $st_size
  $st_atime   $st_mtime  $st_ctime
  $st_blksize $st_blocks
);
our @EXPORT_OK   = ( @fields, "stat_cando" );
our %EXPORT_TAGS = ( FIELDS => [ @fields, @EXPORT ] );

use Fcntl qw(S_IRUSR S_IWUSR S_IXUSR);

BEGIN {
    no strict 'refs';
    for (qw(suid sgid svtx)) {
        my $val = eval { &{"Fcntl::S_I\U$_"} };
        *{"_$_"} = defined $val ? sub { $_[0] & $val ? 1 : "" } : sub { "" };
    }
    for (qw(SOCK CHR BLK REG DIR LNK)) {
        *{"S_IS$_"} =
          defined eval { &{"Fcntl::S_IF$_"} }
          ? \&{"Fcntl::S_IS$_"}
          : sub { "" };
    }
    *{"S_ISFIFO"} = defined &Fcntl::S_IFIFO ? \&Fcntl::S_ISFIFO : sub { "" };
}

sub _ingroup {
    my ( $gid, $eff ) = @_;

    $^O eq "VMS" and return $_[0] == $);

    my ( $egid, @supp ) = split " ", $);
    my ($rgid) = split " ", $(;

    $gid == ( $eff ? $egid : $rgid ) and return 1;
    grep $gid == $_, @supp and return 1;

    return "";
}

if ( grep $^O eq $_, qw/os2 MSWin32/ ) {

    *cando = sub { ( $_[0][2] & $_[1] ) ? 1 : "" };
}
else {

    *cando = sub {
        my ( $s, $mode, $eff ) = @_;
        my $uid = $eff ? $> : $<;
        my ( $stmode, $stuid, $stgid ) = @$s[ 2, 4, 5 ];

        if (
            _IS_CYGWIN ? _ingroup( 544, $eff ) : ( $uid == 0 && $^O ne "VMS" ) )
        {
            return 1 if !( $mode & 0111 );
            return 1 if $stmode & 0111 || S_ISDIR($stmode);
            return "";
        }

        if ( $stuid == $uid ) {
            $stmode & $mode and return 1;
        }
        elsif ( _ingroup( $stgid, $eff ) ) {
            $stmode & ( $mode >> 3 ) and return 1;
        }
        else {
            $stmode & ( $mode >> 6 ) and return 1;
        }
        return "";
    };
}

*stat_cando = \&cando;

my %op = (
    r => sub { cando( $_[0], S_IRUSR, 1 ) },
    w => sub { cando( $_[0], S_IWUSR, 1 ) },
    x => sub { cando( $_[0], S_IXUSR, 1 ) },
    o => sub { $_[0][4] == $> },

    R => sub { cando( $_[0], S_IRUSR, 0 ) },
    W => sub { cando( $_[0], S_IWUSR, 0 ) },
    X => sub { cando( $_[0], S_IXUSR, 0 ) },
    O => sub { $_[0][4] == $< },

    e => sub { 1 },
    z => sub { $_[0][7] == 0 },
    s => sub { $_[0][7] },

    f => sub { S_ISREG( $_[0][2] ) },
    d => sub { S_ISDIR( $_[0][2] ) },
    l => sub { S_ISLNK( $_[0][2] ) },
    p => sub { S_ISFIFO( $_[0][2] ) },
    S => sub { S_ISSOCK( $_[0][2] ) },
    b => sub { S_ISBLK( $_[0][2] ) },
    c => sub { S_ISCHR( $_[0][2] ) },

    u => sub { _suid( $_[0][2] ) },
    g => sub { _sgid( $_[0][2] ) },
    k => sub { _svtx( $_[0][2] ) },

    M => sub { ( $^T - $_[0][9] ) / 86400 },
    C => sub { ( $^T - $_[0][10] ) / 86400 },
    A => sub { ( $^T - $_[0][8] ) / 86400 },
);

use constant HINT_FILETEST_ACCESS => 0x00400000;

use overload
  fallback => 1,
  -X       => sub {
    my ( $s, $op ) = @_;

    if ( index( "rwxRWX", $op ) >= 0 ) {
        ( caller 0 )[8] & HINT_FILETEST_ACCESS
          and warnif("File::stat ignores use filetest 'access'");

        $^O eq "VMS" and warnif("File::stat ignores VMS ACLs");

    }

    if ( $op{$op} ) {
        return $op{$op}->( $_[0] );
    }
    else {
        croak "-$op is not implemented on a File::stat object";
    }
  };

use Class::Struct qw(struct);
struct 'File::stat' => [
    map { $_ => '$' }
      qw{
      dev ino mode nlink uid gid rdev size
      atime mtime ctime blksize blocks
      }
];

sub populate {
    return unless @_;
    my $stob = new();
    @$stob = (
        $st_dev,   $st_ino,     $st_mode, $st_nlink, $st_uid,
        $st_gid,   $st_rdev,    $st_size, $st_atime, $st_mtime,
        $st_ctime, $st_blksize, $st_blocks
    ) = @_;
    return $stob;
}

sub lstat : prototype($) { populate( CORE::lstat(shift) ) }

sub stat : prototype($) {
    my $arg = shift;
    my $st  = populate( CORE::stat $arg );
    return $st if defined $st;
    my $fh;
    {
        local $!;
        no strict 'refs';
        require Symbol;
        $fh = \*{ Symbol::qualify( $arg, caller() ) };
        return unless defined fileno $fh;
    }
    return populate( CORE::stat $fh );
}

__END__

