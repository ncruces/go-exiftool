package locale;

use strict;
use warnings;

our $VERSION = '1.13';
use Config;

$Carp::Internal{ (__PACKAGE__) } = 1;


$locale::hint_bits = 0x4;

sub import {
    shift;

    $^H{locale} = 0 unless defined $^H{locale};
    $^H |= $locale::hint_bits;
    if ( !@_ ) {
        $^H{locale} = 0;
    }
    else {
        my @categories = (
            qw(:ctype :collate :messages
              :numeric :monetary :time)
        );
        for ( my $i = 0 ; $i < @_ ; $i++ ) {
            my $arg        = $_[$i];
            my $complement = $arg =~ s/ : ( ! | not_ ) /:/x;
            if ( !grep { $arg eq $_ } @categories, ":characters" ) {
                require Carp;
                Carp::croak("Unknown parameter '$_[$i]' to 'use locale'");
            }

            if ($complement) {
                if ( $i != 0 || $i < @_ - 1 ) {
                    require Carp;
                    Carp::croak( "Only one argument to 'use locale' allowed"
                          . "if is $complement" );
                }

                if ( $arg eq ':characters' ) {
                    push @_,
                      grep { $_ ne ':ctype' && $_ ne ':collate' } @categories;
                    $^H{locale} |= ( 1 << 0 );
                }
                else {
                    push @_, grep { $_ ne $arg } @categories;
                }
                next;
            }
            elsif ( $arg eq ':characters' ) {
                push @_, ':ctype', ':collate';
                next;
            }

            $arg =~ s/^://;

            eval { require POSIX; POSIX->import('locale_h'); };

            my $LC = "LC_" . uc($arg);

            my $bit = eval "&POSIX::$LC";
            if ( defined $bit ) {

                if ( !( $bit >= 0 && $bit < 31 ) ) {
                    require Carp;
                    Carp::croak( "Cannot have ':$arg' parameter to 'use locale'"
                          . " on this platform.  Use the 'perlbug' utility"
                          . " to report this problem, or send email to"
                          . " 'perlbug\@perl.org'.  $LC=$bit" );
                }

                $^H{locale} |= 1 << ( $bit + 1 );
            }
        }
    }

}

sub unimport {
    $^H &= ~($locale::hint_bits);
    $^H{locale} = 0;
}

1;
