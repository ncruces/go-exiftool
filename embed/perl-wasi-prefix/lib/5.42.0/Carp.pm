package Carp;

{ use 5.006; }
use strict;
use warnings;

BEGIN {
    if ( !defined($warnings::VERSION) || eval($warnings::VERSION) < 1.06 ) {
        ${^WARNING_BITS} = "";
    }
    else {
        "warnings"->unimport("utf8");
    }
}

sub _fetch_sub {
    my ( $pack, $sub ) = @_;
    $pack .= '::';
    return unless exists( $::{$pack} );
    for ( $::{$pack} ) {
        return unless ref \$_ eq 'GLOB' && *$_{HASH} && exists $$_{$sub};
        for ( $$_{$sub} ) {
            return ref \$_ eq 'GLOB' ? *$_{CODE} : undef;
        }
    }
}

BEGIN {
    if ( "$]" < 5.013011 ) {
        *UTF8_REGEXP_PROBLEM = sub () { 1 };
    }
    else {
        *UTF8_REGEXP_PROBLEM = sub () { 0 };
    }
}

BEGIN {
    if ( defined( my $sub = _fetch_sub utf8 => 'is_utf8' ) ) {
        *is_utf8 = $sub;
    }
    else {
        *is_utf8 = sub { unpack( "C", "\xaa" . $_[0] ) != 170 };
    }
}

BEGIN {
    if ( defined( my $sub = _fetch_sub utf8 => 'downgrade' ) ) {
        *downgrade = \&{"utf8::downgrade"};
    }
    else {
        *downgrade = sub {
            my $r = "";
            my $l = length( $_[0] );
            for ( my $i = 0 ; $i != $l ; $i++ ) {
                my $o = ord( substr( $_[0], $i, 1 ) );
                return if $o > 255;
                $r .= chr($o);
            }
            $_[0] = $r;
        };
    }
}

BEGIN {
    *is_safe_printable_codepoint = "$]" >= 5.007_003
      ? eval(
        q(sub ($) {
		my $u = utf8::native_to_unicode($_[0]);
		$u >= 0x20 && $u <= 0x7e;
	    })
      )
      : ord("A") == 65 ? sub ($) { $_[0] >= 0x20 && $_[0] <= 0x7e }
      : sub ($) {
        $_[0] >= ord(" ")
          && $_[0] <= 0xff
          && $_[0] != ( ord("^") == 106 ? 0x5f : 0xff );
      };
}

sub _univ_mod_loaded {
    return 0 unless exists( $::{"UNIVERSAL::"} );
    for ( $::{"UNIVERSAL::"} ) {
        return 0 unless ref \$_ eq "GLOB" && *$_{HASH} && exists $$_{"$_[0]::"};
        for ( $$_{"$_[0]::"} ) {
            return 0
              unless ref \$_ eq "GLOB" && *$_{HASH} && exists $$_{"VERSION"};
            for ( $$_{"VERSION"} ) {
                return 0 unless ref \$_ eq "GLOB";
                return ${ *$_{SCALAR} };
            }
        }
    }
}

my $isa;

BEGIN {
    if ( _univ_mod_loaded('isa') ) {
        *_maybe_isa = sub { 1 }
    }
    else {
        *_maybe_isa = $isa = _fetch_sub( UNIVERSAL => "isa" );
    }
}

BEGIN {
    if ( eval { require "overloading.pm" } ) {
        *_StrVal = eval 'sub { no overloading; "$_[0]" }';
    }
    else {

        *_mycan =
          _univ_mod_loaded('can')
          ? do { require "overload.pm"; _fetch_sub overload => 'mycan' }
          : \&UNIVERSAL::can;

        *_blessed =
          $isa
          ? sub { &$isa( $_[0], "UNIVERSAL" ) }
          : sub {
            my $probe = "UNIVERSAL::Carp_probe_" . rand;
            no strict 'refs';
            local *$probe = sub { "unlikely string" };
            local $@;
            local $SIG{__DIE__} = sub { };
            ( eval { $_[0]->$probe } || '' ) eq 'unlikely string';
          };

        *_StrVal = sub {
            my $pack = ref $_[0];
            return "$_[0]" unless _mycan( $pack, "()" );
            return "$_[0]" if not _blessed( $_[0] );
            bless $_[0], "Carp";
            my $str = "$_[0]";
            bless $_[0], $pack;
            $pack . substr $str, index $str, "=";
        }
    }
}

our $VERSION = '1.54';
$VERSION =~ tr/_//d;

our $MaxEvalLen      = 0;
our $Verbose         = 0;
our $CarpLevel       = 0;
our $MaxArgLen       = 64;
our $MaxArgNums      = 8;
our $RefArgFormatter = undef;

require Exporter;
our @ISA         = ('Exporter');
our @EXPORT      = qw(confess croak carp);
our @EXPORT_OK   = qw(cluck verbose longmess shortmess);
our @EXPORT_FAIL = qw(verbose);

our %CarpInternal;
our %Internal;

$CarpInternal{Carp}++;
$CarpInternal{warnings}++;
$Internal{Exporter}++;
$Internal{'Exporter::Heavy'}++;

sub export_fail { shift; $Verbose = shift if $_[0] eq 'verbose'; @_ }

sub _cgc {
    no strict 'refs';
    return \&{"CORE::GLOBAL::caller"} if defined &{"CORE::GLOBAL::caller"};
    return;
}

sub longmess {
    local ( $!, $^E );
    my $cgc       = _cgc();
    my $call_pack = $cgc ? $cgc->() : caller();
    if ( $Internal{$call_pack} or $CarpInternal{$call_pack} ) {
        return longmess_heavy(@_);
    }
    else {
        local $CarpLevel = $CarpLevel + 1;
        return longmess_heavy(@_);
    }
}

our @CARP_NOT;

sub shortmess {
    local ( $!, $^E );
    my $cgc = _cgc();

    local @CARP_NOT = scalar( $cgc ? $cgc->() : caller() );
    shortmess_heavy(@_);
}

sub croak   { die shortmess @_ }
sub confess { die longmess @_ }
sub carp    { warn shortmess @_ }
sub cluck   { warn longmess @_ }

BEGIN {
    if (   "$]" >= 5.015002
        || ( "$]" >= 5.014002 && "$]" < 5.015 )
        || ( "$]" >= 5.012005 && "$]" < 5.013 ) )
    {
        *CALLER_OVERRIDE_CHECK_OK = sub () { 1 };
    }
    else {
        *CALLER_OVERRIDE_CHECK_OK = sub () { 0 };
    }
}

sub caller_info {
    my $i = shift(@_) + 1;
    my %call_info;
    my $cgc = _cgc();
    {
        @DB::args = \$i if CALLER_OVERRIDE_CHECK_OK;

        package DB;
        @call_info{
            qw(pack file line sub has_args wantarray evaltext is_require)} =
          $cgc ? $cgc->($i) : caller($i);
    }

    unless ( defined $call_info{file} ) {
        return ();
    }

    my $sub_name = Carp::get_subname( \%call_info );
    if ( $call_info{has_args} ) {
        my @args = map {
            my $arg;
            local $@ = $@;
            eval {
                $arg = $_;
                1;
            } or do {
                $arg = '** argument not available anymore **';
            };
            $arg;
        } @DB::args;
        if (   CALLER_OVERRIDE_CHECK_OK
            && @args == 1
            && ref $args[0] eq ref \$i
            && $args[0] == \$i )
        {
            @args = ();
            local $@;
            my $where = eval {
                my $func = $cgc or return '';
                my $gv =
                  ( _fetch_sub B => 'svref_2object' or return '' )->($func)->GV;
                my $package = $gv->STASH->NAME;
                my $subname = $gv->NAME;
                return unless defined $package && defined $subname;

                return if $package eq 'CORE::GLOBAL' && $subname eq 'caller';
                " in &${package}::$subname";
            } || '';
            @args =
"** Incomplete caller override detected$where; \@DB::args were not set **";
        }
        else {
            my $overflow;
            if ( $MaxArgNums and @args > $MaxArgNums ) {
                $#args    = $MaxArgNums - 1;
                $overflow = 1;
            }

            @args = map { Carp::format_arg($_) } @args;

            if ($overflow) {
                push @args, '...';
            }
        }

        $sub_name .= '(' . join( ', ', @args ) . ')';
    }
    $call_info{sub_name} = $sub_name;
    return wantarray() ? %call_info : \%call_info;
}

our $in_recurse;

sub format_arg {
    my $arg = shift;

    if ( my $pack = ref($arg) ) {

        if (
               !$in_recurse
            && _maybe_isa( $arg, 'UNIVERSAL' )
            && do {
                local $@;
                local $in_recurse = 1;
                local $SIG{__DIE__} = sub { };
                eval { $arg->can('CARP_TRACE') }
            }
          )
        {
            return $arg->CARP_TRACE();
        }
        elsif (
               !$in_recurse
            && defined($RefArgFormatter)
            && do {
                local $@;
                local $in_recurse = 1;
                local $SIG{__DIE__} = sub { };
                eval { $arg = $RefArgFormatter->($arg); 1 }
            }
          )
        {
            return $arg;
        }
        else {
            return _StrVal $arg;
        }
    }
    return "undef" if !defined($arg);
    downgrade( $arg, 1 );
    return $arg
      if !( UTF8_REGEXP_PROBLEM && is_utf8($arg) )
      && $arg =~ /\A-?[0-9]+(?:\.[0-9]*)?(?:[eE][-+]?[0-9]+)?\z/;
    my $suffix = "";
    if ( 2 < $MaxArgLen and $MaxArgLen < length($arg) ) {
        substr( $arg, $MaxArgLen - 3 ) = "";
        $suffix = "...";
    }
    if ( UTF8_REGEXP_PROBLEM && is_utf8($arg) ) {
        for ( my $i = length($arg) ; $i-- ; ) {
            my $c = substr( $arg, $i, 1 );
            my $x = substr( $arg, 0,  0 );
            if ( $c eq "\"" || $c eq "\\" || $c eq "\$" || $c eq "\@" ) {
                substr $arg, $i, 0, "\\";
                next;
            }
            my $o = ord($c);
            substr $arg, $i, 1, sprintf( "\\x{%x}", $o )
              unless is_safe_printable_codepoint($o);
        }
    }
    else {
        $arg =~ s/([\"\\\$\@])/\\$1/g;
        $arg =~
s/([^ !"#\$\%\&'()*+,\-.\/0123456789:;<=>?\@ABCDEFGHIJKLMNOPQRSTUVWXYZ\[\\\]^_`abcdefghijklmnopqrstuvwxyz\{|}~])/sprintf("\\x{%x}",ord($1))/eg;
    }
    downgrade( $arg, 1 );
    return "\"" . $arg . "\"" . $suffix;
}

sub Regexp::CARP_TRACE {
    my $arg = "$_[0]";
    downgrade( $arg, 1 );
    if ( UTF8_REGEXP_PROBLEM && is_utf8($arg) ) {
        for ( my $i = length($arg) ; $i-- ; ) {
            my $o = ord( substr( $arg, $i, 1 ) );
            my $x = substr( $arg, 0, 0 );
            substr $arg, $i, 1, sprintf( "\\x{%x}", $o )
              unless is_safe_printable_codepoint($o);
        }
    }
    else {
        $arg =~
s/([^ !"#\$\%\&'()*+,\-.\/0123456789:;<=>?\@ABCDEFGHIJKLMNOPQRSTUVWXYZ\[\\\]^_`abcdefghijklmnopqrstuvwxyz\{|}~])/sprintf("\\x{%x}",ord($1))/eg;
    }
    downgrade( $arg, 1 );
    my $suffix = "";
    if ( $arg =~ /\A\(\?\^?([a-z]*)(?:-[a-z]*)?:(.*)\)\z/s ) {
        ( $suffix, $arg ) = ( $1, $2 );
    }
    if ( 2 < $MaxArgLen and $MaxArgLen < length($arg) ) {
        substr( $arg, $MaxArgLen - 3 ) = "";
        $suffix = "..." . $suffix;
    }
    return "qr($arg)$suffix";
}

sub get_status {
    my $cache = shift;
    my $pkg   = shift;
    $cache->{$pkg} ||= [ { $pkg => $pkg }, [ trusts_directly($pkg) ] ];
    return @{ $cache->{$pkg} };
}

sub get_subname {
    my $info = shift;
    if ( defined( $info->{evaltext} ) ) {
        my $eval = $info->{evaltext};
        if ( $info->{is_require} ) {
            return "require $eval";
        }
        else {
            $eval =~ s/([\\\'])/\\$1/g;
            return "eval '" . str_len_trim( $eval, $MaxEvalLen ) . "'";
        }
    }

    if ( !defined( $info->{sub} ) ) {
        return '__ANON__::__ANON__';
    }

    return ( $info->{sub} eq '(eval)' ) ? 'eval {...}' : $info->{sub};
}

sub long_error_loc {
    my $i;
    my $lvl = $CarpLevel;
    {
        ++$i;
        my $cgc    = _cgc();
        my @caller = $cgc ? $cgc->($i) : caller($i);
        my $pkg    = $caller[0];
        unless ( defined($pkg) ) {

            if (%Internal) {
                local %Internal;
                $i = long_error_loc();
                last;
            }
            elsif ( defined $caller[2] ) {
                redo unless 0 > --$lvl;
                last;
            }
            else {
                return 2;
            }
        }
        redo if $CarpInternal{$pkg};
        redo unless 0 > --$lvl;
        redo if $Internal{$pkg};
    }
    return $i - 1;
}

sub longmess_heavy {
    if ( ref( $_[0] ) ) {
        return wantarray ? @_ : $_[0];
    }
    my $i = long_error_loc();
    return ret_backtrace( $i, @_ );
}

BEGIN {
    if ( "$]" >= 5.017004 ) {
        $Carp::{LAST_FH} = \eval '\${^LAST_FH}';
    }
    else {
        eval '*LAST_FH = sub () { 0 }';
    }
}

sub ret_backtrace {
    my ( $i, @error ) = @_;
    my $mess;
    my $err = join '', @error;
    $i++;

    my $tid_msg = '';
    if ( defined &threads::tid ) {
        my $tid = threads->tid;
        $tid_msg = " thread $tid" if $tid;
    }

    my %i = caller_info($i);
    $mess = "$err at $i{file} line $i{line}$tid_msg";
    if ($.) {
        if (LAST_FH) {
            if ( ${ +LAST_FH } ) {
                $mess .= sprintf ", <%s> %s %d", *${ +LAST_FH }{NAME},
                  ( $/ eq "\n" ? "line" : "chunk" ), $.;
            }
        }
        else {
            local $@ = '';
            local $SIG{__DIE__};
            eval { CORE::die; };
            if ( $@ =~ /^Died at .*(, <.*?> (?:line|chunk) \d+).$/ ) {
                $mess .= $1;
            }
        }
    }
    $mess .= "\.\n";

    while ( my %i = caller_info( ++$i ) ) {
        $mess .= "\t$i{sub_name} called at $i{file} line $i{line}$tid_msg\n";
    }

    return $mess;
}

sub ret_summary {
    my ( $i, @error ) = @_;
    my $err = join '', @error;
    $i++;

    my $tid_msg = '';
    if ( defined &threads::tid ) {
        my $tid = threads->tid;
        $tid_msg = " thread $tid" if $tid;
    }

    my %i = caller_info($i);
    return "$err at $i{file} line $i{line}$tid_msg\.\n";
}

sub short_error_loc {
    my $cache = {};
    my $i     = 1;
    my $lvl   = $CarpLevel;
    {
        my $cgc    = _cgc();
        my $called = $cgc ? $cgc->($i) : caller($i);
        $i++;
        my $caller = $cgc ? $cgc->($i) : caller($i);

        if ( !defined($caller) ) {
            my @caller = $cgc ? $cgc->($i) : caller($i);
            if (@caller) {
                redo if defined($called) && $CarpInternal{$called};
                redo unless 0 > --$lvl;
                last;
            }
            else {
                return 0;
            }
        }
        redo if $Internal{$caller};
        redo if $CarpInternal{$caller};
        redo if $CarpInternal{$called};
        redo if trusts( $called, $caller, $cache );
        redo if trusts( $caller, $called, $cache );
        redo unless 0 > --$lvl;
    }
    return $i - 1;
}

sub shortmess_heavy {
    return longmess_heavy(@_) if $Verbose;
    return @_                 if ref( $_[0] );
    my $i = short_error_loc();
    if ($i) {
        ret_summary( $i, @_ );
    }
    else {
        longmess_heavy(@_);
    }
}

sub str_len_trim {
    my $str = shift;
    my $max = shift || 0;
    if ( 2 < $max and $max < length($str) ) {
        substr( $str, $max - 3 ) = '...';
    }
    return $str;
}

sub trusts {
    my $child  = shift;
    my $parent = shift;
    my $cache  = shift;
    my ( $known, $partial ) = get_status( $cache, $child );

    while ( @$partial and not exists $known->{$parent} ) {
        my $anc = shift @$partial;
        next if exists $known->{$anc};
        $known->{$anc}++;
        my ( $anc_knows, $anc_partial ) = get_status( $cache, $anc );
        my @found = keys %$anc_knows;
        @$known{@found} = ();
        push @$partial, @$anc_partial;
    }
    return exists $known->{$parent};
}

sub trusts_directly {
    my $class = shift;
    no strict 'refs';
    my $stash = \%{"$class\::"};
    for my $var (qw/ CARP_NOT ISA /) {
        if (   $stash->{$var}
            && ref \$stash->{$var} eq 'GLOB'
            && *{ $stash->{$var} }{ARRAY}
            && @{ $stash->{$var} } )
        {
            return @{ $stash->{$var} };
        }
    }
    return;
}

if (
      !defined($warnings::VERSION)
    || do { no warnings "numeric"; $warnings::VERSION < 1.03 }
  )
{
    no strict "refs";
    *{"warnings::$_"} = \&$_ foreach @EXPORT;
}

1;

__END__

