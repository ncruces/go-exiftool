package Thread::Queue;

use strict;
use warnings;

our $VERSION = '3.14';
$VERSION = eval $VERSION;

use threads::shared 1.21;
use Scalar::Util 1.10 qw(looks_like_number blessed reftype refaddr);

our @CARP_NOT = ("threads::shared");

sub new {
    my $class = shift;
    my @queue : shared = map { shared_clone($_) } @_;
    my %self  : shared = ( 'queue' => \@queue );
    return bless( \%self, $class );
}

sub enqueue {
    my $self = shift;
    lock(%$self);

    if ( $$self{'ENDED'} ) {
        require Carp;
        Carp::croak("'enqueue' method called on queue that has been 'end'ed");
    }

    my $queue = $$self{'queue'};
    cond_wait(%$self)
      while ( $$self{'LIMIT'} && ( @$queue >= $$self{'LIMIT'} ) );

    push( @$queue, map { shared_clone($_) } @_ )
      and cond_signal(%$self);
}

sub limit : lvalue {
    my $self = shift;
    lock(%$self);
    $$self{'LIMIT'};
}

sub pending {
    my $self = shift;
    lock(%$self);
    return if ( $$self{'ENDED'} && !@{ $$self{'queue'} } );
    return scalar( @{ $$self{'queue'} } );
}

sub end {
    my $self = shift;
    lock(%$self);
    $$self{'ENDED'} = 1;

    cond_signal(%$self);
}

sub dequeue {
    my $self = shift;
    lock(%$self);
    my $queue = $$self{'queue'};

    my $count = @_ ? $self->_validate_count(shift) : 1;

    cond_wait(%$self) while ( ( @$queue < $count ) && !$$self{'ENDED'} );

    return $self->dequeue_nb($count) if ( $$self{'ENDED'} );

    if ( $count == 1 ) {
        my $item = shift(@$queue);
        cond_signal(%$self);
        return $item;
    }

    my @items;
    push( @items, shift(@$queue) ) for ( 1 .. $count );
    cond_signal(%$self);
    return @items;
}

sub dequeue_nb {
    my $self = shift;
    lock(%$self);
    my $queue = $$self{'queue'};

    my $count = @_ ? $self->_validate_count(shift) : 1;

    if ( $count == 1 ) {
        my $item = shift(@$queue);
        cond_signal(%$self);
        return $item;
    }

    my @items;
    for ( 1 .. $count ) {
        last if ( !@$queue );
        push( @items, shift(@$queue) );
    }
    cond_signal(%$self);
    return @items;
}

sub dequeue_timed {
    my $self = shift;
    lock(%$self);
    my $queue = $$self{'queue'};

    my $timeout = @_ ? $self->_validate_timeout(shift) : -1;
    if ( $timeout < 32000000 ) {
        $timeout += time();
    }

    my $count = @_ ? $self->_validate_count(shift) : 1;

    while ( ( @$queue < $count ) && !$$self{'ENDED'} ) {
        last if ( !cond_timedwait( %$self, $timeout ) );
    }

    return $self->dequeue_nb($count);
}

sub peek {
    my $self = shift;
    lock(%$self);
    my $index = @_ ? $self->_validate_index(shift) : 0;
    return $$self{'queue'}[$index];
}

sub insert {
    my $self = shift;
    lock(%$self);

    if ( $$self{'ENDED'} ) {
        require Carp;
        Carp::croak("'insert' method called on queue that has been 'end'ed");
    }

    my $queue = $$self{'queue'};

    my $index = $self->_validate_index(shift);

    return if ( !@_ );

    if ( $index < 0 ) {
        $index += @$queue;
        if ( $index < 0 ) {
            $index = 0;
        }
    }

    my @tmp;
    while ( @$queue > $index ) {
        unshift( @tmp, pop(@$queue) );
    }

    push( @$queue, map { shared_clone($_) } @_ );

    push( @$queue, @tmp );

    cond_signal(%$self);
}

sub extract {
    my $self = shift;
    lock(%$self);
    my $queue = $$self{'queue'};

    my $index = @_ ? $self->_validate_index(shift) : 0;
    my $count = @_ ? $self->_validate_count(shift) : 1;

    if ( $index < 0 ) {
        $index += @$queue;
        if ( $index < 0 ) {
            $count += $index;
            return if ( $count <= 0 );
            return $self->dequeue_nb($count);
        }
    }

    my @tmp;
    while ( @$queue > ( $index + $count ) ) {
        unshift( @tmp, pop(@$queue) );
    }

    my @items;
    unshift( @items, pop(@$queue) ) while ( @$queue > $index );

    push( @$queue, @tmp );

    cond_signal(%$self);

    return $items[0] if ( $count == 1 );

    return @items;
}

sub _validate_index {
    my $self  = shift;
    my $index = shift;

    if (   !defined($index)
        || !looks_like_number($index)
        || ( int($index) != $index ) )
    {
        require Carp;
        my ($method) = ( caller(1) )[3];
        my $class_name = ref($self);
        $method =~ s/$class_name\:://;
        $index = 'undef' if ( !defined($index) );
        Carp::croak("Invalid 'index' argument ($index) to '$method' method");
    }

    return $index;
}

sub _validate_count {
    my $self  = shift;
    my $count = shift;

    if (   !defined($count)
        || !looks_like_number($count)
        || ( int($count) != $count )
        || ( $count < 1 )
        || ( $$self{'LIMIT'} && $count > $$self{'LIMIT'} ) )
    {
        require Carp;
        my ($method) = ( caller(1) )[3];
        my $class_name = ref($self);
        $method =~ s/$class_name\:://;
        $count = 'undef' if ( !defined($count) );
        if ( $$self{'LIMIT'} && $count > $$self{'LIMIT'} ) {
            Carp::croak(
"'count' argument ($count) to '$method' method exceeds queue size limit ($$self{'LIMIT'})"
            );
        }
        else {
            Carp::croak(
                "Invalid 'count' argument ($count) to '$method' method");
        }
    }

    return $count;
}

sub _validate_timeout {
    my $self    = shift;
    my $timeout = shift;

    if (   !defined($timeout)
        || !looks_like_number($timeout) )
    {
        require Carp;
        my ($method) = ( caller(1) )[3];
        my $class_name = ref($self);
        $method =~ s/$class_name\:://;
        $timeout = 'undef' if ( !defined($timeout) );
        Carp::croak(
            "Invalid 'timeout' argument ($timeout) to '$method' method");
    }

    return $timeout;
}

1;

