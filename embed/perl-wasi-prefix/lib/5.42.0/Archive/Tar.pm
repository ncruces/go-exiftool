
package Archive::Tar;
require 5.005_03;

use Cwd;
use IO::Zlib;
use IO::File;
use Carp             qw(carp croak);
use File::Spec       ();
use File::Spec::Unix ();
use File::Path       ();

use Archive::Tar::File;
use Archive::Tar::Constant;

require Exporter;

use strict;
use vars qw[$DEBUG $error $VERSION $WARN $FOLLOW_SYMLINK $CHOWN $CHMOD
  $DO_NOT_USE_PREFIX $HAS_PERLIO $HAS_IO_STRING $SAME_PERMISSIONS
  $INSECURE_EXTRACT_MODE $ZERO_PAD_NUMBERS @ISA @EXPORT $RESOLVE_SYMLINK
  $EXTRACT_BLOCK_SIZE
];

@ISA                   = qw[Exporter];
@EXPORT                = qw[ COMPRESS_GZIP COMPRESS_BZIP COMPRESS_XZ ];
$DEBUG                 = 0;
$WARN                  = 1;
$FOLLOW_SYMLINK        = 0;
$VERSION               = "3.04";
$CHOWN                 = 1;
$CHMOD                 = 1;
$SAME_PERMISSIONS      = $> == 0 ? 1 : 0;
$DO_NOT_USE_PREFIX     = 0;
$INSECURE_EXTRACT_MODE = 0;
$ZERO_PAD_NUMBERS      = 0;
$RESOLVE_SYMLINK       = $ENV{'PERL5_AT_RESOLVE_SYMLINK'} || 'speed';
$EXTRACT_BLOCK_SIZE    = 1024 * 1024 * 1024;

BEGIN {
    use Config;
    $HAS_PERLIO = $Config::Config{useperlio};

    $HAS_IO_STRING = eval {
        require IO::String;
        IO::String->import;
        1;
    } || 0;
}


my $tmpl = {
    _data => [],
    _file => 'Unknown',
};

for my $key ( keys %$tmpl ) {
    no strict 'refs';
    *{ __PACKAGE__ . "::$key" } = sub {
        my $self = shift;
        $self->{$key} = $_[0] if @_;
        return $self->{$key};
    }
}

sub new {
    my $class = shift;
    $class = ref $class if ref $class;

    my $obj = bless { _data => [], _file => 'Unknown', _error => '' }, $class;

    if (@_) {
        unless ( $obj->read(@_) ) {
            $obj->_error(qq[No data could be read from file]);
            return;
        }
    }

    return $obj;
}


sub read {
    my $self = shift;
    my $file = shift;
    my $gzip = shift || 0;
    my $opts = shift || {};

    unless ( defined $file ) {
        $self->_error(qq[No file to read from!]);
        return;
    }
    else {
        $self->_file($file);
    }

    my $handle = $self->_get_handle( $file, $gzip, READ_ONLY->(ZLIB) )
      or return;

    my $data = $self->_read_tar( $handle, $opts ) or return;

    $self->_data($data);

    return wantarray ? @$data : scalar @$data;
}

sub _get_handle {
    my $self = shift;
    my $file = shift;
    return unless defined $file;
    my $compress = shift || 0;
    my $mode     = shift || READ_ONLY->(ZLIB);

    if ( ref $file ) {
        return $file if eval { *$file{IO} };
        return $file if eval { $file->isa(q{IO::Handle}) };
        $file = q{} . $file;
    }

    my $fh;
    {
        my $magic = '';
        if ( MODE_READ->($mode) ) {
            open my $tmp, $file or do {
                $self->_error(qq[Could not open '$file' for reading: $!]);
                return;
            };

            sysread( $tmp, $magic, 6 );
            close $tmp;
        }

        if (
            XZ
            and (  ( $compress eq COMPRESS_XZ )
                or ( MODE_READ->($mode) and $magic =~ XZ_MAGIC_NUM ) )
          )
        {
            if ( MODE_READ->($mode) ) {
                $fh = IO::Uncompress::UnXz->new($file) or do {
                    $self->_error( qq[Could not read '$file': ]
                          . $IO::Uncompress::UnXz::UnXzError );
                    return;
                };
            }
            else {
                $fh = IO::Compress::Xz->new($file) or do {
                    $self->_error( qq[Could not write to '$file': ]
                          . $IO::Compress::Xz::XzError );
                    return;
                };
            }

        }
        elsif (
            BZIP
            and (  ( $compress eq COMPRESS_BZIP )
                or ( MODE_READ->($mode) and $magic =~ BZIP_MAGIC_NUM ) )
          )
        {

            if ( MODE_READ->($mode) ) {
                $fh = IO::Uncompress::Bunzip2->new( $file, MultiStream => 1 )
                  or do {
                    $self->_error( qq[Could not read '$file': ]
                          . $IO::Uncompress::Bunzip2::Bunzip2Error );
                    return;
                  };

            }
            else {
                $fh = IO::Compress::Bzip2->new($file) or do {
                    $self->_error( qq[Could not write to '$file': ]
                          . $IO::Compress::Bzip2::Bzip2Error );
                    return;
                };
            }

        }
        elsif ( ZLIB
            and ( $compress or MODE_READ->($mode) or $magic =~ GZIP_MAGIC_NUM )
          )
        {
            $fh = IO::Zlib->new;

            unless ( $fh->open( $file, $mode ) ) {
                $self->_error(qq[Could not create filehandle for '$file': $!]);
                return;
            }

        }
        else {
            $fh = IO::File->new;

            unless ( $fh->open( $file, $mode ) ) {
                $self->_error(qq[Could not create filehandle for '$file': $!]);
                return;
            }

            binmode $fh;
        }
    }

    return $fh;
}

sub _read_tar {
    my $self   = shift;
    my $handle = shift or return;
    my $opts   = shift || {};

    my $count     = $opts->{limit} || 0;
    my $filter    = $opts->{filter};
    my $md5       = $opts->{md5} || 0;
    my $filter_cb = $opts->{filter_cb};
    my $extract   = $opts->{extract} || 0;

    my $limit = 0;
    $limit = 1 if $count > 0;

    my $tarfile = [];
    my $chunk;
    my $read = 0;
    my $real_name;

    my $data;

  LOOP:
    while ( $handle->read( $chunk, HEAD ) ) {
        my $offset;
        if ( ref($handle) ne 'IO::Zlib' ) {
            local $@;
            $offset = eval { tell $handle } || 'unknown';
            $@      = '';
        }
        else {
            $offset = 'unknown';
        }

        unless ( $read++ ) {
            my $gzip = GZIP_MAGIC_NUM;
            if ( $chunk =~ /$gzip/ ) {
                $self->_error(qq[Cannot read compressed format in tar-mode]);
                return;
            }

            if ( length $chunk != HEAD ) {
                $self->_error(qq[Cannot read enough bytes from the tarfile]);
                return;
            }
        }

        last if length $chunk != HEAD;

        next if $chunk eq TAR_END;

        {
            my $nulls = join '', "\0" x 12;
            unless ( $nulls eq substr( $chunk, 500, 12 ) ) {
                $self->_error(qq[Invalid header block at offset $offset]);
                next LOOP;
            }
        }

        my $entry;
        {
            my %extra_args = ();
            $extra_args{'name'} = $$real_name if defined $real_name;

            unless (
                $entry = Archive::Tar::File->new(
                    chunk => $chunk,
                    %extra_args
                )
              )
            {
                $self->_error(qq[Couldn't read chunk at offset $offset]);
                next LOOP;
            }
        }

        next if $entry->is_label;

        if ( length $entry->type
            and ( $entry->is_file || $entry->is_longlink ) )
        {

            if ( $entry->is_file && !$entry->validate ) {
                my $name = $entry->name;
                $name = substr( $name, 0, 100 ) if length $name > 100;
                $name =~ s/\n/ /g;

                $self->_error( $name . qq[: checksum error] );
                next LOOP;
            }

            my $block = BLOCK_SIZE->( $entry->size );

            $data = $entry->get_content_by_ref;

            my $skip = 0;
            my $ctx;

            if ($md5) {
                $ctx  = Digest::MD5->new;
                $skip = 5;

            }
            elsif ( $filter && $entry->name !~ $filter ) {
                $skip = 1;

            }
            elsif ( $filter_cb && !$filter_cb->($entry) ) {
                $skip = 2;

            }
            elsif ( $entry->name eq PAX_HEADER or $entry->type =~ /^(x|g)$/ ) {
                $skip = 3;
            }

            if ($skip) {
                my $amt = $block;
                my $fsz = $entry->size;
                while ( $amt > 0 ) {
                    $$data = '';
                    my $this = 64 * BLOCK;
                    $this = $amt if $this > $amt;
                    if ( $handle->read( $$data, $this ) < $this ) {
                        $self->_error(
                                qq[Read error on tarfile (missing data) ']
                              . $entry->full_path
                              . "' at offset $offset" );
                        next LOOP;
                    }
                    $amt -= $this;
                    $fsz -= $this;
                    substr( $$data, $fsz ) = "" if ( $fsz < 0 );
                    $ctx->add($$data) if ( $skip == 5 );
                }
                $$data = $ctx->hexdigest
                  if ( $skip == 5
                    && !$entry->is_longlink
                    && !$entry->is_unknown
                    && !$entry->is_label );
            }
            else {

                if ( $handle->read( $$data, $block ) < $block ) {
                    $self->_error( qq[Read error on tarfile (missing data) ']
                          . $entry->full_path
                          . "' at offset $offset" );
                    next LOOP;
                }
                substr( $$data, $entry->size ) = "" if defined $$data;
            }

            if ( $entry->is_longlink ) {

                my $nulls = $$data =~ tr/\0/\0/;

                $entry->size( $entry->size - $nulls );
                substr( $$data, $entry->size ) = "";
            }
        }

        if ( $entry->is_longlink ) {
            $real_name = $data;
            next LOOP;
        }
        elsif ( defined $real_name ) {
            $entry->name($$real_name);
            $entry->prefix('');
            undef $real_name;
        }

        if ( $filter && $entry->name !~ $filter ) {
            next LOOP;

        }
        elsif ( $filter_cb && !$filter_cb->($entry) ) {
            next LOOP;

        }
        elsif ( $entry->name eq PAX_HEADER or $entry->type =~ /^(x|g)$/ ) {
            next LOOP;
        }

        if (   $extract
            && !$entry->is_longlink
            && !$entry->is_unknown
            && !$entry->is_label )
        {
            $self->_extract_file($entry) or return;
        }

        last LOOP if $entry->name eq '';

        push @$tarfile, ( $extract ? $entry->name : $entry );

        if ($limit) {
            $count--  unless $entry->is_longlink || $entry->is_dir;
            last LOOP unless $count;
        }
    }
    continue {
        undef $data;
    }

    return $tarfile;
}


sub contains_file {
    my $self = shift;
    my $full = shift;

    return unless defined $full;

    local $WARN = 0;
    return 1 if $self->_find_entry($full);
    return;
}


sub extract {
    my $self = shift;
    my @args = @_;
    my @files;
    my $hashmap;

    local ( $self->{cwd} ) = cwd() unless $self->{cwd};

    if (@args) {
        for my $file (@args) {

            if ( UNIVERSAL::isa( $file, 'Archive::Tar::File' ) ) {
                push @files, $file;
                next;

            }
            else {

                $hashmap =
                  $hashmap || { map { $_->full_path, $_ } @{ $self->_data } };

                if ( exists $hashmap->{$file} ) {
                    push @files, $hashmap->{$file};
                }
                else {
                    return $self->_error(qq[Could not find '$file' in archive]);
                }
            }
        }

    }
    else {
        @files = $self->get_files;
    }

    unless ( scalar @files ) {
        $self->_error( qq[No files found for ] . $self->_file );
        return;
    }

    for my $entry (@files) {
        unless ( $self->_extract_file($entry) ) {
            $self->_error( q[Could not extract '] . $entry->full_path . q['] );
            return;
        }
    }

    return @files;
}


sub extract_file {
    my $self = shift;
    my $file = shift;
    return unless defined $file;
    my $alt = shift;

    my $entry = $self->_find_entry($file)
      or $self->_error(qq[Could not find an entry for '$file']), return;

    return $self->_extract_file( $entry, $alt );
}

sub _extract_file {
    my $self  = shift;
    my $entry = shift or return;
    my $alt   = shift;

    my $name = defined $alt ? $alt : $entry->full_path;

    my ( $vol, $dirs, $file );
    if ( defined $alt ) {
        ( $vol, $dirs, $file ) = File::Spec->splitpath( $alt, $entry->is_dir );
    }
    else {
        ( $vol, $dirs, $file ) =
          File::Spec::Unix->splitpath( $name, $entry->is_dir );
    }

    my $dir;
    if ( $vol || File::Spec->file_name_is_absolute($dirs) ) {

        if ( not defined $alt and not $INSECURE_EXTRACT_MODE ) {
            $self->_error( q[Entry ']
                  . $entry->full_path
                  . q[' is an absolute path. ]
                  . q[Not extracting absolute paths under SECURE EXTRACT MODE]
            );
            return;
        }

        $dir = File::Spec->catpath( $vol, $dirs, "" );

    }
    else {
        my $cwd =
          ( ref $self and defined $self->{cwd} )
          ? $self->{cwd}
          : cwd();

        my @dirs =
          defined $alt
          ? File::Spec->splitdir($dirs)
          : File::Spec::Unix->splitdir($dirs);

        if (    not defined $alt
            and not $INSECURE_EXTRACT_MODE )
        {

            if ( grep { $_ eq '..' } @dirs ) {

                $self->_error( q[Entry ']
                      . $entry->full_path
                      . q[' is attempting to leave ]
                      . q[the current working directory. Not extracting under ]
                      . q[SECURE EXTRACT MODE] );
                return;
            }

            my $full_path = $cwd;
            for my $d (@dirs) {
                $full_path = File::Spec->catdir( $full_path, $d );

                next if ref $self and $self->{_link_cache}->{$full_path};

                if ( -l $full_path ) {
                    my $to   = readlink $full_path;
                    my $diag = "symlinked directory ($full_path => $to)";

                    $self->_error( q[Entry ']
                          . $entry->full_path
                          . q[' is attempting to ]
                          . qq[extract to a $diag. This is considered a security ]
                          . q[vulnerability and not allowed under SECURE EXTRACT ]
                          . q[MODE] );
                    return;
                }

                $self->{_link_cache}->{$full_path} = 1 if ref $self;
            }
        }

        map { length() ? VMS::Filespec::vmsify( $_ . '/' ) : $_ } @dirs
          if ON_VMS;

        my ( $cwd_vol, $cwd_dir, $cwd_file ) = File::Spec->splitpath($cwd);
        my @cwd = File::Spec->splitdir($cwd_dir);
        push @cwd, $cwd_file if length $cwd_file;

        $dir = File::Spec->catpath( $cwd_vol, File::Spec->catdir( @cwd, @dirs ),
            '' );

        unless ( defined $dir ) {
            $^W && $self->_error(qq[Could not compose a path for '$dirs'\n]);
            return;
        }

    }

    if ( -e $dir && !-d _ ) {
        $^W && $self->_error(qq['$dir' exists, but it's not a directory!\n]);
        return;
    }

    unless ( -d _ ) {
        eval { File::Path::mkpath( $dir, 0, 0777 ) };
        if ($@) {
            my $fp = $entry->full_path;
            $self->_error(qq[Could not create directory '$dir' for '$fp': $@]);
            return;
        }

    }

    return 1 if $entry->is_dir;

    my $full = File::Spec->catfile( $dir, $file );

    if ( $entry->is_unknown ) {
        $self->_error(qq[Unknown file type for file '$full']);
        return;
    }

    if ( -l $full || -e _ ) {
        if ( !unlink $full ) {
            $self->_error(qq[Could not remove old file '$full': $!]);
            return;
        }
    }
    if ( length $entry->type && $entry->is_file ) {
        my $fh = IO::File->new;
        $fh->open( $full, '>' )
          or ( $self->_error(qq[Could not open file '$full': $!]), return );

        if ( $entry->size ) {
            binmode $fh;
            my $offset  = 0;
            my $content = $entry->get_content_by_ref();
            while ( $offset < $entry->size ) {
                my $written = syswrite $fh, $$content, $EXTRACT_BLOCK_SIZE,
                  $offset;
                if ( defined $written ) {
                    $offset += $written;
                }
                else {
                    $self->_error(qq[Could not write data to '$full': $!]);
                    return;
                }
            }
        }

        close $fh
          or ( $self->_error(qq[Could not close file '$full']), return );

    }
    else {
        $self->_make_special_file( $entry, $full ) or return;
    }

    if ( not -l $full ) {
        utime time, $entry->mtime - TIME_OFFSET, $full
          or $self->_error(qq[Could not update timestamp]);
    }

    if ( $CHOWN && CAN_CHOWN->() and not -l $full ) {
        CORE::chown( $entry->uid, $entry->gid, $full )
          or $self->_error(qq[Could not set uid/gid on '$full']);
    }

    if ( $CHMOD and not -l $full ) {
        my $mode = $entry->mode;
        unless ($SAME_PERMISSIONS) {
            $mode &= ~( oct(7000) | umask );
        }
        CORE::chmod( $mode, $full )
          or $self->_error( qq[Could not chown '$full' to ] . $entry->mode );
    }

    return 1;
}

sub _make_special_file {
    my $self  = shift;
    my $entry = shift or return;
    my $file  = shift;
    return unless defined $file;

    my $err;

    if ( $entry->is_symlink ) {
        my $fail;
        if (ON_UNIX) {
            symlink( $entry->linkname, $file ) or $fail++;

        }
        else {
            $self->_extract_special_file_as_plain_file( $entry, $file )
              or $fail++;
        }

        $err =
            qq[Making symbolic link '$file' to ']
          . $entry->linkname
          . q[' failed]
          if $fail;

    }
    elsif ( $entry->is_hardlink ) {
        my $fail;
        if (ON_UNIX) {
            link( $entry->linkname, $file ) or $fail++;

        }
        else {
            $self->_extract_special_file_as_plain_file( $entry, $file )
              or $fail++;
        }

        $err =
            qq[Making hard link from ']
          . $entry->linkname
          . qq[' to '$file' failed]
          if $fail;

    }
    elsif ( $entry->is_fifo ) {
        ON_UNIX && !system( 'mknod', $file, 'p' )
          or $err = qq[Making fifo '] . $entry->name . qq[' failed];

    }
    elsif ( $entry->is_blockdev or $entry->is_chardev ) {
        my $mode = $entry->is_blockdev ? 'b' : 'c';

        ON_UNIX
          && !
          system( 'mknod', $file, $mode, $entry->devmajor, $entry->devminor )
          or $err =
            qq[Making block device ']
          . $entry->name
          . qq[' (maj=]
          . $entry->devmajor
          . qq[ min=]
          . $entry->devminor
          . qq[) failed.];

    }
    elsif ( $entry->is_socket ) {
        1;
    }

    return $err ? $self->_error($err) : 1;
}

sub _extract_special_file_as_plain_file {
    my $self  = shift;
    my $entry = shift or return;
    my $file  = shift;
    return unless defined $file;

    my $err;
  TRY: {
        my $orig = $self->_find_entry( $entry->linkname, $entry );

        unless ($orig) {
            $err =
              qq[Could not find file '] . $entry->linkname . qq[' in memory.];
            last TRY;
        }

        my $clone = $orig->clone;
        $clone->_downgrade_to_plainfile;
        $self->_extract_file( $clone, $file ) or last TRY;

        return 1;
    }

    return $self->_error($err);
}


sub list_files {
    my $self = shift;
    my $aref = shift || [];

    unless ( $self->_data ) {
        $self->read() or return;
    }

    if ( @$aref == 0 or ( @$aref == 1 and $aref->[0] eq 'name' ) ) {
        return map { $_->full_path } @{ $self->_data };
    }
    else {

        return map {
            my $o = $_;
            +{ map { $_ => $o->$_() } @$aref }
        } @{ $self->_data };
    }
}

sub _find_entry {
    my $self = shift;
    my $file = shift;

    unless ( defined $file ) {
        $self->_error(qq[No file specified]);
        return;
    }

    return $file if UNIVERSAL::isa( $file, 'Archive::Tar::File' );

  seach_entry:
    if ( $self->_data ) {
        for my $entry ( @{ $self->_data } ) {
            my $path = $entry->full_path;
            return $entry if $path eq $file;
        }
    }

    if ( $Archive::Tar::RESOLVE_SYMLINK !~ /none/ ) {
        if ( my $link_entry = shift() ) {
            $file = _symlinks_resolver( $link_entry->name, $file );
            goto seach_entry if $self->_data;

            my $iterargs = $link_entry->{'_archive'};
            if ( $Archive::Tar::RESOLVE_SYMLINK =~ /speed/ && @$iterargs == 3 )
            {
                my $archive = Archive::Tar->new;
                $archive->read(@$iterargs);
                push @$iterargs, $archive;
                if ( $archive->_data ) {
                    $self->_data( $archive->_data );
                    goto seach_entry;
                }
            }

            {

                my $next = Archive::Tar->iter(@$iterargs);
                while ( my $e = $next->() ) {
                    if ( $e->full_path eq $file ) {
                        undef $next;
                        return $e;
                    }
                }
            }
        }
    }

    $self->_error(qq[No such file in archive: '$file']);
    return;
}


sub get_files {
    my $self = shift;

    return @{ $self->_data } unless @_;

    my @list;
    for my $file (@_) {
        push @list, grep { defined } $self->_find_entry($file);
    }

    return @list;
}


sub get_content {
    my $self  = shift;
    my $entry = $self->_find_entry(shift) or return;

    return $entry->data;
}


sub replace_content {
    my $self  = shift;
    my $entry = $self->_find_entry(shift) or return;

    return $entry->replace_content(shift);
}


sub rename {
    my $self = shift;
    my $file = shift;
    return unless defined $file;
    my $new = shift;
    return unless defined $new;

    my $entry = $self->_find_entry($file) or return;

    return $entry->rename($new);
}


sub chmod {
    my $self = shift;
    my $file = shift;
    return unless defined $file;
    my $mode = shift;
    return unless defined $mode && $mode =~ /^[0-7]{1,4}$/;
    my @args = ("$mode");

    my $entry = $self->_find_entry($file) or return;
    my $x     = $entry->chmod(@args);
    return $x;
}


sub chown {
    my $self = shift;
    my $file = shift;
    return unless defined $file;
    my $uname = shift;
    return unless defined $uname;
    my @args = ($uname);
    push( @args, shift );

    my $entry = $self->_find_entry($file) or return;
    my $x     = $entry->chown(@args);
    return $x;
}


sub remove {
    my $self = shift;
    my @list = @_;

    my %seen = map { $_->full_path => $_ } @{ $self->_data };
    delete $seen{$_} for @list;

    $self->_data( [ values %seen ] );

    return values %seen;
}


sub clear {
    my $self = shift or return;

    $self->_data( [] );
    $self->_file('');

    return 1;
}


sub write {
    my $self = shift;
    my $file = shift;
    $file = '' unless defined $file;
    my $gzip       = shift || 0;
    my $ext_prefix = shift;
    $ext_prefix = '' unless defined $ext_prefix;
    my $dummy = '';

    my $handle =
      length($file) ? ( $self->_get_handle( $file, $gzip, WRITE_ONLY->($gzip) )
          or return )
      : $HAS_PERLIO    ? do { open my $h, '>', \$dummy; $h }
      : $HAS_IO_STRING ? IO::String->new
      :                  __PACKAGE__->no_string_support();

    local $\;

    for my $entry ( @{ $self->_data } ) {
        my @write_me;

        my $clone = $entry->clone;

        if ($DO_NOT_USE_PREFIX) {

            $clone->name(
                length $ext_prefix
                ? File::Spec::Unix->catdir( $ext_prefix, $clone->full_path )
                : $clone->full_path
            );
            $clone->prefix('');

        }
        else {

            my ( $prefix, $name ) =
              $clone->_prefix_and_file( $clone->full_path );

            $prefix = File::Spec::Unix->catdir( $ext_prefix, $prefix )
              if length $ext_prefix;

            $clone->prefix($prefix);
            $clone->name($name);
        }

        my $make_longlink = (
                 length( $clone->name ) > NAME_LENGTH
              or length( $clone->prefix ) > PREFIX_LENGTH
        ) || 0;

        if ($make_longlink) {
            my $longlink = Archive::Tar::File->new(
                data => LONGLINK_NAME,
                $clone->full_path,
                { type => LONGLINK }
            );

            unless ($longlink) {
                $self->_error( qq[Could not create 'LongLink' entry for ]
                      . qq[oversize file ']
                      . $clone->full_path
                      . "'" );
                return;
            }

            push @write_me, $longlink;
        }

        push @write_me, $clone;

        for my $clone (@write_me) {

            my $link_ok = $clone->is_symlink  && $Archive::Tar::FOLLOW_SYMLINK;
            my $data_ok = !$clone->is_symlink && $clone->has_content;

            $clone->_downgrade_to_plainfile if $link_ok;

            my $header = $self->_format_tar_entry($clone);
            unless ($header) {
                $self->_error(
                    q[Could not format header for: ] . $clone->full_path );
                return;
            }

            unless ( print $handle $header ) {
                $self->_error(
                    q[Could not write header for: ] . $clone->full_path );
                return;
            }

            if ( $link_ok or $data_ok ) {
                unless ( print $handle $clone->data ) {
                    $self->_error(
                        q[Could not write data for: ] . $clone->full_path );
                    return;
                }

                print $handle TAR_PAD->( $clone->size ) if $clone->size % BLOCK;
            }

        }
    }

    print $handle TAR_END x 2
      or return $self->_error(qq[Could not write tar end markers]);

    my $rv =
        length($file) ? 1
      : $HAS_PERLIO   ? $dummy
      :                 do { seek $handle, 0, 0; local $/; <$handle> };

    if ( $file ne $handle ) {
        unless ( close $handle ) {
            $self->_error(qq[Could not write tar]);
            return;
        }
    }

    return $rv;
}

sub _format_tar_entry {
    my $self       = shift;
    my $entry      = shift or return;
    my $ext_prefix = shift;
    $ext_prefix = '' unless defined $ext_prefix;
    my $no_prefix = shift || 0;

    my $file   = $entry->name;
    my $prefix = $entry->prefix;
    $prefix = '' unless defined $prefix;

    $prefix = File::Spec::Unix->catdir( $ext_prefix, $prefix )
      if length $ext_prefix;

    my $l = PREFIX_LENGTH;
    substr( $prefix, 0, -$l ) = "" if length $prefix >= PREFIX_LENGTH;

    my $f1 = "%06o";
    my $f2 = $ZERO_PAD_NUMBERS ? "%011o" : "%11o";

    my $tar = pack(
        PACK,
        $file,

        ( map { sprintf( $f1, $entry->$_() ) } qw[mode uid gid] ),
        ( map { sprintf( $f2, $entry->$_() ) } qw[size mtime] ),

        "",

        ( map { $entry->$_() } qw[type linkname magic] ),

        $entry->version || TAR_VERSION,

        ( map { $entry->$_() } qw[uname gname] ),
        ( map { sprintf( $f1, $entry->$_() ) } qw[devmajor devminor] ),

        ( $no_prefix ? '' : $prefix )
    );

    my $checksum_fmt = $ZERO_PAD_NUMBERS ? "%06o\0" : "%06o\0";
    substr( $tar, 148, 7 ) = sprintf( "%6o\0", unpack( "%16C*", $tar ) );

    return $tar;
}


sub add_files {
    my $self  = shift;
    my @files = @_ or return;

    my @rv;
    for my $file (@files) {

        if ( UNIVERSAL::isa( $file, 'Archive::Tar::File' ) ) {
            push @rv, $file->clone;
            next;
        }

        eval {
            if ( utf8::is_utf8($file) ) {
                utf8::encode($file);
            }
        };

        unless ( -e $file || -l $file ) {
            $self->_error(qq[No such file: '$file']);
            next;
        }

        my $obj = Archive::Tar::File->new( file => $file );
        unless ($obj) {
            $self->_error(qq[Unable to add file: '$file']);
            next;
        }

        push @rv, $obj;
    }

    push @{ $self->{_data} }, @rv;

    return @rv;
}


sub add_data {
    my $self = shift;
    my ( $file, $data, $opt ) = @_;

    my $obj = Archive::Tar::File->new( data => $file, $data, $opt );
    unless ($obj) {
        $self->_error(qq[Unable to add file: '$file']);
        return;
    }

    push @{ $self->{_data} }, $obj;

    return $obj;
}


{
    $error = '';
    my $longmess;

    sub _error {
        my $self = shift;
        my $msg  = $error = shift;
        $longmess = Carp::longmess($error);
        if ( ref $self ) {
            $self->{_error}    = $error;
            $self->{_longmess} = $longmess;
        }

        if ($WARN) {
            carp $DEBUG ? $longmess : $msg;
        }

        return;
    }

    sub error {
        my $self = shift;
        if ( ref $self ) {
            return shift() ? $self->{_longmess} : $self->{_error};
        }
        else {
            return shift() ? $longmess : $error;
        }
    }
}


sub setcwd {
    my $self = shift;
    my $cwd  = shift;

    $self->{cwd} = $cwd;
}


sub create_archive {
    my $class = shift;

    my $file = shift;
    return unless defined $file;
    my $gzip  = shift || 0;
    my @files = @_;

    unless (@files) {
        return $class->_error(qq[Cowardly refusing to create empty archive!]);
    }

    my $tar = $class->new;
    $tar->add_files(@files);
    return $tar->write( $file, $gzip );
}


sub iter {
    my $class    = shift;
    my $filename = shift;
    return unless defined $filename;
    my $compressed = shift || 0;
    my $opts       = shift || {};

    my $handle =
      $class->_get_handle( $filename, $compressed, READ_ONLY->(ZLIB) )
      or return;

    my @data;
    my $CONSTRUCT_ARGS = [ $filename, $compressed, $opts ];
    return sub {
        return shift(@data) if @data;
        return unless $handle;

        my $tarfile = $class->_read_tar( $handle, { %$opts, limit => 1 } );
        @data = @$tarfile if ref $tarfile && ref $tarfile eq 'ARRAY';
        if ( $Archive::Tar::RESOLVE_SYMLINK !~ /none/ ) {
            foreach (@data) {
                if ( $_->linkname ) {
                    $_->{'_archive'} = $CONSTRUCT_ARGS;
                }
            }
        }

        return shift(@data) if @data;

        undef $handle;
        if ( @$CONSTRUCT_ARGS == 4 ) {
            undef $CONSTRUCT_ARGS->[-1];
        }
        return;
    };
}


sub list_archive {
    my $class = shift;
    my $file  = shift;
    return unless defined $file;
    my $gzip = shift || 0;

    my $tar = $class->new( $file, $gzip );
    return unless $tar;

    return $tar->list_files(@_);
}


sub extract_archive {
    my $class = shift;
    my $file  = shift;
    return unless defined $file;
    my $gzip = shift || 0;

    my $tar = $class->new() or return;

    return $tar->read( $file, $gzip, { extract => 1 } );
}


sub has_io_string { return $HAS_IO_STRING; }


sub has_perlio { return $HAS_PERLIO; }


sub has_zlib_support { return ZLIB }


sub has_bzip2_support { return BZIP }


sub has_xz_support { return XZ }


sub can_handle_compressed_files { return ZLIB && BZIP ? 1 : 0 }

sub no_string_support {
    croak(
        "You have to install IO::String to support writing archives to strings"
    );
}

sub _symlinks_resolver {
    my ( $src, $trg ) = @_;
    my @src = split /[\/\\]/, $src;
    my @trg = split /[\/\\]/, $trg;
    pop @src;
    if ( @trg and $trg[0] eq '' ) {
        shift @trg;
        @src = ();
    }
    foreach my $part (@trg) {
        next if $part eq '.';
        if ( $part eq '..' ) {
            pop @src;
        }
        else {
            push @src, $part;
        }
    }
    my $path = join( '/', @src );
    warn "_symlinks_resolver('$src','$trg') = $path" if $DEBUG;
    return $path;
}

1;

__END__


