package Archive::Tar::File;
use strict;

use Carp ();
use IO::File;
use File::Spec::Unix ();
use File::Spec       ();
use File::Basename   ();

use Archive::Tar::Constant;

use vars qw[@ISA $VERSION];
$VERSION = '3.04';

my $tmpl = [
    name     => 0,
    mode     => 1,
    uid      => 1,
    gid      => 1,
    size     => 0,
    mtime    => 1,
    chksum   => 1,
    type     => 0,
    linkname => 0,
    magic    => 0,
    version  => 0,
    uname    => 0,
    gname    => 0,
    devmajor => 1,
    devminor => 1,
    prefix   => 0,

    raw  => 0,
    data => 0,

];

for ( my $i = 0 ; $i < scalar @$tmpl ; $i += 2 ) {
    my $key = $tmpl->[$i];
    no strict 'refs';
    *{ __PACKAGE__ . "::$key" } = sub {
        my $self = shift;
        $self->{$key} = $_[0] if @_;

        {
            local $^W = 0;
            return $self->{$key};
        }
    }
}


sub new {
    my $class = shift;
    my $what  = shift;

    my $obj =
        ( $what eq 'chunk' ) ? __PACKAGE__->_new_from_chunk(@_)
      : ( $what eq 'file' )  ? __PACKAGE__->_new_from_file(@_)
      : ( $what eq 'data' )  ? __PACKAGE__->_new_from_data(@_)
      :                        undef;

    return $obj;
}

sub clone {
    my $self = shift;
    return bless {%$self}, ref $self;
}

sub _new_from_chunk {
    my $class = shift;
    my $chunk = shift or return;
    my %hash  = @_;

    my %args = map { $_ => $hash{$_} } grep { defined $hash{$_} } keys %hash;

    my $i     = -1;
    my %entry = map {
        my ( $s, $v ) = ( $tmpl->[ ++$i ], $tmpl->[ ++$i ] );
        ($_) = ( $_ =~ /^([^\0]*)/ ) unless ( $s eq 'size' );
        $s => $v ? oct $_ : $_

    } unpack( UNPACK, $chunk );

    if ( substr( $entry{'size'}, 0, 1 ) eq "\x80" ) {
        my @sz = unpack( "aCSNN", $entry{'size'} );
        $entry{'size'} = $sz[4] + ( 2**32 ) * $sz[3] + $sz[2] * ( 2**64 );
    }
    else {
        ( $entry{'size'} ) = ( $entry{'size'} =~ /^([^\0]*)/ );
        $entry{'size'} = oct $entry{'size'};
    }

    my $obj = bless { %entry, %args }, $class;

    return unless $obj->magic !~ /\W/;

    $obj->raw($chunk);

    $obj->type(FILE) if ( ( !length $obj->type ) or ( $obj->type =~ /\W/ ) );
    $obj->type(DIR)  if ( ( $obj->is_file ) && ( $obj->name =~ m|/$| ) );

    return $obj;

}

sub _new_from_file {
    my $class = shift;
    my $path  = shift;

    return unless defined $path;

    my $type = __PACKAGE__->_filetype($path);
    my $data = '';

  READ: {
        unless ( $type == DIR ) {
            my $fh = IO::File->new;

            unless ( $fh->open( $path, 'r' ) ) {
                last READ if $type == SYMLINK;

                return;
            }

            binmode $fh;
            $data = do { local $/; <$fh> };
            close $fh;
        }
    }

    my @items = qw[mode uid gid size mtime];
    my %hash  = map { shift(@items), $_ } ( lstat $path )[ 2, 4, 5, 7, 9 ];

    if (ON_VMS) {

        if ( $hash{uid} > 0x10000 ) {
            $hash{uid} = $hash{uid} & 0xFFFF;
        }

        my $data_len = length $data;
        $hash{size} = $data_len if $hash{size} < $data_len;

    }
    $hash{size} = 0 if ( $type == DIR or $type == SYMLINK );
    $hash{mtime} -= TIME_OFFSET;

    $hash{mode} = STRIP_MODE->( $hash{mode} );

    my $obj = {
        %hash,
        name     => '',
        chksum   => CHECK_SUM,
        type     => $type,
        linkname => ( $type == SYMLINK and CAN_READLINK )
        ? readlink $path
        : '',
        magic    => MAGIC,
        version  => TAR_VERSION,
        uname    => UNAME->( $hash{uid} ),
        gname    => GNAME->( $hash{gid} ),
        devmajor => 0,
        devminor => 0,
        prefix   => '',
        data     => $data,
    };

    bless $obj, $class;

    my ( $prefix, $file ) = $obj->_prefix_and_file($path);
    $obj->prefix($prefix);
    $obj->name($file);

    return $obj;
}

sub _new_from_data {
    my $class = shift;
    my $path  = shift;
    return unless defined $path;
    my $data = shift;
    return unless defined $data;
    my $opt = shift;

    my $obj = {
        data     => $data,
        name     => '',
        mode     => MODE,
        uid      => UID,
        gid      => GID,
        size     => length $data,
        mtime    => time - TIME_OFFSET,
        chksum   => CHECK_SUM,
        type     => FILE,
        linkname => '',
        magic    => MAGIC,
        version  => TAR_VERSION,
        uname    => UNAME->(UID),
        gname    => GNAME->(GID),
        devminor => 0,
        devmajor => 0,
        prefix   => '',
    };

    if ( $opt and ref $opt eq 'HASH' ) {
        for my $key ( keys %$opt ) {

            next unless exists $obj->{$key};
            $obj->{$key} = $opt->{$key};
        }
    }

    bless $obj, $class;

    my ( $prefix, $file ) = $obj->_prefix_and_file($path);
    $obj->prefix($prefix);
    $obj->name($file);

    return $obj;
}

sub _prefix_and_file {
    my $self = shift;
    my $path = shift;

    my ( $vol, $dirs, $file ) = File::Spec->splitpath( $path, $self->is_dir );
    my @dirs = File::Spec->splitdir( File::Spec->canonpath($dirs) );

    $file = pop @dirs if $self->is_dir and not length $file;

    if (ON_VMS) {
        map { $_ = '..' if $_ eq '-'; $_ = '' if $_ eq '000000' } @dirs;
        if ( length($vol) ) {
            $vol = VMS::Filespec::unixify($vol);
            unshift @dirs, $vol;
        }
    }

    my $prefix = File::Spec::Unix->catdir(@dirs);
    return ( $prefix, $file );
}

sub _filetype {
    my $self = shift;
    my $file = shift;

    return unless defined $file;

    return SYMLINK if ( -l $file );

    return FILE if ( -f _ );

    return DIR if ( -d _ );

    return FIFO if ( -p _ );

    return SOCKET if ( -S _ );

    return BLOCKDEV if ( -b _ );

    return CHARDEV if ( -c _ );

    return LONGLINK if ( $file eq LONGLINK_NAME );

    return UNKNOWN;

}

sub _downgrade_to_plainfile {
    my $entry = shift;
    $entry->type(FILE);
    $entry->mode(MODE);
    $entry->linkname('');

    return 1;
}


sub extract {
    my $self = shift;

    local $Carp::CarpLevel += 1;

    require Archive::Tar;
    return Archive::Tar->_extract_file( $self, @_ );
}


sub full_path {
    my $self = shift;

    return $self->name unless defined $self->prefix and length $self->prefix;

    my $path = File::Spec::Unix->catfile( $self->prefix, $self->name );
    $path .= "/" if $self->name =~ m{/$};
    return $path;
}


sub validate {
    my $self = shift;

    my $raw = $self->raw;

    substr( $raw, 148, 8 ) = "        ";

    return (
             ( unpack( "%16C*", $raw ) == $self->chksum )
          or ( unpack( "%16c*", $raw ) == $self->chksum )
    ) ? 1 : 0;
}


sub has_content {
    my $self = shift;
    return defined $self->data() && length $self->data() ? 1 : 0;
}


sub get_content {
    my $self = shift;
    $self->data();
}


sub get_content_by_ref {
    my $self = shift;

    return \$self->{data};
}


sub replace_content {
    my $self = shift;
    my $data = shift || '';

    $self->data($data);
    $self->size( length $data );
    return 1;
}


sub rename {
    my $self = shift;
    my $path = shift;

    return unless defined $path;

    my ( $prefix, $file ) = $self->_prefix_and_file($path);

    $self->name($file);
    $self->prefix($prefix);

    return 1;
}


sub chmod {
    my $self = shift;
    my $mode = shift;
    return unless defined $mode && $mode =~ /^[0-7]{1,4}$/;
    $self->{mode} = oct($mode);
    return 1;
}


sub chown {
    my $self  = shift;
    my $uname = shift;
    return unless defined $uname;
    my $gname;
    if ( -1 != index( $uname, ':' ) ) {
        ( $uname, $gname ) = split( /:/, $uname );
    }
    else {
        $gname = shift if @_ > 0;
    }

    $self->uname($uname);
    $self->gname($gname) if $gname;
    return 1;
}


sub is_file     { local $^W; FILE == $_[0]->type }
sub is_dir      { local $^W; DIR == $_[0]->type }
sub is_hardlink { local $^W; HARDLINK == $_[0]->type }
sub is_symlink  { local $^W; SYMLINK == $_[0]->type }
sub is_chardev  { local $^W; CHARDEV == $_[0]->type }
sub is_blockdev { local $^W; BLOCKDEV == $_[0]->type }
sub is_fifo     { local $^W; FIFO == $_[0]->type }
sub is_socket   { local $^W; SOCKET == $_[0]->type }
sub is_unknown  { local $^W; UNKNOWN == $_[0]->type }
sub is_longlink { local $^W; LONGLINK eq $_[0]->type }
sub is_label    { local $^W; LABEL eq $_[0]->type }

1;
