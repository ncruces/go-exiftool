package Locale::Maketext;
use strict;
our $USE_LITERALS;
use Carp                   ();
use I18N::LangTags         ();
use I18N::LangTags::Detect ();

BEGIN {
    unless ( defined &DEBUG ) {
        *DEBUG = sub () { 0 }
    }
}

BEGIN {

    if ( exists $INC{'utf8.pm'}
        || eval { local $SIG{'__DIE__'}; require utf8; } )
    {
        utf8->import();
        DEBUG and warn " utf8 on for _compile()\n";
    }
    else {
        DEBUG
          and warn " utf8 not available for _compile() ($INC{'utf8.pm'})\n$@\n";
    }
}

our $VERSION = '1.33';
our @ISA     = ();

our $MATCH_SUPERS         = 1;
our $MATCH_SUPERS_TIGHTLY = 1;
our $USING_LANGUAGE_TAGS  = 1;

$USE_LITERALS = 1 unless defined $USE_LITERALS;

my %isa_scan = ();

sub quant {
    my ( $handle, $num, @forms ) = @_;

    return $num      if @forms == 0;
    return $forms[2] if @forms > 2 and $num == 0;

    return ( $handle->numf($num) . ' ' . $handle->numerate( $num, @forms ) );
}

sub numerate {
    my ( $handle, $num, @forms ) = @_;
    my $s = ( $num == 1 );

    return '' unless @forms;
    if ( @forms == 1 ) {
        return $s ? $forms[0] : ( $forms[0] . 's' );
    }
    else {
        return $s ? $forms[0] : $forms[1];
    }
}

sub numf {
    my ( $handle, $num ) = @_[ 0, 1 ];
    if (    $num < 10_000_000_000
        and $num > -10_000_000_000
        and $num == int($num) )
    {
        $num += 0;

    }
    else {
        $num = CORE::sprintf( '%G', $num );
    }
    while ( $num =~ s/^([-+]?\d+)(\d{3})/$1,$2/s ) { 1 }

    $num =~ tr<.,><,.> if ref($handle) and $handle->{'numf_comma'};
    return $num;
}

sub sprintf {
    no integer;
    my ( $handle, $format, @params ) = @_;
    return CORE::sprintf( $format, @params );
}

use integer;

sub language_tag {
    my $it = ref( $_[0] ) || $_[0];
    return undef unless $it =~ m/([^':]+)(?:::)?$/s;
    $it = lc($1);
    $it =~ tr<_><->;
    return $it;
}

sub encoding {
    my $it = $_[0];
    return ( ( ref($it) && $it->{'encoding'} ) || 'iso-8859-1' );
}

sub fallback_languages { return ( 'i-default', 'en', 'en-US' ) }

sub fallback_language_classes { return () }

sub fail_with {
    my ( $handle, @params ) = @_;
    return unless ref($handle);
    $handle->{'fail'} = $params[0] if @params;
    return $handle->{'fail'};
}

sub _exclude {
    my ( $handle, @methods ) = @_;

    unless ( defined $handle->{'denylist'} ) {
        no strict 'refs';

        $handle->{'denylist'} = {
            map { $_ => 1 } (
                qw/
                  blacklist
                  denylist
                  encoding
                  fail_with
                  failure_handler_auto
                  fallback_language_classes
                  fallback_languages
                  get_handle
                  init
                  language_tag
                  maketext
                  new
                  whitelist
                  allowlist
                  /, grep { /^_/ } keys %{ __PACKAGE__ . "::" }
            ),
        };
    }

    if ( scalar @methods ) {
        $handle->{'denylist'} =
          { %{ $handle->{'denylist'} }, map { $_ => 1 } @methods };
    }

    delete $handle->{'_external_lex_cache'};
    return;
}

sub blacklist {
    my ( $handle, @methods ) = @_;
    _exclude( $handle, @methods );
    return;
}

sub denylist {
    my ( $handle, @methods ) = @_;
    _exclude( $handle, @methods );
    return;
}

sub _include {
    my ( $handle, @methods ) = @_;
    if ( scalar @methods ) {
        $handle->{'allowlist'} = {} unless defined $handle->{'allowlist'};
        $handle->{'allowlist'} =
          { %{ $handle->{'allowlist'} }, map { $_ => 1 } @methods };
    }

    delete $handle->{'_external_lex_cache'};
    return;
}

sub whitelist {
    my ( $handle, @methods ) = @_;
    _include( $handle, @methods );
    return;
}

sub allowlist {
    my ( $handle, @methods ) = @_;
    _include( $handle, @methods );
    return;
}

sub failure_handler_auto {

    my $handle = shift;
    my $phrase = shift;

    $handle->{'failure_lex'} ||= {};
    my $lex = $handle->{'failure_lex'};

    my $value ||= ( $lex->{$phrase} ||= $handle->_compile($phrase) );

    return ${$value} if ref($value) eq 'SCALAR';
    return $value    if ref($value) ne 'CODE';
    {
        local $SIG{'__DIE__'};
        eval { $value = &$value( $handle, @_ ) };
    }
    if ($@) {
        $@ =~ s{\s+at\s+\(eval\s+\d+\)\s+line\s+(\d+)\.?\n?}
                 {\n in bracket code [compiled line $1],}s;
        Carp::croak "Error in maketexting \"$phrase\":\n$@ as used";
    }
    else {
        return $value;
    }
}

sub new {
    my $class  = ref( $_[0] ) || $_[0];
    my $handle = bless {}, $class;
    $handle->blacklist;
    $handle->denylist;
    $handle->init;
    return $handle;
}

sub init { return }

sub maketext {
    Carp::croak 'maketext requires at least one parameter' unless @_ > 1;

    my ( $handle, $phrase ) = splice( @_, 0, 2 );
    Carp::confess('No handle/phrase')
      unless ( defined($handle) && defined($phrase) );

    my $at = $@;

    @_ = @_;

    my $value;
    if ( exists $handle->{'_external_lex_cache'}{$phrase} ) {
        DEBUG and warn "* Using external lex cache version of \"$phrase\"\n";
        $value = $handle->{'_external_lex_cache'}{$phrase};
    }
    else {
        foreach my $h_r (
            @{ $isa_scan{ ref($handle) || $handle } || $handle->_lex_refs } )
        {
            DEBUG and warn "* Looking up \"$phrase\" in $h_r\n";
            if ( exists $h_r->{$phrase} ) {
                DEBUG and warn "  Found \"$phrase\" in $h_r\n";
                unless ( ref( $value = $h_r->{$phrase} ) ) {
                    if ( $handle->{'use_external_lex_cache'} ) {
                        $value = $handle->{'_external_lex_cache'}{$phrase} =
                          $handle->_compile($value);
                    }
                    else {
                        $value = $h_r->{$phrase} = $handle->_compile($value);
                    }
                }
                last;
            }
            elsif (
                $phrase !~ m/^_/s
                and (
                    $handle->{'use_external_lex_cache'}
                    ? (
                        exists $handle->{'_external_lex_cache'}{'_AUTO'}
                        ? $handle->{'_external_lex_cache'}{'_AUTO'}
                        : $h_r->{'_AUTO'} )
                    : $h_r->{'_AUTO'}
                )
              )
            {
                DEBUG and warn "  Automaking \"$phrase\" into $h_r\n";
                if ( $handle->{'use_external_lex_cache'} ) {
                    $value = $handle->{'_external_lex_cache'}{$phrase} =
                      $handle->_compile($phrase);
                }
                else {
                    $value = $h_r->{$phrase} = $handle->_compile($phrase);
                }
                last;
            }
            DEBUG > 1 and print "  Not found in $h_r, nor automakable\n";
        }
    }

    unless ( defined($value) ) {
        DEBUG
          and warn "! Lookup of \"$phrase\" in/under ",
          ref($handle) || $handle, " fails.\n";
        if ( ref($handle) and $handle->{'fail'} ) {
            DEBUG and warn "WARNING0: maketext fails looking for <$phrase>\n";
            my $fail;
            if ( ref( $fail = $handle->{'fail'} ) eq 'CODE' ) {
                $@ = $at;
                return &{$fail}( $handle, $phrase, @_ );
            }
            else {
                $@ = $at;
                return $handle->$fail( $phrase, @_ );
            }
        }
        else {
            Carp::croak(
                "maketext doesn't know how to say:\n$phrase\nas needed");
        }
    }

    if ( ref($value) eq 'SCALAR' ) {
        $@ = $at;
        return $$value;
    }
    if ( ref($value) ne 'CODE' ) {
        $@ = $at;
        return $value;
    }

    {
        local $SIG{'__DIE__'};
        eval { $value = &$value( $handle, @_ ) };
    }
    if ($@) {
        $@ =~ s{\s+at\s+\(eval\s+\d+\)\s+line\s+(\d+)\.?\n?}
                 {\n in bracket code [compiled line $1],}s;
        Carp::croak "Error in maketexting \"$phrase\":\n$@ as used";
    }
    else {
        $@ = $at;
        return $value;
    }
    $@ = $at;
}

sub get_handle {

    my ( $base_class, @languages ) = @_;
    $base_class = ref($base_class) || $base_class;

    if (@languages) {
        DEBUG and warn 'Lgs@', __LINE__, ': ', map( "<$_>", @languages ), "\n";
        if ($USING_LANGUAGE_TAGS) {
            @languages =
              map { ; $_, I18N::LangTags::alternate_language_tags($_) }
              map I18N::LangTags::locale2language_tag($_),
              @languages;
            DEBUG
              and warn 'Lgs@', __LINE__, ': ', map( "<$_>", @languages ), "\n";
        }
    }
    else {
        @languages = $base_class->_ambient_langprefs;
    }

    @languages = $base_class->_langtag_munging(@languages);

    my %seen;
    foreach my $module_name ( map { $base_class . '::' . $_ } @languages ) {
        next unless length $module_name;
        next
          if $seen{$module_name}++
          || !&_try_use($module_name);
        return ( $module_name->new );
    }

    return undef;
}

sub _langtag_munging {
    my ( $base_class, @languages ) = @_;

    DEBUG and warn 'Lgs1: ', map( "<$_>", @languages ), "\n";

    if ($USING_LANGUAGE_TAGS) {
        DEBUG and warn 'Lgs@', __LINE__, ': ', map( "<$_>", @languages ), "\n";
        @languages = $base_class->_add_supers(@languages);

        push @languages, I18N::LangTags::panic_languages(@languages);
        DEBUG and warn "After adding panic languages:\n",
          ' Lgs@', __LINE__, ': ', map( "<$_>", @languages ), "\n";

        push @languages, $base_class->fallback_languages;
        DEBUG and warn 'Lgs@', __LINE__, ': ', map( "<$_>", @languages ), "\n";

        @languages =
          map {
            my $it = $_;
            $it =~ tr<-A-Z><_a-z>;
            $it =~ tr<_a-z0-9><>cd;
            $it;
          } @languages;
        DEBUG and warn "Nearing end of munging:\n",
          ' Lgs@', __LINE__, ': ', map( "<$_>", @languages ), "\n";
    }
    else {
        DEBUG and warn "Bypassing language-tags.\n",
          ' Lgs@', __LINE__, ': ', map( "<$_>", @languages ), "\n";
    }

    DEBUG and warn "Before adding fallback classes:\n",
      ' Lgs@', __LINE__, ': ', map( "<$_>", @languages ), "\n";

    push @languages, $base_class->fallback_language_classes;

    DEBUG and warn "Finally:\n",
      ' Lgs@', __LINE__, ': ', map( "<$_>", @languages ), "\n";

    return @languages;
}

sub _ambient_langprefs {
    return I18N::LangTags::Detect::detect();
}

sub _add_supers {
    my ( $base_class, @languages ) = @_;

    if ( !$MATCH_SUPERS ) {
        DEBUG and warn "Bypassing any super-matching.\n",
          ' Lgs@', __LINE__, ': ', map( "<$_>", @languages ), "\n";

    }
    elsif ($MATCH_SUPERS_TIGHTLY) {
        DEBUG and warn "Before adding new supers tightly:\n",
          ' Lgs@', __LINE__, ': ', map( "<$_>", @languages ), "\n";
        @languages = I18N::LangTags::implicate_supers(@languages);
        DEBUG and warn "After adding new supers tightly:\n",
          ' Lgs@', __LINE__, ': ', map( "<$_>", @languages ), "\n";

    }
    else {
        DEBUG and warn "Before adding supers to end:\n",
          ' Lgs@', __LINE__, ': ', map( "<$_>", @languages ), "\n";
        @languages = I18N::LangTags::implicate_supers_strictly(@languages);
        DEBUG and warn "After adding supers to end:\n",
          ' Lgs@', __LINE__, ': ', map( "<$_>", @languages ), "\n";
    }

    return @languages;
}

my %tried = ();

sub _try_use {

    return $tried{ $_[0] } if exists $tried{ $_[0] };

    my $module = $_[0];
    {
        no strict 'refs';
        no warnings 'once';
        return ( $tried{$module} = 1 )
          if %{ $module . '::Lexicon' }
          or @{ $module . '::ISA' };
    }

    DEBUG and warn " About to use $module ...\n";

    local $SIG{'__DIE__'};
    local $@;
    local @INC = @INC;
    pop @INC if $INC[-1] eq '.';
    eval "require $module";

    if ($@) {
        DEBUG and warn "Error using $module \: $@\n";
        return $tried{$module} = 0;
    }
    else {
        DEBUG and warn " OK, $module is used\n";
        return $tried{$module} = 1;
    }
}

sub _lex_refs {

    no strict 'refs';
    no warnings 'once';
    my $class = ref( $_[0] ) || $_[0];
    DEBUG and warn "Lex refs lookup on $class\n";
    return $isa_scan{$class} if exists $isa_scan{$class};

    my @lex_refs;
    my $seen_r = ref( $_[1] ) ? $_[1] : {};

    if ( defined( *{ $class . '::Lexicon' }{'HASH'} ) ) {
        push @lex_refs, *{ $class . '::Lexicon' }{'HASH'};
        DEBUG and warn '%' . $class . '::Lexicon contains ',
          scalar( keys %{ $class . '::Lexicon' } ), " entries\n";
    }

    foreach my $superclass ( @{ $class . '::ISA' } ) {
        DEBUG and warn " Super-class search into $superclass\n";
        next if $seen_r->{$superclass}++;
        push @lex_refs, @{ &_lex_refs( $superclass, $seen_r ) };
    }

    $isa_scan{$class} = \@lex_refs;
    return \@lex_refs;
}

sub clear_isa_scan { %isa_scan = (); return; }

sub _compile {

    my $string_to_compile = $_[1];

    return \"$string_to_compile" if ( $string_to_compile !~ m/[\[~\]]/ms );

    my $handle = $_[0];

    my (@code);
    my (@c)        = ('');
    my $call_count = 0;
    my $big_pile   = '';
    {
        my $in_group = 0;
        my ( $m, @params );

        while (
            $string_to_compile =~ m/(
                [^\~\[\]]+  # non-~[] stuff (Capture everything else here)
                |
                ~.       # ~[, ~], ~~, ~other
                |
                \[          # [ presumably opening a group
                |
                \]          # ] presumably closing a group
                |
                ~           # terminal ~ ?
                |
                $
            )/xgs
          )
        {
            DEBUG > 2 and warn qq{  "$1"\n};

            if ( $1 eq '[' or $1 eq '' ) {

                if ($in_group) {
                    if ( $1 eq '' ) {
                        $handle->_die_pointing( $string_to_compile,
                            'Unterminated bracket group' );
                    }
                    else {
                        $handle->_die_pointing( $string_to_compile,
                            'You can\'t nest bracket groups' );
                    }
                }
                else {
                    if ( $1 eq '' ) {
                        DEBUG > 2 and warn "   [end-string]\n";
                    }
                    else {
                        $in_group = 1;
                    }
                    die "How come \@c is empty?? in <$string_to_compile>"
                      unless @c;
                    if ( length $c[-1] ) {
                        $big_pile .= $c[-1];
                        if (
                            $USE_LITERALS and (
                                ( ord('A') == 65 )
                                ? $c[-1] !~ m/[^\x20-\x7E]/s
                                : $c[-1] !~
m/[^ !"\#\$%&'()*+,\-.\/0-9:;<=>?\@A-Z[\\\]^_`a-z{|}~\x07]/s
                            )
                          )
                        {
                            $c[-1] =~ s/'/\\'/g;
                            push @code, q{ '} . $c[-1] . "',\n";
                            $c[-1] = '';
                        }
                        else {
                            $c[-1] =~ s/\\\\/\\/g;
                            push @code, ' $c[' . $#c . "],\n";
                            push @c,    '';
                        }
                    }
                }

            }
            elsif ( $1 eq ']' ) {

                if ($in_group) {
                    $in_group = 0;

                    DEBUG > 2 and warn "   --Closing group [$c[-1]]\n";

                    if ( !length( $c[-1] ) or $c[-1] =~ m/^\s+$/s ) {
                        DEBUG > 2 and warn "   -- (Ignoring)\n";
                        $c[-1] = '';
                        next;
                    }

                    ( $m, @params ) = split( /,/, $c[-1], -1 );

                    if ( ord('A') == 65 ) {
                        foreach ( $m, @params ) { tr/\x7F/,/ }
                    }
                    else {

                        foreach ( $m, @params ) { tr/\x07/,/ }
                    }

                    if ( $m eq '_*' or $m =~ m/^_(-?\d+)$/s ) {
                        unshift @params, $m;
                        $m = '';
                    }
                    elsif ( $m eq '*' ) {
                        $m = 'quant';
                    }
                    elsif ( $m eq '#' ) {
                        $m = 'numf';
                    }

                    if ( $m eq '' ) {
                        push @code, ' (';
                    }
                    elsif (
                           $m =~ /^\w+$/s
                        && !$handle->{'blacklist'}{$m}
                        && !$handle->{'denylist'}{$m}
                        && ( !defined $handle->{'whitelist'}
                            || $handle->{'whitelist'}{$m} )
                        && ( !defined $handle->{'allowlist'}
                            || $handle->{'allowlist'}{$m} )
                      )
                    {
                        push @code, ' $_[0]->' . $m . '(';
                    }
                    else {
                        $handle->_die_pointing(
                            $string_to_compile,
"Can't use \"$m\" as a method name in bracket group",
                            2 + length( $c[-1] )
                        );
                    }

                    pop @c;
                    ++$call_count;

                    foreach my $p (@params) {
                        if ( $p eq '_*' ) {
                            $code[-1] .= ' @_[1 .. $#_], ';
                        }
                        elsif ( $p =~ m/^_(-?\d+)$/s ) {
                            $code[-1] .= '$_[' . ( 0 + $1 ) . '], ';
                        }
                        elsif (
                            $USE_LITERALS and (
                                ( ord('A') == 65 )
                                ? $p !~ m/[^\x20-\x7E]/s
                                : $p !~
m/[^ !"\#\$%&'()*+,\-.\/0-9:;<=>?\@A-Z[\\\]^_`a-z{|}~\x07]/s
                            )
                          )
                        {
                            $p =~ s/'/\\'/g;
                            $code[-1] .= q{'} . $p . q{', };
                        }
                        else {
                            push @c,    $p;
                            push @code, ' $c[' . $#c . '], ';
                        }
                    }
                    $code[-1] .= "),\n";

                    push @c, '';
                }
                else {
                    $handle->_die_pointing( $string_to_compile,
                        q{Unbalanced ']'} );
                }

            }
            elsif ( substr( $1, 0, 1 ) ne '~' ) {
                my $text = $1;
                $text =~ s/\\/\\\\/g;
                $c[-1] .= $text;

            }
            elsif ( $1 eq '~~' ) {
                $c[-1] .= '~';

            }
            elsif ( $1 eq '~[' ) {
                $c[-1] .= '[';

            }
            elsif ( $1 eq '~]' ) {
                $c[-1] .= ']';

            }
            elsif ( $1 eq '~,' ) {
                if ($in_group) {
                    if ( ord('A') == 65 ) {
                        $c[-1] .= "\x7F";
                    }
                    else {
                        $c[-1] .= "\x07";
                    }
                }
                else {
                    $c[-1] .= '~,';
                }

            }
            elsif ( $1 eq '~' ) {
                $c[-1] .= '~';

            }
            else {
                my $text = $1;
                $text =~ s/\\/\\\\/g;
                $c[-1] .= $text;
            }
        }
    }

    if ($call_count) {
        undef $big_pile;
    }
    else {
        return \$big_pile;
    }

    die q{Last chunk isn't null??} if @c and length $c[-1];
    DEBUG and warn scalar(@c), " chunks under closure\n";
    if ( @code == 0 ) {
        DEBUG and warn "Empty code\n";
        return \'';
    }
    elsif ( @code > 1 ) {
        unshift @code, "join '',\n";
    }
    unshift @code, "use strict; sub {\n";
    push @code, "}\n";

    DEBUG and warn @code;
    my $sub = eval( join '', @code );
    die "$@ while evalling" . join( '', @code ) if $@;
    return $sub;
}

sub _die_pointing {
    my $target = shift;
    $target = ref($target) || $target;

    my $i = index( $_[0], "\n" );

    my $pointy;
    my $pos = pos( $_[0] ) - ( defined( $_[2] ) ? $_[2] : 0 ) - 1;
    if ( $pos < 1 ) {
        $pointy = "^=== near there\n";
    }
    else {
        my $first_tab = index( $_[0], "\t" );
        if ( $pos > 2 and ( -1 == $first_tab or $first_tab > pos( $_[0] ) ) ) {
            $pointy = ( '=' x $pos ) . "^ near there\n";
        }
        else {
            $pointy = substr( $_[0], 0, $pos );
            $pointy =~ tr/\t //cd;
            $pointy .= "^=== near there\n";
        }
    }

    my $errmsg = "$_[1], in\:\n$_[0]";

    if ( $i == -1 ) {
        $errmsg .= "\n" . $pointy;
    }
    elsif ( $i == ( length( $_[0] ) - 1 ) ) {
        $errmsg .= $pointy;
    }
    else {
    }
    Carp::croak("$errmsg via $target, as used");
}

1;
