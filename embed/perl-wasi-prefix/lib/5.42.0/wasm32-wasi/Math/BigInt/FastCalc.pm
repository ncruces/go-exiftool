package Math::BigInt::FastCalc;

use 5.006001;
use strict;
use warnings;

use Carp qw< carp croak >;

use Math::BigInt::Calc 1.999801;

BEGIN {
    our @ISA = qw< Math::BigInt::Calc >;
}

our $VERSION = '0.5020';

my $MAX_EXP_F;
my $MAX_EXP_I;
my $BASE_LEN;
my $USE_INT;

sub _base_len {
    my $class = shift;

    if (@_) {
        my ( $base_len, $use_int ) = @_;

        croak "The base length must be a positive integer"
          unless defined($base_len)
          && $base_len == int($base_len)
          && $base_len > 0;

        if ( $use_int && ( $base_len > $MAX_EXP_I )
            || !$use_int && ( $base_len > $MAX_EXP_F ) )
        {
            croak "The maximum base length (exponent) is $MAX_EXP_I with",
              " 'use integer' and $MAX_EXP_F without 'use integer'. The",
              " requested settings, a base length of $base_len ",
              $use_int ? "with" : "without", " 'use integer', is invalid.";
        }

        return $class->SUPER::_base_len( $base_len, $use_int );
    }

    return $class->SUPER::_base_len();
}

BEGIN {

    my @params = Math::BigInt::FastCalc->SUPER::_base_len();
    $BASE_LEN  = $params[0];
    $MAX_EXP_F = $params[8];
    $MAX_EXP_I = $params[9];

    require Config;
    my $max_exp_i = int( 8 * $Config::Config{uvsize} * log(2) / log(10) );
    $MAX_EXP_I = $max_exp_i if $max_exp_i < $MAX_EXP_I;
    $MAX_EXP_F = $MAX_EXP_I if $MAX_EXP_I < $MAX_EXP_F;

    ( $BASE_LEN, $USE_INT ) =
      $MAX_EXP_I > $MAX_EXP_F
      ? ( $MAX_EXP_I, 1 )
      : ( $MAX_EXP_F, 0 );

    Math::BigInt::FastCalc->SUPER::_base_len( $BASE_LEN, $USE_INT );
}

sub api_version () { 2; }

require XSLoader;
XSLoader::load( __PACKAGE__, $VERSION, Math::BigInt::Calc->_base_len() );

1;

__END__

