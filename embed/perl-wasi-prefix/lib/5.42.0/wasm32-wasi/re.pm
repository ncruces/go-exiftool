package re;

use strict;
use warnings;

our $VERSION   = "0.48";
our @ISA       = qw(Exporter);
our @EXPORT_OK = qw{
  is_regexp regexp_pattern
  regname regnames regnames_count
  regmust optimization
};
our %EXPORT_OK = map { $_ => 1 } @EXPORT_OK;

my %bitmask = (
    taint => 0x00100000,
    eval  => 0x00200000,
);

my $flags_hint  = 0x02000000;
my $PMMOD_SHIFT = 0;
my %reflags     = (
    m      => 1 << ( $PMMOD_SHIFT + 0 ),
    s      => 1 << ( $PMMOD_SHIFT + 1 ),
    i      => 1 << ( $PMMOD_SHIFT + 2 ),
    x      => 1 << ( $PMMOD_SHIFT + 3 ),
    xx     => 1 << ( $PMMOD_SHIFT + 4 ),
    n      => 1 << ( $PMMOD_SHIFT + 5 ),
    p      => 1 << ( $PMMOD_SHIFT + 6 ),
    strict => 1 << ( $PMMOD_SHIFT + 10 ),
    d  => 0,
    l  => 1,
    u  => 2,
    a  => 3,
    aa => 4,
);

sub setcolor {
    eval {
        require Term::Cap;

        my $terminal = Tgetent Term::Cap( { OSPEED => 9600 } );
        my $props    = $ENV{PERL_RE_TC} || 'md,me,so,se,us,ue';
        my @props    = split /,/, $props;
        my $colors   = join "\t", map { $terminal->Tputs( $_, 1 ) } @props;

        $colors =~ s/\0//g;
        $ENV{PERL_RE_COLORS} = $colors;
    };
    if ($@) {
        $ENV{PERL_RE_COLORS} ||= qq'\t\t> <\t> <\t\t';
    }

}

my %flags = (
    COMPILE  => 0x0000FF,
    PARSE    => 0x000001,
    OPTIMISE => 0x000002,
    TRIEC    => 0x000004,
    DUMP     => 0x000008,
    FLAGS    => 0x000010,
    TEST     => 0x000020,

    EXECUTE => 0x00FF00,
    INTUIT  => 0x000100,
    MATCH   => 0x000200,
    TRIEE   => 0x000400,

    EXTRA             => 0x3FF0000,
    TRIEM             => 0x0010000,
    STATE             => 0x0080000,
    OPTIMISEM         => 0x0100000,
    STACK             => 0x0280000,
    BUFFERS           => 0x0400000,
    GPOS              => 0x0800000,
    DUMP_PRE_OPTIMIZE => 0x1000000,
    WILDCARD          => 0x2000000,
);
$flags{ALL} =
  -1 & ~( $flags{BUFFERS} | $flags{DUMP_PRE_OPTIMIZE} | $flags{WILDCARD} );
$flags{All}   = $flags{all} = $flags{DUMP} | $flags{EXECUTE};
$flags{Extra} = $flags{EXECUTE} | $flags{COMPILE} | $flags{GPOS};
$flags{More}  = $flags{MORE} =
  $flags{All} | $flags{TRIEC} | $flags{TRIEM} | $flags{STATE};
$flags{State} = $flags{DUMP} | $flags{EXECUTE} | $flags{STATE};
$flags{TRIE}  = $flags{DUMP} | $flags{EXECUTE} | $flags{TRIEC};

if ( defined &DynaLoader::boot_DynaLoader ) {
    require XSLoader;
    XSLoader::load();
}

sub _load_unload {
    my ($on) = @_;
    if ($on) {

        $^H{regcomp} = install();
    }
    else {
        delete $^H{regcomp};
    }
}

sub bits {
    my $on              = shift;
    my $bits            = 0;
    my $turning_all_off = !@_ && !$on;
    my $seen_Debug      = 0;
    my $seen_debug      = 0;
    if ($turning_all_off) {

        push @_, keys %bitmask;
        push @_, 'strict';
    }

  ARG:
    foreach my $idx ( 0 .. $#_ ) {
        my $s = $_[$idx];
        if ( $s eq 'Debug' or $s eq 'Debugcolor' ) {
            if ( !$seen_Debug ) {
                $seen_Debug = 1;

                ${^RE_DEBUG_FLAGS} = 0;
            }
            setcolor() if $s =~ /color/i;
            for my $idx ( $idx + 1 .. $#_ ) {
                if ( $flags{ $_[$idx] } ) {
                    if ($on) {
                        ${^RE_DEBUG_FLAGS} |= $flags{ $_[$idx] };
                    }
                    else {
                        ${^RE_DEBUG_FLAGS} &= ~$flags{ $_[$idx] };
                    }
                }
                else {
                    require Carp;
                    Carp::carp(
"Unknown \"re\" Debug flag '$_[$idx]', possible flags: ",
                        join( ", ", sort keys %flags )
                    );
                }
            }
            _load_unload( $on ? 1 : ${^RE_DEBUG_FLAGS} );
            last;
        }
        elsif ( $s eq 'debug' or $s eq 'debugcolor' ) {

            ${^RE_DEBUG_FLAGS} = $flags{'EXECUTE'} | $flags{'DUMP'};
            setcolor() if $s =~ /color/i;
            _load_unload($on);
            $seen_debug = 1;
        }
        elsif ( exists $bitmask{$s} ) {
            $bits |= $bitmask{$s};
        }
        elsif ( $EXPORT_OK{$s} ) {
            require Exporter;
            re->export_to_level( 2, 're', $s );
        }
        elsif ( $s eq 'strict' ) {
            if ($on) {
                $^H{reflags} |= $reflags{$s};
                warnings::warnif( 'experimental::re_strict',
                    "\"use re 'strict'\" is experimental" );

                if ( !warnings::enabled('regexp') ) {
                    require warnings;
                    warnings->import('regexp');
                    $^H{re_strict} = 1;
                }
            }
            else {
                $^H{reflags} &= ~$reflags{$s} if $^H{reflags};

                warnings->unimport('regexp') if $^H{re_strict};
            }
            if ( $^H{reflags} ) {
                $^H |= $flags_hint;
            }
            else {
                $^H &= ~$flags_hint;
            }
        }
        elsif ( $s =~ s/^\/// ) {
            my $reflags = $^H{reflags} || 0;
            my $seen_charset;
            my $x_count = 0;
            while ( $s =~ m/( . )/gx ) {
                local $_ = $1;
                if (/[adul]/) {
                    if ( $_ eq 'a' ) {
                        my $sav_pos = pos $s;
                        my $a_count = $s =~ s/a//g;
                        pos $s = $sav_pos - 1;
                        if ( $a_count > 2 ) {
                            require Carp;
                            Carp::carp(
qq 'The "a" flag may only appear a maximum of twice'
                            );
                        }
                        elsif ( $a_count == 2 ) {
                            $_ = 'aa';
                        }
                    }
                    if ($on) {
                        if ($seen_charset) {
                            require Carp;
                            if ( $seen_charset ne $_ ) {
                                Carp::carp(
                                        qq 'The "$seen_charset" and "$_" flags '
                                      . qq 'are exclusive' );
                            }
                            else {
                                Carp::carp(
qq 'The "$seen_charset" flag may not appear '
                                      . qq 'twice' );
                            }
                        }
                        $^H{reflags_charset} = $reflags{$_};
                        $seen_charset = $_;
                    }
                    else {
                        delete $^H{reflags_charset}
                          if defined $^H{reflags_charset}
                          && $^H{reflags_charset} == $reflags{$_};
                    }
                }
                elsif ( exists $reflags{$_} ) {
                    if ( $_ eq 'x' ) {
                        $x_count++;
                        if ( $x_count > 2 ) {
                            require Carp;
                            Carp::carp(
qq 'The "x" flag may only appear a maximum of twice'
                            );
                        }
                        elsif ( $x_count == 2 ) {
                            $_ = 'xx';
                        }
                    }

                    $on
                      ? $reflags |= $reflags{$_}
                      : ( $reflags &= ~$reflags{$_} );
                }
                else {
                    require Carp;
                    Carp::carp(qq'Unknown regular expression flag "$_"');
                    next ARG;
                }
            }
            ( $^H{reflags} = $reflags or defined $^H{reflags_charset} )
              ? $^H |= $flags_hint
              : ( $^H &= ~$flags_hint );
        }
        else {
            require Carp;
            if ( $seen_debug && defined $flags{$s} ) {
                Carp::carp( "Use \"Debug\" not \"debug\", to list debug types"
                      . " in \"re\".  \"$s\" ignored" );
            }
            else {
                Carp::carp(
                    "Unknown \"re\" subpragma '$s' (known ones are: ",
                    join( ', ',
                        map { qq('$_') } 'debug',
                        'debugcolor',
                        sort keys %bitmask ),
                    ")"
                );
            }
        }
    }

    if ($turning_all_off) {
        _load_unload(0);
        $^H{reflags}         = 0;
        $^H{reflags_charset} = 0;
        $^H &= ~$flags_hint;
    }

    $bits;
}

sub import {
    shift;
    $^H |= bits( 1, @_ );
}

sub unimport {
    shift;
    $^H &= ~bits( 0, @_ );
}

1;

__END__

