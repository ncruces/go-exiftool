
package Image::ExifTool::DNG;

use strict;
use vars            qw($VERSION);
use Image::ExifTool qw(:DataAccess :Utils);
use Image::ExifTool::Exif;
use Image::ExifTool::MakerNotes;
use Image::ExifTool::CanonRaw;

$VERSION = '1.25';

sub ProcessOriginalRaw($$$);
sub ProcessAdobeData($$$);
sub ProcessAdobeMakN($$$);
sub ProcessAdobeCRW($$$);
sub ProcessAdobeRAF($$$);
sub ProcessAdobeMRW($$$);
sub ProcessAdobeSR2($$$);
sub ProcessAdobeIFD($$$);
sub WriteAdobeStuff($$$);

%Image::ExifTool::DNG::OriginalRaw = (
    GROUPS       => { 2 => 'Image' },
    PROCESS_PROC => \&ProcessOriginalRaw,
    NOTES        => q{
        This table defines tags extracted from the DNG OriginalRawFileData
        information.
    },
    0 => { Name => 'OriginalRawImage',    Binary => 1 },
    1 => { Name => 'OriginalRawResource', Binary => 1 },
    2 => 'OriginalRawFileType',
    3 => 'OriginalRawCreator',
    4 => { Name => 'OriginalTHMImage',    Binary => 1 },
    5 => { Name => 'OriginalTHMResource', Binary => 1 },
    6 => 'OriginalTHMFileType',
    7 => 'OriginalTHMCreator',
);

%Image::ExifTool::DNG::AdobeData = (
    GROUPS       => { 0 => 'MakerNotes', 1 => 'AdobeDNG', 2 => 'Image' },
    PROCESS_PROC => \&ProcessAdobeData,
    WRITE_PROC   => \&WriteAdobeStuff,
    NOTES        => q{
        This information is found in the "Adobe" DNGPrivateData.

        The maker notes ('MakN') are processed by ExifTool, but some information may
        have been lost by the Adobe DNG Converter.  This is because the Adobe DNG
        Converter (as of version 6.3) doesn't properly handle information referenced
        from inside the maker notes that lies outside the original maker notes
        block.  This information is lost when only the maker note block is copied to
        the DNG image.   While this doesn't effect all makes of cameras, it is a
        problem for some major brands such as Olympus and Sony.

        Other entries in this table represent proprietary information that is
        extracted from the original RAW image and restructured to a different (but
        still proprietary) Adobe format.
    },
    MakN   => [],
    'CRW ' => {
        Name         => 'AdobeCRW',
        SubDirectory => {
            TagTable    => 'Image::ExifTool::CanonRaw::Main',
            ProcessProc => \&ProcessAdobeCRW,
            WriteProc   => \&WriteAdobeStuff,
        },
    },
    'MRW ' => {
        Name         => 'AdobeMRW',
        SubDirectory => {
            TagTable    => 'Image::ExifTool::MinoltaRaw::Main',
            ProcessProc => \&ProcessAdobeMRW,
            WriteProc   => \&WriteAdobeStuff,
        },
    },
    'SR2 ' => {
        Name         => 'AdobeSR2',
        SubDirectory => {
            TagTable    => 'Image::ExifTool::Sony::SR2Private',
            ProcessProc => \&ProcessAdobeSR2,
        },
    },
    'RAF ' => {
        Name         => 'AdobeRAF',
        SubDirectory => {
            TagTable    => 'Image::ExifTool::FujiFilm::RAF',
            ProcessProc => \&ProcessAdobeRAF,
        },
    },
    'Pano' => {
        Name         => 'AdobePano',
        SubDirectory => {
            TagTable    => 'Image::ExifTool::PanasonicRaw::Main',
            ProcessProc => \&ProcessAdobeIFD,
        },
    },
    'Koda' => {
        Name         => 'AdobeKoda',
        SubDirectory => {
            TagTable    => 'Image::ExifTool::Kodak::IFD',
            ProcessProc => \&ProcessAdobeIFD,
        },
    },
    'Leaf' => {
        Name         => 'AdobeLeaf',
        SubDirectory => {
            TagTable    => 'Image::ExifTool::Leaf::SubIFD',
            ProcessProc => \&ProcessAdobeIFD,
        },
    },
);

%Image::ExifTool::DNG::ImageSeq = (
    PROCESS_PROC => \&Image::ExifTool::ProcessBinaryData,
    0            => { Name => 'SeqID',        Format => 'var_string' },
    1            => { Name => 'SeqType',      Format => 'var_string' },
    2            => { Name => 'SeqFrameInfo', Format => 'var_string' },
    3            => { Name => 'SeqIndex',     Format => 'int32u' },
    7            => { Name => 'SeqCount',     Format => 'int32u' },
    11           => {
        Name      => 'SeqFinal',
        Format    => 'int8u',
        PrintConv => { 0 => 'No', 1 => 'Yes' }
    },
);

%Image::ExifTool::DNG::ProfileDynamicRange = (
    PROCESS_PROC => \&Image::ExifTool::ProcessBinaryData,
    0            => { Name => 'PDRVersion', Format => 'int16u' },
    2            => {
        Name      => 'DynamicRange',
        Format    => 'int16u',
        PrintConv => { 0 => 'Standard', 1 => 'High' }
    },
    4 => { Name => 'HintMaxOutputValue', Format => 'float' },
);

{
    my $tagInfo;
    my $list = $Image::ExifTool::DNG::AdobeData{MakN};
    foreach $tagInfo (@Image::ExifTool::MakerNotes::Main) {
        unless ( ref $tagInfo eq 'HASH' ) {
            push @$list, $tagInfo;
            next;
        }
        my %copy = %$tagInfo;
        delete $copy{Groups};
        delete $copy{GotGroups};
        delete $copy{Table};
        push @$list, \%copy;
    }
}

sub ProcessOriginalRaw($$$) {
    my ( $et, $dirInfo, $tagTablePtr ) = @_;
    my $dataPt = $$dirInfo{DataPt};
    my $start  = $$dirInfo{DirStart};
    my $end    = $start + $$dirInfo{DirLen};
    my $pos    = $start;
    my ( $index, $err );

    SetByteOrder('MM');
    for ( $index = 0 ; $index < 8 ; ++$index ) {
        last if $pos + 4 > $end;
        my $val = Get32u( $dataPt, $pos );
        $val or $pos += 4, next;
        my $tagInfo = $et->GetTagInfo( $tagTablePtr, $index );
        $tagInfo or $err = "Missing DNG tag $index", last;
        if ( $index & 0x02 ) {
            $val = substr( $$dataPt, $pos, 4 );
            $pos += 4;
        }
        else {
            my $n      = int( ( $val + 65535 ) / 65536 );
            my $hdrLen = 4 * ( $n + 2 );
            $pos + $hdrLen > $end and $err = '', last;
            my $tag = $$tagInfo{Name};
            my $lcTag = lc $tag;
            if ( ( $$et{OPTIONS}{Binary} and not $$et{EXCL_TAG_LOOKUP}{$lcTag} )
                or $$et{REQ_TAG_LOOKUP}{$lcTag} )
            {
                unless ( eval { require Compress::Zlib } ) {
                    $err =
                      'Install Compress::Zlib to extract compressed images';
                    last;
                }
                my $i;
                $val = '';
                my $p2 = $pos + Get32u( $dataPt, $pos + 4 );
                for ( $i = 0 ; $i < $n ; ++$i ) {
                    my $p1 = $p2;
                    $p2 = $pos + Get32u( $dataPt, $pos + ( $i + 2 ) * 4 );
                    if ( $p1 >= $p2 or $p2 > $end ) {
                        $err = 'Bad compressed RAW image';
                        last;
                    }
                    my $buff = substr( $$dataPt, $p1, $p2 - $p1 );
                    my ( $v2, $stat );
                    my $inflate = Compress::Zlib::inflateInit();
                    $inflate and ( $v2, $stat ) = $inflate->inflate($buff);
                    if ( $inflate and $stat == Compress::Zlib::Z_STREAM_END() )
                    {
                        $val .= $v2;
                    }
                    else {
                        $err = 'Error inflating compressed RAW image';
                        last;
                    }
                }
                $pos = $p2;
            }
            else {
                $pos + $hdrLen > $end and $err = '', last;
                my $len = Get32u( $dataPt, $pos + $hdrLen - 4 );
                $pos + $len > $end and $err = '', last;
                $val = substr( $$dataPt, $pos + $hdrLen, $len - $hdrLen );
                $val = "Binary data $len bytes";
                $pos += $len;
            }
        }
        $et->FoundTag( $tagInfo, $val );
    }
    $et->Warn( $err || 'Bad OriginalRawFileData' ) if defined $err;
    return 1;
}

sub ProcessAdobeData($$$) {
    my ( $et, $dirInfo, $tagTablePtr ) = @_;
    my $dataPt   = $$dirInfo{DataPt};
    my $dataPos  = $$dirInfo{DataPos};
    my $pos      = $$dirInfo{DirStart};
    my $end      = $$dirInfo{DirLen} + $pos;
    my $outfile  = $$dirInfo{OutFile};
    my $verbose  = $et->Options('Verbose');
    my $htmlDump = $et->Options('HtmlDump');

    return 0 unless $$dataPt =~ /^Adobe\0/;
    unless ($outfile) {
        $et->VerboseDir($dirInfo);
        my $fast = $et->Options('FastScan');
        return 1 if $fast and $fast > 1;
    }
    $htmlDump and $et->HDump( $dataPos, 6, 'Adobe DNGPrivateData header' );
    SetByteOrder('MM');
    $pos += 6;
    while ( $pos + 8 <= $end ) {
        my ( $tag, $size ) = unpack( "x${pos}a4N", $$dataPt );
        $pos += 8;
        last if $pos + $size > $end;
        my $tagInfo = $$tagTablePtr{$tag};
        if ($htmlDump) {
            my $name = "Adobe$tag";
            $name =~ tr/ //d;
            $et->HDump( $dataPos + $pos - 8,
                8, "$name header", "Data Size: $size bytes" );
            unless ( $tag =~ /^(MakN|SR2 )$/ ) {
                $et->HDump( $dataPos + $pos, $size, "$name data" );
            }
        }
        if ( $verbose and not $outfile ) {
            $tagInfo
              or $et->VPrint( 0,
                "$$et{INDENT}Unsupported DNGAdobeData record: ($tag)\n" );
            $et->VerboseInfo(
                $tag,
                ref $tagInfo eq 'HASH' ? $tagInfo : undef,
                DataPt  => $dataPt,
                DataPos => $dataPos,
                Start   => $pos,
                Size    => $size,
            );
        }
        my $value;
        while ($tagInfo) {
            my ( $subTable, $subName, $processProc );
            if ( ref $tagInfo eq 'HASH' ) {
                unless ( $$tagInfo{SubDirectory} ) {
                    if ($outfile) {
                        $value = substr( $$dataPt, $pos, $size );
                    }
                    else {
                        $et->HandleTag( $tagTablePtr, $tag,
                            substr( $$dataPt, $pos, $size ) );
                    }
                    last;
                }
                $subTable = GetTagTable( $tagInfo->{SubDirectory}->{TagTable} );
                $subName  = $$tagInfo{Name};
                $processProc = $tagInfo->{SubDirectory}->{ProcessProc};
            }
            else {
                $subTable    = $tagTablePtr;
                $subName     = 'AdobeMakN';
                $processProc = \&ProcessAdobeMakN;
            }
            my %dirInfo = (
                Base     => $$dirInfo{Base},
                DataPt   => $dataPt,
                DataPos  => $dataPos,
                DataLen  => $$dirInfo{DataLen},
                DirStart => $pos,
                DirLen   => $size,
                DirName  => $subName,
            );
            if ($outfile) {
                $dirInfo{Proc} = $processProc;
                $value = $et->WriteDirectory( \%dirInfo, $subTable,
                    \&WriteAdobeStuff );
                defined $value or $value = substr( $$dataPt, $pos, $size );
            }
            else {
                $et->ProcessDirectory( \%dirInfo, $subTable, $processProc );
            }
            last;
        }
        if ( defined $value and length $value ) {
            $$outfile = "Adobe\0" unless $$outfile and length $$outfile;
            $$outfile .= $tag . pack( 'N', length $value ) . $value;
            $$outfile .= "\0" if length($value) & 0x01;
        }
        $pos += $size;
        ++$pos if $size & 0x01;
    }
    $pos == $end or $et->Warn("$pos $end Adobe private data is corrupt");
    return 1;
}

sub ProcessAdobeCRW($$$) {
    my ( $et, $dirInfo, $tagTablePtr ) = @_;
    my $dataPt          = $$dirInfo{DataPt};
    my $start           = $$dirInfo{DirStart};
    my $end             = $start + $$dirInfo{DirLen};
    my $verbose         = $et->Options('Verbose');
    my $buildMakerNotes = $et->Options('MakerNotes');
    my $outfile         = $$dirInfo{OutFile};
    my ( $newTags, $oldChanged );

    SetByteOrder('MM');
    return 0 if $$dirInfo{DirLen} < 4;
    my $byteOrder = substr( $$dataPt, $start, 2 );
    return 0 unless $byteOrder =~ /^(II|MM)$/;

    $buildMakerNotes and Image::ExifTool::CanonRaw::InitMakerNotes($et);

    my $entries = Get16u( $dataPt, $start + 2 );
    my $pos     = $start + 4;
    $et->VerboseDir( $dirInfo, $entries ) unless $outfile;
    if ($outfile) {
        $newTags    = $et->GetNewTagInfoHash($tagTablePtr);
        $$outfile   = substr( $$dataPt, $start, 4 );
        $oldChanged = $$et{CHANGED};
    }
    my $index;
    for ( $index = 0 ; $index < $entries ; ++$index ) {
        last if $pos + 6 > $end;
        my $tag  = Get16u( $dataPt, $pos );
        my $size = Get32u( $dataPt, $pos + 2 );
        $pos += 6;
        last if $pos + $size > $end;
        my $value   = substr( $$dataPt, $pos, $size );
        my $tagID   = $tag & 0x3fff;
        my $tagType = ( $tag >> 8 ) & 0x38;
        my $format  = $Image::ExifTool::CanonRaw::crwTagFormat{$tagType};
        my $count;
        my $tagInfo = $et->GetTagInfo( $tagTablePtr, $tagID, \$value );

        if ($tagInfo) {
            $format = $$tagInfo{Format} if $$tagInfo{Format};
            $count  = $$tagInfo{Count};
        }
        if (    not defined $count
            and $tag & 0x4000
            and $format
            and $format ne 'string' )
        {
            $count = 1;
        }
        if ( $format and not $count ) {
            my $fnum = $Image::ExifTool::Exif::formatNumber{$format};
            my $fsiz = $Image::ExifTool::Exif::formatSize[$fnum];
            $count = int( $size / $fsiz );
        }
        $format or $format = 'undef';
        SetByteOrder($byteOrder);
        my $val = ReadValue( \$value, 0, $format, $count, $size );
        if ($outfile) {
            if ($tagInfo) {
                my $subdir = $$tagInfo{SubDirectory};
                if ( $subdir and $$subdir{TagTable} ) {
                    my $name        = $$tagInfo{Name};
                    my $newTagTable = GetTagTable( $$subdir{TagTable} );
                    return 0 unless $newTagTable;
                    my $subdirStart = 0;
                    $subdirStart = eval $$subdir{Start} if $$subdir{Start};
                    my $dirData    = \$value;
                    my %subdirInfo = (
                        Name     => $name,
                        DataPt   => $dirData,
                        DataLen  => $size,
                        DirStart => $subdirStart,
                        DirLen   => $size - $subdirStart,
                        Parent   => $$dirInfo{DirName},
                    );
                    if ( defined $$subdir{Validate}
                        and not eval $$subdir{Validate} )
                    {
                        $et->Warn("Invalid $name data");
                    }
                    else {
                        $subdir =
                          $et->WriteDirectory( \%subdirInfo, $newTagTable );
                        if ( defined $subdir and length $subdir ) {
                            if ($subdirStart) {
                                $value =
                                  substr( $value, 0, $subdirStart ) . $subdir;
                            }
                            else {
                                $value = $subdir;
                            }
                        }
                    }
                }
                elsif ( $$newTags{$tagID} ) {
                    my $nvHash = $et->GetNewValueHash($tagInfo);
                    if ( $et->IsOverwriting( $nvHash, $val ) ) {
                        my $newVal = $et->GetNewValue($nvHash);
                        my $verboseVal;
                        $verboseVal = $newVal if $verbose > 1;
                        if ( defined $newVal and $format ) {
                            $newVal = WriteValue( $newVal, $format, $count );
                        }
                        if ( defined $newVal ) {
                            $et->VerboseValue( "- CanonRaw:$$tagInfo{Name}",
                                $value );
                            $et->VerboseValue( "+ CanonRaw:$$tagInfo{Name}",
                                $verboseVal );
                            $value = $newVal;
                            ++$$et{CHANGED};
                        }
                    }
                }
            }
            SetByteOrder('MM');
            $$outfile .= Set16u($tag) . Set32u( length($value) ) . $value;
        }
        else {
            $et->HandleTag(
                $tagTablePtr, $tagID, $val,
                Index   => $index,
                DataPt  => $dataPt,
                DataPos => $$dirInfo{DataPos},
                Start   => $pos,
                Size    => $size,
                TagInfo => $tagInfo,
            );
            if ($buildMakerNotes) {
                Image::ExifTool::CanonRaw::BuildMakerNotes( $et, $tagID,
                    $tagInfo, \$value, $format, $count );
            }
        }
        $$et{DIR_NAME} = 'ImageDescription' if $tagID == 0x0805;
        SetByteOrder('MM');
        $pos += $size;
    }
    if (
        $outfile
        and (  not defined $$outfile
            or $index != $entries
            or $$et{CHANGED} == $oldChanged )
      )
    {
        $$et{CHANGED} = $oldChanged;
        undef $$outfile;
    }
    if ( $index != $entries ) {
        $et->Warn('Truncated CRW notes');
    }
    elsif ( $pos < $end ) {
        $et->Warn( $end - $pos . ' extra bytes at end of CRW notes' );
    }
    if ($buildMakerNotes) {
        SetByteOrder($byteOrder);
        Image::ExifTool::CanonRaw::SaveMakerNotes($et);
    }
    return 1;
}

sub ProcessAdobeMRW($$$) {
    my ( $et, $dirInfo, $tagTablePtr ) = @_;
    my $dataPt   = $$dirInfo{DataPt};
    my $dirLen   = $$dirInfo{DirLen};
    my $dirStart = $$dirInfo{DirStart};
    my $outfile  = $$dirInfo{OutFile};

    my $buff = "\0MRM" . pack( 'N', $dirLen - 4 );
    $buff .= substr( $$dataPt, $dirStart + 4, $dirLen - 4 );
    my $raf     = File::RandomAccess->new( \$buff );
    my %dirInfo = ( RAF => $raf, OutFile => $outfile );
    my $rtnVal  = Image::ExifTool::MinoltaRaw::ProcessMRW( $et, \%dirInfo );
    if ( $outfile and defined $$outfile and length $$outfile ) {
        $$outfile = substr( $$dataPt, $dirStart, 4 ) . substr( $$outfile, 8 );
    }
    return $rtnVal;
}

sub ProcessAdobeRAF($$$) {
    my ( $et, $dirInfo, $tagTablePtr ) = @_;
    return 0 if $$dirInfo{OutFile};
    my $dataPt = $$dirInfo{DataPt};
    my $pos    = $$dirInfo{DirStart};
    my $dirEnd = $$dirInfo{DirLen} + $pos;
    my ( $readIt, $warn );

    if ( $pos + 2 <= $dirEnd and SetByteOrder( substr( $$dataPt, $pos, 2 ) ) ) {
        $pos += 2;
    }
    else {
        $et->Warn('Invalid DNG RAF data');
        return 0;
    }
    $et->VerboseDir($dirInfo);
    my $raf = File::RandomAccess->new($dataPt);
    my $num = '';
    for ( ; ; ) {
        last if $pos + 4 > $dirEnd;
        my $len = Get32u( $dataPt, $pos );
        $pos += 4 + $len;
        $len or last;
        $readIt or $readIt = 1, next;
        my %dirInfo = (
            RAF      => $raf,
            DirStart => $pos - $len,
        );
        $$et{SET_GROUP1} = "RAF$num";
        $et->ProcessDirectory( \%dirInfo, $tagTablePtr ) or $warn = 1;
        delete $$et{SET_GROUP1};
        $num = ( $num || 1 ) + 1;
    }
    $warn and $et->Warn('Possibly corrupt RAF information');
    return 1;
}

sub ProcessAdobeSR2($$$) {
    my ( $et, $dirInfo, $tagTablePtr ) = @_;
    return 0 if $$dirInfo{OutFile};
    my $dataPt = $$dirInfo{DataPt};
    my $start  = $$dirInfo{DirStart};
    my $len    = $$dirInfo{DirLen};

    return 0 if $len < 6;
    SetByteOrder('MM');
    my $originalPos = Get32u( $dataPt, $start + 2 );
    return 0 unless SetByteOrder( substr( $$dataPt, $start, 2 ) );

    $et->VerboseDir($dirInfo);
    my $dataPos  = $$dirInfo{DataPos};
    my $dirStart = $start + 6;
    my $dirLen   = $len - 6;

    my $fix        = $dataPos + $dirStart - $originalPos;
    my %subdirInfo = (
        DirName  => 'AdobeSR2',
        Base     => $$dirInfo{Base} + $fix,
        DataPt   => $dataPt,
        DataPos  => $dataPos - $fix,
        DataLen  => $$dirInfo{DataLen},
        DirStart => $dirStart,
        DirLen   => $dirLen,
        Parent   => $$dirInfo{DirName},
    );

    if ( $et->Options('HtmlDump') ) {
        $et->HDump( $dataPos + $start, 6, 'Adobe SR2 data' );
    }
    $et->ProcessDirectory( \%subdirInfo, $tagTablePtr );
    return 1;
}

sub ProcessAdobeIFD($$$) {
    my ( $et, $dirInfo, $tagTablePtr ) = @_;
    return 0 if $$dirInfo{OutFile};
    my $dataPt  = $$dirInfo{DataPt};
    my $pos     = $$dirInfo{DirStart};
    my $dataPos = $$dirInfo{DataPos};

    return 0 if $$dirInfo{DirLen} < 4;
    my $dataOrder = substr( $$dataPt, $pos, 2 );
    return 0 unless SetByteOrder($dataOrder);

    SetByteOrder('MM');
    my $entries = Get16u( $dataPt, $pos + 2 );
    $et->VerboseDir( $dirInfo, $entries );
    $pos += 4;

    my $end = $pos + $$dirInfo{DirLen};
    my $index;
    for ( $index = 0 ; $index < $entries ; ++$index ) {
        last if $pos + 8 > $end;
        SetByteOrder('MM');
        my $tagID  = Get16u( $dataPt, $pos );
        my $format = Get16u( $dataPt, $pos + 2 );
        my $count  = Get32u( $dataPt, $pos + 4 );
        if ( $format < 1 or $format > 13 ) {
            $format and $et->Warn(
                sprintf(
                    "Unknown format ($format) for $$dirInfo{DirName} tag 0x%x",
                    $tagID )
            );
            return 0;
        }
        my $size = $Image::ExifTool::Exif::formatSize[$format] * $count;
        last if $pos + 8 + $size > $end;
        my $formatStr = $Image::ExifTool::Exif::formatName[$format];
        SetByteOrder($dataOrder);
        my $val = ReadValue( $dataPt, $pos + 8, $formatStr, $count, $size );
        $et->HandleTag(
            $tagTablePtr, $tagID, $val,
            Index   => $index,
            DataPt  => $dataPt,
            DataPos => $dataPos,
            Start   => $pos + 8,
            Size    => $size
        );
        $pos += 8 + $size;
    }
    if ( $index < $entries ) {
        $et->Warn("Truncated $$dirInfo{DirName} directory");
        return 0;
    }
    return 1;
}

sub ProcessAdobeMakN($$$) {
    my ( $et, $dirInfo, $tagTablePtr ) = @_;
    my $dataPt  = $$dirInfo{DataPt};
    my $start   = $$dirInfo{DirStart};
    my $len     = $$dirInfo{DirLen};
    my $outfile = $$dirInfo{OutFile};

    return 0 if $len < 6;
    SetByteOrder('MM');
    my $originalPos = Get32u( $dataPt, $start + 2 );
    return 0 unless SetByteOrder( substr( $$dataPt, $start, 2 ) );

    $et->VerboseDir($dirInfo) unless $outfile;
    my $dataPos = $$dirInfo{DataPos};
    my $hdrLen  = 6;

    $hdrLen += 12
      if $len >= 18 and substr( $$dataPt, $start + 6, 4 ) eq "\0\0\0\x01";

    my $dirStart = $start + $hdrLen;
    my $dirLen   = $len - $hdrLen;

    my $hdr     = substr( $$dataPt, $dirStart, $dirLen < 48 ? $dirLen : 48 );
    my $tagInfo = $et->GetTagInfo( $tagTablePtr, 'MakN', \$hdr );
    return 0 unless $tagInfo and $$tagInfo{SubDirectory};
    my $subdir   = $$tagInfo{SubDirectory};
    my $subTable = GetTagTable( $$subdir{TagTable} );
    my %subdirInfo = (
        DirName    => 'MakerNotes',
        Name       => $$tagInfo{Name},
        Base       => $$dirInfo{Base},
        DataPt     => $dataPt,
        DataPos    => $dataPos,
        DataLen    => $$dirInfo{DataLen},
        DirStart   => $dirStart,
        DirLen     => $dirLen,
        TagInfo    => $tagInfo,
        FixBase    => $$subdir{FixBase},
        EntryBased => $$subdir{EntryBased},
        Parent     => $$dirInfo{DirName},
    );
    my $loc = Image::ExifTool::MakerNotes::LocateIFD( $et, \%subdirInfo );

    unless ( defined $loc ) {
        $et->Warn('Maker notes could not be parsed');
        return 0;
    }
    if ( $et->Options('HtmlDump') ) {
        $et->HDump( $dataPos + $start,    $hdrLen, 'Adobe MakN data' );
        $et->HDump( $dataPos + $dirStart, $loc,    "$$tagInfo{Name} header" )
          if $loc;
    }

    my $fix = 0;
    unless ( $$subdir{Base} ) {
        $fix = $dataPos + $dirStart - $originalPos;
        $subdirInfo{Base}    += $fix;
        $subdirInfo{DataPos} -= $fix;
    }
    if ($outfile) {
        my $fixup      = $subdirInfo{Fixup} = Image::ExifTool::Fixup->new;
        my $oldChanged = $$et{CHANGED};
        my $buff       = $et->WriteDirectory( \%subdirInfo, $subTable );
        unless ( defined $buff and $$et{CHANGED} != $oldChanged ) {
            $$et{CHANGED} = $oldChanged;
            return 1;
        }
        unless ( length $buff ) {
            $$outfile = '';
            return 1;
        }
        if ( $subdirInfo{Relative} ) {
            my $baseShift =
              $dataPos + $dirStart + $$dirInfo{Base} - $subdirInfo{Base};
            $fixup->{Shift} += $baseShift;
        }
        else {
            $fixup->{Shift} += $originalPos;
        }
        $loc = 0 if $subdirInfo{BlockWrite};
        $fixup->{Shift} += $loc;
        $fixup->ApplyFixup( \$buff );

        my $header = substr( $$dataPt, $start, $hdrLen + $loc );
        $$outfile = $header . $buff;
    }
    else {
        $et->ProcessDirectory( \%subdirInfo, $subTable, $$subdir{ProcessProc} );
        if (   $et->Options('MakerNotes')
            or $$et{REQ_TAG_LOOKUP}{ lc( $$tagInfo{Name} ) } )
        {
            my $val;
            if ( $$tagInfo{MakerNotes} ) {
                $subdirInfo{Base}     = $$dirInfo{Base} + $fix;
                $subdirInfo{DataPos}  = $dataPos - $fix;
                $subdirInfo{DirStart} = $dirStart;
                $subdirInfo{DirLen}   = $dirLen;
                $val =
                  Image::ExifTool::Exif::RebuildMakerNotes( $et, \%subdirInfo,
                    $subTable );
                if ( not defined $val and $dirLen > 4 ) {
                    $et->Warn('Error rebuilding maker notes (may be corrupt)');
                }
            }
            else {
                return 1 unless $$tagInfo{Writable};
            }
            $val = substr( $$dataPt, 20 ) unless defined $val;
            my $key = $et->FoundTag( $tagInfo, $val );
            if ( $$et{MAKER_NOTE_FIXUP} ) {
                $$et{TAG_EXTRA}{$key}{Fixup} = $$et{MAKER_NOTE_FIXUP};
                delete $$et{MAKER_NOTE_FIXUP};
            }
        }
    }
    return 1;
}

sub WriteAdobeStuff($$$) {
    my ( $et, $dirInfo, $tagTablePtr ) = @_;
    $et or return 1;
    my $proc = $$dirInfo{Proc} || \&ProcessAdobeData;
    my $buff;
    $$dirInfo{OutFile} = \$buff;
    &$proc( $et, $dirInfo, $tagTablePtr ) or undef $buff;
    return $buff;
}

1;

__END__

