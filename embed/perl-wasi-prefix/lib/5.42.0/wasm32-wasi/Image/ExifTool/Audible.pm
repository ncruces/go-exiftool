
package Image::ExifTool::Audible;

use strict;
use vars            qw($VERSION);
use Image::ExifTool qw(:DataAccess :Utils);

$VERSION = '1.02';

sub ProcessAudible_meta($$$);
sub ProcessAudible_cvrx($$$);

%Image::ExifTool::Audible::Main = (
    GROUPS => { 2 => 'Audio' },
    NOTES  => q{
        ExifTool will extract any information found in the metadata dictionary of
        Audible .AA files, even if not listed in the table below.
    },
    pubdate        => { Name => 'PublishDate',      Groups => { 2 => 'Time' } },
    pub_date_start => { Name => 'PublishDateStart', Groups => { 2 => 'Time' } },
    author         => { Name => 'Author',    Groups => { 2 => 'Author' } },
    copyright      => { Name => 'Copyright', Groups => { 2 => 'Author' } },

    _chapter_count => { Name => 'ChapterCount' },
    _cover_art     => {
        Name   => 'CoverArt',
        Groups => { 2 => 'Preview' },
        Binary => 1,
    },
);

%Image::ExifTool::Audible::tags = (
    GROUPS => { 0 => 'QuickTime', 2 => 'Audio' },
    NOTES  => 'Information found in "tags" atom of Audible M4B audio books.',
    meta   => {
        Name         => 'Audible_meta',
        SubDirectory => { TagTable => 'Image::ExifTool::Audible::meta' },
    },
    cvrx => {
        Name         => 'Audible_cvrx',
        SubDirectory => { TagTable => 'Image::ExifTool::Audible::cvrx' },
    },
    tseg => {
        Name         => 'Audible_tseg',
        SubDirectory => { TagTable => 'Image::ExifTool::Audible::tseg' },
    },
);

%Image::ExifTool::Audible::meta = (
    PROCESS_PROC    => \&ProcessAudible_meta,
    GROUPS          => { 0 => 'QuickTime', 2 => 'Audio' },
    NOTES           => 'Information found in Audible M4B "meta" atom.',
    Album           => 'Album',
    ALBUMARTIST     => { Name => 'AlbumArtist', Groups => { 2 => 'Author' } },
    Artist          => { Name => 'Artist',      Groups => { 2 => 'Author' } },
    Comment         => 'Comment',
    Genre           => 'Genre',
    itunesmediatype =>
      { Name => 'iTunesMediaType', Description => 'iTunes Media Type' },
    SUBTITLE => 'Subtitle',
    Title    => 'Title',
    TOOL     => 'CreatorTool',
    Year     => { Name => 'Year', Groups => { 2 => 'Time' } },
    track    => 'ChapterName',
);

%Image::ExifTool::Audible::cvrx = (
    PROCESS_PROC => \&ProcessAudible_cvrx,
    GROUPS       => { 0 => 'QuickTime', 2 => 'Audio' },
    NOTES        => 'Audible cover art information in M4B audio books.',
    VARS         => { ID_FMT => 'none' },
    CoverArtType => 'CoverArtType',
    CoverArt     => {
        Name   => 'CoverArt',
        Groups => { 2 => 'Preview' },
        Binary => 1,
    },
);

%Image::ExifTool::Audible::tseg = (
    GROUPS => { 0 => 'QuickTime', 2 => 'Audio' },
    tshd   => {
        Name      => 'ChapterNumber',
        Format    => 'int32u',
        ValueConv => '$val + 1',
    },
    meta => {
        Name         => 'Audible_meta2',
        SubDirectory => { TagTable => 'Image::ExifTool::Audible::meta' },
    },
);

sub ProcessAudible_meta($$$) {
    my ( $et, $dirInfo, $tagTablePtr ) = @_;
    my $dataPt  = $$dirInfo{DataPt};
    my $dataPos = $$dirInfo{DataPos};
    my $dirLen  = length $$dataPt;
    return 0 if $dirLen < 4;
    my $num = Get32u( $dataPt, 0 );
    $et->VerboseDir( 'Audible_meta', $num );
    my $pos = 4;
    my $index;

    for ( $index = 0 ; $index < $num ; ++$index ) {
        last if $pos + 3 > $dirLen;
        my $unk = Get8u( $dataPt, $pos );
        last unless $unk == 0x80 or $unk == 0x00;
        my $len = Get16u( $dataPt, $pos + 1 );
        $pos += 3;
        last if $pos + $len + 6 > $dirLen or not $len;
        my $tag = substr( $$dataPt, $pos, $len );
        my $ver = Get16u( $dataPt, $pos + $len );
        last unless $ver == 0x0001;
        my $size = Get32u( $dataPt, $pos + $len + 2 );
        $pos += $len + 6;
        last if $pos + $size > $dirLen;
        my $val = $et->Decode( substr( $$dataPt, $pos, $size ), 'UTF8' );

        unless ( $$tagTablePtr{$tag} ) {
            my $name = Image::ExifTool::MakeTagName(
                ( $tag =~ /[a-z]/ ) ? $tag : lc($tag) );
            AddTagToTable( $tagTablePtr, $tag, { Name => $name } );
        }
        $et->HandleTag(
            $tagTablePtr, $tag, $val,
            DataPt  => $dataPt,
            DataPos => $dataPos,
            Start   => $pos,
            Size    => $size,
            Index   => $index,
        );
        $pos += $size;
    }
    return 1;
}

sub ProcessAudible_cvrx($$$) {
    my ( $et, $dirInfo, $tagTablePtr ) = @_;
    my $dataPt  = $$dirInfo{DataPt};
    my $dataPos = $$dirInfo{DataPos};
    my $dirLen  = length $$dataPt;
    return 0 if 0x0a > $dirLen;
    my $len = Get16u( $dataPt, 0x08 );
    return 0 if 0x0a + $len + 6 > $dirLen;
    my $size = Get32u( $dataPt, 0x0a + $len + 2 );
    return 0 if 0x0a + $len + 6 + $size > $dirLen;
    $et->VerboseDir( 'Audible_cvrx', undef, $dirLen );
    $et->HandleTag(
        $tagTablePtr, 'CoverArtType', undef,
        DataPt  => $dataPt,
        DataPos => $dataPos,
        Start   => 0x0a,
        Size    => $len,
    );
    $et->HandleTag(
        $tagTablePtr, 'CoverArt', undef,
        DataPt  => $dataPt,
        DataPos => $dataPos,
        Start   => 0x0a + $len + 6,
        Size    => $size,
    );
    return 1;
}

sub ProcessAA($$) {
    my ( $et, $dirInfo ) = @_;
    my $raf = $$dirInfo{RAF};
    my ( $buff, $toc, $entry, $i );

    return 0
      unless $raf->Read( $buff, 16 ) == 16
      and $buff =~ /^.{4}\x57\x90\x75\x36/s;
    if ( defined $$et{VALUE}{FileSize} ) {
        unpack( 'N', $buff ) == $$et{VALUE}{FileSize} or return 0;
    }
    $et->SetFileType();
    SetByteOrder('MM');
    my $bytes = 12 * Get32u( \$buff, 8 );
    $bytes > 0xc00 and $et->Warn('Invalid TOC'), return 1;
    $raf->Read( $toc, $bytes ) == $bytes
      or $et->Warn('Truncated TOC'), return 1;
    my $tagTablePtr = GetTagTable('Image::ExifTool::Audible::Main');
    for ( $entry = 0 ; $entry < $bytes ; $entry += 12 ) {
        my $type = Get32u( \$toc, $entry );
        next unless $type == 2 or $type == 6 or $type == 11;
        my $offset = Get32u( \$toc, $entry + 4 );
        my $length = Get32u( \$toc, $entry + 8 ) or next;
        $raf->Seek( $offset, 0 ) or $et->Warn("Chunk $type seek error"), last;
        if ( $type == 6 ) {
            next if $length < 4 or $raf->Read( $buff, 4 ) != 4;
            $et->HandleTag( $tagTablePtr, '_chapter_count',
                Get32u( \$buff, 0 ) );
            next;
        }
        $length > 100000000 and $et->Warn("Chunk $type too big"), next;
        $raf->Read( $buff, $length ) == $length
          or $et->Warn("Chunk $type read error"), last;
        if ( $type == 11 ) {
            next if $length < 8;
            my $len = Get32u( \$buff, 0 );
            my $off = Get32u( \$buff, 4 );
            next if $off < $offset + 8 or $off - $offset + $len > $length;
            $et->HandleTag( $tagTablePtr, '_cover_art',
                substr( $buff, $off - $offset, $len ) );
            next;
        }
        $length < 4 and $et->Warn('Bad dictionary'), next;
        my $num = Get32u( \$buff, 0 );
        $num > 0x200 and $et->Warn('Bad dictionary count'), next;
        my $pos = 4;
        require Image::ExifTool::HTML;
        $et->VerboseDir( 'Audible Metadata', $num );
        for ( $i = 0 ; $i < $num ; ++$i ) {
            my $tagPos = $pos + 9;
            $tagPos > $length and $et->Warn('Truncated dictionary'), last;
            my $tagLen = Get32u( \$buff, $pos + 1 );
            my $valLen = Get32u( \$buff, $pos + 5 );
            my $valPos = $tagPos + $tagLen;
            my $nxtPos = $valPos + $valLen;
            $nxtPos > $length and $et->Warn('Bad dictionary entry'), last;
            my $tag = substr( $buff, $tagPos, $tagLen );
            my $val = substr( $buff, $valPos, $valLen );

            unless ( $$tagTablePtr{$tag} ) {
                my $name = Image::ExifTool::MakeTagName($tag);
                $name =~ s/_(.)/\U$1/g;
                AddTagToTable( $tagTablePtr, $tag, { Name => $name } );
            }
            $val =
              $et->Decode( Image::ExifTool::HTML::UnescapeHTML($val), 'UTF8' );
            $et->HandleTag(
                $tagTablePtr, $tag, $val,
                DataPos => $offset,
                DataPt  => \$buff,
                Start   => $valPos,
                Size    => $valLen,
                Index   => $i,
            );
            $pos = $nxtPos;
        }
    }
    return 1;
}

1;

__END__


