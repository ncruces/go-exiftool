
package Image::ExifTool::PPM;

use strict;
use vars            qw($VERSION);
use Image::ExifTool qw(:DataAccess :Utils);

$VERSION = '1.11';

sub ProcessPPM($$) {
    my ( $et, $dirInfo ) = @_;
    my $raf     = $$dirInfo{RAF};
    my $outfile = $$dirInfo{OutFile};
    my $verbose = $et->Options('Verbose');
    my $out     = $et->Options('TextOut');
    my ( $buff, $num, $type, %info, $seal );
    for ( ; ; ) {
        if ( defined $buff ) {
            my $tmp;
            return 0 unless $raf->Read( $tmp, 1024 );
            $buff .= $tmp;
        }
        else {
            return 0 unless $raf->Read( $buff, 1024 );
        }
        return 0 unless $buff =~ /^P([1-6])\s+/g;
        $num = $1;
        if ( $buff =~ /\G#/gc ) {
            next unless $buff =~ /\G ?(.*[\n\r]+(#.*[\n\r]+)*)\s*/g;
            $info{Comment} = $1;
            next if $buff =~ /\G#/gc;
        }
        else {
            delete $info{Comment};
        }
        next unless $buff =~ /\G(\S+)\s+(\S+)\s+/g;
        $info{ImageWidth}  = $1;
        $info{ImageHeight} = $2;
        $type              = [qw{PPM PBM PGM}]->[ $num % 3 ];
        last if $type eq 'PBM';
        if ( $buff =~ /\G\s*#/gc ) {
            next unless $buff =~ /\G ?(.*[\n\r]+(#.*[\n\r]+)*)\s*/g;
            $info{Comment} = '' unless exists $info{Comment};
            $info{Comment} .= $1;
            next if $buff =~ /\G#/gc;
        }
        next unless $buff =~ /\G(\S+)\s/g;
        $info{MaxVal} = $1;
        last;
    }
    foreach ( keys %info ) {
        next if $_ eq 'Comment';
        return 0 unless $info{$_} =~ /^\d+$/;
    }
    if ( defined $info{Comment} ) {
        $info{Comment} =~ s/^# ?//mg;
        $info{Comment} =~ s/[\n\r]+$//;
        $seal = 1 if $info{Comment} =~ /^<seal seal=/;
    }
    $et->SetFileType($type);
    my $len = pos($buff);
    if ($outfile) {
        my $nvHash;
        my $newComment = $et->GetNewValue( 'Comment', \$nvHash );
        my $oldComment = $info{Comment};
        if ( $et->IsOverwriting( $nvHash, $oldComment ) ) {
            ++$$et{CHANGED};
            $et->VerboseValue( '- Comment', $oldComment )
              if defined $oldComment;
            $et->VerboseValue( '+ Comment', $newComment )
              if defined $newComment;
        }
        elsif ( $seal and $$et{DEL_GROUP}{SEAL} ) {
            $et->VerboseValue( '- Comment', $oldComment );
            ++$$et{CHANGED};
        }
        else {
            $newComment = $oldComment;
        }
        my $hdr = "P$num\n";
        if ( defined $newComment ) {
            $newComment =~ s/\n/\n# /g;
            $hdr .= "# $newComment\n";
        }
        $hdr .= "$info{ImageWidth} $info{ImageHeight}\n";
        $hdr .= "$info{MaxVal}\n" if $type ne 'PBM';
        Write( $outfile, $hdr, substr( $buff, $len ) ) or return -1;
        while ( $raf->Read( $buff, 0x10000 ) ) {
            Write( $outfile, $buff ) or return -1;
        }
        return 1;
    }
    if ( $verbose > 2 ) {
        print $out "$type header ($len bytes):\n";
        $et->VerboseDump( \$buff, Len => $len );
    }
    if ($seal) {
        $et->ProcessDirectory( { DataPt => \$info{Comment} },
            GetTagTable('Image::ExifTool::XMP::SEAL') );
        delete $info{Comment};
    }
    my $tag;
    foreach $tag (qw{Comment ImageWidth ImageHeight MaxVal}) {
        $et->FoundTag( $tag, $info{$tag} ) if defined $info{$tag};
    }
    return 1;
}

1;

__END__


