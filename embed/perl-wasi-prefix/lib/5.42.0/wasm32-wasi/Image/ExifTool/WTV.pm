
package Image::ExifTool::WTV;

use strict;
use vars            qw($VERSION);
use Image::ExifTool qw(:DataAccess :Utils);

$VERSION = '1.01';

sub ProcessMetadata($$$);

my %timeInfo = (
    ValueConv => q{ # (719162 days from 0001:01:01 to 1970:01:01)
        my $t = $val / 1e7 - 719162*24*3600;
        return Image::ExifTool::ConvertUnixTime($t) . 'Z';
    },
    PrintConv => '$self->ConvertDateTime($val)',
);

my %bool = ( PrintConv => { 0 => 'No', 1 => 'Yes' }, PrintConvColumns => 2 );

%Image::ExifTool::WTV::Main = (
    GROUPS => { 0 => 'WTV', 1 => 'WTV', 2 => 'Video' },
    NOTES  => 'Tags found in Windows recorded TV (WTV) videos.',
    'table.0.entries.legacy_attrib' => {
        Name         => 'Metdata',
        SubDirectory => { TagTable => 'Image::ExifTool::WTV::Metadata' },
    },
);

%Image::ExifTool::WTV::Metadata = (
    GROUPS       => { 0 => 'WTV', 1 => 'WTV', 2 => 'Video' },
    PROCESS_PROC => \&ProcessMetadata,
    NOTES => 'ExifTool will extract any tag found, even if not in this table.',
    VARS  => { ID_FMT => 'none' },
    'Duration' => {
        Name      => 'Duration',
        ValueConv => '$val/1e7',
        PrintConv => 'ConvertDuration($val)',
    },
    'Title'                    => {},
    'WM/Genre'                 => 'Genre',
    'WM/Language'              => 'Language',
    'WM/MediaClassPrimaryID'   => 'MediaClassPrimaryID',
    'WM/MediaClassSecondaryID' => 'MediaClassSecondaryID',
    'WM/MediaCredits'          => 'MediaCredits',
    'WM/MediaIsDelay'          => { Name => 'MediaIsDelay',    %bool },
    'WM/MediaIsFinale'         => { Name => 'MediaIsFinale',   %bool },
    'WM/MediaIsLive'           => { Name => 'MediaIsLive',     %bool },
    'WM/MediaIsMovie'          => { Name => 'MediaIsMovie',    %bool },
    'WM/MediaIsPremiere'       => { Name => 'MediaIsPremiere', %bool },
    'WM/MediaIsRepeat'         => { Name => 'MediaIsRepeat',   %bool },
    'WM/MediaIsSAP'            => { Name => 'MediaIsSAP',      %bool },
    'WM/MediaIsSport'          => { Name => 'MediaIsSport',    %bool },
    'WM/MediaIsStereo'         =>
      { Name => 'MediaIsStereo', %bool, Groups => { 2 => 'Audio' } },
    'WM/MediaIsSubtitled' => { Name => 'MediaIsSubtitled', %bool },
    'WM/MediaIsTape'      => { Name => 'MediaIsTape',      %bool },
    'WM/MediaNetworkAffiliation'        => 'MediaNetworkAffiliation',
    'WM/MediaOriginalBroadcastDateTime' => {
        Name      => 'MediaOriginalBroadcastDateTime',
        Groups    => { 2 => 'Time' },
        ValueConv => '$val =~ tr/-T/: /; $val',
        PrintConv => '$self->ConvertDateTime($val)',
    },
    'WM/MediaOriginalChannel'          => { Name => 'MediaOriginalChannel' },
    'WM/MediaOriginalChannelSubNumber' =>
      { Name => 'MediaOriginalChannelSubNumber' },
    'WM/MediaOriginalRunTime' => {
        Name      => 'MediaOriginalRunTime',
        ValueConv => '$val / 1e7',
        PrintConv => 'ConvertDuration($val)',
    },
    'WM/MediaStationCallSign'       => 'MediaStationCallSign',
    'WM/MediaStationName'           => 'MediaStationName',
    'WM/MediaThumbAspectRatioX'     => 'MediaThumbAspectRatioX',
    'WM/MediaThumbAspectRatioY'     => 'MediaThumbAspectRatioY',
    'WM/MediaThumbHeight'           => 'MediaThumbHeight',
    'WM/MediaThumbRatingAttributes' => { Name => 'MediaThumbRatingAttributes' },
    'WM/MediaThumbRatingLevel'      => 'MediaThumbRatingLevel',
    'WM/MediaThumbRatingSystem'     => 'MediaThumbRatingSystem',
    'WM/MediaThumbRet'              => 'MediaThumbRet',
    'WM/MediaThumbStride'           => 'MediaThumbStride',
    'WM/MediaThumbTimeStamp'        =>
      { Name => 'MediaThumbTimeStamp', Notes => 'unknown units', Unknown => 1 },
    'WM/MediaThumbWidth'     => 'MediaThumbWidth',
    'WM/OriginalReleaseTime' => {
        Name      => 'OriginalReleaseTime',
        Groups    => { 2 => 'Time' },
        ValueConv => '$val=~tr/-T/: /; $val',
        PrintConv => '$self->ConvertDateTime($val)',
    },
    'WM/ParentalRating'        => 'ParentalRating',
    'WM/ParentalRatingReason'  => 'ParentalRatingReason',
    'WM/Provider'              => 'Provider',
    'WM/ProviderCopyright'     => 'ProviderCopyright',
    'WM/ProviderRating'        => 'ProviderRating',
    'WM/SubTitle'              => 'Subtitle',
    'WM/SubTitleDescription'   => 'SubtitleDescription',
    'WM/VideoClosedCaptioning' => { Name => 'VideoClosedCaptioning', %bool },
    'WM/WMRVATSCContent'       => { Name => 'ATSCContent',           %bool },
    'WM/WMRVActualSoftPostPadding' => 'ActualSoftPostPadding',
    'WM/WMRVActualSoftPrePadding'  => 'ActualSoftPrePadding',
    'WM/WMRVBitrate'               =>
      { Name => 'Bitrate', Notes => 'unknown units', Unknown => 1 },
    'WM/WMRVBrandingImageID'         => 'BrandingImageID',
    'WM/WMRVBrandingName'            => 'BrandingName',
    'WM/WMRVContentProtected'        => { Name => 'ContentProtected', %bool },
    'WM/WMRVContentProtectedPercent' => 'ContentProtectedPercent',
    'WM/WMRVDTVContent'              => { Name => 'DTVContent', %bool },
    'WM/WMRVEncodeTime'              =>
      { Name => 'EncodeTime', Groups => { 2 => 'Time' }, %timeInfo },
    'WM/WMRVEndTime' =>
      { Name => 'EndTime', Groups => { 2 => 'Time' }, %timeInfo },
    'WM/WMRVExpirationDate' => {
        Name   => 'ExpirationDate',
        Groups => { 2 => 'Time' },
        %timeInfo, Unknown => 1
    },
    'WM/WMRVExpirationSpan' =>
      { Name => 'ExpirationSpan', Notes => 'unknown units', Unknown => 1 },
    'WM/WMRVHDContent'               => { Name => 'HDContent', %bool },
    'WM/WMRVHardPostPadding'         => 'HardPostPadding',
    'WM/WMRVHardPrePadding'          => 'HardPrePadding',
    'WM/WMRVInBandRatingAttributes'  => 'InBandRatingAttributes',
    'WM/WMRVInBandRatingLevel'       => 'InBandRatingLevel',
    'WM/WMRVInBandRatingSystem'      => 'InBandRatingSystem',
    'WM/WMRVKeepUntil'               => 'KeepUntil',
    'WM/WMRVOriginalSoftPostPadding' => 'OriginalSoftPostPadding',
    'WM/WMRVOriginalSoftPrePadding'  => 'OriginalSoftPrePadding',
    'WM/WMRVProgramID'               => 'ProgramID',
    'WM/WMRVQuality'                 => 'Quality',
    'WM/WMRVRequestID'               => 'RequestID',
    'WM/WMRVScheduleItemID'          => 'ScheduleItemID',
    'WM/WMRVSeriesUID'               => 'SeriesUID',
    'WM/WMRVServiceID'               => 'ServiceID',
    'WM/WMRVWatched'                 => { Name => 'Watched', %bool },
);

sub ReadSectors($$$$) {
    my ( $raf, $secPt, $pos, $secSize ) = @_;
    my ( $data, $buff );
    while ( $pos <= length($$secPt) - 4 ) {
        my $sec = Get32u( $secPt, $pos );
        return undef if $sec == 0xffff;
        last unless $sec;
        defined($data) ? ( $data .= $buff ) : ( $data = $buff );
        return undef
          unless $raf->Seek( $sec * $secSize, 0 )
          and $raf->Read( $buff, $secSize ) == $secSize;
        $pos += 4;
    }
    return defined($data) ? $data . $buff : $buff;
}

sub ProcessMetadata($$$) {
    my ( $et, $dirInfo, $tagTablePtr ) = @_;
    my $dataPt = $$dirInfo{DataPt};
    my $pos    = 0;
    my $end    = length $$dataPt;
    $et->VerboseDir( 'WTV Metadata', undef, $end );
    while ( $pos + 0x18 < $end ) {
        last
          unless substr( $$dataPt, $pos, 16 ) eq
          "\x5a\xfe\xd7\x6d\xc8\x1d\x8f\x4a\x99\x22\xfa\xb1\x1c\x38\x14\x53";
        my $fmt = Get32u( $dataPt, $pos + 0x10 );
        my $len = Get32u( $dataPt, $pos + 0x14 );
        my $str = '';
        $pos += 0x18;
        for ( ; ; ) {
            $pos + 2 > $end and $et->Warn('Corrupt metadata directory'), last;
            my $ch = substr( $$dataPt, $pos, 2 );
            $pos += 2;
            last if $ch eq "\0\0";
            $str .= $ch;
        }
        last if $pos + $len > $end;
        my $tag = $et->Decode( $str, 'UTF16', undef, 'UTF8' );
        my $dat = substr( $$dataPt, $pos, $len );
        unless ( $$tagTablePtr{$tag} ) {
            my $name = $tag;
            $name =~ s{^(WTV_Metadata_)?WM/(WMRV)?}{};
            AddTagToTable( $tagTablePtr, $tag, $name );
            $et->VPrint( 0, $$et{INDENT}, "[adding WTV:$name]\n" );
        }
        my $val;
        if ( $fmt == 0 or $fmt == 3 ) {
            $val = Get32s( \$dat, 0 );
        }
        elsif ( $fmt == 1 ) {
            $val = $et->Decode( $dat, 'UTF16' );
        }
        elsif ( $fmt == 6 ) {
            $val = unpack( 'H*', $dat );
        }
        elsif ( $fmt == 4 ) {
            $val = Get64u( \$dat, 0 );
        }
        else {
            $val = $dat;
            $fmt = "Unknown($fmt)";
        }
        $et->HandleTag(
            $tagTablePtr, $tag, $val,
            Format => "format $fmt",
            Size   => length $dat,
        );
        $et->VerboseDump( \$dat );
        $pos += $len;
    }
}

sub ProcessWTV($$) {
    my ( $et, $dirInfo ) = @_;
    my $raf     = $$dirInfo{RAF};
    my $verbose = $et->Options('Verbose');
    my ( $buff, $tagTablePtr, $pos, $len );

    return 0 unless $raf->Read( $buff, 0x60 ) == 0x60;
    return 0
      unless $buff =~
      /^\xb7\xd8\x00\x20\x37\x49\xda\x11\xa6\x4e\x00\x07\xe9\x5e\xad\x8d/;
    $et->SetFileType();
    SetByteOrder('II');
    my $secSize = Get32u( \$buff, 0x28 );
    $secSize = 0x1000 unless $secSize == 0x1000 or $secSize == 0x100;
    $buff    = ReadSectors( $raf, \$buff, 0x38, $secSize );
    return 0 unless defined $buff;
    $tagTablePtr = GetTagTable('Image::ExifTool::WTV::Main');
    $et->VerboseDir('WTV');

    for ( $pos = 0 ; $pos < length($buff) - 0x28 ; $pos += $len ) {
        unless (
            substr( $buff, $pos, 0x10 ) eq
            "\x92\xb7\x74\x91\x59\x70\x70\x44\x88\xdf\x06\x3b\x82\xcc\x21\x3d" )
        {
            $et->Warn("WTV directory wasn't at expected location") unless $pos;
            last;
        }
        $len = Get32u( \$buff, $pos + 0x10 );
        last if $pos + $len > length($buff);
        my $n = Get32u( \$buff, $pos + 0x20 );
        0x28 + $n * 2 + 8 > $len and $et->Warn('WTV directory error'), last;
        my $tag = $et->Decode( substr( $buff, $pos + 0x28, $n * 2 ),
            'UTF16', undef, 'UTF8' );
        my $ptr = $pos + 0x28 + $n * 2;
        my $flg = Get32u( \$buff, $ptr + 4 );

        if ($verbose) {
            my $s = Get32s( \$buff, $ptr );
            $s = sprintf( '0x%x', $s ) unless $s < 0;
            $et->VPrint( 1, "- Tag '${tag}' (sector=$s, flag=$flg)" );
        }
        next unless $$tagTablePtr{$tag} and ( $flg == 0 or $flg == 1 );
        my $sec  = substr( $buff, $ptr, 4 );
        my $data = ReadSectors( $raf, \$sec, 0, $secSize );
        last unless defined $data;
        $data = ReadSectors( $raf, \$data, 0, $secSize ) if $flg == 1;
        defined $data or $et->Warn("Error fetching data for $tag"), next;
        $et->HandleTag( $tagTablePtr, $tag, $data );
    }
    return 1;
}

1;

__END__


