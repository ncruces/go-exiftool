package Image::ExifTool::LigoGPS;

use strict;
use vars qw($VERSION);
use Image::ExifTool;

$VERSION = '1.06';

sub ProcessLigoGPS($$$;$);
sub ProcessLigoJSON($$$);
sub OrderCipherDigits($$$;$);

my $knotsToKph = 1.852;

sub CleanupCipher($) {
    my $et = shift;
    if ( $$et{LigoCipher} and $$et{LigoCipher}{'next'} ) {
        $et->Warn(
            'Not enough GPS points to determine cipher for decoding LIGOGPSINFO'
        );
    }
    delete $$et{LigoCipher};
}

sub UnfuzzLigoGPS($$$) {
    my ( $lat, $lon, $scl ) = @_;
    my $lat2 = int( $lat / 10 ) * 10;
    my $lon2 = int( $lon / 10 ) * 10;
    return ( $lat2 + ( $lon - $lon2 ) * $scl, $lon2 + ( $lat - $lat2 ) * $scl );
}

sub DecryptLigoGPS($) {
    my $str = shift;
    my $num = unpack( 'x4V', $str );
    return undef if $num < 4;
    $num = 0x84 if $num > 0x84;
    my @in = unpack( "x8C$num", $str );
    my @out;
    while (@in) {
        my $b = shift @in;

        my $steeringBits = $b & 0xe0;
        if ( $steeringBits >= 0xc0 ) {
            return undef if @in < 4;
            push @out, ( shift(@in) | $b & 0x01 ) ^ 0x20,
              ( shift(@in) | $b & 0x02 ) ^ 0x20,
              ( shift(@in) | $b & 0x0c ) ^ 0x20,
              shift(@in) ^ 0x20 | $b & 0x30;
        }
        elsif ( $steeringBits >= 0x40 ) {
            return undef if @in < 3;
            if ( $steeringBits == 0x40 ) {
                push @out, 0x20,
                  ( shift(@in) | $b & 0x01 ) ^ 0x20,
                  ( shift(@in) | $b & 0x06 ) ^ 0x20,
                  ( shift(@in) | $b & 0x18 ) ^ 0x20;
            }
            elsif ( $steeringBits == 0x60 ) {
                push @out, ( shift(@in) | $b & 0x03 ) ^ 0x20,
                  0x20,
                  ( shift(@in) | $b & 0x04 ) ^ 0x20,
                  ( shift(@in) | $b & 0x18 ) ^ 0x20;
            }
            elsif ( $steeringBits == 0x80 ) {
                push @out, ( shift(@in) | $b & 0x03 ) ^ 0x20,
                  ( shift(@in) | $b & 0x0c ) ^ 0x20,
                  0x20,
                  ( shift(@in) | $b & 0x10 ) ^ 0x20;
            }
            else {
                push @out, ( shift(@in) | $b & 0x01 ) ^ 0x20,
                  ( shift(@in) | $b & 0x06 ) ^ 0x20,
                  ( shift(@in) | $b & 0x18 ) ^ 0x20,
                  0x20;
            }
        }
        elsif ( $steeringBits == 0x00 ) {
            return undef if @in < 1;
            push @out, shift(@in) | $b & 0x13;
        }
        else {
            return undef;
        }
    }
    return pack 'C*', @out;
}

sub OrderCipherDigits($$$;$) {
    my ( $ch, $next, $order, $did ) = @_;
    $did or $did = {};
    while ( $$next{$ch} ) {
        if ( @$order < 10 ) {
            last if $$did{$ch};
        }
        else {
            return 1 if @$order == 10 and $ch eq $$order[0];
            last;
        }
        push @$order, $ch;
        $$did{$ch} = 1;
        @{ $$next{$ch} } == 1 and $ch = $$next{$ch}[0], next;
        my $n = $#$order;
        foreach ( @{ $$next{$ch} } ) {
            my %did = %$did;
            return 1 if OrderCipherDigits( $_, $next, $order, \%did );
            $#$order = $n;
        }
        last;
    }
    return 0;
}

sub DecipherLigoGPS($$$;$) {
    my ( $et, $str, $tagTbl, $noFuzz ) = @_;

    $str =~
      m[^####.{4}([0-_])[0-_]{3}/[0-_]{2}/[0-_]{2} ..([0-_])..([0-_]).([0-_]) ]s
      or return undef;
    return undef unless $2 eq $3;

    my $cipherInfo = $$et{LigoCipher};
    unless ($cipherInfo) {
        $cipherInfo = $$et{LigoCipher} = { cache => [], 'next' => {} };
        $et->AddCleanup( \&CleanupCipher );
    }
    my $decipher = $$cipherInfo{decipher};
    my $cache    = $$cipherInfo{cache};

    unless ($decipher) {
        push @$cache, $str;
        my $next = $$cipherInfo{next};
        my ( $millennium, $colon, $ch2 ) = ( $1, $2, $4 );
        my $ch1 = $$cipherInfo{ch1};
        $$cipherInfo{ch1} = $ch2;
        return 1 if not defined $ch1 or $ch1 eq $ch2;
        if ( $$next{$ch1} ) {
            return 1 if grep /\Q$ch2\E/, @{ $$next{$ch1} };
            push @{ $$next{$ch1} }, $ch2;
        }
        else {
            $$next{$ch1} = [$ch2];
        }
        scalar( keys %$next ) < 10 and return 1;
        scalar( keys %$next ) > 10 and $$cipherInfo{'next'} = {}, return 1;
        my ( @order, $two );
        return 1 unless OrderCipherDigits( $ch1, $next, \@order );
        $order[$_] eq $millennium and $two = $_, last foreach 0 .. 9;
        defined $two or $et->Warn('Problem deciphering LIGOGPSINFO'), return 1;
        delete $$cipherInfo{'next'};
        my %decipher = ( $colon => ':' );

        foreach ( 0 .. 9 ) {
            my $ch = $order[ ( $_ + $two - 2 + 10 ) % 10 ];
            $decipher{$ch} = chr( $_ + 0x30 );
        }
        if ( $str =~ / ([0-_])$colon(-?).*? ([0-_])$colon(-?)/ ) {
            @decipher{ $1, $3 } = ( $2 ? 'S' : 'N', $4 ? 'W' : 'E' );
            unless ( $2 or $4 ) {
                my ( $ns, $ew ) = ( $1, $3 );
                if (    $$et{OPTIONS}{GPSQuadrant}
                    and $$et{OPTIONS}{GPSQuadrant} =~ /^([NS])([EW])$/i )
                {
                    @decipher{ $ns, $ew } = ( uc($1), uc($2) );
                }
                else {
                    $et->Warn(
                        'May need to set API GPSQuadrant option (eg. "NW")');
                }
            }
        }
        defined $decipher{$_}
          or $decipher{$_} = '?'
          foreach map( chr, 0x30 .. 0x5f );
        $decipher = $$cipherInfo{decipher} = \%decipher;
        $str      = shift @$cache;
    }

    do {
        my $pre = substr( $str, 4, 4 );
        ( $str = substr( $str, 8 ) ) =~ s/\0+$//;
        $str =~ s/([0-_])/$$decipher{$1}/g;
        if ( $$et{OPTIONS}{Verbose} > 1 ) {
            $et->VPrint( 1,
                    "$$et{INDENT}\(Deciphered: "
                  . unpack( 'H8', $pre )
                  . " $str)\n" );
        }
        ParseLigoGPS( $et, "$pre$str", $tagTbl, $noFuzz );
    } while $str = shift @$cache;

    return 1;
}

sub ParseLigoGPS($$$;$) {
    my ( $et, $str, $tagTbl, $flags ) = @_;

    unless ( $str =~
/^.{4}(\S+ \S+)\s+([NS?]):(-?)([.\d]+)\s+([EW?]):(-?)([\.\d]+)\s+([.\d]+)/s
      )
    {
        $et->Warn('LIGOGPSINFO format error');
        return;
    }
    $flags or $flags = 0;
    my ( $time, $latRef, $latNeg, $lat, $lonRef, $lonNeg, $lon, $spd ) =
      ( $1, $2, $3, $4, $5, $6, $7, $8 );
    my %gpsScl = ( 1 => 1.524855137, 2 => 1.456027985, 3 => 1.15368 );
    my $spdScl =
      $flags & 0x01 ? ( $flags & 0x02 ? 1 : $knotsToKph ) : 1.85407333;
    $$et{DOC_NUM} = ++$$et{DOC_COUNT};
    $time =~ tr(/)(:);
    $lat =~ /^\d{3}/
      and Image::ExifTool::QuickTime::ConvertLatLon( $lat, $lon ), $spdScl = 1;

    unless ( $flags & 0x01 ) {
        my $scl = $$et{OPTIONS}{LigoGPSScale} || $$et{LigoGPSScale} || 1;
        $scl = $gpsScl{$scl} if $gpsScl{$scl};
        ( $lat, $lon ) = UnfuzzLigoGPS( $lat, $lon, $scl );
    }
    ( $lat > 90 or $lon > 180 )
      and $et->Warn('LIGOGPSINFO coordinates out of range'), return;
    $$et{SET_GROUP1} = 'LIGO';
    $et->HandleTag( $tagTbl, 'GPSDateTime', $time );
    $et->HandleTag( $tagTbl, 'GPSLatitude',
        $lat * ( ( $latNeg or $latRef eq 'S' ) ? -1 : 1 ) );
    $et->HandleTag( $tagTbl, 'GPSLongitude',
        $lon * ( ( $lonNeg or $lonRef eq 'W' ) ? -1 : 1 ) );
    $et->HandleTag( $tagTbl, 'GPSSpeed',          $spd * $spdScl );
    $et->HandleTag( $tagTbl, 'GPSTrack',          $1 ) if $str =~ /\bA:(\S+)/;
    $et->HandleTag( $tagTbl, 'GPSAltitude',       $1 ) if $str =~ /\bH:(\S+)/;
    $et->HandleTag( $tagTbl, 'MagneticVariation', $1 ) if $str =~ /\bM:(\S+)/;
    $et->HandleTag( $tagTbl, 'Accelerometer', "$1 $2 $3" )
      if $str =~ /x:(\S+)\sy:(\S+)\sz:(\S+)/;
    delete $$et{SET_GROUP1};
}

sub ProcessGKU($$$;$) {
    my ( $et, $dirInfo, $tagTbl ) = @_;
    my $dataPt = $$dirInfo{DataPt};
    my $pos    = unpack( 'V', $$dataPt );
    return 0
      if $pos + 13 > length $$dataPt
      or substr( $$dataPt, $pos, 13 ) ne 'LIGOGPSINFO {';
    pos($$dataPt) = $pos;
    return ProcessLigoJSON( $et, $dirInfo, $tagTbl );
}

sub ProcessLigoGPS($$$;$) {
    my ( $et, $dirInfo, $tagTbl, $noFuzz ) = @_;
    my $dataPt = $$dirInfo{DataPt};
    my $pos    = ( $$dirInfo{DirStart} || 0 ) + 0x14;
    return undef if $pos > length $$dataPt;
    my $cipherInfo = $$et{LigoCipher};
    my $dirName    = $$dirInfo{DirName} || 'LigoGPS';
    push @{ $$et{PATH} }, $dirName unless $$dirInfo{DirID};
    $noFuzz = 1 if substr( $$dataPt, $pos - 8, 4 ) =~ /^\0\0\0[\x01\x14]/;
    $et->VerboseDir($dirName);

    for ( ; $pos + 0x84 <= length($$dataPt) ; $pos += 0x84 ) {
        my $dat = substr( $$dataPt, $pos, 0x84 );
        unless ( $dat =~ /^####/ ) {
            next unless $dat =~ m(^.{4}\d{4}/\d{2}/\d{2} )s;

            $dat =~ s/\0+$//;
            ParseLigoGPS( $et, $dat, $tagTbl, 0x03 );
            next;
        }
              $cipherInfo
          and $$cipherInfo{decipher}
          and DecipherLigoGPS( $et, $dat, $tagTbl, $noFuzz )
          and next;
        my $str = DecryptLigoGPS($dat);
        defined $str or DecipherLigoGPS( $et, $dat, $tagTbl, $noFuzz ), next;
        $et->VPrint(
            1,
            "$$et{INDENT}\(Decrypted: ",
            unpack( 'V', $str ),
            ' ', substr( $str, 4 ), ")\n"
        ) if $$et{OPTIONS}{Verbose} > 1;
        ParseLigoGPS( $et, $str, $tagTbl, $noFuzz );
    }
    pop @{ $$et{PATH} } unless $$dirInfo{DirID};
    delete $$et{DOC_NUM};
    return 1;
}

sub ProcessLigoJSON($$$) {
    my ( $et, $dirInfo, $tagTbl ) = @_;
    my $dataPt = $$dirInfo{DataPt};
    my $dirLen = $$dirInfo{DirLen};
    require Image::ExifTool::Import;
    $et->VerboseDir( 'LIGO_JSON', undef,
        length($$dataPt) - ( pos($$dataPt) || 0 ) );
    $$et{SET_GROUP1} = 'LIGO';
    while ( $$dataPt =~ /LIGOGPSINFO (\{.*?\})/g ) {
        my $json = $1;
        my %dbase;
        Image::ExifTool::Import::ReadJSON( \$json, \%dbase );
        my $info = $dbase{'*'} or next;
        next unless defined $$info{status} and $$info{status} eq 'A';
        $$et{DOC_NUM} = ++$$et{DOC_COUNT};
        my $num = 0;
        defined $$info{$_} and ++$num
          foreach qw(Year Month Day Hour Minute Second);

        if ( $num == 6 ) {
            my $time = sprintf( '%.4d:%.2d:%.2d %.2d:%.2d:%.2dZ',
                @$info{qw{Year Month Day Hour Minute Second}} );
            $et->HandleTag( $tagTbl, GPSDateTime => $time );
        }
        if ( $$info{Latitude} and $$info{Longitude} ) {
            my $lat = $$info{Latitude};
            $lat = -$lat if $$info{NS} and $$info{NS} eq 'S';
            my $lon = $$info{Longitude};
            $lon = -$lon if $$info{EW} and $$info{EW} eq 'W';
            $et->HandleTag( $tagTbl, GPSLatitude  => $lat );
            $et->HandleTag( $tagTbl, GPSLongitude => $lon );
        }
        $et->HandleTag( $tagTbl, GPSSpeed => $$info{Speed} * $knotsToKph )
          if defined $$info{Speed};
        if (    defined $$info{GsensorX}
            and defined $$info{GsensorY}
            and defined $$info{GsensorZ} )
        {
            $et->HandleTag( $tagTbl,
                Accelerometer =>
                  "$$info{GsensorX} $$info{GsensorY} $$info{GsensorZ}" );
        }
        $num = 0;
        defined $$info{$_} and ++$num
          foreach qw(MYear MMonth MDay MHour MMinute MSecond);
        if ( $num == 6 ) {
            my $time = sprintf( '%.4d:%.2d:%.2d %.2d:%.2d:%.2d',
                @$info{qw{MYear MMonth MDay MHour MMinute MSecond}} );
            $et->HandleTag( $tagTbl, DateTimeOriginal => $time );
        }
        if ( defined $$info{OLatitude} and defined $$info{OLongitude} ) {
            my $lat = $$info{OLatitude};
            $lat = -$lat if $$info{NS} and $$info{NS} eq 'S';
            my $lon = $$info{OLongitude};
            $lon = -$lon if $$info{EW} and $$info{EW} eq 'W';
            $et->HandleTag( $tagTbl, GPSLatitude2  => $lat );
            $et->HandleTag( $tagTbl, GPSLongitude2 => $lon );
        }
        unless ( $et->Options('ExtractEmbedded') ) {
            $et->Warn(
                'Use the ExtractEmbedded option to extract all timed GPS', 3 );
            last;
        }
    }
    delete $$et{DOC_NUM};
    delete $$et{SET_GROUP1};
    return 1;
}

1;

__END__

