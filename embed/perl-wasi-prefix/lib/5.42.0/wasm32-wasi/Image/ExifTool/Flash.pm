
package Image::ExifTool::Flash;

use strict;
use vars            qw($VERSION);
use Image::ExifTool qw(:DataAccess :Utils);
use Image::ExifTool::FLAC;

$VERSION = '1.13';

sub ProcessMeta($$$;$);

my %processMetaPacket = ( onMetaData => 1, onXMPData => 1 );

%Image::ExifTool::Flash::Main = (
    GROUPS => { 2           => 'Video' },
    VARS   => { ALPHA_FIRST => 1 },
    NOTES  => q{
        The information below is extracted from SWF (Shockwave Flash) files.  Tags
        with string ID's represent information extracted from the file header.
    },
    FlashVersion => {},
    Compressed   => { PrintConv => { 0 => 'False', 1 => 'True' } },
    ImageWidth   => {},
    ImageHeight  => {},
    FrameRate    => {},
    FrameCount   => {},
    Duration     => {
        Notes     => 'calculated from FrameRate and FrameCount',
        PrintConv => 'ConvertDuration($val)',
    },
    69 => {
        Name      => 'FlashAttributes',
        PrintConv => {
            BITMASK => {
                0 => 'UseNetwork',
                3 => 'ActionScript3',
                4 => 'HasMetadata',
            }
        },
    },
    77 => {
        Name         => 'XMP',
        SubDirectory => { TagTable => 'Image::ExifTool::XMP::Main' },
    },
);

%Image::ExifTool::Flash::FLV = (
    NOTES => q{
        Information is extracted from the following packets in FLV (Flash Video)
        files.
    },
    0x08 => {
        Name         => 'Audio',
        BitMask      => 0x04,
        SubDirectory => { TagTable => 'Image::ExifTool::Flash::Audio' },
    },
    0x09 => {
        Name         => 'Video',
        BitMask      => 0x01,
        SubDirectory => { TagTable => 'Image::ExifTool::Flash::Video' },
    },
    0x12 => {
        Name         => 'Meta',
        SubDirectory => { TagTable => 'Image::ExifTool::Flash::Meta' },
    },
);

%Image::ExifTool::Flash::Audio = (
    PROCESS_PROC => \&Image::ExifTool::FLAC::ProcessBitStream,
    GROUPS       => { 2 => 'Audio' },
    NOTES        => 'Information extracted from the Flash Audio header.',
    'Bit0-3'     => {
        Name      => 'AudioEncoding',
        PrintConv => {
            0 => 'PCM-BE (uncompressed)',
            1 => 'ADPCM',
            2 => 'MP3',
            3 => 'PCM-LE (uncompressed)',
            4 => 'Nellymoser 16kHz Mono',
            5 => 'Nellymoser 8kHz Mono',
            6 => 'Nellymoser',
            7 => 'G.711 A-law logarithmic PCM',
            8 => 'G.711 mu-law logarithmic PCM',

            10 => 'AAC',
            11 => 'Speex',
            13 => 'MP3 8-Khz',
            15 => 'Device-specific sound',
        },
    },
    'Bit4-5' => {
        Name      => 'AudioSampleRate',
        ValueConv => {
            0 => 5512,
            1 => 11025,
            2 => 22050,
            3 => 44100,
        },
    },
    'Bit6' => {
        Name      => 'AudioBitsPerSample',
        ValueConv => '8 * ($val + 1)',
    },
    'Bit7' => {
        Name      => 'AudioChannels',
        ValueConv => '$val + 1',
        PrintConv => {
            1 => '1 (mono)',
            2 => '2 (stereo)',
        },
    },
);

%Image::ExifTool::Flash::Video = (
    PROCESS_PROC => \&Image::ExifTool::FLAC::ProcessBitStream,
    GROUPS       => { 2 => 'Video' },
    NOTES        => 'Information extracted from the Flash Video header.',
    'Bit4-7'     => {
        Name      => 'VideoEncoding',
        PrintConv => {
            1 => 'JPEG',
            2 => 'Sorensen H.263',
            3 => 'Screen Video',
            4 => 'On2 VP6',
            5 => 'On2 VP6 Alpha',
            6 => 'Screen Video 2',
            7 => 'H.264',
        },
    },
);

%Image::ExifTool::Flash::Meta = (
    PROCESS_PROC => \&ProcessMeta,
    GROUPS       => { 2 => 'Video' },
    NOTES        => q{
        Below are a few observed FLV Meta tags, but ExifTool will attempt to extract
        information from any tag found.
    },
    'audiocodecid'  => { Name => 'AudioCodecID', Groups => { 2 => 'Audio' } },
    'audiodatarate' => {
        Name      => 'AudioBitrate',
        Groups    => { 2 => 'Audio' },
        ValueConv => '$val * 1000',
        PrintConv => 'ConvertBitrate($val)',
    },
    'audiodelay'      => { Name => 'AudioDelay', Groups => { 2 => 'Audio' } },
    'audiosamplerate' =>
      { Name => 'AudioSampleRate', Groups => { 2 => 'Audio' } },
    'audiosamplesize' =>
      { Name => 'AudioSampleSize', Groups => { 2 => 'Audio' } },
    'audiosize'     => { Name => 'AudioSize', Groups => { 2 => 'Audio' } },
    'bytelength'    => 'ByteLength',
    'canseekontime' => 'CanSeekOnTime',
    'canSeekToEnd'  => 'CanSeekToEnd',
    'creationdate'  => {
        Name      => 'CreateDate',
        Groups    => { 2 => 'Time' },
        ValueConv => '$val=~s/\s+$//; $val',
    },
    'createdby' => 'CreatedBy',
    'cuePoints' => {
        Name         => 'CuePoint',
        SubDirectory => { TagTable => 'Image::ExifTool::Flash::CuePoint' },
    },
    'datasize' => 'DataSize',
    'duration' => {
        Name      => 'Duration',
        PrintConv => 'ConvertDuration($val)',
    },
    'filesize'  => 'FileSizeBytes',
    'framerate' => {
        Name      => 'FrameRate',
        PrintConv => 'int($val * 1000 + 0.5) / 1000',
    },
    'hasAudio'       => { Name => 'HasAudio', Groups => { 2 => 'Audio' } },
    'hasCuePoints'   => 'HasCuePoints',
    'hasKeyframes'   => 'HasKeyFrames',
    'hasMetadata'    => 'HasMetadata',
    'hasVideo'       => 'HasVideo',
    'height'         => 'ImageHeight',
    'httphostheader' => 'HTTPHostHeader',
    'keyframesTimes' => 'KeyFramesTimes',
    'keyframesFilepositions' => 'KeyFramePositions',
    'lasttimestamp'          => 'LastTimeStamp',
    'lastkeyframetimestamp'  => 'LastKeyFrameTime',
    'metadatacreator'        => 'MetadataCreator',
    'metadatadate'           => {
        Name      => 'MetadataDate',
        Groups    => { 2 => 'Time' },
        PrintConv => '$self->ConvertDateTime($val)',
    },
    'purl'       => 'URL',
    'pmsg'       => 'Message',
    'sourcedata' => 'SourceData',
    'starttime'  => {
        Name      => 'StartTime',
        PrintConv => 'ConvertDuration($val)',
    },
    'stereo'        => { Name => 'Stereo', Groups => { 2 => 'Audio' } },
    'totalduration' => {
        Name      => 'TotalDuration',
        PrintConv => 'ConvertDuration($val)',
    },
    'totaldatarate' => {
        Name      => 'TotalDataRate',
        ValueConv => '$val * 1000',
        PrintConv => 'int($val + 0.5)',
    },
    'totalduration' => 'TotalDuration',
    'videocodecid'  => 'VideoCodecID',
    'videodatarate' => {
        Name      => 'VideoBitrate',
        ValueConv => '$val * 1000',
        PrintConv => 'ConvertBitrate($val)',
    },
    'videosize' => 'VideoSize',
    'width'     => 'ImageWidth',
    'liveXML' => {
        Name         => 'XMP',
        SubDirectory => { TagTable => 'Image::ExifTool::XMP::Main' },
    },
);

%Image::ExifTool::Flash::CuePoint = (
    PROCESS_PROC => \&ProcessMeta,
    GROUPS       => { 2 => 'Video' },
    NOTES        => q{
        These tag names are added to the CuePoint name to generate complete tag
        names like "CuePoint0Name".
    },
    'name'       => 'Name',
    'type'       => 'Type',
    'time'       => 'Time',
    'parameters' => {
        Name         => 'Parameter',
        SubDirectory => { TagTable => 'Image::ExifTool::Flash::Parameter' },
    },
);

%Image::ExifTool::Flash::Parameter = (
    PROCESS_PROC => \&ProcessMeta,
    GROUPS       => { 2 => 'Video' },
    NOTES        => q{
        There are no pre-defined parameter tags, but ExifTool will extract any
        existing parameters, with tag names like "CuePoint0ParameterXxx".
    },
);

my @amfType = qw(double boolean string object movieClip null undefined reference
  mixedArray objectEnd array date longString unsupported recordSet
  XML typedObject AMF3data);

my %isStruct = ( 0x03 => 1, 0x08 => 1, 0x10 => 1 );

sub ProcessMeta($$$;$) {
    my ( $et, $dirInfo, $tagTablePtr, $single ) = @_;
    my $dataPt  = $$dirInfo{DataPt};
    my $dataPos = $$dirInfo{DataPos};
    my $dirLen  = $$dirInfo{DirLen} || length($$dataPt);
    my $pos     = $$dirInfo{Pos}    || 0;
    my ( $type, $val, $rec );

    $et->VerboseDir('Meta') unless $single;

  Record: for ( $rec = 0 ; ; ++$rec ) {
        last if $pos >= $dirLen;
        $type = ord( substr( $$dataPt, $pos ) );
        ++$pos;
        if ( $type == 0x00 or $type == 0x0b ) {
            last if $pos + 8 > $dirLen;
            $val = GetDouble( $dataPt, $pos );
            $pos += 8;
            if ( $type == 0x0b ) {
                $val /= 1000;

                last if $pos + 2 > $dirLen;
                my $tz = Get16s( $dataPt, $pos );
                $pos += 2;
                $val = Image::ExifTool::ConvertUnixTime( $val, 0, 6 );
                if ( $tz < 0 ) {
                    $val .= '-';
                    $tz *= -1;
                }
                else {
                    $val .= '+';
                }
                $val .= sprintf( '%.2d:%.2d', int( $tz / 60 ), $tz % 60 );
            }
        }
        elsif ( $type == 0x01 ) {
            last if $pos + 1 > $dirLen;
            $val = Get8u( $dataPt, $pos );
            $val = { 0 => 'No', 1 => 'Yes' }->{$val} if $val < 2;
            ++$pos;
        }
        elsif ( $type == 0x02 ) {
            last if $pos + 2 > $dirLen;
            my $len = Get16u( $dataPt, $pos );
            last if $pos + 2 + $len > $dirLen;
            $val = substr( $$dataPt, $pos + 2, $len );
            $pos += 2 + $len;
        }
        elsif ( $isStruct{$type} ) {
            $et->VPrint( 1, "  + [$amfType[$type]]\n" );
            my $getName;
            $val = '';
            if ( $type == 0x08 ) {

                last if $pos + 4 > $dirLen;
                $pos += 4;
            }
            elsif ( $type == 0x10 ) {
                $getName = 1;
            }
            for ( ; ; ) {
                last Record if $pos + 2 > $dirLen;
                my $len = Get16u( $dataPt, $pos );
                if ( $pos + 2 + $len > $dirLen ) {
                    $et->Warn("Truncated $amfType[$type] record");
                    last Record;
                }
                my $tag = substr( $$dataPt, $pos + 2, $len );
                $pos += 2 + $len;
                if ($getName) {
                    $et->VPrint( 1, "  | (object name '${tag}')\n" );
                    undef $getName;
                    next;
                }
                my $subTablePtr = $tagTablePtr;
                my $tagInfo     = $$subTablePtr{$tag};
                if ( $tagInfo and $$tagInfo{SubDirectory} ) {
                    my $subTable = $tagInfo->{SubDirectory}->{TagTable};
                    if ( $subTable =~ /^Image::ExifTool::Flash::/ ) {
                        $tag         = $$tagInfo{Name};
                        $subTablePtr = GetTagTable($subTable);
                    }
                }
                my $valPos = $pos + 1;
                $$dirInfo{Pos} = $pos;
                my $structName = $$dirInfo{StructName};
                $tag = $structName . ucfirst($tag) if defined $structName;
                $$dirInfo{StructName} = $tag;
                my ( $t, $v ) = ProcessMeta( $et, $dirInfo, $subTablePtr, 1 );
                $$dirInfo{StructName} = $structName;
                $pos = $$dirInfo{Pos};

                last Record unless defined $t and defined $v;
                next if $isStruct{$t};
                next if ref($v) eq 'ARRAY' and not @$v;
                last if $t == 0x09;
                if ( not $$subTablePtr{$tag} and $tag =~ /^\w+$/ ) {
                    AddTagToTable( $subTablePtr, $tag,
                        { Name => ucfirst($tag) } );
                    $et->VPrint( 1, "  | (adding $tag)\n" );
                }
                $et->HandleTag(
                    $subTablePtr, $tag, $v,
                    DataPt  => $dataPt,
                    DataPos => $dataPos,
                    Start   => $valPos,
                    Size    => $pos - $valPos,
                    Format  => $amfType[$t] || sprintf( '0x%x', $t ),
                );
            }
        }
        elsif ($type == 0x05
            or $type == 0x06
            or $type == 0x09
            or $type == 0x0d )
        {
            $val = '';
        }
        elsif ( $type == 0x07 ) {
            last if $pos + 2 > $dirLen;
            $val = Get16u( $dataPt, $pos );
            $pos += 2;
        }
        elsif ( $type == 0x0a ) {
            last if $pos + 4 > $dirLen;
            my $num = Get32u( $dataPt, $pos );
            $$dirInfo{Pos} = $pos + 4;
            my ( $i, @vals );
            my $structName = $$dirInfo{StructName};
            for ( $i = 0 ; $i < $num ; ++$i ) {
                $$dirInfo{StructName} = $structName . $i if defined $structName;
                my ( $t, $v ) = ProcessMeta( $et, $dirInfo, $tagTablePtr, 1 );
                last Record unless defined $v;
                push @vals, $v unless $isStruct{$t};
            }
            $$dirInfo{StructName} = $structName;
            $pos                  = $$dirInfo{Pos};
            $val                  = \@vals;
        }
        elsif ( $type == 0x0c or $type == 0x0f ) {
            last if $pos + 4 > $dirLen;
            my $len = Get32u( $dataPt, $pos );
            last if $pos + 4 + $len > $dirLen;
            $val = substr( $$dataPt, $pos + 4, $len );
            $pos += 4 + $len;
        }
        else {
            my $t = $amfType[$type] || sprintf( 'type 0x%x', $type );
            $et->Warn("AMF $t record not yet supported");
            undef $type;
            last;
        }
        last if $single;
        unless ( $isStruct{$type} ) {
            if ( $type == 0x02 and not $rec ) {
                my $verb = $processMetaPacket{$val} ? 'processing' : 'ignoring';
                $et->VPrint( 0, "  | ($verb $val information)\n" );
                last unless $processMetaPacket{$val};
            }
            else {
                my $t = $amfType[$type] || sprintf( 'type 0x%x', $type );
                $et->VPrint( 1, "  | (ignored lone $t value '${val}')\n" );
            }
        }
    }
    if ( not defined $val and defined $type ) {
        $et->Warn( sprintf( "Truncated AMF record 0x%x", $type ) );
    }
    return 1 unless $single;
    $$dirInfo{Pos} = $pos;
    return ( $type, $val );
}

sub ProcessFLV($$) {
    my ( $et, $dirInfo ) = @_;
    my $verbose = $et->Options('Verbose');
    my $raf     = $$dirInfo{RAF};
    my $buff;

    $raf->Read( $buff, 9 ) == 9 or return 0;
    $buff =~ /^FLV\x01/         or return 0;
    SetByteOrder('MM');
    $et->SetFileType();
    my ( $flags, $offset ) = unpack( 'x4CN', $buff );
    $raf->Seek( $offset - 9, 1 ) or return 1 if $offset > 9;
    $flags &= 0x05;
    my $found       = 0;
    my $tagTablePtr = GetTagTable('Image::ExifTool::Flash::FLV');

    for ( ; ; ) {
        $raf->Read( $buff, 15 ) == 15 or last;
        my $len  = unpack( 'x4N', $buff );
        my $type = $len >> 24;
        $len &= 0x00ffffff;
        my $tagInfo = $et->GetTagInfo( $tagTablePtr, $type );
        if ( $verbose > 1 ) {
            my $name = $tagInfo ? $$tagInfo{Name} : "type $type";
            $et->VPrint( 1, "FLV $name packet, len $len\n" );
        }
        undef $buff;
        if ( $tagInfo and $$tagInfo{SubDirectory} ) {
            my $mask = $$tagInfo{BitMask};
            if ($mask) {
                unless ( $found & $mask ) {
                    $found |= $mask;
                    $flags &= ~$mask;
                    if ( $len >= 1 and $raf->Read( $buff, 1 ) == 1 ) {
                        $len -= 1;
                    }
                    else {
                        $et->Warn("Bad $$tagInfo{Name} packet");
                        last;
                    }
                }
            }
            elsif ( $raf->Read( $buff, $len ) == $len ) {
                $len = 0;
            }
            else {
                $et->Warn('Truncated Meta packet');
                last;
            }
        }
        if ( defined $buff ) {
            $et->HandleTag(
                $tagTablePtr, $type, undef,
                DataPt  => \$buff,
                DataPos => $raf->Tell() - length($buff),
            );
        }
        last unless $flags;
        $raf->Seek( $len, 1 ) or last if $len;
    }
    return 1;
}

sub FoundFlashTag($$$) {
    my ( $et, $tag, $val ) = @_;
    $et->HandleTag( \%Image::ExifTool::Flash::Main, $tag, $val );
}

sub ReadCompressed($$$$) {
    my ( $raf, $len, $inflate ) = ( $_[0], $_[2], $_[3] );
    my $buff;
    unless ( $raf->Read( $buff, $len ) ) {
        $_[3] = 'Error reading file';
        return 0;
    }
    if ($inflate) {
        unless ( ref $inflate ) {
            unless ( eval { require Compress::Zlib } ) {
                $_[3] =
                  'Install Compress::Zlib to extract compressed information';
                return 0;
            }
            $inflate = Compress::Zlib::inflateInit();
            unless ($inflate) {
                $_[3] = 'Error initializing inflate for Flash data';
                return 0;
            }
            $_[3] = $inflate;
        }
        my $tmp = $buff;
        $buff = '';
        for ( ; ; ) {
            my ( $dat, $stat ) = $inflate->inflate($tmp);
            if (   $stat == Compress::Zlib::Z_STREAM_END()
                or $stat == Compress::Zlib::Z_OK() )
            {
                $buff .= $dat;
                last
                  if length $buff >= $len
                  or $stat == Compress::Zlib::Z_STREAM_END();
                $raf->Read( $tmp, 64 ) or last;
            }
            else {
                $buff = '';
                last;
            }
        }
        $_[3] = 'Error inflating compressed Flash data' unless length $buff;
    }
    $_[1] = defined $_[1] ? $_[1] . $buff : $buff;
    return length $buff;
}

sub ProcessSWF($$) {
    my ( $et, $dirInfo ) = @_;
    my $raf = $$dirInfo{RAF};
    my ( $buff, $hasMeta );

    $raf->Read( $buff, 8 ) == 8 or return 0;
    $buff =~ /^(F|C)WS([^\0])/  or return 0;
    my ( $compressed, $vers ) = ( $1 eq 'C' ? 1 : 0, ord($2) );

    SetByteOrder('II');
    $et->SetFileType();
    GetTagTable('Image::ExifTool::Flash::Main');

    FoundFlashTag( $et, FlashVersion => $vers );
    FoundFlashTag( $et, Compressed   => $compressed );

    $buff = '';
    unless ( ReadCompressed( $raf, $buff, 64, $compressed ) ) {
        $et->Warn($compressed) if $compressed;
        return 1;
    }

    my $nBits   = unpack( 'C', $buff ) >> 3;
    my $totBits = 5 + $nBits * 4;
    my $nBytes  = int( ( $totBits + 7 ) / 8 );
    if ( length $buff < $nBytes + 4 ) {
        $et->Warn('Truncated Flash file');
        return 1;
    }
    my $bits = unpack( "B$totBits", $buff );
    my @vals = unpack( 'x5' . "a$nBits" x 4, $bits );
    map { $_ = unpack( 'N', pack( 'B32', '0' x ( 32 - length $_ ) . $_ ) ) }
      @vals;

    FoundFlashTag( $et, ImageWidth  => ( $vals[1] - $vals[0] ) / 20 );
    FoundFlashTag( $et, ImageHeight => ( $vals[3] - $vals[2] ) / 20 );

    @vals = unpack( "x${nBytes}v2", $buff );
    FoundFlashTag( $et, FrameRate  => $vals[0] / 256 );
    FoundFlashTag( $et, FrameCount => $vals[1] );
    FoundFlashTag( $et, Duration   => $vals[1] * 256 / $vals[0] ) if $vals[0];

    $buff = substr( $buff, $nBytes + 4 );
    for ( ; ; ) {
        my $buffLen = length $buff;
        last if $buffLen < 2;
        my $code = Get16u( \$buff, 0 );
        my $pos  = 2;
        my $tag  = $code >> 6;
        my $size = $code & 0x3f;
        $et->VPrint( 1, "SWF tag $tag ($size bytes):\n" );
        last unless $tag == 69 or $tag == 77 or $hasMeta;

        if ( $pos + $size > $buffLen ) {
            unless ( ReadCompressed( $raf, $buff, $size + 2, $compressed ) ) {
                $et->Warn($compressed) if $compressed;
                return 1;
            }
            $buffLen = length $buff;
            last if $pos + $size > $buffLen;
        }
        if ( $size == 0x3f ) {
            last if $pos + 4 > $buffLen;
            $size = Get32u( \$buff, $pos );
            $pos += 4;
            last if $size > 1000000;
            if ( $pos + $size > $buffLen ) {
                unless ( ReadCompressed( $raf, $buff, $size + 2, $compressed ) )
                {
                    $et->Warn($compressed) if $compressed;
                    return 1;
                }
                $buffLen = length $buff;
                last if $pos + $size > $buffLen;
            }
            $et->VPrint( 1, "  [extended size $size bytes]\n" );
        }
        if ( $tag == 69 ) {
            last unless $size;
            my $flags = Get8u( \$buff, $pos );
            FoundFlashTag( $et, $tag => $flags );
            last unless $flags & 0x10;
            $hasMeta = 1;
        }
        elsif ( $tag == 77 ) {
            my $val = substr( $buff, $pos, $size );
            FoundFlashTag( $et, $tag => $val );
            last;
        }
        last if $pos + 2 > $buffLen;
        $buff = substr( $buff, $pos );
    }
    return 1;
}

1;

__END__


