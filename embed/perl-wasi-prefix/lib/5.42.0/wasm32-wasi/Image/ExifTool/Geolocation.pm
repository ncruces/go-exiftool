
package Image::ExifTool::Geolocation;

use strict;
use vars qw($VERSION $geoDir $altDir $dbInfo);

$VERSION = '1.10';

my $debug;

sub ReadDatabase($);
sub SortDatabase($);
sub AddEntry(@);
sub GetEntry($;$$);
sub Geolocate($;$);

my ( @cityList,   @countryList, @regionList,   @subregionList, @timezoneList );
my ( %countryNum, %regionNum,   %subregionNum, %timezoneNum );
my ( @sortOrder, @altNames, %langLookup, $nCity, %featureCodes, %featureTypes );
my ( $lastArgs,  %lastFound, @lastByPop, @lastByLat );
my $dbVer       = '1.03';
my $sortedBy    = 'Latitude';
my $pi          = 3.1415926536;
my $earthRadius = 6371;
my @featureCodes = qw(Other PPL PPLA PPLA2 PPLA3 PPLA4 PPLA5 PPLC
  PPLCH PPLF PPLG PPLL PPLR PPLS STLMT PPLX);

my $defaultDir = $INC{'Image/ExifTool/Geolocation.pm'};
if ($defaultDir) {
    $defaultDir =~ s(/Geolocation\.pm$)();
}
else {
    $defaultDir = '.';
    warn("Error getting Geolocation.pm directory\n");
}

unless ( defined $geoDir and not $geoDir ) {
    unless ( $geoDir and ReadDatabase("$geoDir/Geolocation.dat") ) {
        ReadDatabase("$defaultDir/Geolocation.dat");
    }
}

$geoDir = $defaultDir unless defined $geoDir;
if ( not defined $altDir and $geoDir and -e "$geoDir/AltNames.dat" ) {
    $altDir = $geoDir;
}

if (@Image::ExifTool::UserDefined::Geolocation) {
    AddEntry(@$_) foreach @Image::ExifTool::UserDefined::Geolocation;
}

sub ReadDatabase($) {
    my $datfile = shift;
    open DATFILE, "< $datfile" or warn("Error reading $datfile\n"), return 0;
    binmode DATFILE;
    my $line = <DATFILE>;
    unless ( $line =~ /^Geolocation(\d+\.\d+)\t(\d+)/ ) {
        warn("Bad format Geolocation database\n");
        close(DATFILE);
        return 0;
    }
    ( $dbVer, $nCity ) = ( $1, $2 );
    if ( $dbVer !~ /^1\.0[23]$/ ) {
        my $which = $dbVer < 1.03 ? 'database' : 'ExifTool';
        warn("Incompatible Geolocation database (update your $which)\n");
        close(DATFILE);
        return 0;
    }
    my $comment = <DATFILE>;
    defined $comment and $comment =~ / (\d+) / or close(DATFILE), return 0;
    $dbInfo = "$datfile v$dbVer: $nCity cities with population > $1";
    my $isUserDefined = @Image::ExifTool::UserDefined::Geolocation;

    undef @altNames;

    undef @cityList;
    my $i = 0;
    for ( ; ; ) {
        $line = <DATFILE>;
        last if length($line) == 6 and $line =~ /\0\0\0\0/;
        $line .= <DATFILE> while length($line) < 14;
        chomp $line;
        push @cityList, $line;
    }
    @cityList == $nCity
      or warn("Bad number of entries in Geolocation database\n"), return 0;
    for ( ; ; ) {
        $line = <DATFILE>;
        last if length($line) == 6 and $line =~ /\0\0\0\0/;
        chomp $line;
        push @countryList, $line;
        $countryNum{ lc substr( $line, 0, 2 ) } = $#countryList
          if $isUserDefined;
    }
    for ( ; ; ) {
        $line = <DATFILE>;
        last if length($line) == 6 and $line =~ /\0\0\0\0/;
        chomp $line;
        push @regionList, $line;
        $regionNum{ lc $line } = $#regionList if $isUserDefined;
    }
    for ( ; ; ) {
        $line = <DATFILE>;
        last if length($line) == 6 and $line =~ /\0\0\0\0/;
        chomp $line;
        push @subregionList, $line;
        $subregionNum{ lc $line } = $#subregionList if $isUserDefined;
    }
    for ( ; ; ) {
        $line = <DATFILE>;
        last if length($line) == 6 and $line =~ /\0\0\0\0/;
        chomp $line;
        push @timezoneList, $line;
        $timezoneNum{ lc $line } = $#timezoneList if $isUserDefined;
    }
    if ( $line eq "\0\0\0\0\x05\n" ) {
        undef @featureCodes;
        for ( ; ; ) {
            $line = <DATFILE>;
            last if length($line) == 6 and $line =~ /\0\0\0\0/;
            chomp $line;
            $featureTypes{$line} = $1 if $line =~ s/ (.*)//;
            push @featureCodes, $line;
        }
    }
    close DATFILE;
    $i            = 0;
    %featureCodes = map { lc($_) => $i++ } @featureCodes;
    return 1;
}

sub ReadAltNames() {
    my $success;
    if ( $altDir and $nCity ) {
        if ( open ALTFILE, "< $altDir/AltNames.dat" ) {
            binmode ALTFILE;
            local $/ = "\0";
            my $i = 0;
            while (<ALTFILE>) { chop; $altNames[ $i++ ] = $_; }
            close ALTFILE;
            if ( $i == $nCity ) {
                $success = 1;
            }
            else {
                warn("Bad number of entries in AltNames database\n");
                undef @altNames;
            }
        }
        else {
            warn "Error reading $altDir/AltNames.dat\n";
        }
        undef $altDir;
    }
    return $success;
}

sub ClearLastMatches() {
    undef $lastArgs;
    undef %lastFound;
    undef @lastByPop;
    undef @lastByLat;
}

sub SortDatabase($) {
    my $field = shift;
    return 1 if $field eq $sortedBy;
    undef @sortOrder;
    if ( $field eq 'Latitude' ) {
        @sortOrder = sort { $cityList[$a] cmp $cityList[$b] } 0 .. $#cityList;
    }
    elsif ( $field eq 'City' ) {
        @sortOrder =
          sort { substr( $cityList[$a], 13 ) cmp substr( $cityList[$b], 13 ) }
          0 .. $#cityList;
    }
    elsif ( $field eq 'Country' ) {
        my %lkup;
        foreach ( 0 .. $#cityList ) {
            my $city = substr( $cityList[$_], 13 );
            my $ctry =
              substr( $countryList[ ord substr( $cityList[$_], 5, 1 ) ], 2 );
            $lkup{$_} = "$ctry $city";
        }
        @sortOrder = sort { $lkup{$a} cmp $lkup{$b} } 0 .. $#cityList;
    }
    else {
        return 0;
    }
    $sortedBy = $field;
    ClearLastMatches();
    return 1;
}

sub AddEntry(@) {
    my (
        $city, $region, $subregion, $cc,  $country, $timezone,
        $fc,   $pop,    $lat,       $lon, $altNames
    ) = @_;
    @_ < 10
      and warn(
        "Too few arguments in $city definition (check for updated format)\n"),
      return 0;
    length($cc) != 2
      and warn("Country code '${cc}' is not 2 characters\n"), return 0;
    $featureTypes{$fc} = $1 if $fc =~ s/ (.*)//;
    my $fn = $featureCodes{ lc $fc };
    unless ( defined $fn ) {
        if ( $dbVer eq '1.02' or @featureCodes > 0x3f or not length $fc ) {
            $fn = 0;
        }
        else {
            push @featureCodes, uc($fc);
            $featureCodes{ lc $fc } = $fn = $#featureCodes;
        }
    }
    chomp $lon;

    unless (%countryNum) {
        my $i;
        $i = 0;
        $countryNum{ lc substr( $_, 0, 2 ) } = $i++ foreach @countryList;
        $i                     = 0;
        $regionNum{ lc $_ }    = $i++ foreach @regionList;
        $i                     = 0;
        $subregionNum{ lc $_ } = $i++ foreach @subregionList;
        $i                     = 0;
        $timezoneNum{ lc $_ }  = $i++ foreach @timezoneList;
    }
    my $cn = $countryNum{ lc $cc };
    unless ( defined $cn ) {
        $#countryList >= 0xff
          and warn("AddEntry: Too many countries\n"), return 0;
        push @countryList, "$cc$country";
        $cn = $countryNum{ lc $cc } = $#countryList;
    }
    elsif ($country) {
        $countryList[$cn] = "$cc$country";
    }
    my $tn = $timezoneNum{ lc $timezone };
    unless ( defined $tn ) {
        $#timezoneList >= 0x1ff
          and warn("AddEntry: Too many time zones\n"), return 0;
        push @timezoneList, $timezone;
        $tn = $timezoneNum{ lc $timezone } = $#timezoneList;
    }
    my $rn = $regionNum{ lc $region };
    unless ( defined $rn ) {
        $#regionList >= 0xfff
          and warn("AddEntry: Too many regions\n"), return 0;
        push @regionList, $region;
        $rn = $regionNum{ lc $region } = $#regionList;
    }
    my $sn = $subregionNum{ lc $subregion };
    unless ( defined $sn ) {
        my $max = $dbVer eq '1.02' ? 0x7fff : 0xffff;
        $#subregionList >= $max
          and warn("AddEntry: Too many subregions\n"), return 0;
        push @subregionList, $subregion;
        $sn = $subregionNum{ lc $subregion } = $#subregionList;
    }
    $pop = sprintf( '%.1e', $pop );

    my $code = ( $cn << 24 ) | ( substr( $pop, -1, 1 ) << 20 ) |
      ( substr( $pop, 0, 1 ) << 16 ) | ( substr( $pop, 2, 1 ) << 12 ) | $rn;
    if ( $tn > 255 ) {
        if ( $dbVer eq '1.02' ) {
            $sn |= 0x8000;
        }
        else {
            $fn |= 0x80;
        }
        $tn -= 256;
    }
    $lat = int( ( $lat + 90 ) / 180 * 0x100000 + 0.5 ) & 0xfffff;
    $lon = int( ( $lon + 180 ) / 360 * 0x100000 + 0.5 ) & 0xfffff;
    my $hdr = pack( 'nCnNnCC',
        $lat >> 4, ( ( $lat & 0x0f ) << 4 ) | ( $lon & 0x0f ),
        $lon >> 4, $code, $sn, $tn, $fn );
    push @cityList, "$hdr$city";
    if ($altNames) {
        chomp $altNames;
        $altNames =~ tr/,/\n/;
        foreach ( 11 .. $#_ ) {
            chomp $_[$_];
            $altNames .= "\n$_[$_]";
        }
        $altNames[$#cityList] = $altNames;
    }
    $sortedBy = '';
    undef $lastArgs;
    return 1;
}

sub GetEntry($;$$) {
    my ( $entryNum, $lang, $sort ) = @_;
    return ()                         if $entryNum > $#cityList;
    $entryNum = $sortOrder[$entryNum] if $sort and @sortOrder > $entryNum;
    my ( $lt, $f, $ln, $code, $sn, $tn, $fn ) =
      unpack( 'nCnNnCC', $cityList[$entryNum] );
    my $city = substr( $cityList[$entryNum], 13 );
    my $ctry = $countryList[ $code >> 24 ];
    my $rgn  = $regionList[ $code & 0x0fff ];
    if ( $dbVer eq '1.02' ) {
        $sn & 0x8000 and $tn += 256, $sn &= 0x7fff;
    }
    else {
        $fn & 0x80 and $tn += 256;
    }
    my $sub = $subregionList[$sn];
    my $pop =
      (     ( $code >> 16 & 0x0f ) . '.'
          . ( $code >> 12 & 0x0f ) . 'e+'
          . ( $code >> 20 & 0x0f ) ) + 0;
    $lt =
      sprintf( '%.4f', ( ( $lt << 4 ) | ( $f >> 4 ) ) * 180 / 0x100000 - 90 );
    $ln = sprintf( '%.4f',
        ( ( $ln << 4 ) | ( $f & 0x0f ) ) * 360 / 0x100000 - 180 );
    my $fc      = $featureCodes[ $fn & 0x3f ] || 'Other';
    my $cc      = substr( $ctry, 0, 2 );
    my $country = substr( $ctry, 2 );
    my $ft      = $featureTypes{$fc};

    if ( $lang and $lang ne 'en' ) {
        my $xlat = $langLookup{$lang};
        if ( not defined $xlat ) {
            unshift @INC, $geoDir;
            if ( eval "require 'GeoLang/$lang.pm'" ) {
                my $trans = "Image::ExifTool::GeoLang::${lang}::Translate";
                no strict 'refs';
                $xlat = \%$trans if %$trans;
            }
            shift @INC;
            if (%Image::ExifTool::Geolocation::geoLang) {
                my $userLang = $Image::ExifTool::Geolocation::geoLang{$lang};
                if ( $userLang and ref($userLang) eq 'HASH' ) {
                    if ($xlat) {
                        $$xlat{$_} = $$userLang{$_} foreach keys %$userLang;
                    }
                    else {
                        $xlat = $userLang;
                    }
                }
            }
            $langLookup{$lang} = $xlat || 0;
        }
        if ($xlat) {
            my $r2 = $rgn;
            $city =
                 $$xlat{"$cc$r2,$sub,$city"}
              || $$xlat{"$cc$r2,$city"}
              || $$xlat{"$cc,$city"}
              || $$xlat{",$city"}
              || $$xlat{$city}
              || $city;
            $sub     = $$xlat{"$cc$rgn,$sub,"} || $$xlat{$sub}     || $sub;
            $rgn     = $$xlat{"$cc$rgn,"}      || $$xlat{$rgn}     || $rgn;
            $country = $$xlat{"$cc,"}          || $$xlat{$country} || $country;
            $ft      = $$xlat{$fc} if $$xlat{$fc};
        }
    }
    return ( $city, $rgn, $sub, $cc, $country, $timezoneList[$tn], $fc, $pop,
        $lt, $ln, $ft );
}

sub GetAltNames($;$) {
    my ( $entryNum, $sort ) = @_;
    $entryNum = $sortOrder[$entryNum] if $sort and @sortOrder > $entryNum;
    my $alt = $altNames[$entryNum] or return '';
    $alt =~ tr/\n/,/;
    return $alt;
}

sub Geolocate($;$) {
    my ( $arg,    $opts ) = @_;
    my ( $city,   @exact,    %regex,    @multiCity,  $other,    $idx,  @cargs );
    my ( $minPop, $minDistU, $minDistC, @matchParms, @coords,   %fcOK, $both );
    my ( $pop,    $maxDist,  $multi,    $fcodes,     $altNames, @startTime );

    $opts
      and ( $pop, $maxDist, $multi, $fcodes, $altNames ) =
      @$opts{
        qw(GeolocMinPop GeolocMaxDist GeolocMulti GeolocFeature GeolocAltNames)
      };

    if ($debug) {
        require Time::HiRes;
        @startTime = Time::HiRes::gettimeofday();
    }
    @cityList or warn('No Geolocation database'), return ();
    if ($pop) {
        $pop = sprintf( '%.1e', $pop );
        $minPop =
            chr( ( substr( $pop, -1, 1 ) << 4 ) | ( substr( $pop, 0, 1 ) ) )
          . chr( substr( $pop, 2, 1 ) << 4 );
    }
    if ($fcodes) {
        my $neg    = $fcodes =~ s/^-//;
        my @fcodes = split /\s*,-?\s*/, lc $fcodes;
        if ($neg) {
            $fcOK{$_} = 1 foreach 0 .. $#featureCodes;
            defined $featureCodes{$_} and delete $fcOK{ $featureCodes{$_} }
              foreach @fcodes;
        }
        else {
            defined $featureCodes{$_} and $fcOK{ $featureCodes{$_} } = 1
              foreach @fcodes;
        }
    }
    my $num = 1;
    $arg =~ s/^\s+//;
    $arg =~ s/\s+$//;
    my @args = split /\s*,\s*/, $arg;
    my %ri   = ( cc => 0, co => 1, re => 2, sr => 3, ci => 8, '' => 9 );
    foreach (@args) {
        if (m{^(-)?(\w{2})?/(.*)/(i?)$}) {
            my $re = $4 ? qr/$3/im : qr/$3/m;
            next if not defined( $idx = $ri{$2} );
            push @cargs, $_;
            $other = 1 if $idx < 5;
            $idx += 10 if $1;
            $regex{$idx} or $regex{$idx} = [];
            push @{ $regex{$idx} }, $re;
            $city = '' unless defined $city;
        }
        elsif (/^[-+]?\d+(\.\d+)?$/) {
            push @coords, $_ if @coords < 2;
        }
        elsif (
/^([-+]?\d+(?:\.\d+)?) *(([NS])[A-Z]*)? +([-+]?\d+(?:\.\d+)?) *(([EW])[A-Z]*)?/i
          )
        {
            next if @coords;
            my ( $lat, $lon ) = ( $1, $4 );
            $lat = -abs($lat) if $3 and uc($3) eq 'S';
            $lon = -abs($lon) if $6 and uc($6) eq 'W';
            push @coords, $lat, $lon;
        }
        elsif ( lc $_ eq 'both' ) {
            $both = 1;
        }
        elsif ( $_ =~ /^num=(\d+)$/i ) {
            $num = $1;
        }
        elsif ($_) {
            push @cargs, $_;
            if ($city) {
                push @exact, lc $_;
            }
            else {
                $city = lc $_;
            }
        }
    }
    unless ( defined $city or @coords == 2 ) {
        warn("Insufficient information to determine geolocation\n");
        return ();
    }
    SortDatabase('Latitude') if @coords == 2 and ( $both or not defined $city );
    while ( defined $city and ( @coords != 2 or $both ) ) {
        my $cargs =
          join( ',', @cargs, $pop || '', $maxDist || '', $fcodes || '' );
        my $i = 0;
        if ( $lastArgs and $lastArgs eq $cargs ) {
            $i = @cityList;
        }
        else {
            ClearLastMatches();
            $lastArgs = $cargs;
        }
        if ($altNames) {
            ReadAltNames() if $city and $altDir;
            $altNames = \@altNames;
        }
        else {
            $altNames = [];
        }
      Entry: for ( ; $i < @cityList ; ++$i ) {
            my $cty = substr( $cityList[$i], 13 );
            if ( $city and $city ne lc $cty ) {
                next unless $$altNames[$i] and $$altNames[$i] =~ /^$city$/im;
            }
            if ( $regex{8} ) { $cty =~ $_ or next Entry foreach @{ $regex{8} } }
            if ( $regex{18} ) {
                $cty !~ $_ or next Entry foreach @{ $regex{18} };
            }
            my ( $cd, $sn ) = unpack( 'x5Nn', $cityList[$i] );
            my $ct = $countryList[ $cd >> 24 ];
            $sn &= 0x7fff if $dbVer eq '1.02';
            my @geo = (
                substr( $ct, 0, 2 ),
                substr( $ct, 2 ),
                $regionList[ $cd & 0x0fff ],
                $subregionList[$sn]
            );
            if (@exact) {
                my %geoLkup;
                $_ and $geoLkup{ lc $_ } = 1 foreach @geo;
                $geoLkup{$_} or next Entry foreach @exact;
            }
            if ($other) {
                foreach $idx ( 0 .. 3 ) {
                    if ( $regex{$idx} ) {
                        $geo[$idx] =~ $_ or next Entry foreach @{ $regex{$idx} };
                    }
                    if ( $regex{ $idx + 10 } ) {
                        $geo[$idx] !~ $_
                          or next Entry
                          foreach @{ $regex{ $idx + 10 } };
                    }
                }
            }
            if ( $regex{9} or $regex{19} ) {
                my $str = join "\n", $cty, @geo;
                $str =~ $_ or next Entry foreach @{ $regex{9} };
                $str !~ $_ or next Entry foreach @{ $regex{19} };
            }
            next
              if $fcodes
              and not $fcOK{ ord( substr( $cityList[$i], 12, 1 ) ) & 0x3f };
            my $pc = substr( $cityList[$i], 6, 2 );
            if ( not defined $minPop or $pc ge $minPop ) {
                $lastFound{$i} = $pc;
                push @lastByLat, $i if @coords == 2;
            }
        }
        @startTime and printf( "= Processing time: %.3f sec\n",
            Time::HiRes::tv_interval( \@startTime ) );
        if (%lastFound) {
            last if @coords == 2;
            scalar( keys %lastFound ) > 200
              and warn("Too many matching cities\n"), return ();
            if ( $num > 1 and scalar( keys %lastFound ) == 1 ) {
                my ($i) = keys %lastFound;
                my @entry = GetEntry($i);
                @coords = @entry[ 8, 9 ];
                SortDatabase('Latitude');
                last;
            }
            unless (@lastByPop) {
                @lastByPop = sort {
                         $lastFound{$b} cmp $lastFound{$a}
                      or $cityList[$a] cmp $cityList[$b]
                } keys %lastFound;
            }
            return ( \@lastByPop );
        }
        warn "No such city in Geolocation database\n";
        return ();
    }
    my ( $lat, $lon ) = @coords;
    if ($maxDist) {
        $minDistU = $maxDist / ( 2 * $earthRadius );
        $minDistC = $maxDist * 0x100000 / ( $pi * $earthRadius );
    }
    else {
        $minDistU = $pi;
        $minDistC = 0x200000;
    }
    my $cos = cos( $lat * $pi / 180 );

    $lat         = int( ( $lat + 90 ) / 180 * 0x100000 + 0.5 ) & 0xfffff;
    $lon         = int( ( $lon + 180 ) / 360 * 0x100000 + 0.5 ) & 0xfffff;
    $lat or $lat = $coords[0] < 0 ? 1 : 0xfffff;
    my $coord = pack( 'nCn',
        $lat >> 4, ( ( $lat & 0x0f ) << 4 ) | ( $lon & 0x0f ),
        $lon >> 4 );
    my $numEntries = @lastByLat || @cityList;
    my ( $n0, $n1 ) = ( 0, $numEntries - 1 );
    my $sorted =
      @lastByLat ? \@lastByLat : ( @sortOrder ? \@sortOrder : undef );

    while ( $n1 - $n0 > 1 ) {
        my $n = int( ( $n0 + $n1 ) / 2 );
        if ( $coord lt $cityList[ $sorted ? $$sorted[$n] : $n ] ) {
            $n1 = $n;
        }
        else {
            $n0 = $n;
        }
    }
    my ( $inc, $end, $n ) = ( -1, -1, $n0 + 1 );
    my ( $p0, $t0 ) =
      ( $lat * $pi / 0x100000 - $pi / 2, $lon * $pi / 0x080000 - $pi );
    my $cp0 = cos($p0);
    my ( @matches, @rtnList, @dist );

    for ( ; ; ) {
        if ( ( $n += $inc ) == $end ) {
            last if $inc == 1 or $n0 == $n1;
            ( $inc, $end, $n ) = ( 1, $numEntries, $n1 );
        }
        my $i = $sorted ? $$sorted[$n] : $n;
        my ( $lt, $f, $ln ) = unpack( 'nCn', $cityList[$i] );
        $lt = ( $lt << 4 ) | ( $f >> 4 );
        abs( $lt - $lat ) > $minDistC and $n = $end - $inc, next;
        next if defined $minPop and $minPop ge substr( $cityList[$i], 6, 2 );
        next
          if $fcodes
          and not $fcOK{ ord( substr( $cityList[$i], 12, 1 ) ) & 0x3f };
        $ln = ( $ln << 4 ) | ( $f & 0x0f );
        my ( $p1, $t1 ) =
          ( $lt * $pi / 0x100000 - $pi / 2, $ln * $pi / 0x080000 - $pi );
        my ( $sp, $st ) =
          ( sin( ( $p1 - $p0 ) / 2 ), sin( ( $t1 - $t0 ) / 2 ) );
        my $a     = $sp * $sp + $cp0 * cos($p1) * $st * $st;
        my $distU = atan2( sqrt($a), sqrt( 1 - $a ) );
        next if $distU > $minDistU;
        @matchParms = ( $i, $p1, $t1, $distU );

        if ( $num <= 1 ) {
            $minDistU = $distU;
        }
        else {
            my $j;
            for ( $j = 0 ; $j < @matches ; ++$j ) {
                last if $distU < $matches[$j][3];
            }
            if ( $j < $#matches ) {
                splice @matches, $j, 0, [@matchParms];
            }
            else {
                $matches[$j] = [@matchParms];
            }
            pop @matches if @matches > $num;
            $minDistU = $matches[-1][3] if @matches >= $num;
        }
        $minDistC = $minDistU * 0x200000 / $pi;
    }
    @matchParms
      or warn("No suitable location in Geolocation database\n"), return ();
    $num = @matches;

    @startTime and printf( "- Processing time: %.3f sec\n",
        Time::HiRes::tv_interval( \@startTime ) );

    for ( ; ; ) {
        if ( $num > 1 ) {
            last unless @matches;
            @matchParms = @{ $matches[0] };
            shift @matches;
        }
        my ( $ii, $p1, $t1, $distU ) = @matchParms;
        my $km = sprintf( '%.2f', 2 * $earthRadius * $distU );
        my $be = atan2( sin( $t1 - $t0 ) * cos( $p1 - $p0 ),
            $cp0 * sin($p1) - sin($p0) * cos($p1) * cos( $t1 - $t0 ) );
        $be = int( $be * 180 / $pi + 360.5 ) % 360;
        push @rtnList, $ii;
        push @dist,    [ $km, $be ];
        last if $num <= 1;
    }
    return wantarray ? ( \@rtnList, \@dist ) : \@rtnList;
}

1;

__END__


1; #end
