
package Image::ExifTool::IPTC;

use strict;

my %mandatory = (
    1 => {
        0 => 4,
    },
    2 => {
        0 => 4,
    },
    3 => {
        0 => 4,
    },
);

my %manufacturer = (
    1  => 'Associated Press, USA',
    2  => 'Eastman Kodak Co, USA',
    3  => 'Hasselblad Electronic Imaging, Sweden',
    4  => 'Tecnavia SA, Switzerland',
    5  => 'Nikon Corporation, Japan',
    6  => 'Coatsworth Communications Inc, Canada',
    7  => 'Agence France Presse, France',
    8  => 'T/One Inc, USA',
    9  => 'Associated Newspapers, UK',
    10 => 'Reuters London',
    11 => 'Sandia Imaging Systems Inc, USA',
    12 => 'Visualize, Spain',
);

my %iptcCharsetInv = ( 'UTF8' => "\x1b%G", 'UTF-8' => "\x1b%G" );

sub PrintInvCodedCharset($) {
    my $val  = shift;
    my $code = $iptcCharsetInv{ uc($val) };
    unless ($code) {
        if ( ( $code = $val ) =~ s/ESC */\x1b/ig ) {
            $code =~ s/, \x1b/\x1b/g;
            $code =~ tr/ //d;
        }
        else {
            warn "Bad syntax (use 'UTF8' or 'ESC X Y[, ...]')\n";
        }
    }
    return $code;
}

sub CheckIPTC($$$) {
    my ( $et, $tagInfo, $valPtr ) = @_;
    my $format = $$tagInfo{Format} || $$tagInfo{Table}{FORMAT} || '';
    if ( $format =~ /^int(\d+)/ ) {
        my $bytes = int( ( $1 || 0 ) / 8 );
        if ( $bytes != 1 and $bytes != 2 and $bytes != 4 ) {
            return "Can't write $bytes-byte integer";
        }
        my $val = $$valPtr;
        unless ( Image::ExifTool::IsInt($val) ) {
            return 'Not an integer' unless Image::ExifTool::IsHex($val);
            $val = $$valPtr = hex($val);
        }
        my $n;
        for ( $n = 0 ; $n < $bytes ; ++$n ) { $val >>= 8; }
        return "Value too large for $bytes-byte format" if $val;
    }
    elsif ( $format =~ /^(string|digits|undef)\[?(\d+),?(\d*)\]?$/ ) {
        my ( $fmt, $minlen, $maxlen ) = ( $1, $2, $3 );
        my $len = length $$valPtr;
        if ( $fmt eq 'digits' ) {
            return 'Non-numeric characters in value' unless $$valPtr =~ /^\d*$/;
            if ( $len < $minlen and $len ) {
                $$valPtr = ( '0' x ( $minlen - $len ) ) . $$valPtr;
                $len     = $minlen;
            }
        }
        if ( defined $minlen and $fmt ne 'string' ) {
            $maxlen or $maxlen = $minlen;
            if ( $len < $minlen ) {
                unless ( $$et{OPTIONS}{IgnoreMinorErrors} ) {
                    return "[Minor] String too short (minlen is $minlen)";
                }
                $$et{CHECK_WARN} =
                  "String too short for IPTC:$$tagInfo{Name} (written anyway)";
            }
            elsif ( $len > $maxlen and not $$et{OPTIONS}{IgnoreMinorErrors} ) {
                $$et{CHECK_WARN} =
"[Minor] IPTC:$$tagInfo{Name} exceeds length limit (truncated)";
                $$valPtr = substr( $$valPtr, 0, $maxlen );
            }
        }
    }
    else {
        return "Bad IPTC Format ($format)";
    }
    return undef;
}

sub FormatIPTC($$$$$;$) {
    my ( $et, $tagInfo, $valPtr, $xlatPtr, $rec, $read ) = @_;
    my $format = $$tagInfo{Format} || $$tagInfo{Table}{FORMAT};
    return unless $format;
    if ( $format =~ /^int(\d+)/ ) {
        if ($read) {
            my $len = length($$valPtr);
            if ( $len <= 8 ) {
                my $val = 0;
                my $i;
                for ( $i = 0 ; $i < $len ; ++$i ) {
                    $val = $val * 256 + ord( substr( $$valPtr, $i, 1 ) );
                }
                $$valPtr = $val;
            }
        }
        else {
            my $len = int( ( $1 || 0 ) / 8 );
            if ( $len == 1 ) {
                $$valPtr = chr( $$valPtr & 0xff );
            }
            elsif ( $len == 2 ) {
                $$valPtr = pack( 'n', $$valPtr );
            }
            else {
                $$valPtr = pack( 'N', $$valPtr );
            }
        }
    }
    elsif ( $format =~ /^string/ ) {
        if ( $rec == 1 ) {
            if ( $$tagInfo{Name} eq 'CodedCharacterSet' ) {
                $$xlatPtr = HandleCodedCharset( $et, $$valPtr );
            }
        }
        elsif ( $$xlatPtr and $rec < 7 and $$valPtr =~ /[\x80-\xff]/ ) {
            TranslateCodedString( $et, $valPtr, $xlatPtr, $read );
        }
        if ( not $read and $format =~ /^string\[(\d+),?(\d*)\]$/ ) {
            my ( $minlen, $maxlen ) = ( $1, $2 );
            my $len = length $$valPtr;
            $maxlen or $maxlen = $minlen;
            if ( $len < $minlen ) {
                if (
                    $et->Warn(
                        "String too short for IPTC:$$tagInfo{Name} (padded)", 2
                    )
                  )
                {
                    $$valPtr .= ' ' x ( $minlen - $len );
                }
            }
            elsif ( $len > $maxlen ) {
                if (
                    $et->Warn(
                        "IPTC:$$tagInfo{Name} exceeds length limit (truncated)",
                        2
                    )
                  )
                {
                    $$valPtr = substr( $$valPtr, 0, $maxlen );
                    if ( ( $$xlatPtr || $et->Options('Charset') ) eq 'UTF8' ) {
                        require Image::ExifTool::XMP;
                        Image::ExifTool::XMP::FixUTF8( $valPtr, '.' );
                    }
                }
            }
        }
    }
}

sub IptcDate($) {
    my $val = shift;
    unless ( $val =~ s{^.*?(\d{4})[-:/.]?(\d{2})[-:/.]?(\d{2}).*}{$1$2$3}s ) {
        warn "Invalid date format (use YYYY:mm:dd)\n";
        undef $val;
    }
    return $val;
}

sub IptcTime($) {
    my $val = shift;
    if ( $val =~ /(.*?)\b(\d{1,2})(:?)(\d{2})(:?)(\d{2})(\S*)\s*$/s
        and ( $3 or not $5 ) )
    {
        $val = sprintf( "%.2d%.2d%.2d", $2, $4, $6 );
        my ( $date, $tz ) = ( $1, $7 );
        if ( $tz =~ /([+-]\d{1,2}):?(\d{2})/ ) {
            $tz = sprintf( "%+.2d%.2d", $1, $2 );
        }
        elsif ( $tz =~ /Z/i ) {
            $tz = '+0000';
        }
        else {
            my ( @tm, $time );
            if (    $date
                and $date =~ /^(\d{4}):(\d{2}):(\d{2})\s*$/
                and eval { require Time::Local } )
            {
                my @d = ( $3, $2 - 1, $1 );
                $val =~ /(\d{2})(\d{2})(\d{2})/;
                @tm   = ( $3, $2, $1, @d );
                $time = Image::ExifTool::TimeLocal(@tm);
            }
            else {
                $time = time;
                @tm   = localtime($time);
            }
            ( $tz = Image::ExifTool::TimeZoneString( \@tm, $time ) ) =~ tr/://d;
        }
        $val .= $tz;
    }
    else {
        warn "Invalid time format (use HH:MM:SS[+/-HH:MM])\n";
        undef $val;
    }
    return $val;
}

sub InverseDateOrTime($$) {
    my ( $et, $val ) = @_;
    return $et->TimeNow() if lc($val) eq 'now';
    return $val;
}

sub ConvertPictureNumber($) {
    my $val = shift;
    if ( $val eq "\0" x 16 ) {
        $val = 'Unknown';
    }
    elsif ( length $val >= 16 ) {
        my @vals = unpack( 'nNA8n', $val );
        $val = $vals[0];
        my $manu = $manufacturer{$val};
        $val .= " ($manu)" if $manu;
        $val .= ', equip ' . $vals[1];
        $vals[2] =~ s/(\d{4})(\d{2})(\d{2})/$1:$2:$3/;
        $val .= ", $vals[2], no. $vals[3]";
    }
    else {
        $val = '<format error>';
    }
    return $val;
}

sub InvConvertPictureNumber($) {
    my $val = shift;
    $val =~ s/\(.*\)//g;
    $val =~ tr/://d;
    $val =~ tr/0-9/ /c;
    my @vals = split ' ', $val;
    if ( @vals >= 4 ) {
        $val = pack( 'nNA8n', @vals );
    }
    elsif ( $val =~ /unknown/i ) {
        $val = "\0" x 16;
    }
    else {
        undef $val;
    }
    return $val;
}

sub DoWriteIPTC($$$) {
    my ( $et, $dirInfo, $tagTablePtr ) = @_;
    my $verbose = $et->Options('Verbose');
    my $out     = $et->Options('TextOut');

    return undef unless exists $$et{EDIT_DIRS}{ $$dirInfo{DirName} } or
      ( $tagTablePtr eq \%Image::ExifTool::IPTC::Main
        and exists $$et{EDIT_DIRS}{IPTC} );
    my $dataPt = $$dirInfo{DataPt};
    unless ($dataPt) {
        my $emptyData = '';
        $dataPt = \$emptyData;
    }
    my $start  = $$dirInfo{DirStart} || 0;
    my $dirLen = $$dirInfo{DirLen};
    my ( $tagInfo, %iptcInfo, $tag );

    my $xlat = $et->Options('CharsetIPTC');
    undef $xlat if $xlat eq $et->Options('Charset');

    unless ( defined $dirLen ) {
        my $dataLen = $$dirInfo{DataLen};
        $dataLen = length($$dataPt) unless defined $dataLen;
        $dirLen  = $dataLen - $start;
    }
    if (    $dirLen >= 4
        and substr( $$dataPt, $start,     1 ) ne "\x1c"
        and substr( $$dataPt, $start + 3, 1 ) eq "\x1c" )
    {
        $et->Warn('IPTC data was improperly byte-swapped');
        my $newData = pack( 'N*',
            unpack( 'V*', substr( $$dataPt, $start, $dirLen ) . "\0\0\0" ) );
        $dataPt = \$newData;
        $start  = 0;
    }
    my %recordNum;
    foreach $tag ( Image::ExifTool::TagTableKeys($tagTablePtr) ) {
        $tagInfo = $$tagTablePtr{$tag};
        $$tagInfo{SubDirectory} or next;
        my $table       = $$tagInfo{SubDirectory}{TagTable} or next;
        my $subTablePtr = Image::ExifTool::GetTagTable($table);
        $recordNum{$subTablePtr} = $tag;
    }

    foreach $tagInfo ( $et->GetNewTagInfoList() ) {
        my $table  = $$tagInfo{Table};
        my $record = $recordNum{$table};
        next                    unless defined $record;
        $iptcInfo{$record} = [] unless defined $iptcInfo{$record};
        push @{ $iptcInfo{$record} }, $tagInfo;
    }

    my @recordList = sort { $a <=> $b } keys %iptcInfo;
    my ( $record, %set );
    foreach $record (@recordList) {
        @{ $iptcInfo{$record} } =
          sort { $$a{TagID} <=> $$b{TagID} } @{ $iptcInfo{$record} };
        foreach $tagInfo ( @{ $iptcInfo{$record} } ) {
            $set{$record}->{ $$tagInfo{TagID} } = $tagInfo;
        }
    }
    my $pos          = $start;
    my $tail         = $pos;
    my $dirEnd       = $start + $dirLen;
    my $newData      = '';
    my $lastRec      = -1;
    my $lastRecPos   = 0;
    my $allMandatory = 0;
    my %foundRec;
    my $addNow;

    for ( ; ; $tail = $pos ) {
        my ( $id, $rec, $tag, $len, $valuePtr );
        if ( $pos + 5 <= $dirEnd ) {
            my $buff = substr( $$dataPt, $pos, 5 );
            ( $id, $rec, $tag, $len ) = unpack( "CCCn", $buff );
            if ( $id == 0x1c ) {
                if ( $rec < $lastRec ) {
                    if ( $rec == 0 ) {
                        return undef
                          if $et->Warn(
"IPTC record 0 encountered, subsequent records ignored",
                            2
                          );
                        undef $rec;
                        $pos = $dirEnd;
                        $len = 0;
                    }
                    else {
                        return undef
                          if $et->Warn(
"IPTC doesn't conform to spec: Records out of sequence",
                            2
                          );
                    }
                }
                $pos += 5;
                if ( $len & 0x8000 ) {
                    my $n = $len & 0x7fff;
                    if ( $pos + $n <= $dirEnd and $n <= 8 ) {
                        for ( $len = 0 ; $n ; ++$pos, --$n ) {
                            $len =
                              $len * 256 + ord( substr( $$dataPt, $pos, 1 ) );
                        }
                    }
                    else {
                        $len = $dirEnd;
                    }
                }
                $valuePtr = $pos;
                $pos += $len;

                $pos = $dirEnd if $pos > $dirEnd;
            }
            else {
                undef $rec;
            }
        }
        my $writeRec = ( not defined $rec or $rec != $lastRec );
        if ( $writeRec or $addNow ) {
            for ( ; ; ) {
                my $newRec = $recordList[0];
                if ($addNow) {
                    $tagInfo = $addNow;
                }
                elsif ( not defined $newRec or $newRec != $lastRec ) {
                    if ( length $newData > $lastRecPos ) {
                        if ( $allMandatory > 1 ) {
                            my $num = 0;
                            foreach ( keys %{ $foundRec{$lastRec} } ) {
                                my $code = $foundRec{$lastRec}->{$_};
                                $num = 0, last if $code & 0x04;
                                ++$num if ( $code & 0x03 ) == 0x01;
                            }
                            if ($num) {
                                $newData = substr( $newData, 0, $lastRecPos );
                                $verbose > 1
                                  and print $out "    - $num mandatory tags\n";
                            }
                        }
                        elsif ( $mandatory{$lastRec}
                            and $tagTablePtr eq \%Image::ExifTool::IPTC::Main )
                        {
                            my $mandatory = $mandatory{$lastRec};
                            my ( $mandTag, $subTablePtr );
                            foreach $mandTag ( sort { $a <=> $b }
                                keys %$mandatory )
                            {
                                next if $foundRec{$lastRec}->{$mandTag};
                                unless ($subTablePtr) {
                                    $tagInfo = $$tagTablePtr{$lastRec};
                                    $tagInfo and $$tagInfo{SubDirectory}
                                      or warn("WriteIPTC: Internal error 1\n"),
                                      next;
                                    $$tagInfo{SubDirectory}{TagTable} or next;
                                    $subTablePtr = Image::ExifTool::GetTagTable(
                                        $$tagInfo{SubDirectory}{TagTable} );
                                }
                                $tagInfo = $$subTablePtr{$mandTag}
                                  or warn("WriteIPTC: Internal error 2\n"),
                                  next;
                                my $value = $$mandatory{$mandTag};
                                $et->VerboseValue( "+ IPTC:$$tagInfo{Name}",
                                    $value, ' (mandatory)' );
                                FormatIPTC(
                                    $et,    $tagInfo, \$value,
                                    \$xlat, $lastRec
                                );
                                $len = length $value;
                                my $entry = pack( "CCCn",
                                    0x1c, $lastRec, $mandTag, length($value) );
                                $newData .= $entry . $value;

                            }
                        }
                    }
                    last unless defined $newRec;
                    $lastRec      = $newRec;
                    $lastRecPos   = length $newData;
                    $allMandatory = 1;
                }
                unless ($addNow) {
                    last if defined $rec and $rec <= $newRec;
                    $tagInfo = ${ $iptcInfo{$newRec} }[0];
                }
                my $newTag = $$tagInfo{TagID};
                my $nvHash = $et->GetNewValueHash($tagInfo);
                my ( $doSet, @values );
                my $found = $foundRec{$newRec}->{$newTag} || 0;
                if ( $found & 0x02 ) {
                    $doSet = 1 unless $found & 0x04;
                }
                elsif ( $$tagInfo{List} ) {
                    $doSet = 1
                      if $found
                      ? not $$nvHash{CreateOnly}
                      : $$nvHash{IsCreating};
                }
                else {
                    $doSet = 1 if not $found and $$nvHash{IsCreating};
                }
                if ($doSet) {
                    @values = $et->GetNewValue($nvHash);
                    @values and $foundRec{$newRec}->{$newTag} = $found | 0x04;
                    my $value;
                    foreach $value (@values) {
                        $et->VerboseValue(
                            "+ $$dirInfo{DirName}:$$tagInfo{Name}", $value );
                        if ($allMandatory) {
                            my $mandatory = $mandatory{$newRec};
                            $allMandatory = 0
                              unless $mandatory and $$mandatory{$newTag};
                        }
                        FormatIPTC( $et, $tagInfo, \$value, \$xlat, $newRec );
                        $len = length $value;
                        my $entry = pack( "CCC", 0x1c, $newRec, $newTag );
                        if ( $len <= 0x7fff ) {
                            $entry .= pack( "n", $len );
                        }
                        else {
                            $entry .= pack( "nN", 0x8004, $len );
                        }
                        $newData .= $entry . $value;
                        ++$$et{CHANGED};
                    }
                }
                if ($addNow) {
                    undef $addNow;
                    next if $writeRec;
                    last;
                }
                shift @{ $iptcInfo{$newRec} };
                shift @recordList unless @{ $iptcInfo{$newRec} };
            }
            if ($writeRec) {
                last unless defined $rec;
                $lastRec      = $rec;
                $lastRecPos   = length $newData;
                $allMandatory = 1;
            }
        }
        $foundRec{$rec}->{$tag} = ( $foundRec{$rec}->{$tag} || 0 ) || 0x01;
        $tagInfo = $set{$rec}->{$tag};
        if ($tagInfo) {
            my $nvHash = $et->GetNewValueHash($tagInfo);
            $len = $pos - $valuePtr;
            my $val = substr( $$dataPt, $valuePtr, $len );
            $val =~ s/\0+$//
              if $$tagInfo{Format} and $$tagInfo{Format} =~ /^string/;
            my $oldXlat = $xlat;
            FormatIPTC( $et, $tagInfo, \$val, \$xlat, $rec, 1 );
            if ( $et->IsOverwriting( $nvHash, $val ) ) {
                $xlat = $oldXlat;
                $et->VerboseValue( "- $$dirInfo{DirName}:$$tagInfo{Name}",
                    $val );
                ++$$et{CHANGED};
                $foundRec{$rec}->{$tag} |= 0x02;
                $allMandatory and ++$allMandatory;
                if (    $$nvHash{Value}
                    and @{ $$nvHash{Value} }
                    and @recordList
                    and $recordList[0] == $rec
                    and not $foundRec{$rec}->{$tag} & 0x04 )
                {
                    $addNow = $tagInfo;
                }
                next;
            }
        }
        elsif ( $rec == 1 and $tag == 90 ) {
            my $val = substr( $$dataPt, $valuePtr, $pos - $valuePtr );
            $xlat = HandleCodedCharset( $et, $val );
        }
        if ($allMandatory) {
            my $mandatory = $mandatory{$rec};
            unless ( $mandatory and $$mandatory{$tag} ) {
                $allMandatory = 0;
            }
        }
        $newData .= substr( $$dataPt, $tail, $pos - $tail );
    }
    if ( $tail < $dirEnd ) {
        my $pad = substr( $$dataPt, $tail, $dirEnd - $tail );
        if ( $pad =~ /[^\0]/ ) {
            return undef if $et->Warn( 'Unrecognized data in IPTC padding', 2 );
        }
    }
    return $newData;
}

sub WriteIPTC($$$) {
    my ( $et, $dirInfo, $tagTablePtr ) = @_;
    $et or return 1;

    my $newData = DoWriteIPTC( $et, $dirInfo, $tagTablePtr );

    while ($Image::ExifTool::Photoshop::iptcDigestInfo) {
        my $nvHash =
          $$et{NEW_VALUE}{$Image::ExifTool::Photoshop::iptcDigestInfo};
        last unless defined $nvHash;
        last unless IsStandardIPTC( $et->MetadataPath() );
        my @values = $et->GetNewValue($nvHash);
        push @values, @{ $$nvHash{DelValue} } if $$nvHash{DelValue};
        my $new = grep /^new$/, @values;
        my $old = grep /^old$/, @values;
        last unless $new or $old;

        unless ( eval { require Digest::MD5 } ) {
            $et->Warn('Digest::MD5 must be installed to calculate IPTC digest');
            last;
        }
        my $dataPt;
        if ($new) {
            if ( defined $newData ) {
                $dataPt = \$newData;
            }
            else {
                $dataPt = $$dirInfo{DataPt};
                if ( $$dirInfo{DirStart}
                    or length($$dataPt) != $$dirInfo{DirLen} )
                {
                    my $buff = substr( $$dataPt, $$dirInfo{DirStart},
                        $$dirInfo{DirLen} );
                    $dataPt = \$buff;
                }
            }
            $$et{NewIPTCDigest} = Digest::MD5::md5($$dataPt) if length $$dataPt;
        }
        if ($old) {
            if ( $new and not defined $newData ) {
                $$et{OldIPTCDigest} = $$et{NewIPTCDigest};
            }
            elsif ( $$dirInfo{DataPt} ) {
                $dataPt = $$dirInfo{DataPt};
                if ( $$dirInfo{DirStart}
                    or length($$dataPt) != $$dirInfo{DirLen} )
                {
                    my $buff = substr( $$dataPt, $$dirInfo{DirStart},
                        $$dirInfo{DirLen} );
                    $dataPt = \$buff;
                }
                $$et{OldIPTCDigest} = Digest::MD5::md5($$dataPt)
                  if length $$dataPt;
            }
        }
        last;
    }
    ++$$et{CHANGED}
      if defined $newData
      and length $newData
      and $$et{FORCE_WRITE}{IPTC};
    return $newData;
}

1;

__END__

