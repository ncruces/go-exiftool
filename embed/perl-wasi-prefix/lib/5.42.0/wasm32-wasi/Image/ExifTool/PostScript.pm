
package Image::ExifTool::PostScript;

use strict;
use vars            qw($VERSION $AUTOLOAD);
use Image::ExifTool qw(:DataAccess :Utils);

$VERSION = '1.46';

sub WritePS($$);
sub ProcessPS($$;$);

%Image::ExifTool::PostScript::Main = (
    PROCESS_PROC => \&ProcessPS,
    WRITE_PROC   => \&WritePS,
    PREFERRED    => 1,
    GROUPS       => { 2 => 'Image' },
    Author =>
      { Priority => 0, Groups => { 2 => 'Author' }, Writable => 'string' },
    BoundingBox  => { Priority => 0 },
    Copyright    => { Priority => 0, Writable => 'string' },
    CreationDate => {
        Name         => 'CreateDate',
        Priority     => 0,
        Groups       => { 2 => 'Time' },
        Writable     => 'string',
        PrintConv    => '$self->ConvertDateTime($val)',
        PrintConvInv => '$self->InverseDateTime($val)',
    },
    Creator   => { Priority => 0, Writable => 'string' },
    ImageData => { Priority => 0 },
    For       => {
        Priority => 0,
        Writable => 'string',
        Notes    => 'for whom the document was prepared'
    },
    Keywords => { Priority => 0, Writable => 'string' },
    ModDate  => {
        Name         => 'ModifyDate',
        Priority     => 0,
        Groups       => { 2 => 'Time' },
        Writable     => 'string',
        PrintConv    => '$self->ConvertDateTime($val)',
        PrintConvInv => '$self->InverseDateTime($val)',
    },
    Pages   => { Priority => 0 },
    Routing => { Priority => 0, Writable => 'string' },
    Subject => { Priority => 0, Writable => 'string' },
    Title   => { Priority => 0, Writable => 'string' },
    Version => { Priority => 0, Writable => 'string' },

    BeginPhotoshop => {
        Name         => 'PhotoshopData',
        SubDirectory => {
            TagTable => 'Image::ExifTool::Photoshop::Main',
        },
    },
    BeginICCProfile => {
        Name         => 'ICC_Profile',
        SubDirectory => {
            TagTable => 'Image::ExifTool::ICC_Profile::Main',
        },
    },
    begin_xml_packet => {
        Name         => 'XMP',
        SubDirectory => {
            TagTable => 'Image::ExifTool::XMP::Main',
        },
    },
    TIFFPreview => {
        Groups => { 2 => 'Preview' },
        Binary => 1,
        Notes  => q{
            not a real tag ID, but used to represent the TIFF preview extracted from DOS
            EPS images
        },
    },
    BeginDocument => {
        Name         => 'EmbeddedFile',
        SubDirectory => {
            TagTable => 'Image::ExifTool::PostScript::Main',
        },
        Notes =>
'extracted with L<ExtractEmbedded|../ExifTool.html#ExtractEmbedded> option',
    },
    EmbeddedFileName => {
        Notes => q{
            not a real tag ID, but the file name from a BeginDocument statement.
            Extracted with document metadata when L<ExtractEmbedded|../ExifTool.html#ExtractEmbedded> option is used
        },
    },
    AI9_ColorModel => {
        Name      => 'AIColorModel',
        PrintConv => {
            1 => 'RGB',
            2 => 'CMYK',
        },
    },
    AI3_ColorUsage => { Name => 'AIColorUsage' },
    AI5_RulerUnits => {
        Name      => 'AIRulerUnits',
        PrintConv => {
            0 => 'Inches',
            1 => 'Millimeters',
            2 => 'Points',
            3 => 'Picas',
            4 => 'Centimeters',
            6 => 'Pixels',
        },
    },
    AI5_TargetResolution => { Name => 'AITargetResolution' },
    AI5_NumLayers        => { Name => 'AINumLayers' },
    AI5_FileFormat       => { Name => 'AIFileFormat' },
    AI8_CreatorVersion   => { Name => 'AICreatorVersion' },
    AI12_BuildNumber     => { Name => 'AIBuildNumber' },
);

%Image::ExifTool::PostScript::Composite = (
    GROUPS => { 2 => 'Image' },
    ImageWidth => {
        Desire => {
            0 => 'Main:PostScript:ImageData',
            1 => 'PostScript:BoundingBox',
        },
        ValueConv => 'Image::ExifTool::PostScript::ImageSize(\@val, 0)',
    },
    ImageHeight => {
        Desire => {
            0 => 'Main:PostScript:ImageData',
            1 => 'PostScript:BoundingBox',
        },
        ValueConv => 'Image::ExifTool::PostScript::ImageSize(\@val, 1)',
    },
);

Image::ExifTool::AddCompositeTags('Image::ExifTool::PostScript');

sub AUTOLOAD {
    return Image::ExifTool::DoAutoLoad( $AUTOLOAD, @_ );
}

sub ImageSize($$) {
    my ( $vals, $getHeight ) = @_;
    my ( $w, $h );
    if ( $$vals[0] and $$vals[0] =~ /^(\d+) (\d+)/ ) {
        ( $w, $h ) = ( $1, $2 );
    }
    elsif ( $$vals[1] and $$vals[1] =~ /^(\d+) (\d+) (\d+) (\d+)/ ) {
        ( $w, $h ) = ( $3 - $1, $4 - $2 );
    }
    return $getHeight ? $h : $w;
}

sub PSErr($$) {
    my ( $et, $str ) = @_;
    my $ext = $$et{FILE_EXT};
    $et->SetFileType( ( $ext and $ext eq 'AI' ) ? 'AI' : 'PS' );
    $et->Warn("PostScript format error ($str)");
    return 1;
}

sub GetInputRecordSeparator($) {
    my $raf = shift;
    my $pos = $raf->Tell();
    my ( $data, $sep );
    $raf->Read( $data, 256 ) or return undef;
    my ( $a, $d ) = ( 999, 999 );
    $a = pos($data), pos($data) = 0 if $data =~ /\x0a/g;
    $d = pos($data) if $data =~ /\x0d/g;
    my $diff = $a - $d;

    if ( $diff == 1 ) {
        $sep = "\x0d\x0a";
    }
    elsif ( $diff == -1 ) {
        $sep = "\x0a\x0d";
    }
    elsif ( $diff > 0 ) {
        $sep = "\x0d";
    }
    elsif ( $diff < 0 ) {
        $sep = "\x0a";
    }
    $raf->Seek( $pos, 0 );
    return $sep;
}

sub SplitLine($$) {
    my ( $dataPt, $lines ) = @_;
    for ( ; ; ) {
        my $endl;
        $endl = pos($$dataPt), pos($$dataPt) = 0 if $$dataPt =~ /\x0a/g;
        if ( $$dataPt =~ /\x0d/g ) {
            if ( defined $endl ) {
                $endl = pos($$dataPt) if pos($$dataPt) < $endl - 1;
            }
            else {
                $endl = pos($$dataPt);
            }
        }
        elsif ( not defined $endl ) {
            push @$lines, $$dataPt;
            last;
        }
        if ( length $$dataPt == $endl ) {
            push @$lines, $$dataPt;
            last;
        }
        else {
            push @$lines, substr( $$dataPt, 0, $endl );
            $$dataPt = substr( $$dataPt, $endl );
        }
    }
}

sub CheckPSEnd($$) {
    my ( $raf, $dataPt ) = @_;
    my $pos = $raf->Tell();
    if ( $pos >= $$raf{PSEnd} ) {
        $raf->Seek( 0, 2 );
        $$dataPt = substr( $$dataPt, 0, length($$dataPt) - $pos + $$raf{PSEnd} )
          if $pos > $$raf{PSEnd};
    }
}

sub GetNextLine($$) {
    my ( $raf, $lines ) = @_;
    my ( $data, $changedNL );
    my $altnl = ( $/ eq "\x0d" ) ? "\x0a" : "\x0d";
    for ( ; ; ) {
        $raf->ReadLine($data) or last;
        $$raf{PSEnd} and CheckPSEnd( $raf, \$data );
        if ( $data =~ /$altnl/ ) {
            if ( length($data) > 500000 and Image::ExifTool::IsPC() ) {
                unless ($changedNL) {
                    $changedNL = $/;
                    $/         = $altnl;
                    $altnl     = $changedNL;
                    $raf->Seek( -length($data), 1 );
                    next;
                }
            }
            else {
                SplitLine( \$data, $lines );
            }
        }
        else {
            push @$lines, $data;
        }
        $/ = $changedNL if $changedNL;
        return 1;
    }
    return 0;
}

sub DecodeComment($$$;$) {
    my ( $val, $raf, $lines, $dataPt ) = @_;
    $val =~ s/\x0d*\x0a*$//;

    for ( ; ; ) {
        @$lines or GetNextLine( $raf, $lines ) or last;
        last unless $$lines[0] =~ /^%%\+/;
        $$dataPt .= $$lines[0] if $dataPt;
        $$lines[0] =~ s/\x0d*\x0a*$//;
        $val .= substr( shift(@$lines), 3 );
    }
    my @vals;
    if ( $val =~ s/^\((.*)\)$/$1/ ) {

        my $nesting = 1;
        while ( $val =~ /(\(|\))/g ) {
            my $bra         = $1;
            my $pos         = pos($val) - 2;
            my $backslashes = 0;
            while ( $pos and substr( $val, $pos, 1 ) eq '\\' ) {
                --$pos;
                ++$backslashes;
            }
            next if $backslashes & 0x01;
            if ( $bra eq '(' ) {
                ++$nesting;
            }
            else {
                --$nesting;
                unless ($nesting) {
                    push @vals, substr( $val, 0, pos($val) - 1 );
                    $val = substr( $val, pos($val) );
                    ++$nesting if $val =~ s/\s*\(//;
                }
            }
        }
        push @vals, $val;
        foreach $val (@vals) {
            while ( $val =~ /\\(.)/sg ) {
                my $n = pos($val) - 2;
                my $c = $1;
                my $r;
                if ( $c =~ /[0-7]/ ) {
                    $c .= $1 if $val =~ /\G([0-7]{1,2})/g;
                    $r = chr( oct($c) & 0xff );
                }
                else {
                    ( $r = $c ) =~ tr/nrtbf/\n\r\t\b\f/;
                }
                substr( $val, $n, length($c) + 1 ) = $r;
                pos($val) = $n + length($r);
            }
        }
        $val = @vals > 1 ? \@vals : $vals[0];
    }
    return $val;
}

sub UnescapePostScript($) {
    my $str = shift;
    while ( $str =~ /\\(.)/sg ) {
        my $n = pos($str) - 2;
        my $c = $1;
        my $r;
        if ( $c =~ /[0-7]/ ) {
            $c .= $1 if $str =~ /\G([0-7]{1,2})/g;
            $r = chr( oct($c) & 0xff );
        }
        elsif ( $c eq "\x0d" ) {
            $c .= $1 if $str =~ /\G(\x0a)/g;
            $r = '';
        }
        elsif ( $c eq "\x0a" ) {
            $r = '';
        }
        else {
            ( $r = $c ) =~ tr/nrtbf/\n\r\t\b\f/;
        }
        substr( $str, $n, length($c) + 1 ) = $r;
        pos($str) = $n + length($r);
    }
    return $str;
}

sub ProcessPS($$;$) {
    my ( $et, $dirInfo, $tagTablePtr ) = @_;
    my $raf      = $$dirInfo{RAF};
    my $embedded = $et->Options('ExtractEmbedded');
    my ( $data, $dos, $endDoc, $fontTable, $comment );

    unless ($raf) {
        $raf = File::RandomAccess->new( $$dirInfo{DataPt} );
        $et->VerboseDir('PostScript');
    }
    $raf->Read( $data, 4 ) == 4 or return 0;
    return 0 unless $data =~ /^(%!PS|%!Ad|%!Fo|\xc5\xd0\xd3\xc6)/;
    if ( $data =~ /^%!Ad/ ) {
        return 0 unless $raf->Read( $data, 6 ) == 6 and $data eq "obe-PS";
    }
    elsif ( $data =~ /^\xc5\xd0\xd3\xc6/ ) {
        $raf->Read( $dos, 26 ) == 26 or return 0;
        SetByteOrder('II');
        my $psStart = Get32u( \$dos, 0 );
        unless ($raf->Seek( $psStart, 0 )
            and $raf->Read( $data, 4 ) == 4
            and $data eq '%!PS' )
        {
            return PSErr( $et, 'invalid header' );
        }
        $$raf{PSEnd} = $psStart + Get32u( \$dos, 4 );
    }
    else {
        my $d2;
        $data .= $d2 if $raf->Read( $d2, 12 );
        if ( $data =~ /^%!(PS-(AdobeFont-|Bitstream )|FontType1-)/ ) {
            $et->SetFileType('PFA');
            $fontTable = GetTagTable('Image::ExifTool::Font::PSInfo');
            $comment = 1;
        }
        $raf->Seek( -length($data), 1 );
    }
    local $/ = GetInputRecordSeparator($raf);
    $/ or return PSErr( $et, 'invalid PS data' );

    $raf->ReadLine($data) or $data = '';
    my $type;
    if ( $data =~ /EPSF/ ) {
        $type = 'EPS';
    }
    else {
        my $line2;
        my $pos = $raf->Tell();
        if (    $raf->ReadLine($line2)
            and $line2 =~ /^%%Creator: Adobe Illustrator/ )
        {
            $type = 'AI';
        }
        else {
            $type = 'PS';
        }
        $raf->Seek( $pos, 0 );
    }
    $et->SetFileType($type);
    return 1 if $$et{OPTIONS}{FastScan} and $$et{OPTIONS}{FastScan} == 3;
    $tagTablePtr
      or $tagTablePtr = GetTagTable('Image::ExifTool::PostScript::Main');
    if ($dos) {
        my $base = Get32u( \$dos, 16 );
        if ($base) {
            my $pos = $raf->Tell();
            my $len = Get32u( \$dos, 20 );
            my $val = $et->ExtractBinary( $base, $len, 'TIFFPreview' );
            if ( defined $val and $val =~ /^(MM\0\x2a|II\x2a\0|Binary)/ ) {
                $et->HandleTag( $tagTablePtr, 'TIFFPreview', $val );
            }
            else {
                $et->Warn('Bad TIFF preview image');
            }
            my %dirInfo = (
                Parent => '',
                RAF    => $raf,
                Base   => $base,
            );
            $et->ProcessTIFF( \%dirInfo ) or $et->Warn('Bad embedded TIFF');
            $raf->Seek( $pos, 0 );
        }
    }
    my ( $buff, $mode, $beginToken, $endToken, $docNum, $subDocNum,
        $changedNL );
    my ( @lines, $altnl );
    if ( $/ eq "\x0d" ) {
        $altnl = "\x0a";
    }
    else {
        $/     = "\x0a";
        $altnl = "\x0d";
    }
    for ( ; ; ) {
        if (@lines) {
            $data = shift @lines;
        }
        else {
            $raf->ReadLine($data) or last;
            if ( $data =~ /$altnl/ ) {
                if ( length($data) > 500000 and Image::ExifTool::IsPC() ) {
                    unless ($changedNL) {
                        $changedNL = 1;
                        my $t = $/;
                        $/     = $altnl;
                        $altnl = $t;
                        $raf->Seek( -length($data), 1 );
                        next;
                    }
                }
                else {
                    @lines = split /$altnl/, $data, -1;
                    $data  = shift @lines;
                    if ( @lines == 1 and $lines[0] eq $/ ) {
                        $data .= $lines[0];
                        undef @lines;
                    }
                }
            }
        }
        undef $changedNL;
        if ($mode) {
            if ( not $endToken ) {
                $buff .= $data;
                next unless $data =~ m{<\?xpacket end=.(w|r).\?>(\n|\r|$)};
            }
            elsif ( $data !~ /^$endToken/i ) {
                if ( $mode eq 'XMP' ) {
                    $buff .= $data;
                }
                elsif ( $mode eq 'Document' ) {
                    $docNum .= '-1' if $data =~ /^$beginToken/;
                }
                else {
                    $data =~ tr/0-9A-Fa-f//dc;
                    $buff .= pack( 'H*', $data );
                }
                next;
            }
            elsif ( $mode eq 'Document' ) {
                $docNum =~ s/-?\d+$//;

                undef $mode unless $docNum;
                next;
            }
        }
        elsif ( $endDoc and $data =~ /^$endDoc/i ) {
            $docNum =~ s/-?(\d+)$//;
            $subDocNum = $1;
            $$et{DOC_NUM} = $docNum;
            undef $endDoc unless $docNum;
            next;
        }
        elsif ( $data =~
/^(%{1,2})(Begin)(_xml_packet|Photoshop|ICCProfile|Document|Binary)/i
          )
        {
            my %modeLookup = (
                _xml_packet => 'XMP',
                photoshop   => 'Photoshop',
                iccprofile  => 'ICC_Profile',
                document    => 'Document',
                binary      => undef,
            );
            $mode = $modeLookup{ lc $3 };
            unless ($mode) {
                if ( not @lines and $data =~ /^%{1,2}BeginBinary:\s*(\d+)/i ) {
                    $raf->Seek( $1, 1 ) or last;
                }
                next;
            }
            $buff       = '';
            $beginToken = $1 . $2 . $3;
            $endToken   = $1 . ( $2 eq 'begin' ? 'end' : 'End' ) . $3;
            if ( $mode eq 'Document' ) {
                if ($docNum) {
                    $docNum .= '-' . ( ++$subDocNum );
                }
                else {
                    $docNum = $$et{DOC_COUNT} + 1;
                }
                $subDocNum = 0;
                next unless $embedded;

                $$et{DOC_NUM}   = $docNum;
                $$et{LIST_TAGS} = {};
                $$et{PROCESSED} = {};
                $endDoc         = $endToken;

                undef $endToken;
                undef $mode;
                if ( $data =~ /^$beginToken:\s+([^\n\r]+)/i ) {
                    my $docName = $1;
                    $docName = $1 if $docName =~ /^\((.*)\)$/;
                    $et->HandleTag( $tagTablePtr, 'EmbeddedFileName',
                        $docName );
                }
            }
            next;
        }
        elsif ( $data =~ /^<\?xpacket begin=.{7,13}W5M0MpCehiHzreSzNTczkc9d/ ) {
            $mode = 'XMP';
            $buff = $data;
            undef $endToken;

            next unless $data =~ m{<\?xpacket end=.(w|r).\?>(\n|\r|$)};
        }
        elsif ( $data =~ /^%%?(\w+): ?(.*)/s and $$tagTablePtr{$1} ) {
            my ( $tag, $val ) = ( $1, $2 );
            next unless $data =~ /^%(%|AI\d+_)/ or $tag eq 'ImageData';
            $val = DecodeComment( $val, $raf, \@lines );
            $et->HandleTag( $tagTablePtr, $tag, $val );
            next;
        }
        elsif ( $embedded and $data =~ /^%AI12_CompressedData/ ) {
            unless ( eval { require Compress::Zlib } ) {
                $et->Warn(
                    'Install Compress::Zlib to extract compressed embedded data'
                );
                last;
            }
            my $tlen = length($data) + @lines;
            $tlen += length $_ foreach @lines;
            my $backTo = $raf->Tell() - $tlen - 64;
            $backTo = 0 if $backTo < 0;
            last unless $raf->Seek( $backTo, 0 ) and $raf->Read( $data, 2048 );
            last unless $data =~ s/.*?%AI12_CompressedData//;
            my $inflate = Compress::Zlib::inflateInit();
            $inflate or $et->Warn('Error initializing inflate'), last;
            my $verbose = $et->Options('Verbose');

            if ( $verbose > 1 ) {
                $et->VerboseDir('AI12_CompressedData (first 4kB)');
                $et->VerboseDump( \$data );
            }
            $data =~ s/^.{0,256}EndData[\x0d\x0a]+//s;
            my $val;
            for ( ; ; ) {
                my ( $v2, $stat ) = $inflate->inflate($data);
                $stat == Compress::Zlib::Z_STREAM_END() and $val .= $v2, last;
                $stat != Compress::Zlib::Z_OK() and undef($val), last;
                if ( defined $val ) {
                    $val .= $v2;
                }
                elsif ( $v2 =~ /^%!PS/ ) {
                    $val = $v2;
                }
                else {
                    $val = "%!PS-Adobe-3.0$/" . $v2;
                }
                $raf->Read( $data, 65536 ) or last;
            }
            defined $val
              or $et->Warn('Error inflating AI compressed data'), last;
            if ( $verbose > 1 ) {
                $et->VerboseDir('Uncompressed AI12 Data');
                $et->VerboseDump( \$val );
            }
            $val = ProcessPS( $et, { DataPt => \$val } );
            last;
        }
        elsif ($fontTable) {
            if ( defined $comment ) {
                if ( $data =~ /^%\s+(.*?)[\x0d\x0a]/ ) {
                    $comment .= "\n" if $comment;
                    $comment .= $1;
                    next;
                }
                elsif ( $data !~ /^%/ ) {
                    $et->FoundTag( 'Comment', $comment ) if length $comment;
                    undef $comment;
                }
            }
            if ( $data =~ m{^\s*/(\w+)\s*(.*)} and $$fontTable{$1} ) {
                my ( $tag, $val ) = ( $1, $2 );
                if ( $val =~ /^\((.*)\)/ ) {
                    $val = UnescapePostScript($1);
                }
                elsif ( $val =~ m{/?(\S+)} ) {
                    $val = $1;
                }
                $et->HandleTag( $fontTable, $tag, $val );
            }
            elsif ( $data =~ /^currentdict end/ ) {
                undef $fontTable;
            }
            next;
        }
        else {
            next;
        }
        my %dirInfo = (
            DataPt   => \$buff,
            DataLen  => length $buff,
            DirStart => 0,
            DirLen   => length $buff,
            Parent   => 'PostScript',
        );
        my $subTablePtr = GetTagTable("Image::ExifTool::${mode}::Main");
        unless ( $et->ProcessDirectory( \%dirInfo, $subTablePtr ) ) {
            $et->Warn("Error processing $mode information in PostScript file");
        }
        undef $buff;
        undef $mode;
    }
    $mode = 'Document' if $endDoc and not $mode;
    $mode and PSErr( $et, "unterminated $mode data" );
    return 1;
}

sub ProcessEPS($$) {
    return ProcessPS( $_[0], $_[1] );
}

1;

__END__

