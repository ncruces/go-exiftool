package Image::ExifTool::XMP;

use strict;
use vars qw(%specialStruct %dateTimeInfo %stdXlatNS);

use Image::ExifTool qw(:DataAccess :Utils);

sub CheckXMP($$$;$);
sub CaptureXMP($$$;$);
sub SetPropertyPath($$;$$$$);

my $debug       = 0;
my $numPadLines = 24;

my $newDescThresh = 10240;

my %extendedRes = (
    'photoshop:History' => 1,
    'xap:Thumbnails'    => 1,
    'xmp:Thumbnails'    => 1,
    'crs'               => 1,
    'crss'              => 1,
);

my $rdfDesc = 'rdf:Description';
my $pktOpen =
  "<?xpacket begin='\xef\xbb\xbf' id='W5M0MpCehiHzreSzNTczkc9d'?>\n";
my $xmlOpen       = "<?xml version='1.0' encoding='UTF-8'?>\n";
my $xmpOpenPrefix = "<x:xmpmeta xmlns:x='$nsURI{x}'";
my $rdfOpen       = "<rdf:RDF xmlns:rdf='$nsURI{rdf}'>\n";
my $rdfClose      = "</rdf:RDF>\n";
my $xmpClose      = "</x:xmpmeta>\n";
my $pktCloseW     = "<?xpacket end='w'?>";
my $pktCloseR     = "<?xpacket end='r'?>";
my ( $sp, $nl );

sub XMPOpen($) {
    my $et = shift;
    my $nv = $$et{NEW_VALUE}{ $Image::ExifTool::XMP::x{xmptk} };
    my $tk;
    if ( defined $nv ) {
        $tk = $et->GetNewValue($nv);
        $et->VerboseValue( ( $tk ? '+' : '-' ) . ' XMP-x:XMPToolkit', $tk );
        ++$$et{CHANGED};
    }
    else {
        $tk = "Image::ExifTool $Image::ExifTool::VERSION";
    }
    my $str = $tk ? ( " x:xmptk='" . EscapeXML($tk) . "'" ) : '';
    return "$xmpOpenPrefix$str>\n";
}

sub ValidateXMP($;$) {
    my ( $xmpPt, $mode ) = @_;
    $$xmpPt =~ s/^\s*<!--.*?-->\s*//s;
    unless ( $$xmpPt =~ /^\0*<\0*\?\0*x\0*p\0*a\0*c\0*k\0*e\0*t/ ) {
        return '' unless $$xmpPt =~ /^<x(mp)?:x[ma]pmeta/;
        $$xmpPt = $pktOpen . $$xmpPt . $pktCloseW;
    }
    $mode = 'w' unless $mode;
    my $end = substr( $$xmpPt, -32, 32 );
    return ''
      unless $end =~
      s/(e\0*n\0*d\0*=\0*['"]\0*)([rw])(\0*['"]\0*\?\0*>)/$1$mode$3/;
    substr( $$xmpPt, -32, 32 ) = $end if $2 ne $mode;
    return 1;
}

sub ValidateProperty($$;$) {
    my ( $et, $propList, $attr ) = @_;

    if ( $$et{XmpValidate} and @$propList > 2 ) {
        if (    $$propList[0] =~ /^x:x[ma]pmeta$/
            and $$propList[1] eq 'rdf:RDF'
            and $$propList[2] =~ /rdf:Description( |$)/ )
        {
            if ( @$propList > 3 ) {
                if ( $$propList[-1] =~ /^rdf:(Bag|Seq|Alt)$/ ) {
                    $et->Warn(
                        "Ignored empty $$propList[-1] list for $$propList[-2]",
                        1
                    );
                }
                else {
                    if ( $$propList[-2] eq 'rdf:Alt' and $attr ) {
                        my $lang = $$attr{'xml:lang'};
                        if ( $lang and @$propList >= 5 ) {
                            my $langPath = join( '/',
                                @$propList[ 3 .. ( $#$propList - 2 ) ] );
                            my $valLang = $$et{XmpValidateLangAlt}
                              || ( $$et{XmpValidateLangAlt} = {} );
                            $$valLang{$langPath} or $$valLang{$langPath} = {};
                            if ( $$valLang{$langPath}{$lang} ) {
                                $et->Warn(
"Duplicate language ($lang) in lang-alt list: $langPath"
                                );
                            }
                            else {
                                $$valLang{$langPath}{$lang} = 1;
                            }
                        }
                    }
                    my $xmpValidate = $$et{XmpValidate};
                    my $path = join( '/', @$propList[ 3 .. $#$propList ] );
                    if ( defined $$xmpValidate{$path} ) {
                        $et->Warn("Duplicate XMP property: $path");
                    }
                    else {
                        $$xmpValidate{$path} = 1;
                    }
                }
            }
        }
        elsif ($$propList[0] ne 'rdf:RDF'
            or $$propList[1] !~ /rdf:Description( |$)/ )
        {
            $et->Warn( 'Improperly enclosed XMP property: '
                  . join( '/', @$propList ) );
        }
    }
}

sub FormatXMPDate($) {
    my $val = shift;
    my ( $y, $m, $d, $t, $tz );
    if (
        $val =~ /(\d{4}):(\d{2}):(\d{2}) (\d{2}:\d{2}(?::\d{2}(?:\.\d*)?)?)(.*)/
        or $val =~
        /(\d{4})-(\d{2})-(\d{2})T(\d{2}:\d{2}(?::\d{2}(?:\.\d*)?)?)(.*)/ )
    {
        ( $y, $m, $d, $t, $tz ) = ( $1, $2, $3, $4, $5 );
        $val = "$y-$m-${d}T$t";
    }
    elsif ( $val =~ /^\s*\d{4}(:\d{2}){0,2}\s*$/ ) {
        $val =~ tr/:/-/;
    }
    elsif ( $val =~ /^\s*(\d{2}:\d{2}(?::\d{2}(?:\.\d*)?)?)(.*)\s*$/ ) {
        ( $t, $tz ) = ( $1, $2 );
        $val = $t;
    }
    else {
        return undef;
    }
    if ($tz) {
        $tz =~ /^(Z|[+-]\d{2}:\d{2})$/ or return undef;
        $val .= $tz;
    }
    return $val;
}

sub CheckXMP($$$;$) {
    my ( $et, $tagInfo, $valPtr, $convType ) = @_;

    if ( $$tagInfo{Struct} ) {
        require 'Image/ExifTool/XMPStruct.pl';
        my ( $item, $err, $w, $warn );
        unless ( ref $$valPtr ) {
            ( $$valPtr, $warn ) = InflateStruct( $et, $valPtr );
            unless ( ref $$valPtr ) {
                $$valPtr eq '' and $$valPtr = {}, return undef;
                return 'Improperly formed structure';
            }
        }
        if ( ref $$valPtr eq 'ARRAY' ) {
            return 'Not a list tag' unless $$tagInfo{List};
            my @copy = ( @{$$valPtr} );
            $$valPtr = \@copy;
            foreach $item (@copy) {
                unless ( ref $item eq 'HASH' ) {
                    ( $item, $w ) = InflateStruct( $et, \$item );
                    $w and $warn = $w;
                    next if ref $item eq 'HASH';
                    $err = 'Improperly formed structure';
                    last;
                }
                ( $item, $err ) = CheckStruct( $et, $item, $$tagInfo{Struct} );
                last if $err;
            }
        }
        else {
            ( $$valPtr, $err ) =
              CheckStruct( $et, $$valPtr, $$tagInfo{Struct} );
        }
        $warn and $$et{CHECK_WARN} = $warn;
        return $err;
    }
    my $format = $$tagInfo{Writable};
    if ( not $format or $format eq 'string' or $format eq 'lang-alt' ) {
        if ( $$et{OPTIONS}{Charset} ne 'UTF8' ) {
            if ( $$valPtr =~ /[\x80-\xff]/ ) {
                $$valPtr = $et->Encode( $$valPtr, 'UTF8' );
            }
        }
        else {
            $$valPtr =~ tr/\0-\x08\x0b\x0c\x0e-\x1f/./;
            if ( FixUTF8($valPtr) and not $$et{WarnBadUTF8} ) {
                $et->Warn('Malformed UTF-8 character(s)');
                $$et{WarnBadUTF8} = 1;
            }
        }
        return undef;
    }
    if ( $format eq 'rational' or $format eq 'real' ) {
        unless (
            Image::ExifTool::IsFloat($$valPtr)
            or
            (
                $format eq 'rational' and ( $$valPtr eq 'inf'
                    or $$valPtr eq 'undef'
                    or Image::ExifTool::IsRational($$valPtr) )
            )
          )
        {
            return 'Not a floating point number';
        }
        if ( $format eq 'rational' ) {
            $$valPtr =
              join( '/', Image::ExifTool::Rationalize( $$valPtr, 0xffffffff ) );
        }
    }
    elsif ( $format eq 'integer' ) {
        if ( Image::ExifTool::IsInt($$valPtr) ) {
        }
        elsif ( Image::ExifTool::IsHex($$valPtr) ) {
            $$valPtr = hex($$valPtr);
        }
        else {
            return 'Not an integer';
        }
    }
    elsif ( $format eq 'date' ) {
        my $newDate = FormatXMPDate($$valPtr);
        return "Invalid date/time (use YYYY:mm:dd HH:MM:SS[.ss][+/-HH:MM|Z])"
          unless $newDate;
        $$valPtr = $newDate;
    }
    elsif ( $format eq 'boolean' ) {
        if ( not $$valPtr or $$valPtr =~ /false/i or $$valPtr =~ /^no$/i ) {
            if (   not $$valPtr
                or $$valPtr ne 'false'
                or not $convType
                or $convType eq 'PrintConv' )
            {
                $$valPtr = 'False';
            }
        }
        elsif ( $$valPtr ne 'true'
            or not $convType
            or $convType eq 'PrintConv' )
        {
            $$valPtr = 'True';
        }
    }
    elsif ( $format eq '1' ) {
        return 'Invalid XMP data' unless ValidateXMP($valPtr);
    }
    else {
        return "Unknown XMP format: $format";
    }
    return undef;
}

sub GetPropertyPath($) {
    my $tagInfo = shift;
    SetPropertyPath( $$tagInfo{Table}, $$tagInfo{TagID} )
      unless $$tagInfo{PropertyPath};
    return $$tagInfo{PropertyPath};
}

sub SetPropertyPath($$;$$$$) {
    my ( $tagTablePtr, $tagID, $parentID, $structPtr, $propList, $isType ) = @_;
    my $table   = $structPtr || $tagTablePtr;
    my $tagInfo = $$table{$tagID};
    my $flatInfo;

    return if ref($tagInfo) ne 'HASH';

    if ($structPtr) {
        my $flatID = $parentID . ucfirst($tagID);
        $flatInfo = $$tagTablePtr{$flatID};
        if ($flatInfo) {
            return if $$flatInfo{PropertyPath};
        }
        elsif ( @$propList > 50 ) {
            return;
        }
        else {
            $flatInfo = { Name => ucfirst($flatID), Flat => 1 };
            AddTagToTable( $tagTablePtr, $flatID, $flatInfo );
        }
        $isType = 1 if $$structPtr{TYPE};
    }
    else {
        return if $$tagInfo{PropertyPath};
        my $srcInfo = $$tagInfo{SrcTagInfo};
        $$tagInfo{PropertyPath} = GetPropertyPath($srcInfo) if $srcInfo;
        return if $$tagInfo{PropertyPath};
        if ( $$tagInfo{RootTagInfo} ) {
            SetPropertyPath( $tagTablePtr, $$tagInfo{RootTagInfo}{TagID} );
            return if $$tagInfo{PropertyPath};
            warn "Internal Error: Didn't set path from root for $tagID\n";
            warn "(Is the Struct NAMESPACE defined?)\n";
        }
    }
    my $ns = $$tagInfo{Namespace} || $$table{NAMESPACE};
    $ns or warn("No namespace for $tagID\n"), return;
    my ( @propList, $listType );
    $propList and @propList = @$propList;
    push @propList, "$ns:$tagID";
    if ( $$tagInfo{Writable} and $$tagInfo{Writable} eq 'lang-alt' ) {
        $listType = 'Alt';
        $propList[-1] =~ s/-$$tagInfo{LangCode}$// if $$tagInfo{LangCode};
        if ( $$tagInfo{List} and $$tagInfo{List} ne '1' ) {
            push @propList, "rdf:$$tagInfo{List}", 'rdf:li 10';
        }
    }
    else {
        $listType = $$tagInfo{List};
    }
    push @propList, "rdf:$listType", 'rdf:li 10'
      if $listType and $listType ne '1';
    my $strTable = $$tagInfo{Struct};
    if (
        $strTable and not(
            $parentID
            and
            (
                (
                        $$tagTablePtr{$parentID}
                    and $$tagTablePtr{$parentID}{NoSubStruct}
                )
                or length $parentID > 500
            )
        )
      )
    {
        RegisterNamespace($strTable) if ref $$strTable{NAMESPACE};
        my $tag;
        foreach $tag ( keys %$strTable ) {
            next if $specialStruct{$tag} or $$strTable{$tag}{LangCode};
            my $fullID = $parentID ? $parentID . ucfirst($tagID) : $tagID;
            SetPropertyPath( $tagTablePtr, $tag, $fullID, $strTable,
                \@propList, $isType );
        }
    }
    if ($structPtr) {
        $tagInfo = $flatInfo;
        $$tagInfo{StructType} = 1 if $isType;
    }
    $$tagInfo{PropertyPath} = join '/', @propList;
}

sub CaptureXMP($$$;$) {
    my ( $et, $propList, $val, $attrs ) = @_;
    return unless defined $val and @$propList > 2;
    if (    $$propList[0] =~ /^x:x[ma]pmeta$/
        and $$propList[1] eq 'rdf:RDF'
        and $$propList[2] =~ /$rdfDesc( |$)/ )
    {
        return unless @$propList > 3;
        if ( $$propList[-1] =~ /^rdf:(Bag|Seq|Alt)$/ ) {
            $et->Warn( "Ignored empty $$propList[-1] list for $$propList[-2]",
                1 );
            return;
        }
        my $capture = $$et{XMP_CAPTURE};
        my $path    = join( '/', @$propList[ 3 .. $#$propList ] );
        if ( defined $$capture{$path} ) {
            $$et{XMP_ERROR} = "Duplicate XMP property: $path";
        }
        else {
            $$capture{$path} = [ $val, $attrs || {} ];
        }
    }
    elsif ( $$propList[0] eq 'rdf:RDF'
        and $$propList[1] =~ /$rdfDesc( |$)/ )
    {
        $$et{XMP_NO_XMPMETA} = 1;
        unshift @$propList, 'x:xmpmeta';
        CaptureXMP( $et, $propList, $val, $attrs );
    }
    else {
        $$et{XMP_ERROR} =
          'Improperly enclosed XMP property: ' . join( '/', @$propList );
    }
}

sub SaveBlankInfo($$$;$) {
    my ( $blankInfo, $propListPt, $val, $attrs ) = @_;

    my $propPath = join '/', @$propListPt;
    my @ids      = ( $propPath =~ m{ #([^ /]*)}g );
    my $id;
    foreach $id (@ids) {
        my ( $pre, $prop, $post ) =
          ( $propPath =~ m{^(.*?)/([^/]*) #$id((/.*)?)$} );
        defined $pre or warn("internal error parsing nodeID's"), next;
        unless ( $prop eq $rdfDesc ) {
            if ($post) {
                $post = "/$prop$post";
            }
            else {
                $pre = "$pre/$prop";
            }
        }
        $$blankInfo{Prop}{$id}{Pre}{$pre} = 1;
        if (   ( defined $post and length $post )
            or ( defined $val and length $val ) )
        {
            $$blankInfo{Prop}{$id}{Post}{$post} = [ $val, $attrs, $propPath ];
        }
    }
}

sub ProcessBlankInfo($$$;$) {
    my ( $et, $tagTablePtr, $blankInfo, $isWriting ) = @_;
    $et->VPrint( 1, "  [Elements with nodeID set:]\n" ) unless $isWriting;
    my ( $id, $pre, $post );
    foreach $id ( sort keys %{ $$blankInfo{Prop} } ) {
        my $path = $$blankInfo{Prop}{$id};
        my %unused;
        foreach $post ( keys %{ $$path{Post} } ) {
            $unused{$post} = 1;
        }
        foreach $pre ( sort keys %{ $$path{Pre} } ) {
            next unless $pre =~ m{/$rdfDesc/};
            foreach $post ( sort keys %{ $$path{Post} } ) {
                my @propList = split m{/}, "$pre$post";
                my ( $val, $attrs ) = @{ $$path{Post}{$post} };
                if ($isWriting) {
                    CaptureXMP( $et, \@propList, $val, $attrs );
                }
                else {
                    FoundXMP( $et, $tagTablePtr, \@propList, $val );
                }
                delete $unused{$post};
            }
        }
        if (%unused) {
            $et->Options('Verbose')
              and $et->Warn('An XMP resource is about nothing');
            foreach $post ( sort keys %unused ) {
                my ( $val, $attrs, $propPath ) = @{ $$path{Post}{$post} };
                my @propList = split m{/}, $propPath;
                if ($isWriting) {
                    CaptureXMP( $et, \@propList, $val, $attrs );
                }
                else {
                    FoundXMP( $et, $tagTablePtr, \@propList, $val );
                }
            }
        }
    }
}

sub ConformPathToNamespace($$) {
    my ( $et, $path ) = @_;
    my @propList = split( '/', $path );
    my $nsUsed   = $$et{XMP_NS};
    my $prop;
    foreach $prop (@propList) {
        my ( $ns, $tag ) = $prop =~ /(.+?):(.*)/;
        next if not defined $ns or $$nsUsed{$ns};
        my $uri = $nsURI{$ns};
        unless ($uri) {
            warn "No URI for namespace prefix $ns!\n";
            next;
        }
        my $ns2;
        foreach $ns2 ( keys %$nsUsed ) {
            next unless $$nsUsed{$ns2} eq $uri;
            $prop = "$ns2:$tag";
            last;
        }
    }
    return join( '/', @propList );
}

sub AddStructType($$$$;$) {
    my ( $et, $tagTablePtr, $capture, $path, $basePath ) = @_;
    my @props = split '/', $path;
    my %doneID;
    for ( ; ; ) {
        pop @props;
        last unless @props;
        my $tagID = GetXMPTagID( \@props );
        next if $doneID{$tagID};
        $doneID{$tagID} = 1;
        my $tagInfo = $$tagTablePtr{$tagID};
        last unless ref $tagInfo eq 'HASH';
        if ( $$tagInfo{Struct} ) {
            my $type = $$tagInfo{Struct}{TYPE};
            if ($type) {
                my $pat = $$tagInfo{PropertyPath};
                $pat or warn("Missing PropertyPath in AddStructType\n"), last;
                $pat = ConformPathToNamespace( $et, $pat );
                $pat  =~ s/ \d+/ \\d\+/g;
                $path =~ /^($pat)/
                  or warn("Wrong path in AddStructType\n"), last;
                my $p = $1 . '/rdf:type';
                $p = "$basePath/$p" if $basePath;
                $$capture{$p} = [ '', { 'rdf:resource' => $type } ]
                  unless $$capture{$p};
            }
        }
        last unless $$tagInfo{StructType};
    }
}

sub ProcessGSpherical($$$) {
    my ( $et, $dirInfo, $tagTablePtr ) = @_;
    if ( $$et{REQ_TAG_LOOKUP}{sphericalvideoxml} ) {
        $et->FoundTag(
            SphericalVideoXML => substr( ${ $$dirInfo{DataPt} }, 16 ) );
    }
    return Image::ExifTool::XMP::ProcessXMP( $et, $dirInfo, $tagTablePtr );
}

sub WriteGSpherical($$$) {
    my ( $et, $dirInfo, $tagTablePtr ) = @_;
    $$dirInfo{Compact} = 1, my $dataPt = $$dirInfo{DataPt};
    if ( $dataPt and $$dataPt ) {
        my $buff = $$dataPt;
        $buff =~
s/<rdf:SphericalVideo/<?xpacket begin='.*?' id='W5M0MpCehiHzreSzNTczkc9d'?>\n<x:xmpmeta xmlns:x='adobe:ns:meta\/'><rdf:RDF/;
        $buff =~ s/\s*xmlns:GSpherical/>\n<rdf:Description xmlns:GSpherical/s;
        $buff =~ s/<\/rdf:SphericalVideo>/<\/rdf:Description>/;
        $buff .= "</rdf:RDF></x:xmpmeta><?xpacket end='w'?>";
        $$dirInfo{DataPt} = \$buff;
        $$dirInfo{DirLen} = length($buff) - ( $$dirInfo{DirStart} || 0 );
    }
    my $xmp = Image::ExifTool::XMP::WriteXMP( $et, $dirInfo, $tagTablePtr );
    if ($xmp) {
        $xmp =~ s/^<\?xpacket begin.*?<rdf:RDF/<rdf:SphericalVideo\n/s;
        $xmp =~ s/>\s*<rdf:Description rdf:about=''\s*/\n /;
        $xmp =~
          s/\s*<\/rdf:Description>\s*(<\/rdf:RDF>)/\n<\/rdf:SphericalVideo>$1/s;
        $xmp =~ s/\s*<\/rdf:RDF>\s*<\/x:xmpmeta>.*//s;
    }
    return $xmp;
}

sub EncodeBase64($;$) {
    my $chunkSize = 45;
    my $len       = length $_[0];
    my $str       = '';
    my $i;
    for ( $i = 0 ; $i < $len ; $i += $chunkSize ) {
        my $n = $len - $i;
        $n = $chunkSize if $n > $chunkSize;
        $str .= substr( pack( 'u', substr( $_[0], $i, $n ) ), 1 );
    }
    $str =~ tr/` -_/AA-Za-z0-9+\//;
    my $pad = 3 - ( $len % 3 );
    substr( $str, -$pad - 1, $pad ) = ( '=' x $pad ) if $pad < 3;
    $str =~ tr/\n//d if $_[1];
    return $str;
}

sub ByTagName {
    return $$a{Name} cmp $$b{Name};
}

sub TypeFirst {
    if ( $a =~ /rdf:type$/ ) {
        return substr( $a, 0, -8 ) cmp $b unless $b =~ /rdf:type$/;
    }
    elsif ( $b =~ /rdf:type$/ ) {
        return $a cmp substr( $b, 0, -8 );
    }
    return $a cmp $b;
}

sub LimitXMPSize($$$$$$) {
    my ( $et, $dataPt, $maxLen, $about, $startPt, $extStart ) = @_;

    return undef if length($$dataPt) < $maxLen;

    push @$startPt, length($$dataPt);
    my $newData = substr( $$dataPt, 0, $$startPt[0] );
    my $guid    = '0' x 32;
    $newData .=
"$nl$sp<$rdfDesc rdf:about='${about}'\n$sp${sp}xmlns:xmpNote='$nsURI{xmpNote}'";
    if ( $$et{OPTIONS}{Compact}{Shorthand} ) {
        $newData .= "\n$sp${sp}xmpNote:HasExtendedXMP='${guid}'/>\n";
    }
    else {
        $newData .=
">$nl$sp$sp<xmpNote:HasExtendedXMP>$guid</xmpNote:HasExtendedXMP>$nl$sp</$rdfDesc>\n";
    }

    my ( $i, %descSize, $start );
    for ( $i = 1 ; $i < @$startPt ; ++$i ) {
        $descSize{ $$startPt[ $i - 1 ] } = $$startPt[$i] - $$startPt[ $i - 1 ];
    }
    pop @$startPt;

    my @descStart = sort { $descSize{$a} <=> $descSize{$b} } @$startPt;
    my $extData   = XMPOpen($et) . $rdfOpen;
    for ( $i = 0 ; $i < 2 ; ++$i ) {
        foreach $start (@descStart) {
            next if $i xor $start >= $extStart;
            my $pt =
              ( length($newData) + $descSize{$start} > $maxLen )
              ? \$extData
              : \$newData;
            $$pt .= substr( $$dataPt, $start, $descSize{$start} );
        }
    }
    $extData .= $rdfClose . $xmpClose;

    if ( eval { require Digest::MD5 } ) {
        $guid = uc unpack( 'H*', Digest::MD5::md5($extData) );
        $newData =~ s/0{32}/$guid/;
    }
    $et->VerboseValue( '+ XMP-xmpNote:HasExtendedXMP', $guid );
    $$dataPt = $newData;
    return ( \$extData, $guid );
}

sub CloseProperty($$$$) {
    my ( $curPropList, $long, $short, $resFlag ) = @_;

    my $prop = pop @$curPropList;
    $prop =~ s/ .*//;
    my $pad = $sp x ( scalar(@$curPropList) + 1 );
    if ( $$resFlag[@$curPropList] ) {
        if ( length $$short[-1] ) {
            if ( length $$long[-1] ) {
                $$long[-2]  .= ">$nl$pad<$rdfDesc";
                $$short[-1] .= ">$nl";
                $$long[-1]  .= "$pad</$rdfDesc>$nl";
            }
            else {
                $$short[-1] .= "/>$nl";
            }
        }
        else {
            $$long[-2] .= ' rdf:parseType="Resource"';
            $$short[-1] = length $$long[-1] ? ">$nl" : "/>$nl";
        }
        $$long[-1] .= "$pad</$prop>$nl" if length $$long[-1];
        $$long[-2] .= $$short[-1] . $$long[-1];
        pop @$short;
        pop @$long;
    }
    elsif ( defined $$resFlag[@$curPropList] ) {
        if ( length $$long[-1] ) {
            $$long[-2] .= $$short[-1] . ">$nl" . $$long[-1] . "$pad</$prop>$nl";
        }
        else {
            $$long[-2] .= $$short[-1] . "/>$nl";
        }
        $$short[-1] = $$long[-1] = '';
    }
    else {
        $$long[-1] .= "$pad</$prop>$nl";
        unless (@$curPropList) {
            $$long[-2] .= ">$nl" . $$long[-1];
            $$long[-1] = '';
        }
    }
    $#$resFlag = $#$curPropList;
}

sub WriteXMP($$;$) {
    my ( $et, $dirInfo, $tagTablePtr ) = @_;
    $et or return 1;
    my $dataPt = $$dirInfo{DataPt};
    my ( %capture, %nsUsed, $xmpErr, $about );
    my $changed = 0;
    my $xmpFile = ( not $tagTablePtr );

    my $preferred =
      $xmpFile || ( $$et{PreferredGroup} and $$et{PreferredGroup} eq 'XMP' );
    my $verbose = $$et{OPTIONS}{Verbose};
    my %compact = ( %{ $$et{OPTIONS}{Compact} } );
    my $dirLen  = $$dirInfo{DirLen};
    $dirLen = length($$dataPt) if not defined $dirLen and $dataPt;
    $$et{XMP_CAPTURE} = \%capture;
    $$et{XMP_NS}      = \%nsUsed;
    delete $$et{XMP_NO_XMPMETA};
    delete $$et{XMP_NO_XPACKET};
    delete $$et{XMP_IS_XML};
    delete $$et{XMP_IS_SVG};

    ( $sp, $nl ) =
      ( $compact{NoIndent} ? '' : ' ', $compact{NoNewline} ? '' : "\n" );

    my $tagInfo = $Image::ExifTool::XMP::rdf{about};
    if ( defined $$et{NEW_VALUE}{$tagInfo} ) {
        $about = $et->GetNewValue( $$et{NEW_VALUE}{$tagInfo} ) || '';
    }

    if ( $xmpFile or $dirLen ) {
        delete $$et{XMP_ERROR};
        my $success = ProcessXMP( $et, $dirInfo, $tagTablePtr );
        unless ( $success and not $$et{XMP_ERROR} ) {
            my $err = $$et{XMP_ERROR} || 'Error parsing XMP';
            if ($xmpFile) {
                my $raf = $$dirInfo{RAF};
                if ( $success or not $raf->Seek( 0, 2 ) or $raf->Tell() ) {
                    return 0 unless $$et{XMP_ERROR};
                    if ( $et->Error( $err, $success ) ) {
                        delete $$et{XMP_CAPTURE};
                        return 0;
                    }
                }
            }
            else {
                $success = 2 if $success and $success eq '1';
                if ( $et->Warn( $err, $success ) ) {
                    delete $$et{XMP_CAPTURE};
                    return undef;
                }
            }
        }
        if ( defined $about ) {
            if ( $verbose > 1 ) {
                my $wasAbout = $$et{XmpAbout};
                $et->VerboseValue( '- XMP-rdf:About', UnescapeXML($wasAbout) )
                  if defined $wasAbout;
                $et->VerboseValue( '+ XMP-rdf:About', $about );
            }
            $about = EscapeXML($about);
            ++$changed;
        }
        else {
            $about = $$et{XmpAbout} || '';
        }
        delete $$et{XMP_ERROR};

        $et->InitWriteDirs( {}, 'XMP' )
          if $xmpFile and $et->GetNewValue('ForceWrite');
        ++$changed if $$et{FORCE_WRITE}{XMP};

    }
    elsif ( defined $about ) {
        $et->VerboseValue( '+ XMP-rdf:About', $about );
        $about = EscapeXML($about);

    }
    else {
        $about = '';
    }
    if ($xmpFile) {
        $tagInfo = $Image::ExifTool::Extra{XMP};
        if ( $tagInfo and $$et{NEW_VALUE}{$tagInfo} ) {
            my $rtnVal = 1;
            my $newVal = $et->GetNewValue( $$et{NEW_VALUE}{$tagInfo} );
            if ( defined $newVal and length $newVal ) {
                $et->VPrint( 0, "  Writing XMP as a block\n" );
                ++$$et{CHANGED};
                Write( $$dirInfo{OutFile}, $newVal ) or $rtnVal = -1;
            }
            delete $$et{XMP_CAPTURE};
            return $rtnVal;
        }
    }
    if (
        %{ $$et{DEL_GROUP} } and (
            grep /^XMP-.+$/, keys %{ $$et{DEL_GROUP} }
            or
            grep m{^http://ns.exiftool.(?:ca|org)/}, values %nsUsed
        )
      )
    {
        my $del = $$et{DEL_GROUP};
        my $path;
        foreach $path ( keys %capture ) {
            my @propList = split( '/', $path );
            my ( $tag, $ns ) = GetXMPTagID( \@propList );
            $ns = $stdXlatNS{$ns} if $stdXlatNS{$ns};
            my ( $grp, @g );
            if (
                $nsUsed{$ns}
                and (
                    @g = (
                        $nsUsed{$ns} =~
                          m{^http://ns.exiftool.(?:ca|org)/(.*?)/(.*?)/}
                    )
                )
              )
            {
                if ( $g[1] =~ /^\d/ ) {
                    $grp = "XML-$g[0]";
                    my $ucg = uc $grp;
                    next
                      unless $$del{$ucg}
                      or ( $$del{'XML-*'} and not $$del{"-$ucg"} );
                }
                else {
                    $grp = $g[1];
                    next
                      unless $$del{$grp}
                      or ( $$del{ $g[0] } and not $$del{"-$grp"} );
                }
            }
            else {
                $grp = "XMP-$ns";
                my $ucg = uc $grp;
                next
                  unless $$del{$ucg}
                  or ( $$del{'XMP-*'} and not $$del{"-$ucg"} );
            }
            $et->VerboseValue( "- $grp:$tag", $capture{$path}->[0] );
            delete $capture{$path};
            ++$changed;
        }
    }
    my $hasExtTag = 'xmpNote:HasExtendedXMP';
    if ( $capture{$hasExtTag} ) {
        $et->VerboseValue( "- XMP-$hasExtTag", $capture{$hasExtTag}->[0] );
        delete $capture{$hasExtTag};
    }
    my $xmpOpen = $$et{XMP_NO_XMPMETA} ? '' : XMPOpen($et);
    my (
        @tagInfoList, @structList,   $delLangPath, %delLangPaths,
        %delAllLang,  $firstNewPath, @langTags
    );
    my $writeGroup = $$dirInfo{WriteGroup};
    foreach $tagInfo ( sort ByTagName $et->GetNewTagInfoList() ) {
        next unless $et->GetGroup( $tagInfo, 0 ) eq 'XMP';
        next if $$tagInfo{Name} eq 'XMP';
        next
          if $writeGroup
          and $writeGroup ne $$et{NEW_VALUE}{$tagInfo}{WriteGroup};
        if ( $$tagInfo{LangCode} ) {
            push @langTags, $tagInfo;
        }
        elsif ( $$tagInfo{Struct} ) {
            push @structList, $tagInfo;
        }
        else {
            push @tagInfoList, $tagInfo;
        }
    }
    if (@langTags) {
        foreach $tagInfo (
            sort { $$et{NEW_VALUE}{$a}{Order} <=> $$et{NEW_VALUE}{$b}{Order} }
            @langTags )
        {
            if ( $$tagInfo{Struct} ) {
                push @structList, $tagInfo;
            }
            else {
                push @tagInfoList, $tagInfo;
            }
        }
    }
    foreach $tagInfo ( @structList, @tagInfoList ) {
        my @delPaths;
        my $tag  = $$tagInfo{TagID};
        my $path = GetPropertyPath($tagInfo);
        unless ($path) {
            $et->Warn("Can't write XMP:$tag (namespace unknown)");
            next;
        }
        if ( $path eq 'rdf:about' or $path eq 'x:xmptk' ) {
            ++$changed;
            next;
        }
        my $isStruct = $$tagInfo{Struct};
        $path = ConformPathToNamespace( $et, $path );
        my $cap = $capture{$path};
        until ($cap) {
            my @fixInfo;
            if ( $isStruct or defined $$tagInfo{Flat} ) {
                my @props = split '/', $path;
                my $tbl   = $$tagInfo{Table};
                while (@props) {
                    my $info = $$tbl{ GetXMPTagID( \@props ) };
                    unshift @fixInfo, $info
                      if ref $info eq 'HASH'
                      and $$info{Struct}
                      and ( not @fixInfo or $fixInfo[0] ne $info );
                    pop @props;
                }
                $et->Warn("Error finding parent structure for $$tagInfo{Name}")
                  unless @fixInfo;
            }
            push @fixInfo, $tagInfo unless @fixInfo and $isStruct;
            my $err;
            while (@fixInfo) {
                my $fixInfo = shift @fixInfo;
                my $fixPath =
                  ConformPathToNamespace( $et, GetPropertyPath($fixInfo) );
                my $regex = quotemeta($fixPath);
                $regex =~ s/ \d+/ \\d\+/g;
                my $ok = $regex;
                my ( $ok2, $match, $i, @fixed, %fixed, $fixed );
                if ( $regex =~
                    s{\\/rdf\\:(Bag|Seq|Alt)\\/}{/rdf:(Bag|Seq|Alt)/}g )
                {
                    if ( $regex =~
                        s{/rdf:\(Bag\|Seq\|Alt\)\/rdf\\:li\\ \\d\+$}{} )
                    {
                        $regex .= '(/.*)?' unless @fixInfo;
                    }
                }
                elsif ( not @fixInfo ) {
                    $ok2 = $regex;
                    $regex .= '(/rdf:(Bag|Seq|Alt)/rdf:li \d+)?';
                }
                if (@fixInfo) {
                    $regex .= '(/.*)?';
                    $ok    .= '(/.*)?';
                }
                my @matches = sort grep m{^$regex$}i, keys %capture;
                last unless @matches;
                if ( $matches[0] =~ m{^$ok$} ) {
                    unless (@fixInfo) {
                        $path = $matches[0];
                        $cap  = $capture{$path};
                    }
                    next;
                }
                my @fixProps = split '/', $fixPath;
                foreach $match (@matches) {
                    my @matchProps = split '/', $match;
                    $#matchProps = $#fixProps
                      if $ok2 and $#matchProps > $#fixProps;
                    for ( $i = 0 ; $i < @fixProps ; ++$i ) {
                        defined $matchProps[$i]
                          or $matchProps[$i] = $fixProps[$i], next;
                        next
                          if $matchProps[$i] =~ / \d+$/
                          or $matchProps[$i] eq $fixProps[$i];
                        $matchProps[$i] = $fixProps[$i];
                    }
                    $fixed = join '/', @matchProps;
                    $err   = 1
                      if $fixed{$fixed}
                      or ( $capture{$fixed} and $match ne $fixed );
                    push @fixed, $fixed;
                    $fixed{$fixed} = 1;
                }
                my $tg = $et->GetGroup( $fixInfo, 1 ) . ':' . $$fixInfo{Name};
                my $wrn =
                  lc( $fixed[0] ) eq lc( $matches[0] )
                  ? 'tag ID case'
                  : 'list type';
                if ($err) {
                    $et->Warn("Incorrect $wrn for existing $tg (not changed)");
                }
                else {
                    my $didFix;
                    foreach $fixed (@fixed) {
                        my $match = shift @matches;
                        next if $fixed eq $match;
                        $capture{$fixed} = $capture{$match};
                        delete $capture{$match};
                        delete $capture{$fixed}[1]{'xml:lang'}
                          if $ok2 and $match !~ /^$ok2$/;
                        $didFix = 1;
                    }
                    $cap = $capture{$path} || $capture{ $fixed[0] }
                      unless @fixInfo;
                    if ($didFix) {
                        $et->Warn( "Fixed incorrect $wrn for $tg", 1 );
                        ++$changed;
                    }
                }
            }
            last;
        }
        my $nvHash    = $et->GetNewValueHash($tagInfo);
        my $overwrite = $et->IsOverwriting($nvHash);
        my $writable  = $$tagInfo{Writable} || '';
        my ( %attrs, $deleted, $added, $existed, $newLang );
        if ( $writable eq 'lang-alt' ) {
            $newLang = lc( $$tagInfo{LangCode} || 'x-default' );
            if ( $delLangPath and $delLangPath eq $path ) {
                @delPaths = @{ $delLangPaths{$newLang} }
                  if $delLangPaths{$newLang};
            }
            else {
                undef %delLangPaths;
                $delLangPath = $path;
                undef %delAllLang;
                undef $firstNewPath;
            }
            if (%delAllLang) {
                my ( $prefix, $reSort );
                foreach $prefix ( keys %delAllLang ) {
                    next if grep /^$prefix/, @delPaths;
                    push @delPaths, "${prefix}10";
                    $reSort = 1;
                }
                @delPaths = sort @delPaths if $reSort;
            }
        }
        if ($isStruct) {
            require 'Image/ExifTool/XMPStruct.pl';
            ( $deleted, $added, $existed ) =
              DeleteStruct( $et, \%capture, \$path, $nvHash, \$changed );
            undef $added
              if not $existed
              and not $$nvHash{IsCreating}
              and $$tagInfo{Avoid};
            next unless $deleted or $added or $et->IsOverwriting($nvHash);
            next if $existed and $$nvHash{CreateOnly};
        }
        elsif ($cap) {
            next if $$nvHash{CreateOnly};

            %attrs = %{ $$cap[1] };
            if ($overwrite) {
                my ( $oldLang, $delLang, $addLang, @matchingPaths,
                    $langPathPat, %langsHere );
                if ( $path =~ / / ) {
                    my $pp;
                    ( $pp = $path ) =~ s/ \d+/ \\d\+/g;
                    @matchingPaths = sort grep( /^$pp$/, keys %capture );
                }
                else {
                    push @matchingPaths, $path;
                }
                my $oldOverwrite = $overwrite;
                foreach $path (@matchingPaths) {
                    my ( $val, $attrs ) = @{ $capture{$path} };
                    if ( $writable eq 'lang-alt' ) {
                        $oldLang = lc( $$attrs{'xml:lang'} || 'x-default' );
                        if ( not $langPathPat or $path !~ /^$langPathPat$/ ) {
                            $overwrite = $oldOverwrite;
                            ( $langPathPat = $path ) =~ s/\d+$/\\d+/;
                        }
                        $langsHere{$langPathPat}{$oldLang} = 1;
                        unless ( defined $addLang ) {
                            $addLang = $$nvHash{IsCreating} ? 1 : 0;
                        }
                        if ( $overwrite < 0 ) {
                            next unless $oldLang eq $newLang;
                            $addLang =
                              $et->IsOverwriting( $nvHash, UnescapeXML($val) );
                            next unless $addLang;
                        }
                        if ( $oldLang eq 'x-default'
                            and not $$tagInfo{LangCode} )
                        {
                            $delLang   = 1;
                            $overwrite = 1;
                        }
                        elsif ( $$tagInfo{LangCode} and not $delLang ) {
                            next unless lc( $$tagInfo{LangCode} ) eq $oldLang;
                        }
                    }
                    elsif ( $overwrite < 0 ) {
                        if ( $$nvHash{Shift} ) {
                            my $fmt = $$tagInfo{Writable} || '';
                            if ( $fmt eq 'rational' ) {
                                ConvertRational($val);
                            }
                            elsif ( $fmt eq 'date' ) {
                                $val = ConvertXMPDate($val);
                            }
                        }
                        next
                          unless $et->IsOverwriting( $nvHash,
                            UnescapeXML($val) );
                    }
                    if ( $verbose > 1 ) {
                        my $grp     = $et->GetGroup( $tagInfo, 1 );
                        my $tagName = $$tagInfo{Name};
                        $tagName =~ s/-$$tagInfo{LangCode}$//
                          if $$tagInfo{LangCode};
                        $tagName .= '-' . $$attrs{'xml:lang'}
                          if $$attrs{'xml:lang'};
                        $et->VerboseValue( "- $grp:$tagName", $val );
                    }
                    %attrs = %$attrs unless @delPaths;
                    if ( $writable eq 'lang-alt' ) {
                        $langsHere{$langPathPat}{$oldLang} = 0;
                    }
                    if ( $writable ne 'lang-alt' or $oldLang eq $newLang ) {
                        push @delPaths, $path;
                    }
                    else {
                        $delLangPaths{$oldLang} or $delLangPaths{$oldLang} = [];
                        push @{ $delLangPaths{$oldLang} }, $path;
                    }
                    if ($delLang) {
                        my $p;
                        ( $p = $path ) =~ s/\d+$//;
                        $delAllLang{$p} = 1;
                    }
                    delete $capture{$path};
                    ++$changed;
                    if ( $path =~ /^(.*)\// and $capture{"$1/rdf:type"} ) {
                        my $pp = $1;
                        my @a  = grep /^\Q$pp\E\/[^\/]+/, keys %capture;
                        delete $capture{"$pp/rdf:type"} if @a == 1;
                    }
                }
                next unless @delPaths or $$tagInfo{List} or $addLang;
                if (@delPaths) {
                    $path = shift @delPaths;
                    while ( $capture{$path} ) {
                        last
                          unless $path =~ s/ \d(\d+)$/' '.length($1+1).($1+1)/e;
                    }
                    $deleted = 1;
                }
                else {
                    next unless $$tagInfo{List} or $oldLang;
                    if ( $writable eq 'lang-alt' and %langsHere ) {
                        foreach ( sort keys %langsHere ) {
                            next unless $path =~ /^$_$/;
                            last unless $langsHere{$_}{$newLang};
                            $path =~ /(.* )\d(\d+)(.*? \d+)$/
                              or $et->Error(
                                'Internal error writing lang-alt list'), last;
                            my $nxt = $2 + 1;
                            $path = $1 . length($nxt) . ($nxt) . $3;
                        }
                    }
                    $path =~ m/.* (\d+)/g
                      or warn "Internal error: no list index!\n", next;
                    $added = $1;
                }
            }
            else {
                my $pat = '.* (\d+)';
                if ( $writable eq 'lang-alt' ) {
                    if ($firstNewPath) {
                        $path      = $firstNewPath;
                        $overwrite = 1;
                    }
                    else {
                        $pat = '.* (\d+)(.*? \d+)';
                    }
                }
                if ( $path =~ m/$pat/g ) {
                    $added = $1;
                    pos($path) = pos($path) - length($2) if $2;
                }
            }
            if ( defined $added ) {
                my $len = length $added;
                my $pos = pos($path) - $len;
                my $nxt = substr( $added, 1 ) + 1;
                if (
                        $overwrite
                    and $writable eq 'lang-alt'
                    and ( not $$tagInfo{LangCode}
                        or $$tagInfo{LangCode} eq 'x-default' )
                  )
                {
                    my $saveCap = $capture{$path};
                    while ($saveCap) {
                        my $p = $path;
                        substr( $p, $pos, $len ) = length($nxt) . $nxt;
                        my $nextCap = $capture{$p};
                        $capture{$p} = $saveCap;
                        last unless $nextCap;
                        $saveCap = $nextCap;
                        ++$nxt;
                    }
                }
                else {
                    while ( $capture{$path} ) {
                        my $try = length($nxt) . $nxt;
                        substr( $path, $pos, $len ) = $try;
                        $len = length $try;
                        ++$nxt;
                    }
                }
            }
        }
        my $isCreating = (
            $$nvHash{IsCreating}
              or (
                (
                    $isStruct
                    or ( $preferred and not defined $$nvHash{Shift} )
                )
                and not $$tagInfo{Avoid}
                and not $$nvHash{EditOnly}
              )
        );

        next unless $deleted or defined $added or
          ( not $cap and $isCreating );

        my @newValues = $et->GetNewValue($nvHash) or next;

        if ( $writable eq 'lang-alt' ) {
            $attrs{'xml:lang'} = $$tagInfo{LangCode} || 'x-default';
            $firstNewPath = $path if defined $added;
        }
        my $subIdx;
        for ( ; ; ) {
            my $newValue = shift @newValues;
            if ($isStruct) {
                ++$changed
                  if AddNewStruct( $et, $tagInfo, \%capture,
                    $path, $newValue, $$tagInfo{Struct} );
            }
            else {
                $newValue = EscapeXML($newValue);
                for ( ; ; ) {
                    if ( $$tagInfo{Resource} ) {
                        if ( $newValue !~
/[^a-z0-9\:\/\?\#\[\]\@\!\$\&\'\(\)\*\+\,\;\=\.\-\_\~]/i
                          )
                        {
                            $capture{$path} =
                              [ '', { %attrs, 'rdf:resource' => $newValue } ];
                            last;
                        }
                        my $grp = $et->GetGroup( $tagInfo, 1 );
                        $et->Warn(
"$grp:$$tagInfo{Name} written as a literal because value is not a valid URI",
                            1
                        );
                    }
                    delete $attrs{'rdf:value'};
                    delete $attrs{'rdf:resource'};
                    $capture{$path} = [ $newValue, \%attrs ];
                    last;
                }
                if ( $verbose > 1 ) {
                    my $grp = $et->GetGroup( $tagInfo, 1 );
                    $et->VerboseValue( "+ $grp:$$tagInfo{Name}", $newValue );
                }
                ++$changed;
                if ( $$tagInfo{StructType} ) {
                    AddStructType( $et, $$tagInfo{Table}, \%capture, $path );
                }
            }
            last unless @newValues;
            my $pat =
              $writable eq 'lang-alt' ? '.* (\d+)(.*? \d+)' : '.* (\d+)';
            pos($path) = 0;
            $path =~ m/$pat/g
              or
              warn("Internal error: no list index for $tag ($path) ($pat)!\n"),
              next;
            my $idx = $1;
            my $len = length $1;
            my $pos = pos($path) - $len - ( $2 ? length $2 : 0 );

            if ($subIdx) {
                $idx    = substr( $idx,    0, -length($subIdx) );
                $subIdx = substr( $subIdx, 1 ) + 1;
                $subIdx = length($subIdx) . $subIdx;
            }
            elsif (@delPaths) {
                $path = shift @delPaths;
                while ( $capture{$path} ) {
                    last unless $path =~ s/ \d(\d+)$/' '.length($1+1).($1+1)/e;
                }
                next;
            }
            else {
                $subIdx = '10';
            }
            substr( $path, $pos, $len ) = $idx . $subIdx;
        }
        if ( defined $$tagInfo{Flat} ) {
            my $p = $path;
            while ( $p =~ s/\/[^\/]+$// ) {
                next unless $capture{$p};
                $et->Error( "Improperly structured XMP ($p)", 1 )
                  if $capture{$p}[0] =~ /\S/;
                delete $capture{$p};
            }
        }
    }
    delete $$et{XMP_CAPTURE};
    delete $$et{XMP_NS};

    my $maxDataLen = $$dirInfo{MaxDataLen};
    $dataPt = $$dirInfo{DataPt};

    unless (
        $changed
        or (    $maxDataLen
            and $dataPt
            and defined $$dataPt
            and length($$dataPt) > $maxDataLen )
      )
    {
        return undef unless $xmpFile;
        Write( $$dirInfo{OutFile}, $$dataPt )
          or return -1
          if $dataPt and defined $$dataPt;
        return 1;
    }
    my ( @long, @short, @resFlag );
    $long[0] = $long[1] = $short[0] = '';
    if ( $$et{XMP_NO_XPACKET} ) {
        $long[-2] .= "\xef\xbb\xbf" if $$et{XMP_NO_XPACKET} == 2;
    }
    else {
        $long[-2] .= $pktOpen;
    }
    $long[-2] .= $xmlOpen if $$et{XMP_IS_XML};
    $long[-2] .= $xmpOpen . $rdfOpen;

    my ( @curPropList, @writeLast, @descStart, $extStart );
    my ( %nsCur,       $prop,      $n,         $path );
    my @pathList = sort TypeFirst keys %capture;
    if ( $maxDataLen and @pathList ) {
        my @pathTmp;
        my ( $lastProp, $lastNS, $propSize ) = ( '', '', 0 );
        my @pathLoop = ( @pathList, '' );
        undef @pathList;
        foreach $path (@pathLoop) {
            $path =~ /^((\w*)[^\/]*)/;
            if ( $1 eq $lastProp ) {
                push @pathTmp, $path;
            }
            else {
                if (   $extendedRes{$lastProp}
                    or $extendedRes{$lastNS}
                    or $propSize > $newDescThresh )
                {
                    push @writeLast, @pathTmp;
                }
                else {
                    push @pathList, @pathTmp;
                }
                last unless $path;
                @pathTmp = ($path);
                ( $lastProp, $lastNS, $propSize ) = ( $1, $2, 0 );
            }
            $propSize += length $capture{$path}->[0];
        }
    }

    for ( ; ; ) {
        my ( %nsNew, $newDesc );
        unless (@pathList) {
            last unless @writeLast;
            @pathList = @writeLast;
            undef @writeLast;
            $newDesc = 2;
        }
        $path = shift @pathList;
        my @propList = split( '/', $path );

        unshift @propList, $rdfDesc;
        foreach $prop (@propList) {
            $prop =~ /(.*):/ or next;
            $1 eq 'rdf' and next;
            my $uri = $nsUsed{$1};
            unless ($uri) {
                $uri = $nsURI{$1};
                unless ($uri) {
                    if ( length $1 ) {
                        my $err = "Undefined XMP namespace: $1";
                        if ( not $xmpErr or $err ne $xmpErr ) {
                            $xmpFile ? $et->Error($err) : $et->Warn($err);
                            $xmpErr = $err;
                        }
                    }
                    next;
                }
            }
            $nsNew{$1} = $uri;
            $newDesc = 1 unless $nsCur{$1};
        }
        my $closeTo = 0;
        if ($newDesc) {
            my ( $path2, $ns2 );
            foreach $path2 (@pathList) {
                my @ns2s    = ( $path2 =~ m{(?:^|/)([^/]+?):}g );
                my $opening = $compact{OneDesc} ? 1 : 0;
                foreach $ns2 (@ns2s) {
                    next if $ns2 eq 'rdf';
                    $nsNew{$ns2} and ++$opening, next;
                    last unless $opening;
                    my $uri = $nsUsed{$ns2} || $nsURI{$ns2} or last;
                    $nsNew{$ns2} = $uri;
                }
                last unless $opening;
            }
        }
        else {
            for ( $closeTo = 0 ; $closeTo < @curPropList ; ++$closeTo ) {
                last unless $closeTo < @propList;
                last unless $propList[$closeTo] eq $curPropList[$closeTo];
            }
        }
        CloseProperty( \@curPropList, \@long, \@short, \@resFlag )
          while @curPropList > $closeTo;

        if ($newDesc) {
            $extStart = length( $long[-2] ) if $newDesc == 2;

            push @descStart, length( $long[-2] ) if $maxDataLen;
            $prop  = $rdfDesc;
            %nsCur = %nsNew;
            my @ns = sort keys %nsCur;
            $long[-2] .= "$nl$sp<$prop rdf:about='${about}'";
            if (    $$et{XMP_NO_XMPMETA}
                and @ns
                and $nsCur{ $ns[0] } =~ m{^http://ns.exiftool.(?:ca|org)/} )
            {
                $long[-2] .= "\n$sp${sp}xmlns:et='http://ns.exiftool.org/1.0/'"
                  . " et:toolkit='Image::ExifTool $Image::ExifTool::VERSION'";
            }
            $long[-2] .= "\n$sp${sp}xmlns:$_='$nsCur{$_}'" foreach @ns;
            push @curPropList, $prop;
            $resFlag[0] = 0 if $compact{Shorthand};
        }
        my ( $val, $attrs ) = @{ $capture{$path} };
        $debug and print "$path = $val\n";
        my ( $attr, $dummy );
        for ( $n = @curPropList ; $n < $#propList ; ++$n ) {
            $prop = $propList[$n];
            push @curPropList, $prop;
            $prop =~ s/ .*//;

            $long[-1] .=
              ( $compact{NoIndent} ? '' : ' ' x scalar(@curPropList) )
              . "<$prop";
            if (
                $prop ne $rdfDesc
                and (
                    $propList[ $n + 1 ] !~ /^rdf:/
                    or (    $propList[ $n + 1 ] eq 'rdf:type'
                        and $n + 1 == $#propList )
                )
              )
            {
                if ( $propList[ $n + 1 ] =~ /:~dummy~$/ ) {
                    $long[-1] .= " rdf:parseType='Resource'/>$nl";
                    pop @curPropList;
                    $dummy = 1;
                    last;
                }
                if ( $compact{Shorthand} ) {
                    $resFlag[$#curPropList] = 1;
                    push @long,  '';
                    push @short, '';
                }
                else {
                    $long[-1] .= " rdf:parseType='Resource'>$nl";
                }
            }
            else {
                $long[-1] .= ">$nl";
            }
        }
        my $prop2 = pop @propList;

        unless ( $dummy or ( $val eq '' and $prop2 =~ /:~dummy~$/ ) ) {
            $prop2 =~ s/ .*//;
            my $pad =
              $compact{NoIndent} ? '' : ' ' x ( scalar(@curPropList) + 1 );
            if (    defined $resFlag[$#curPropList]
                and not %$attrs
                and $val !~ /<!\[CDATA\[/ )
            {
                $short[-1] .= "\n$pad$prop2='${val}'";
            }
            else {
                $long[-1] .= "$pad<$prop2";
                foreach $attr ( sort keys %$attrs ) {
                    my $attrVal = $$attrs{$attr};
                    my $quot    = ( $attrVal =~ /'/ ) ? '"' : "'";
                    $long[-1] .= " $attr=$quot$attrVal$quot";
                }
                $long[-1] .= length $val ? ">$val</$prop2>$nl" : "/>$nl";
            }
        }
    }
    CloseProperty( \@curPropList, \@long, \@short, \@resFlag )
      while @curPropList;

    if ($maxDataLen) {
        $maxDataLen -=
          length($rdfClose) + length($xmpClose) + length($pktCloseW);
        $extStart or $extStart = length $long[-2];
        my @rtn =
          LimitXMPSize( $et, \$long[-2], $maxDataLen, $about, \@descStart,
            $extStart );
        $$dirInfo{ExtendedXMP}  = $rtn[0];
        $$dirInfo{ExtendedGUID} = $rtn[1];
        $compact{NoPadding} = 1
          if length( $long[-2] ) + 101 * $numPadLines > $maxDataLen;
    }
    $compact{NoPadding} = 1 if $$dirInfo{Compact};
    $long[-2] .= $rdfClose;
    $long[-2] .= $xmpClose unless $$et{XMP_NO_XMPMETA};

    delete $$et{XMP_CAPTURE};
    delete $$et{XMP_NS};
    delete $$et{XMP_NO_XMPMETA};

    unless ( $$et{XMP_NO_XPACKET} ) {
        my $pad = ( ' ' x 100 ) . "\n";
        my $len = length( $long[-2] ) + length($pktCloseW);
        if ( $$dirInfo{InPlace}
            and not( $$dirInfo{InPlace} == 2 and $len > $dirLen ) )
        {
            if ( $len > $dirLen ) {
                my $str = 'Not enough room to edit XMP in place';
                $str .= '. Try Shorthand feature' unless $compact{Shorthand};
                $et->Warn($str);
                return undef;
            }
            my $num = int( ( $dirLen - $len ) / length($pad) );
            if ($num) {
                $long[-2] .= $pad x $num;
                $len += length($pad) * $num;
            }
            $len < $dirLen
              and $long[-2] .= ( ' ' x ( $dirLen - $len - 1 ) ) . "\n";
        }
        elsif ( not $compact{NoPadding}
            and not $xmpFile
            and not $$dirInfo{ReadOnly} )
        {
            $long[-2] .= $pad x $numPadLines;
        }
        $long[-2] .= ( $$dirInfo{ReadOnly} ? $pktCloseR : $pktCloseW );
    }
    unless ( %capture or $xmpFile or $$dirInfo{InPlace} or $$dirInfo{NoDelete} )
    {
        $long[-2] = '';
    }
    return ( $xmpFile ? -1 : undef ) if $xmpErr;
    $$et{CHANGED} += $changed;
    $debug > 1 and $long[-2] and print $long[-2], "\n";
    return $long[-2] unless $xmpFile;
    Write( $$dirInfo{OutFile}, $long[-2] ) or return -1;
    return 1;
}

1;

__END__

