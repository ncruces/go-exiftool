
package Image::ExifTool::RTF;

use strict;
use vars            qw($VERSION);
use Image::ExifTool qw(:DataAccess :Utils);

$VERSION = '1.04';

sub ProcessUserProps($$$);

my %rtfEntity = (
    par       => 0x0a,
    tab       => 0x09,
    endash    => 0x2013,
    emdash    => 0x2014,
    lquote    => 0x2018,
    rquote    => 0x2019,
    ldblquote => 0x201c,
    rdblquote => 0x201d,
    bullet    => 0x2022,
);

%Image::ExifTool::RTF::Main = (
    GROUPS => { 2 => 'Document' },
    NOTES  => q{
        This table lists standard tags of the RTF information group, but ExifTool
        will also extract any non-standard tags found in this group.  As well,
        ExifTool will extract any custom properties that are found.  See
        L<http://www.microsoft.com/en-ca/download/details.aspx?id=10725> for the
        specification.
    },
    title     => {},
    subject   => {},
    author    => { Groups => { 2 => 'Author' } },
    manager   => {},
    company   => {},
    copyright => { Groups => { 2 => 'Author' } },
    operator  => { Name   => 'LastModifiedBy' },
    category  => {},
    keywords  => {},
    comment   => {},
    doccomm   => { Name => 'Comments' },
    hlinkbase => { Name => 'HyperlinkBase' },
    creatim   => {
        Name      => 'CreateDate',
        Format    => 'date',
        Groups    => { 2 => 'Time' },
        PrintConv => '$self->ConvertDateTime($val)',
    },
    revtim => {
        Name      => 'ModifyDate',
        Format    => 'date',
        Groups    => { 2 => 'Time' },
        PrintConv => '$self->ConvertDateTime($val)',
    },
    printim => {
        Name      => 'LastPrinted',
        Format    => 'date',
        Groups    => { 2 => 'Time' },
        PrintConv => '$self->ConvertDateTime($val)',
    },
    buptim => {
        Name      => 'BackupTime',
        Format    => 'date',
        Groups    => { 2 => 'Time' },
        PrintConv => '$self->ConvertDateTime($val)',
    },
    edmins => {
        Name      => 'TotalEditTime',
        PrintConv => 'ConvertTimeSpan($val, 60)',
    },
    nofpages   => { Name => 'Pages' },
    nofwords   => { Name => 'Words' },
    nofchars   => { Name => 'Characters' },
    nofcharsws => {
        Name  => 'CharactersWithSpaces',
        Notes => q{
            according to the 2007 Microsoft RTF specification this is clearly the number
            of characters NOT including spaces, but Microsoft Word writes this as the
            number WITH spaces, so ExifTool names this tag according to the de facto
            standard
        },
    },
    id      => { Name => 'InternalIDNumber' },
    version => { Name => 'RevisionNumber' },
    vern    => { Name => 'InternalVersionNumber' },
);

%Image::ExifTool::RTF::UserProps = ( GROUPS => { 2 => 'Document' }, );

sub ReadToNested($;$) {
    my ( $dataPt, $raf ) = @_;
    my $pos   = pos $$dataPt;
    my $level = 1;
    for ( ; ; ) {
        unless ( $$dataPt =~ /(\\*)([{}])/g ) {
            my $p = length $$dataPt;
            my $buff;
            last unless $raf and $raf->Read( $buff, 65536 );
            $$dataPt .= $buff;
            --$p while $p and substr( $$dataPt, $p - 1, 1 ) eq '\\';
            pos($$dataPt) = $p;
            next;
        }
        next if $1 and length($1) & 0x01;
        $2 eq '{' and ++$level, next;
        next unless --$level <= 0;
        return substr( $$dataPt, $pos, pos($$dataPt) - $pos - 1 );
    }
    return undef;
}

sub UnescapeRTF($$$) {
    my ( $et, $val, $charset ) = @_;

    unless ( $val =~ /\\/ ) {
        $val =~ tr/\n\r//d;
        return $val;
    }
    $val =~ s/\\(?:([a-zA-Z]+(?:-?\d+)?)[\n\r]|(.))/'\\'.($1 ? "$1 " : $2)/sge;
    $val =~ s/(\\[\n\r])|(\\.)/$2 || '\\par '/sge;
    $val =~ tr/\n\r//d;

    my $rtnVal = '';
    my $len    = length $val;
    my $skip   = 1;
    my $p0     = 0;

    for ( ; ; ) {
        my $p1 = ( $val =~ /\\/g ) ? pos($val) : $len + 1;
        my $n = $p1 - $p0 - 1;
        $rtnVal .= substr( $val, $p0, $n ) if $n > 0;
        last if $p1 >= $len;
        if ( $val =~ /\G([a-zA-Z]+)(-?\d+)? ?/g ) {
            if ( $1 eq 'uc' ) {
                $skip = $2;
            }
            elsif ( $1 eq 'u' ) {
                if ( $2 < 0 ) {
                    $et->Warn('Invalid Unicode character(s) in text');
                    $rtnVal .= '?';
                }
                else {
                    require Image::ExifTool::Charset;
                    $rtnVal .= Image::ExifTool::Charset::Recompose( $et, [$2] );
                    if ($skip) {
                        last
                          unless $val =~
/\G([^\\]|\\([a-zA-Z]+)(-?\d+)? ?|\\'.{2}|\\.){$skip}/g;
                    }
                }
            }
            elsif ( $rtfEntity{$1} ) {
                require Image::ExifTool::Charset;
                $rtnVal .= Image::ExifTool::Charset::Recompose( $et,
                    [ $rtfEntity{$1} ] );
            }
        }
        else {
            my $ch = substr( $val, $p1, 1 );
            if ( $ch eq "'" ) {
                last if $p1 + 3 > $len;
                my $hex = substr( $val, $p1 + 1, 2 );
                if ( $hex =~ /^[0-9a-fA-F]{2}$/ ) {
                    require Image::ExifTool::Charset;
                    $rtnVal .= $et->Decode( chr( hex($hex) ), $charset );
                }
                pos($val) = $p1 + 3;
            }
            else {
                $rtnVal .= $ch;
                pos($val) = $p1 + 1;
            }
        }
        $p0 = pos($val);
    }
    return $rtnVal;
}

sub ProcessRTF($$) {
    my ( $et, $dirInfo ) = @_;
    my $raf = $$dirInfo{RAF};
    my ( $buff, $buf2, $cs );

    return 0 unless $raf->Read( $buff, 64 ) and $raf->Seek( 0, 0 );
    return 0 unless $buff =~ /^[\n\r]*\{[\n\r]*\\rtf[^a-zA-Z]/;
    $et->SetFileType();
    if ( $buff =~ /\\ansicpg(\d*)/ ) {
        $cs = "cp$1";
    }
    elsif ( $buff =~ /\\(ansi|mac|pc|pca)[^a-zA-Z]/ ) {
        my %trans = (
            ansi => 'Latin',
            mac  => 'MacRoman',
            pc   => 'cp437',
            pca  => 'cp850',
        );
        $cs = $trans{$1};
    }
    else {
        $et->Warn('Unspecified RTF encoding. Will assume Latin');
        $cs = 'Latin';
    }
    my $charset = $Image::ExifTool::charsetName{ lc $cs };
    unless ($charset) {
        $et->Warn("Unsupported RTF encoding $cs. Will assume Latin.");
        $charset = 'Latin';
    }
    my $tagTablePtr = GetTagTable('Image::ExifTool::RTF::Main');
    undef $buff;
    for ( ; ; ) {
        $raf->Read( $buf2, 65536 ) or last;
        if ( defined $buff ) {
            $buff = substr( $buff, -16 ) . $buf2;
        }
        else {
            $buff = $buf2;
        }
        next unless $buff =~ /[^\\]\{[\n\r]*\\info([^a-zA-Z])/g;
        pos($buff) = pos($buff) - 1 if $1 ne ' ';
        my $info = ReadToNested( \$buff, $raf );
        unless ( defined $info ) {
            $et->Warn('Unterminated information group');
            last;
        }
        while ( $info =~ /\{[\n\r]*(\\\*[\n\r]*)?\\([a-zA-Z]+)([^a-zA-Z])/g ) {
            pos($info) = pos($info) - 1 if $3 ne ' ';
            my $tag = $2;
            my $val = ReadToNested( \$info );
            last unless defined $val;
            my $tagInfo = $$tagTablePtr{$tag};
            if (    $tagInfo
                and $$tagInfo{Format}
                and $$tagInfo{Format} eq 'date' )
            {
                my %idx =
                  ( yr => 0, mo => 1, dy => 2, hr => 3, min => 4, sec => 5 );
                my @t = (0) x 6;
                while ( $val =~ /\\([a-z]+)(\d+)/g ) {
                    next unless defined $idx{$1};
                    $t[ $idx{$1} ] = $2;
                }
                $val = sprintf( "%.4d:%.2d:%.2d %.2d:%.2d:%.2d", @t );
            }
            else {
                $val = UnescapeRTF( $et, $val, $charset );
            }
            if ( not $tagInfo ) {
                AddTagToTable( $tagTablePtr, $tag, { Name => ucfirst($tag) } );
            }
            $et->HandleTag( $tagTablePtr, $tag, $val );
        }
    }
    return 1 unless defined $buff;
    pos($buff) = 0;
    while ( $buff =~ /[^\\]\{[\n\r]*\\\*[\n\r]*\\userprops([^a-zA-Z])/g ) {
        pos($buff) = pos($buff) - 1 if $1 ne ' ';
        my $props = ReadToNested( \$buff, $raf );
        $tagTablePtr =
          Image::ExifTool::GetTagTable('Image::ExifTool::RTF::UserProps');
        unless ( defined $props ) {
            $et->Warn('Unterminated user properties');
            last;
        }
        my $tag;
        while ( $props =~ /\{[\n\r]*(\\\*[\n\r]*)?\\([a-zA-Z]+)([^a-zA-Z])/g ) {
            pos($props) = pos($props) - 1 if $3 ne ' ';
            my $t   = $2;
            my $val = ReadToNested( \$props );
            last unless defined $val;
            $val = UnescapeRTF( $et, $val, $charset );
            if ( $t eq 'propname' ) {
                $tag = $val;
                next;
            }
            elsif ( $t ne 'staticval' or not defined $tag ) {
                next;
            }
            $tag =~ s/\s(.)/\U$1/g;
            $tag =~ tr/-_a-zA-Z0-9//dc;
            next unless $tag;
            unless ( $$tagTablePtr{$tag} ) {
                AddTagToTable( $tagTablePtr, $tag, { Name => $tag } );
            }
            $et->HandleTag( $tagTablePtr, $tag, $val );
        }
        last;
    }
    return 1;
}

1;

__END__


