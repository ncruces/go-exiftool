
package Image::ExifTool::InDesign;

use strict;
use vars            qw($VERSION);
use Image::ExifTool qw(:DataAccess :Utils);

$VERSION = '1.09';

my %indMap = ( XMP => 'IND', );

my $masterPageGUID =
  "\x06\x06\xed\xf5\xd8\x1d\x46\xe5\xbd\x31\xef\xe7\xfe\x74\xb7\x1d";
my $objectHeaderGUID =
  "\xde\x39\x39\x79\x51\x88\x4b\x6c\x8E\x63\xee\xf8\xae\xe0\xdd\x38";
my $objectTrailerGUID =
  "\xfd\xce\xdb\x70\xf7\x86\x4b\x4f\xa4\xd3\xc7\x28\xb3\x41\x71\x06";

sub ProcessIND($$) {
    my ( $et, $dirInfo ) = @_;
    my $raf     = $$dirInfo{RAF};
    my $outfile = $$dirInfo{OutFile};
    my ( $hdr, $buff, $buf2, $err, $writeLen, $foundXMP );

    return 0 unless $raf->Read( $hdr, 16 ) == 16;
    return 0 unless $hdr eq $masterPageGUID;
    return 0 unless $raf->Read( $buff, 8 ) == 8;
    $et->SetFileType( $buff eq 'DOCUMENT' ? 'INDD' : 'IND' );

    $raf->Seek( 0, 0 ) or $err = 'Seek error', goto DONE;
    unless ($raf->Read( $buff, 4096 ) == 4096
        and $raf->Read( $buf2, 4096 ) == 4096 )
    {
        $err = 'Unexpected end of file';
        goto DONE;
    }
    SetByteOrder('II');
    unless ( $buf2 =~ /^\Q$masterPageGUID/ ) {
        $err = 'Second master page is invalid';
        goto DONE;
    }
    my $seq1 = Get64u( \$buff, 264 );
    my $seq2 = Get64u( \$buf2, 264 );
    my $curPage = $seq2 > $seq1 ? \$buf2 : \$buff;
    my $streamInt32u = Get8u( $curPage, 24 );
    if ( $streamInt32u == 1 ) {
        $streamInt32u = 'V';
    }
    elsif ( $streamInt32u == 2 ) {
        $streamInt32u = 'N';
    }
    else {
        $err = 'Invalid stream byte order';
        goto DONE;
    }
    my $pages = Get32u( $curPage, 280 );
    $pages < 2 and $err = 'Invalid page count', goto DONE;
    my $pos = $pages * 4096;
    if ( $pos > 0x7fffffff ) {
        if ( not $et->Options('LargeFileSupport') ) {
            $err =
'InDesign files larger than 2 GB not supported (LargeFileSupport not set)';
            goto DONE;
        }
        elsif ( $et->Options('LargeFileSupport') eq '2' ) {
            $et->Warn('Processing large file (LargeFileSupport is 2)');
        }
    }
    if ($outfile) {
        $et->InitWriteDirs( \%indMap, 'XMP' );

        Write( $outfile, $buff, $buf2 ) or $err = 1, goto DONE;
        my $result = Image::ExifTool::CopyBlock( $raf, $outfile, $pos - 8192 );
        unless ($result) {
            $err = defined $result ? 'Error reading InDesign database' : 1;
            goto DONE;
        }
        $writeLen = 0;
    }
    else {
        $raf->Seek( $pos, 0 ) or $err = 'Seek error', goto DONE;
    }
    my $verbose = $et->Options('Verbose');
    my $out     = $et->Options('TextOut');
    for ( ; ; ) {
        $raf->Read( $hdr, 32 ) or last;
        unless ( length($hdr) == 32 and $hdr =~ /^\Q$objectHeaderGUID/ ) {
            last if $hdr =~ /^\0+$/;
            $raf->Read( $buff, 8192 ) and $hdr .= $buff;
            my $n = length $hdr;
            $hdr =~ s/\0+$//;
            if ( $n > 8190 or length($hdr) > 4095 ) {
                $err = 'Corrupt file or unsupported InDesign version';
                last;
            }
            my $non = 'Non-null padding at end of file';
            if ( not $outfile ) {
                $et->Warn( $non, 1 );
            }
            elsif ( not $et->Error( $non, 1 ) ) {
                Write( $outfile, $hdr ) or $err = 1;
                $writeLen += length $hdr;
            }
            last;
        }
        my $len = Get32u( \$hdr, 24 );
        if ($verbose) {
            printf $out "Contiguous object at offset 0x%x (%d bytes):\n",
              $raf->Tell(), $len;
            if ( $verbose > 2 ) {
                my $len2 = $len < 1024000 ? $len : 1024000;
                $raf->Seek( -$raf->Read( $buff, $len2 ), 1 ) or $err = 1;
                $et->VerboseDump( \$buff, Addr => $raf->Tell() );
            }
        }
        if ( $len > 56 ) {
            $raf->Read( $buff, 56 ) == 56
              or $err = 'Unexpected end of file', last;
            if ( $buff =~
/^(....)<\?xpacket begin=(['"])\xef\xbb\xbf\2 id=(['"])W5M0MpCehiHzreSzNTczkc9d\3/s
              )
            {
                my $lenWord = $1;
                $len -= 4;
                $foundXMP = 1;
                if ( $len > 300 * 1024 * 1024 ) {
                    my $msg = sprintf( 'Insanely large XMP (%.0f MiB)',
                        $len / ( 1024 * 1024 ) );
                    if ($outfile) {
                        $et->Error( $msg, 2 ) and $err = 1, last;
                    }
                    elsif ( $et->Options('IgnoreMinorErrors') ) {
                        $et->Warn($msg);
                    }
                    else {
                        $et->Warn( "$msg. Ignored.", 1 );
                        $err = 1;
                        last;
                    }
                }
                unless ($raf->Seek( -52, 1 )
                    and $raf->Read( $buff, $len ) == $len )
                {
                    $err = 'Error reading XMP stream';
                    last;
                }
                my %dirInfo = (
                    DataPt   => \$buff,
                    Parent   => 'IND',
                    NoDelete => 1,
                );
                my $xmpLen = unpack( $streamInt32u, $lenWord );
                unless ( $xmpLen == $len ) {
                    if ( $xmpLen < $len ) {
                        $dirInfo{DirLen} = $xmpLen;
                    }
                    else {
                        $err =
                            'Truncated XMP stream (missing '
                          . ( $xmpLen - $len )
                          . ' bytes)';
                    }
                }
                my $tagTablePtr = GetTagTable('Image::ExifTool::XMP::Main');
                if ($outfile) {
                    last if $err;
                    my $classID = Get32u( \$hdr, 20 );
                    $classID & 0x40000000
                      or $err = 'XMP stream is not writable', last;
                    my $xmp = $et->WriteDirectory( \%dirInfo, $tagTablePtr );
                    if ( $xmp and length $xmp ) {
                        $buff = pack( $streamInt32u, length $xmp ) . $xmp;
                        Set32u( length($buff), \$hdr, 24 );
                        Set32u( 0xffffffff,    \$hdr, 28 );
                    }
                    else {
                        $$et{CHANGED} = 0;
                        $et->Warn(
                            "Can't delete XMP as a block from InDesign file")
                          if defined $xmp;
                        $buff = $lenWord . $buff;
                    }
                }
                else {
                    $et->ProcessDirectory( \%dirInfo, $tagTablePtr );
                }
                $len = 0;
            }
            else {
                $len -= 56;
            }
        }
        else {
            $buff = '';
        }
        if ($outfile) {
            Write( $outfile, $hdr, $buff ) or $err = 1, last;
            my $result = Image::ExifTool::CopyBlock( $raf, $outfile, $len );
            unless ($result) {
                $err = defined $result ? 'Truncated stream data' : 1;
                last;
            }
            $writeLen += 32 + length($buff) + $len;
        }
        elsif ($len) {
            $raf->Seek( $len, 1 ) or $err = 'Seek error', last;
        }
        $raf->Read( $buff, 32 ) == 32 or $err = 'Unexpected end of file', last;
        unless ( $buff =~ /^\Q$objectTrailerGUID/ ) {
            $err = 'Invalid object trailer';
            last;
        }
        if ($outfile) {
            substr( $hdr, 16, 8 ) eq substr( $buff, 16, 8 )
              or $err = 'Non-matching object trailer', last;
            Write( $outfile, $objectTrailerGUID, substr( $hdr, 16 ) )
              or $err = 1, last;
            $writeLen += 32;
        }
    }
    if ($outfile) {
        my $part = $writeLen % 4096;
        Write( $outfile, "\0" x ( 4096 - $part ) ) or $err = 1 if $part;
    }
  DONE:
    if ( not $err ) {
        $et->Warn('No XMP stream to edit') if $outfile and not $foundXMP;
        return 1;
    }
    elsif ( not $outfile ) {
        $et->Warn($err) unless $err eq '1';
    }
    elsif ( $err ne '1' ) {
        $et->Error($err);
    }
    else {
        return -1;
    }
    return 1;
}

1;

__END__


