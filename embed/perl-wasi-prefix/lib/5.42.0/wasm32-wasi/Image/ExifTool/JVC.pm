
package Image::ExifTool::JVC;

use strict;
use vars            qw($VERSION);
use Image::ExifTool qw(:DataAccess :Utils);
use Image::ExifTool::Exif;

$VERSION = '1.04';

sub ProcessJVCText($$$);

%Image::ExifTool::JVC::Main = (
    WRITE_PROC => \&Image::ExifTool::Exif::WriteExif,
    CHECK_PROC => \&Image::ExifTool::Exif::CheckExif,
    GROUPS     => { 0 => 'MakerNotes', 2 => 'Camera' },
    NOTES      => 'JVC EXIF maker note tags.',
    0x0002 => {
        Name => 'CPUVersions',
        ValueConv => '$_=$val; s/(\s*\0)+$//; s/(\s*\0)+/, /g; $_',
    },
    0x0003 => {
        Name      => 'Quality',
        PrintConv => {
            0 => 'Low',
            1 => 'Normal',
            2 => 'Fine',
        },
    },
);

%Image::ExifTool::JVC::Text = (
    GROUPS       => { 0 => 'MakerNotes', 2 => 'Camera' },
    PROCESS_PROC => \&ProcessJVCText,
    NOTES        => 'JVC/Victor text-based maker note tags.',
    VER          => 'MakerNoteVersion',
    QTY          => {
        Name      => 'Quality',
        PrintConv => {
            STND => 'Normal',
            STD  => 'Normal',
            FINE => 'Fine',
        },
    },
);

sub ProcessJVCText($$$) {
    my ( $et, $dirInfo, $tagTablePtr ) = @_;
    my $dataPt   = $$dirInfo{DataPt};
    my $dirStart = $$dirInfo{DirStart} || 0;
    my $dataLen  = $$dirInfo{DataLen};
    my $dirLen   = $$dirInfo{DirLen} || $dataLen - $dirStart;
    my $verbose  = $et->Options('Verbose');

    my $data = substr( $$dataPt, $dirStart, $dirLen );
    unless ( $data =~ /^VER:/ ) {
        $et->Warn('Bad JVC text maker notes');
        return 0;
    }
    while ( $data =~ m/([A-Z]+):(.{3,4})/sg ) {
        my ( $tag, $val ) = ( $1, $2 );
        my $tagInfo = $et->GetTagInfo( $tagTablePtr, $tag );
        $et->VerboseInfo(
            $tag, $tagInfo,
            Table => $tagTablePtr,
            Value => $val,
        ) if $verbose;
        unless ($tagInfo) {
            next unless $$et{OPTIONS}{Unknown};
            $tagInfo = {
                Name      => "JVC_Text_$tag",
                Unknown   => 1,
                PrintConv => \&Image::ExifTool::LimitLongValues,
            };
            AddTagToTable( $tagTablePtr, $tag, $tagInfo );
        }
        $et->FoundTag( $tagInfo, $val );
    }
    return 1;
}

1;

__END__

