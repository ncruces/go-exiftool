
package Image::ExifTool;

use strict;

use Image::ExifTool::TagLookup qw(FindTagInfo TagExists);
use Image::ExifTool::Fixup;

sub AssembleRational($$@);
sub LastInList($);
sub NextFreeTagKey($$);
sub RemoveNewValueHash($$$);
sub RemoveNewValuesForGroup($$);
sub GetWriteGroup1($$);
sub Sanitize($$);
sub ConvInv($$$$$;$$);
sub PushValue($$$;$);

my $loadedAllTables;

my %tiffMap = (
    IFD0         => 'TIFF',
    IFD1         => 'IFD0',
    XMP          => 'IFD0',
    ICC_Profile  => 'IFD0',
    ExifIFD      => 'IFD0',
    GPS          => 'IFD0',
    SubIFD       => 'IFD0',
    GlobParamIFD => 'IFD0',
    PrintIM      => 'IFD0',
    IPTC         => 'IFD0',
    Photoshop    => 'IFD0',
    SEAL         => 'IFD0',
    InteropIFD   => 'ExifIFD',
    MakerNotes   => 'ExifIFD',
    CanonVRD     => 'MakerNotes',
    NikonCapture => 'MakerNotes',
    PhaseOne     => 'MakerNotes',
);
my %exifMap = (
    IFD1         => 'IFD0',
    EXIF         => 'IFD0',
    ExifIFD      => 'IFD0',
    GPS          => 'IFD0',
    SubIFD       => 'IFD0',
    GlobParamIFD => 'IFD0',
    PrintIM      => 'IFD0',
    InteropIFD   => 'ExifIFD',
    MakerNotes   => 'ExifIFD',
    NikonCapture => 'MakerNotes',

);
my %jpegMap = (
    %exifMap,
    JFIF         => 'APP0',
    CIFF         => 'APP0',
    IFD0         => 'APP1',
    XMP          => 'APP1',
    ICC_Profile  => 'APP2',
    FlashPix     => 'APP2',
    MPF          => 'APP2',
    Meta         => 'APP3',
    MetaIFD      => 'Meta',
    RMETA        => 'APP5',
    SEAL         => [ 'APP8', 'APP9' ],
    AROT         => 'APP10',
    JUMBF        => 'APP11',
    Ducky        => 'APP12',
    Photoshop    => 'APP13',
    Adobe        => 'APP14',
    IPTC         => 'Photoshop',
    MakerNotes   => [ 'ExifIFD', 'CIFF' ],
    CanonVRD     => 'MakerNotes',
    NikonCapture => 'MakerNotes',
    Comment      => 'COM',
);
my %dirMap = (
    JPEG => \%jpegMap,
    EXV  => \%jpegMap,
    TIFF => \%tiffMap,
    ORF  => \%tiffMap,
    RAW  => \%tiffMap,
    EXIF => \%exifMap,
);

my %writableType = (
    CRW  => [ 'CanonRaw', 'WriteCRW' ],
    DR4  => 'CanonVRD',
    EPS  => [ 'PostScript', 'WritePS' ],
    FLIF => [ undef,        'WriteFLIF' ],
    GIF  => undef,
    ICC  => [ 'ICC_Profile', 'WriteICC' ],
    IND  => 'InDesign',
    JP2  => 'Jpeg2000',
    JXL  => 'Jpeg2000',
    MIE  => undef,
    MOV  => [ 'QuickTime', 'WriteMOV' ],
    MRW  => 'MinoltaRaw',
    PDF  => [ undef, 'WritePDF' ],
    PNG  => undef,
    PPM  => undef,
    PS   => [ 'PostScript', 'WritePS' ],
    PSD  => 'Photoshop',
    RAF  => [ 'FujiFilm', 'WriteRAF' ],
    RIFF => [ 'RIFF',     'WriteRIFF' ],
    VRD  => 'CanonVRD',
    WEBP => [ 'RIFF', 'WriteRIFF' ],
    X3F  => 'SigmaRaw',
    XMP  => [ undef, 'WriteXMP' ],
);

my %rawType = (
    '3FR' => 1,
    CR3   => 2,
    IIQ   => 1,
    NEF   => 1,
    RW2   => 1,
    ARQ   => 1,
    CRW   => 1,
    K25   => 1,
    NRW   => 1,
    RWL   => 1,
    ARW   => 1,
    DCR   => 1,
    KDC   => 1,
    ORF   => 1,
    SR2   => 1,
    ARW   => 1,
    ERF   => 1,
    MEF   => 1,
    PEF   => 1,
    SRF   => 1,
    CR2   => 1,
    FFF   => 1,
    MOS   => 1,
    RAW   => 1,
    SRW   => 1,
);

my @delGroups = qw(
  Adobe AFCP APP0 APP1 APP2 APP3 APP4 APP5 APP6 APP7 APP8 APP9 APP10 APP11 APP12
  APP13 APP14 APP15 AROT AudioKeys CanonVRD CIFF Ducky EXIF ExifIFD File FlashPix
  FotoStation GlobParamIFD GPS ICC_Profile IFD0 IFD1 Insta360 InteropIFD IPTC
  ItemList iTunes JFIF Jpeg2000 JUMBF Keys MakerNotes Meta MetaIFD Microsoft MIE
  MPF Nextbase NikonApp NikonCapture PDF PDF-update PhotoMechanic Photoshop PNG
  PNG-pHYs PrintIM QuickTime RMETA RSRC SEAL SubIFD Trailer UserData VideoKeys
  Vivo XML XML-* XMP XMP-*
);
my @delGroup2 = qw(
  Audio Author Camera Document ExifTool Image Location Other Preview Printing
  Time Video
);
my %delMore = (
    QuickTime => [qw(ItemList UserData Keys)],
    XMP       => ['XMP-*'],
    XML       => ['XML-*'],
    SEAL      => ['XMP-SEAL'],
);

my %permanentDir = ( QuickTime => 1, Jpeg2000 => 1 );

my %family2groups = map { lc $_ => 1 } @delGroup2, 'Unknown';

my $protectedGroups = '(IFD1|SubIFD|InteropIFD|GlobParamIFD|PDF-update|Adobe)';

my %removeGroups = (
    IFD0    => [ 'EXIF', 'MakerNotes' ],
    EXIF    => ['MakerNotes'],
    ExifIFD => [ 'MakerNotes', 'InteropIFD' ],
    Trailer => ['CanonVRD'],
);
my %excludeGroups = (
    EXIF => [
        qw(IFD0 IFD1 ExifIFD GPS MakerNotes GlobParamIFD InteropIFD PrintIM SubIFD)
    ],
    IFD0         => ['EXIF'],
    IFD1         => ['EXIF'],
    ExifIFD      => ['EXIF'],
    GPS          => ['EXIF'],
    MakerNotes   => ['EXIF'],
    InteropIFD   => ['EXIF'],
    GlobParamIFD => ['EXIF'],
    PrintIM      => ['EXIF'],
    CIFF         => ['MakerNotes'],
    AFCP          => ['Trailer'],
    FotoStation   => ['Trailer'],
    CanonVRD      => ['Trailer'],
    PhotoMechanic => ['Trailer'],
    MIE           => ['Trailer'],
    QuickTime     => [qw(ItemList UserData Keys)],
);
my %translateWantGroup = ( ciff => 'canonraw', );
my %translateWriteGroup = (
    EXIF => 'ExifIFD',
    Meta => 'MetaIFD',
    File => 'Comment',
    MIE   => 'MIE',
    APP14 => 'APP14',
);
my %exifDirs = (
    gps          => 'GPS',
    exififd      => 'ExifIFD',
    subifd       => 'SubIFD',
    globparamifd => 'GlobParamIFD',
    interopifd   => 'InteropIFD',
    previewifd   => 'PreviewIFD',
    metaifd      => 'MetaIFD',
    makernotes   => 'MakerNotes',
);
my %allFam0 = (
    exif       => 1,
    makernotes => 1,
);

my @writableMacOSTags = qw(
  FileCreateDate MDItemFinderComment MDItemFSCreationDate MDItemFSLabel MDItemUserTags
  XAttrQuarantine XAttrMDItemWhereFroms
);

my %intRange = (
    'int8u'     => [  0,                   0xff ],
    'int8s'     => [ -0x80,                0x7f ],
    'int16u'    => [  0,                   0xffff ],
    'int16uRev' => [  0,                   0xffff ],
    'int16s'    => [ -0x8000,              0x7fff ],
    'int32u'    => [  0,                   0xffffffff ],
    'int32s'    => [ -0x80000000,          0x7fffffff ],
    'int64u'    => [  0,                   18446744073709551615 ],
    'int64s'    => [ -9223372036854775808, 9223372036854775807 ],
);
my %blockExifTypes =
  map { $_ => 1 } qw(JPEG PNG JP2 JXL MIE EXIF FLIF MOV MP4 RIFF);

my $maxSegmentLen = 0xfffd;
my $maxXMPLen     = $maxSegmentLen;

my %listSep = ( PrintConv => '; ?', ValueConv => ' ' );

my %ignorePrintConv = map { $_ => 1 } qw(OTHER BITMASK Notes);

sub SetNewValue($;$$%) {
    local $_;
    my ( $self, $tag, $value, %options ) = @_;
    my ( $err, $tagInfo, $family );
    my $verbose   = $$self{OPTIONS}{Verbose};
    my $out       = $$self{OPTIONS}{TextOut};
    my $protected = $options{Protected} || 0;
    my $listOnly  = $options{ListOnly};
    my $setTags   = $options{SetTags};
    my $noFlat    = $options{NoFlat};
    my $numSet    = 0;

    unless ( defined $tag ) {
        delete $$self{NEW_VALUE};
        $$self{SAVE_COUNT} = $$self{NV_COUNT} = 0;
        $$self{DEL_GROUP}  = {};
        return 1;
    }
    if ( ref $value ) {
        if ( ref $value eq 'ARRAY' ) {
            if ( @$value > 1 ) {
                my $replace = $options{Replace};
                my $noJoin;
                foreach (@$value) {
                    $noJoin = 1 if ref $_;
                    my ( $n, $e ) =
                      SetNewValue( $self, $tag, $_, %options, ListOnly => 1 );
                    $err = $e if $e;
                    $numSet += $n;
                    delete $options{Replace};
                }
                return $numSet if $noJoin;

                $value            = join $$self{OPTIONS}{ListSep}, @$value;
                $options{Replace} = $replace;
                $listOnly         = $options{ListOnly} = 0;
            }
            else {
                $value = $$value[0];
                $value = $$value if ref $value eq 'SCALAR';
            }
        }
        elsif ( ref $value eq 'SCALAR' ) {
            $value = $$value;
        }
    }
    $self->Sanitize( \$value )
      if defined $value
      and not ref $value
      and not $options{Sanitized};

    ( $options{Group}, $tag ) = ( $1, $2 ) if $tag =~ /(.*):(.+)/;

    $options{Type} = 'ValueConv' if $tag =~ s/#$//;
    my $convType = $options{Type}
      || ( $$self{OPTIONS}{PrintConv} ? 'PrintConv' : 'ValueConv' );

    $self->Filter( $$self{OPTIONS}{FilterW}, \$value )
      or return 0
      if $convType eq 'PrintConv';

    my ( @wantGroup, $family2 );
    my $wantGroup = $options{Group};
    if ($wantGroup) {
        foreach ( split /:/, $wantGroup ) {
            next unless length($_) and /^(\d+)?(.*)/;
            my ( $f, $g ) = ( $1, $2 );
            my $lcg = lc $g;
            push @wantGroup, [ $f, $lcg ] unless $lcg eq '*' or $lcg eq 'all';
            if ( $g =~ s/^ID-//i ) {
                return 0 if defined $f and $f ne 7;
                $wantGroup[-1] = [ 7, $g ];
            }
            elsif ( defined $f ) {
                $f > 2 and return 0;
                $family2 = 1 if $f == 2;
            }
            else {
                $family2 = 1 if $family2groups{$lcg};
            }
        }
        undef $wantGroup unless @wantGroup;
    }

    $tag =~ s/ .*//;
    $tag = '*' if lc($tag) eq 'all';
    while ( $tag eq '*'
        and not defined $value
        and not $family2
        and @wantGroup < 2 )
    {
        my ( @del, $grp );
        my $remove = ( $options{Replace} and $options{Replace} > 1 );
        if ($wantGroup) {
            @del = grep /^$wantGroup$/i, @delGroups
              unless $wantGroup =~ /^XM[LP]-\*$/i;
            if ( @del and $remove ) {
                push @del, @{ $excludeGroups{ $del[0] } }
                  if $excludeGroups{ $del[0] };
                my $dirName = $del[0];
                my @dirNames;
                for ( ; ; ) {
                    my $parent = $jpegMap{$dirName};
                    if ( ref $parent ) {
                        push @dirNames, @$parent;
                        $parent = pop @dirNames;
                    }
                    $dirName = $parent || shift @dirNames or last;
                    push @del, $dirName;
                }
            }
            push @del, uc($wantGroup)
              if $wantGroup =~ /^(MIE\d+|XM[LP]-[-\w]*\w)$/i;
        }
        else {
            push @del, ( grep !/^$protectedGroups$/, @delGroups ), '*';
        }
        if (@del) {
            ++$numSet;
            my @donegrps;
            my $delGroup = $$self{DEL_GROUP};
            foreach $grp (@del) {
                if ($remove) {
                    my $didExcl;
                    if ( $grp =~ /^(XM[LP])(-.*)?$/ ) {
                        my $x = $1;
                        if ( $grp eq $x ) {
                            foreach ( keys %$delGroup ) {
                                next unless /^(-?)$x-/;
                                push @donegrps, $_ unless $1;
                                delete $$delGroup{$_};
                            }
                        }
                        elsif ( $$delGroup{"$x-*"} and not $$delGroup{"-$grp"} )
                        {
                            if ( $$delGroup{$x} ) {
                                push @donegrps, $x;
                                delete $$delGroup{$x};
                            }
                            $$delGroup{"-$grp"} = 1;
                            $didExcl = 1;
                        }
                    }
                    if ( exists $$delGroup{$grp} ) {
                        delete $$delGroup{$grp};
                    }
                    else {
                        next unless $didExcl;
                    }
                }
                else {
                    $$delGroup{$grp} = 1;
                    if ( $delMore{$grp} ) {
                        $$delGroup{$_} = 1, push @donegrps, $_
                          foreach @{ $delMore{$grp} };
                    }
                    $self->RemoveNewValuesForGroup($grp);
                }
                push @donegrps, $grp;
            }
            if ( $verbose > 1 and @donegrps ) {
                @donegrps = sort @donegrps;
                my $msg =
                  $remove ? 'Excluding from deletion' : 'Deleting tags in';
                print $out "  $msg: @donegrps\n";
            }
        }
        elsif ( grep /^$wantGroup$/i, @delGroup2 ) {
            last;
        }
        else {
            $err = "Not a deletable group: $wantGroup";
        }
        return ( $numSet, $err ) if wantarray;
        $err and warn "$err\n";
        return $numSet;
    }

    my $createOnly;
    my $editOnly  = $options{EditOnly};
    my $editGroup = $options{EditGroup};
    my $writeMode = $$self{OPTIONS}{WriteMode};
    if ( $writeMode ne 'wcg' ) {
        $createOnly = 1 if $writeMode !~ /w/i;
        if ( $writeMode !~ /c/i ) {
            return 0 if $createOnly;
            $editOnly = 1;
        }
        elsif ( $writeMode !~ /g/i ) {
            $editGroup = 1;
        }
    }
    my ( $ifdName, $mieGroup, $movGroup, $fg );
    foreach $fg (@wantGroup) {
        next if defined $$fg[0] and $$fg[0] != 1;
        $_ = $$fg[1];
        my $grpName;
        if (/^IFD(\d+)$/i) {
            $grpName = $ifdName = "IFD$1";
        }
        elsif (/^SubIFD(\d+)$/i) {
            $grpName = $ifdName = "SubIFD$1";
        }
        elsif (/^Version(\d+)$/i) {
            $grpName = $ifdName = "Version$1";
        }
        elsif ( $exifDirs{$_} ) {
            $grpName = $exifDirs{$_};
            $ifdName = $grpName unless $ifdName and $allFam0{$_};
        }
        elsif ( $allFam0{$_} ) {
            $grpName = $allFam0{$_};
        }
        elsif (/^Track(\d+)$/i) {
            $grpName = $movGroup = "Track$1";
        }
        elsif (/^MIE(\d*-?)(\w+)$/i) {
            $grpName = $mieGroup = "MIE$1" . ucfirst( lc($2) );
        }
        elsif ( not $ifdName and /^XMP\b/i ) {
            my $table     = GetTagTable('Image::ExifTool::XMP::Main');
            my $writeProc = $$table{WRITE_PROC};
            if ($writeProc) {
                no strict 'refs';
                &$writeProc();
            }
        }
        $wantGroup =~ s/$grpName/$grpName/i if $grpName and $grpName ne $_;
    }
    my $origTag      = $tag;
    my @matchingTags = FindTagInfo($tag);
    until (@matchingTags) {
        my $langCode;
        if (   $tag =~ /^([?*\w]+)-([a-z]{2})(_[a-z]{2})$/i
            or $tag =~
            /^([?*\w]+)-([a-z]{2,3}|[xi])(-[a-z\d]{2,8}(-[a-z\d]{1,8})*)?$/i )
        {
            $tag = $1;
            $langCode = lc($2);
            $langCode .= ( length($3) == 3 ? uc($3) : lc($3) ) if $3;
            my @newMatches = FindTagInfo($tag);
            foreach $tagInfo (@newMatches) {
                next unless $$tagInfo{Table};
                my $langInfoProc = $$tagInfo{Table}{LANG_INFO} or next;
                my $langInfo     = &$langInfoProc( $tagInfo, $langCode );
                push @matchingTags, $langInfo if $langInfo;
            }
            last if @matchingTags;
        }
        elsif ( not $options{NoShortcut} ) {
            require Image::ExifTool::Shortcuts;
            my ($match) = grep /^\Q$tag\E$/i,
              keys %Image::ExifTool::Shortcuts::Main;
            undef $err;
            if ($match) {
                $options{NoShortcut} = $options{Sanitized} = 1;
                foreach $tag ( @{ $Image::ExifTool::Shortcuts::Main{$match} } )
                {
                    my ( $n, $e ) =
                      $self->SetNewValue( $tag, $value, %options );
                    $numSet += $n;
                    $e and $err = $e;
                }
                undef $err               if $numSet;
                return ( $numSet, $err ) if wantarray;
                $err and warn "$err\n";
                return $numSet;
            }
        }
        unless ($listOnly) {
            if ( not TagExists($tag) ) {
                if ( $tag =~ /^[-\w*?]+$/ ) {
                    my $pre = $wantGroup ? $wantGroup . ':' : '';
                    $err = "Tag '$pre${origTag}' is not defined";
                    $err .= ' or has a bad language code' if $origTag =~ /-/;
                    if ( not $pre and uc($origTag) eq 'TAG' ) {
                        $err .=
" (specify a writable tag name, not '${origTag}' literally)";
                    }
                }
                else {
                    $err = "Invalid tag name '${tag}'";
                    $err .= " (remove the leading '\$')" if $tag =~ /^\$/;
                }
            }
            elsif ($langCode) {
                $err = "Tag '${tag}' does not support alternate languages";
            }
            elsif ($wantGroup) {
                $err =
                  "Sorry, $wantGroup:$origTag doesn't exist or isn't writable";
            }
            else {
                $err = "Sorry, $origTag is not writable";
            }
            $verbose > 2 and print $out "$err\n";
        }
        return ( $numSet, $err ) if wantarray;
        $err and warn "$err\n";
        return $numSet;
    }
    my $foundMatch = 0;
    my ( @tagInfoList, @writeAlsoList, %writeGroup, %preferred, %tagPriority );
    my ( %avoid,       $wasProtected, $noCreate, %highestPriority, %highestQT );

  TAG: foreach $tagInfo (@matchingTags) {
        $tag = $$tagInfo{Name};
        my $lcTag = lc $tag;

        $highestPriority{$lcTag} = -999 unless defined $highestPriority{$lcTag};
        my ( $priority, $writeGroup );
        my $prfTag =
          defined $$tagInfo{Preferred}
          ? $$tagInfo{Preferred}
          : $$tagInfo{Table}{PREFERRED};
        if ($wantGroup) {
            my $wgAll =
              ( $$tagInfo{WriteGroup} and $$tagInfo{WriteGroup} eq 'All' );
            my @grp   = $self->GetGroup($tagInfo);
            my $hiPri = 1000;
            foreach $fg (@wantGroup) {
                my ( $fam, $lcWant ) = @$fg;
                $lcWant = $translateWantGroup{$lcWant}
                  if $translateWantGroup{$lcWant};
                $hiPri += $prfTag if $prfTag;
                if ( not defined $fam ) {
                    if ( $lcWant eq lc $grp[0] ) {
                        $writeGroup = $grp[0] if $wgAll and not $writeGroup;
                        next;
                    }
                    next if $lcWant eq lc $grp[2];
                }
                elsif ( $fam == 7 ) {
                    next if IsSameID( $$tagInfo{TagID}, $lcWant );
                }
                elsif ( $fam != 1 and not $$tagInfo{AllowGroup} ) {
                    next if $lcWant eq lc $grp[$fam];
                    if ( $wgAll and not $fam and $allFam0{$lcWant} ) {
                        $writeGroup or $writeGroup = $allFam0{$lcWant};
                        next;
                    }
                    next TAG;
                }
                if ( $grp[0] eq 'EXIF' or $grp[0] eq 'SonyIDC' or $wgAll ) {
                    unless ( $ifdName and $lcWant eq lc $ifdName ) {
                        next TAG
                          unless $wgAll
                          and not $fam
                          and $allFam0{$lcWant};
                        $writeGroup = $allFam0{$lcWant} unless $writeGroup;
                        next;
                    }
                    next TAG if $wgAll and $allFam0{$lcWant} and $fam;
                    $lcWant eq 'PreviewIFD' and ++$foundMatch, next TAG;
                    $writeGroup = $ifdName;
                }
                elsif ( $grp[0] eq 'QuickTime' ) {
                    if ( $grp[1] eq 'Track#' ) {
                        next TAG unless $movGroup and $lcWant eq lc($movGroup);
                        $writeGroup = $movGroup;
                    }
                    else {
                        my $grp = $$tagInfo{Table}{WRITE_GROUP};
                        next TAG unless $grp and $lcWant eq lc $grp;
                        $writeGroup = $grp;
                    }
                }
                elsif ( $grp[0] eq 'MIE' ) {
                    next TAG unless $mieGroup and $lcWant eq lc($mieGroup);
                    $writeGroup = $mieGroup;

                    if (    $writeGroup =~ /^MIE\d+$/
                        and $$tagInfo{Table}{WRITE_GROUP} )
                    {
                        $writeGroup = $$tagInfo{Table}{WRITE_GROUP};
                        $writeGroup =~ s/^MIE/$mieGroup/;
                    }
                }
                elsif ( not $$tagInfo{AllowGroup}
                    or $lcWant !~ /^$$tagInfo{AllowGroup}$/i )
                {
                    next TAG unless $lcWant eq lc $grp[1];
                }
            }
            $writeGroup
              or $writeGroup =
              (      $$tagInfo{WriteGroup}
                  || $$tagInfo{Table}{WRITE_GROUP}
                  || $grp[0] );
            $priority = $hiPri;
        }
        ++$foundMatch;
        my $table     = $$tagInfo{Table};
        my $writeProc = $$table{WRITE_PROC};
        if ( $$table{SRC_TABLE} ) {
            my $src = GetTagTable( $$table{SRC_TABLE} );
            $writeProc = $$src{WRITE_PROC} unless $writeProc;
        }
        if ($writeProc) {
            unless ( ref $writeProc ) {
                my $module = $writeProc;
                $module =~ s/::\w+$// and eval "require $module";
            }
            no strict 'refs';
            next unless $writeProc and &$writeProc();
        }
        my $writable = $$tagInfo{Writable};
        next
          unless $writable
          or (  $$table{WRITABLE}
            and not defined $writable
            and not $$tagInfo{SubDirectory} );
        if (
            not $writeGroup
            or (
                $translateWriteGroup{$writeGroup}
                and
                ( not $$tagInfo{WriteGroup} or $$tagInfo{WriteGroup} ne 'All' )
            )
          )
        {
            $writeGroup =
              $$tagInfo{WriteGroup} || $$tagInfo{Table}{WRITE_GROUP};
            my $group0 = $self->GetGroup( $tagInfo, 0 );
            $writeGroup or $writeGroup = $group0;
            unless ($priority) {
                if ( $$tagInfo{Avoid} and $$tagInfo{WriteAlso} ) {
                    $priority = 0;
                }
                else {
                    $priority = $$self{WRITE_PRIORITY}{ lc($writeGroup) };
                    unless ($priority) {
                        $priority = $$self{WRITE_PRIORITY}{ lc($group0) } || 0;
                    }
                }
            }
            $priority += $prfTag if $prfTag;
        }
        my $prot = $$tagInfo{Protected};
        $prot = 1 if $noFlat and defined $$tagInfo{Flat};
        if ($prot) {
            $prot &= ~$protected;
            if ($prot) {
                my %lkup = (
                    1 => 'unsafe',
                    2 => 'protected',
                    3 => 'unsafe and protected'
                );
                $wasProtected = $lkup{$prot};
                if ( $verbose > 1 ) {
                    my $wgrp1 = $self->GetWriteGroup1( $tagInfo, $writeGroup );
                    print $out
                      "Sorry, $wgrp1:$tag is $wasProtected for writing\n";
                }
                next;
            }
        }
        $tagPriority{$tagInfo} = $priority;
        $highestQT{$lcTag} = $priority
          if $$table{GROUPS}{0} eq 'QuickTime'
          and
          ( not defined $highestQT{$lcTag} or $highestQT{$lcTag} < $priority );
        if ( $priority > $highestPriority{$lcTag} ) {
            $highestPriority{$lcTag} = $priority;
            $preferred{$lcTag}       = { $tagInfo => 1 };
            $avoid{$lcTag}           = $$tagInfo{Avoid} ? 1 : 0;
        }
        elsif ( $priority == $highestPriority{$lcTag} ) {
            $preferred{$lcTag}{$tagInfo} = 1;
            ++$avoid{$lcTag} if $$tagInfo{Avoid};
        }
        if ( $$tagInfo{WriteAlso} ) {
            push @writeAlsoList, $tagInfo;
        }
        else {
            push @tagInfoList, $tagInfo;
        }
        if ( $writeGroup eq 'XMP' ) {
            my $wg = $$tagInfo{WriteGroup} || $$table{WRITE_GROUP};
            $writeGroup = $wg if $wg;
        }
        $writeGroup{$tagInfo} = $writeGroup;
    }
    @tagInfoList = sort { $tagPriority{$a} <=> $tagPriority{$b} } @tagInfoList;
    unshift @tagInfoList, @writeAlsoList if @writeAlsoList;

    my $lcTag;
    foreach $lcTag ( keys %preferred ) {
        if (    $preferred{$lcTag}
            and $highestPriority{$lcTag} == 0
            and %{ $$self{WRITE_PRIORITY} } )
        {
            delete $preferred{$lcTag};
        }
        if ( $avoid{$lcTag} and $preferred{$lcTag} ) {
            if ( $avoid{$lcTag} < scalar( keys %{ $preferred{$lcTag} } ) ) {
                foreach $tagInfo (@tagInfoList) {
                    next unless $lcTag eq lc $$tagInfo{Name};
                    delete $preferred{$lcTag}{$tagInfo} if $$tagInfo{Avoid};
                }
            }
            elsif ( $highestPriority{$lcTag} < 1000 ) {
                my $nextHighest = 0;
                my @nextBestTags;
                foreach $tagInfo (@tagInfoList) {
                    next unless $lcTag eq lc $$tagInfo{Name};
                    my $priority = $tagPriority{$tagInfo} or next;
                    next if $priority == $highestPriority{$lcTag};
                    next if $priority < $nextHighest;
                    my $permanent = $$tagInfo{Permanent};
                    $permanent = $$tagInfo{Table}{PERMANENT}
                      unless defined $permanent;
                    next if $$tagInfo{Avoid} or $permanent;
                    next if $writeGroup{$tagInfo} eq 'MakerNotes';

                    if ( $nextHighest < $priority ) {
                        $nextHighest = $priority;
                        undef @nextBestTags;
                    }
                    push @nextBestTags, $tagInfo;
                }
                if (@nextBestTags) {
                    delete $preferred{$lcTag};
                    foreach $tagInfo (@nextBestTags) {
                        $preferred{$lcTag}{$tagInfo} = 1;
                    }
                }
            }
        }
    }
    my ( $prioritySet, $createGroups, %alsoWrote );

    delete $$self{CHECK_WARN};

    foreach $tagInfo (@tagInfoList) {
        next if $alsoWrote{$tagInfo};

        next if defined $listOnly and ( $listOnly xor $$tagInfo{List} );
        my $noConv;
        my $writeGroup = $writeGroup{$tagInfo};
        my $permanent  = $$tagInfo{Permanent};
        $permanent = $$tagInfo{Table}{PERMANENT} unless defined $permanent;
        $writeGroup eq 'MakerNotes' and $permanent = 1
          unless defined $permanent;
        my $wgrp1 = $self->GetWriteGroup1( $tagInfo, $writeGroup );
        $tag = $$tagInfo{Name};
        my $lcTag = lc $tag;
        my $pref  = $preferred{$lcTag} || {};
        next
          if not $$pref{$tagInfo}
          and $$tagInfo{Avoid}
          and $$tagInfo{WriteAlso};
        my $shift    = $options{Shift};
        my $addValue = $options{AddValue};

        if ( defined $shift ) {
            my $shiftable;
            if ( $$tagInfo{List} ) {
                $shiftable = '';
            }
            else {
                $shiftable = $$tagInfo{Shift};
                unless ($shift) {
                    $shift = 1 if $addValue;
                    $shift = -1
                      if $options{DelValue}
                      and defined $shiftable
                      and $shiftable eq 'Time';
                }
                if ( $shift and ( not defined $value or not length $value ) ) {
                    undef $shift;
                }
            }
            if (
                ( defined $shiftable and not $shiftable )
                and
                ( $shift or ( $shiftable eq '0' and $options{DelValue} ) )
              )
            {
                $err = "$wgrp1:$tag is not shiftable";
                $verbose and print $out "$err\n";
                next;
            }
        }
        my $val = $value;
        if ( defined $val ) {
            if ( $addValue and not( $shift or $$tagInfo{List} ) ) {
                if ( $addValue eq '2' ) {
                    undef $addValue;
                }
                else {
                    $err = "Can't add $wgrp1:$tag (not a List type)";
                    $verbose > 2 and print $out "$err\n";
                    next;
                }
            }
            if ($shift) {
                if ( $$tagInfo{Shift} and $$tagInfo{Shift} eq 'Time' ) {
                    $val = ( $shift > 0 ? '+' : '-' ) . $val;
                    require 'Image/ExifTool/Shift.pl';
                    my $err2 = CheckShift( $$tagInfo{Shift}, $val );
                    if ($err2) {
                        $err = "$err2 for $wgrp1:$tag";
                        $verbose > 2 and print $out "$err\n";
                        next;
                    }
                }
                elsif ( IsFloat($val) ) {
                    $val *= $shift;
                }
                else {
                    $err = "Shift value for $wgrp1:$tag is not a number";
                    $verbose > 2 and print $out "$err\n";
                    next;
                }
                $noConv = 1;
            }
            elsif ( not length $val and $options{DelValue} ) {
                $noConv = 1;
            }
            elsif ( ref $val eq 'HASH' and not $$tagInfo{Struct} ) {
                $err = "Can't write a structure to $wgrp1:$tag";
                $verbose > 2 and print $out "$err\n";
                next;
            }
        }
        elsif ($permanent) {
            return 0 if $options{IgnorePermanent};
            if ( defined $$tagInfo{DelValue} ) {
                $val    = $$tagInfo{DelValue};
                $noConv = 1;
            }
            else {
                $val = '';
            }
        }
        elsif ( $addValue or $options{DelValue} ) {
            $err = "No value to add or delete in $wgrp1:$tag";
            $verbose > 2 and print $out "$err\n";
            next;
        }
        else {
            if ( $$tagInfo{DelCheck} ) {
                my $err2 = eval $$tagInfo{DelCheck};
                $@ and warn($@), $err2 = 'Error evaluating DelCheck';
                if ( defined $err2 ) {
                    $err2 or goto WriteAlso;
                    $err2 .= ' for' unless $err2 =~ /delete$/;
                    $err = "$err2 $wgrp1:$tag";
                    $verbose > 2 and print $out "$err\n";
                    next;
                }
            }
            if ( $$tagInfo{DelGroup} and not $options{DelValue} ) {
                my @del = ($tag);
                $$self{DEL_GROUP}{$tag} = 1;
                if ( $delMore{$tag} ) {
                    $$self{DEL_GROUP}{$_} = 1, push( @del, $_ )
                      foreach @{ $delMore{$tag} };
                }
                $self->RemoveNewValuesForGroup($tag);
                $verbose and print $out "  Deleting tags in: @del\n";
                ++$numSet;
                next;
            }
            $noConv = 1;
        }
        unless ($noConv) {
            $$self{ConvType} = $convType;
            my $e;
            ( $val, $e ) =
              $self->ConvInv( $val, $tagInfo, $tag, $wgrp1, $$self{ConvType},
                $wantGroup );
            if ( defined $e ) {
                $e or goto WriteAlso;
                $err = $e;
            }
        }
        if ( not defined $val and defined $value ) {
            next unless $options{DelValue};
            $val = 'xxx never delete xxx';
        }
        $$self{NEW_VALUE} or $$self{NEW_VALUE} = {};
        if ( $options{Replace} ) {
            $self->GetNewValueHash( $tagInfo, $writeGroup, 'delete',
                $options{ProtectSaved} );
            if ( $$tagInfo{WriteAlso} ) {
                $$self{INDENT2} = '+';
                my ( $wgrp, $wtag );
                if (    $$tagInfo{WriteGroup}
                    and $$tagInfo{WriteGroup} eq 'All'
                    and $writeGroup )
                {
                    $wgrp = $writeGroup . ':';
                }
                else {
                    $wgrp = '';
                }
                foreach $wtag ( sort keys %{ $$tagInfo{WriteAlso} } ) {
                    my ( $n, $e ) =
                      $self->SetNewValue( $wgrp . $wtag, undef, Replace => 2 );
                    $numSet += $n;
                }
                $$self{INDENT2} = '';
            }
            $options{Replace} == 2 and ++$numSet, next;
        }

        if ( defined $val ) {
            my $nvHash =
              $self->GetNewValueHash( $tagInfo, $writeGroup, 'create',
                $options{ProtectSaved}, ( $options{DelValue} and not $shift ) );
            $nvHash or ++$numSet, next;
            $$nvHash{NoReplace} = 1
              if $$tagInfo{List} and not $options{Replace};
            $$nvHash{WantGroup} = $wantGroup;
            $$nvHash{EditOnly}  = 1 if $editOnly;
            $$nvHash{MAKER_NOTE_FIXUP} = $options{Fixup}
              if $$tagInfo{MakerNotes};
            if ($createOnly) {

                $$nvHash{DelValue}   = [''];
                $$nvHash{CreateOnly} = 1;
            }
            elsif ( $options{DelValue} or $addValue or $shift ) {
                $$nvHash{DelValue} or $$nvHash{DelValue} = [];
                if ($shift) {
                    $$nvHash{Shift} = $val;
                }
                elsif ( $options{DelValue} ) {
                    $$nvHash{IsCreating} = 0
                      unless $val eq '' or $$tagInfo{List};
                    push @{ $$nvHash{DelValue} },
                      ref $val eq 'ARRAY' ? @$val : $val;
                    if ( $verbose > 1 ) {
                        my $verb     = $permanent ? 'Replacing' : 'Deleting';
                        my $fromList = $$tagInfo{List} ? ' from list' : '';
                        my @vals     = ( ref $val eq 'ARRAY' ? @$val : $val );
                        foreach (@vals) {
                            if ( ref $_ eq 'HASH' ) {
                                require 'Image/ExifTool/XMPStruct.pl';
                                $_ =
                                  Image::ExifTool::XMP::SerializeStruct( $self,
                                    $_ );
                            }
                            print $out
"$$self{INDENT2}$verb $wgrp1:$tag$fromList if value is '${_}'\n";
                        }
                    }
                }
            }
            my $prf =
              defined $$tagInfo{Preferred}
              ? $$tagInfo{Preferred}
              : $$tagInfo{Table}{PREFERRED};
            if ( $$tagInfo{Table}{GROUPS}{0} eq 'QuickTime' ) {
                $prf = 0 if $tagPriority{$tagInfo} < $highestQT{$lcTag};
            }
            if ( $$pref{$tagInfo} or $prf ) {
                if ( $permanent or $shift ) {
                    $$nvHash{IsCreating} = 0;
                }
                elsif (
                       ( $$tagInfo{List} and not $options{DelValue} )
                    or not( $$nvHash{DelValue} and @{ $$nvHash{DelValue} } )
                    or
                    grep( /^$/, @{ $$nvHash{DelValue} } )
                  )
                {
                    $$nvHash{IsCreating} =
                      $editOnly ? 0 : ( $editGroup ? 2 : 1 );
                    $createGroups
                      or $createGroups = $options{CreateGroups} || {};
                    $$createGroups{ $self->GetGroup( $tagInfo, 0 ) } = 1;
                    $$nvHash{CreateGroups} = $createGroups;
                }
            }
            if ( $$nvHash{IsCreating} ) {
                if ( %{ $$self{DEL_GROUP} } ) {
                    my ( $grp, @grps );
                    foreach $grp ( keys %{ $$self{DEL_GROUP} } ) {
                        next if $$self{DEL_GROUP}{$grp} == 2;
                        $$self{DEL_GROUP}{$grp} = 2;
                        push @grps, $grp;
                    }
                    if ( $verbose > 1 and @grps ) {
                        @grps = sort @grps;
                        print $out
                          "  Writing new tags after deleting groups: @grps\n";
                    }
                }
            }
            elsif ($createOnly) {
                $noCreate =
                  $permanent
                  ? 'permanent'
                  : ( $$tagInfo{Avoid} ? 'avoided' : '' );
                $noCreate or $noCreate = $shift ? 'shifting' : 'not preferred';
                $verbose > 2
                  and print $out "Not creating $wgrp1:$tag ($noCreate)\n";
                next;
            }
            if ( $shift or not $options{DelValue} ) {
                $$nvHash{Value} or $$nvHash{Value} = [];
                if ( not $$tagInfo{List} ) {
                    $$nvHash{Value}[0] = $val;
                }
                elsif ( defined $$nvHash{AddBefore}
                    and @{ $$nvHash{Value} } >= $$nvHash{AddBefore} )
                {
                    splice @{ $$nvHash{Value} }, -$$nvHash{AddBefore}, 0,
                      ref $val eq 'ARRAY' ? @$val : $val;
                }
                else {
                    push @{ $$nvHash{Value} },
                      ref $val eq 'ARRAY' ? @$val : $val;
                }
                if ( $verbose > 1 ) {
                    my $ifExists;
                    if ( $$tagInfo{IsComposite} ) {
                        if ( $$tagInfo{WriteAlso} ) {
                            $ifExists = ' (+'
                              . join( ',+',
                                sort keys %{ $$tagInfo{WriteAlso} } ) . '):';
                        }
                        else {
                            $ifExists = '';
                        }
                    }
                    else {
                        $ifExists =
                          $$nvHash{IsCreating}
                          ? (
                            $createOnly
                            ? (
                                $$nvHash{IsCreating} == 2
                                ? " if $writeGroup exists and tag doesn't"
                                : " if tag doesn't exist"
                              )
                            : ( $$nvHash{IsCreating} == 2
                                ? " if $writeGroup exists"
                                : '' )
                          )
                          : ( ( $$nvHash{DelValue} and @{ $$nvHash{DelValue} } )
                            ? ' if tag was deleted'
                            : ' if tag exists' );
                    }
                    my $verb =
                      ( $shift
                        ? 'Shifting'
                        : ( $addValue ? 'Adding' : 'Writing' ) );
                    print $out "$$self{INDENT2}$verb $wgrp1:$tag$ifExists\n";
                }
            }
        }
        elsif ($permanent) {
            $err = "Can't delete Permanent tag $wgrp1:$tag";
            $verbose > 1 and print $out "$err\n";
            next;
        }
        elsif ( $addValue or $options{DelValue} ) {
            $verbose > 1
              and print $out "Adding/Deleting nothing does nothing\n";
            next;
        }
        else {
            $self->GetNewValueHash( $tagInfo, $writeGroup, 'delete' );
            my $nvHash =
              $self->GetNewValueHash( $tagInfo, $writeGroup, 'create' );
            $$nvHash{WantGroup} = $wantGroup;
            $verbose > 1 and print $out "$$self{INDENT2}Deleting $wgrp1:$tag\n";
        }
        $$setTags{$tagInfo} = 1 if $setTags;
        $prioritySet        = 1 if $$pref{$tagInfo};
      WriteAlso:
        ++$numSet;
        my $writeAlso = $$tagInfo{WriteAlso};
        if ($writeAlso) {
            $$self{INDENT2} = '+';
            my ( $wgrp, $wtag, $n );
            if (    $$tagInfo{WriteGroup}
                and $$tagInfo{WriteGroup} eq 'All'
                and $writeGroup )
            {
                $wgrp = $writeGroup . ':';
            }
            else {
                $wgrp = '';
            }
            local $SIG{'__WARN__'} = \&SetWarning;
            foreach $wtag ( sort keys %$writeAlso ) {
                my %opts = (
                    Type         => 'ValueConv',
                    Protected    => $protected | 0x02,
                    AddValue     => $addValue,
                    DelValue     => $options{DelValue},
                    Shift        => $options{Shift},
                    Replace      => $options{Replace},
                    CreateGroups => $createGroups,
                    SetTags      => \%alsoWrote,
                );
                undef $evalWarning;
                my $v = eval $$writeAlso{$wtag};
                undef $v unless defined $val;
                $@ and $evalWarning = $@;
                unless ($evalWarning) {
                    ( $n, $evalWarning ) =
                      $self->SetNewValue( $wgrp . $wtag, $v, %opts );
                    $numSet += $n;
                    $prioritySet = 1 if $n and $$pref{$tagInfo};
                }
                if ( $evalWarning and ( not $err or $verbose > 2 ) ) {
                    my $str = CleanWarning();
                    if ($str) {
                        $str .= " for $wtag" unless $str =~ / for [-\w:]+$/;
                        $str .= " in $wgrp1:$tag (WriteAlso)";
                        $err or $err = $str;
                        print $out "$str\n" if $verbose > 2;
                    }
                }
            }
            $$self{INDENT2} = '';
        }
    }
    if ( defined $err and not $prioritySet ) {
        warn "$err\n" if $err and not wantarray;
    }
    elsif ( not $numSet ) {
        my $pre = $wantGroup ? $wantGroup . ':' : '';
        if ($wasProtected) {
            $verbose = 0;
            unless ( $options{Replace} and $options{Replace} == 2 ) {
                $err = "Sorry, $pre$tag is $wasProtected for writing";
            }
        }
        elsif ( not $listOnly ) {
            if ( $origTag =~ /[?*]/ ) {
                if ($noCreate) {
                    $err = "No tags matching 'pre${origTag}' will be created";
                    $verbose = 0;
                }
                elsif ($foundMatch) {
                    $err = "Sorry, no writable tags matching '$pre${origTag}'";
                }
                else {
                    $err = "No matching tags for '$pre${origTag}'";
                }
            }
            elsif ($noCreate) {
                $err     = "Not creating $pre$tag";
                $verbose = 0;
            }
            elsif ($foundMatch) {
                $err = "Sorry, $pre$tag is not writable";
            }
            elsif ( $wantGroup and @matchingTags ) {
                $err = "Sorry, $pre$tag doesn't exist or isn't writable";
            }
            else {
                $err = "Tag '$pre${tag}' is not defined";
            }
        }
        if ($err) {
            $verbose > 2 and print $out "$err\n";
            warn "$err\n" unless wantarray;
        }
    }
    elsif ( $$self{CHECK_WARN} ) {
        $err = $$self{CHECK_WARN};
        $verbose > 2 and print $out "$err\n";
    }
    elsif ( $err and not $verbose ) {
        undef $err;
    }
    return ( $numSet, $err ) if wantarray;
    return $numSet;
}

sub SetNewValuesFromFile($$;@) {
    local $_;
    my ( $self, $srcFile, @setTags ) = @_;
    my ( $srcExifTool, $key, $tag, @exclude, @reqTags, $info );

    my %opts = ( Replace => 1 );
    while ( ref $setTags[0] eq 'HASH' ) {
        $_ = shift @setTags;
        foreach $key ( keys %$_ ) {
            $opts{$key} = $$_{$key};
        }
    }
    @setTags and ExpandShortcuts( \@setTags );
    my $options   = $$self{OPTIONS};
    my $printConv = $$options{PrintConv};
    if ( $opts{Type} ) {
        $opts{SrcType} = $opts{Type};
        $printConv = ( $opts{Type} eq 'PrintConv' ? 1 : 0 );
    }
    my $srcType   = $printConv                ? 'PrintConv'       : 'ValueConv';
    my $structOpt = defined $$options{Struct} ? $$options{Struct} : 2;

    if ( ref $srcFile and UNIVERSAL::isa( $srcFile, 'Image::ExifTool' ) ) {
        $srcExifTool = $srcFile;
        $info        = $srcExifTool->GetInfo( { PrintConv => $printConv } );
    }
    else {
        $srcExifTool = Image::ExifTool->new;
        $srcExifTool->Options( PrintConv => $printConv );
        $$srcExifTool{TAGS_FROM_FILE} = 1;
        $$srcExifTool{FILE_SEQUENCE} = $$self{FILE_SEQUENCE}++;
        $structOpt = 1 if $structOpt eq '2' and not @setTags;
        foreach (
            qw(ByteUnit Charset CharsetEXIF CharsetFileName CharsetID3 CharsetIPTC
            CharsetPhotoshop Composite DateFormat Debug EncodeHangs Escape
            ExtendedXMP ExtractEmbedded FastScan Filter FixBase Geolocation
            GeolocAltNames GeolocFeature GeolocMinPop GeolocMaxDist
            GlobalTimeShift GPSQuadrant HexTagIDs IgnoreGroups IgnoreMinorErrors
            IgnoreTags ImageHashType KeepUTCTime Lang LargeFileSupport
            LigoGPSScale ListItem ListSep MDItemTags MissingTagValue NoPDFList
            NoWarning Password PrintConv QuickTimeUTC RequestTags SaveFormat
            SavePath ScanForXMP StructFormat SystemTags SystemTimeRes TimeZone
            Unknown UserParam Validate WindowsLongPath WindowsWideFile XAttrTags
            XMPAutoConv)
          )
        {
            $srcExifTool->Options( $_ => $$options{$_} );
        }
        $srcExifTool->Options(
            Binary      => 1,
            CoordFormat => $$options{CoordFormat} || '%d %d %.8f',
            Duplicates  => 1,
            LimitLongValues => 10000000,
            List            => 1,
            MakerNotes      => $$options{FastScan}
              && $$options{FastScan} > 1 ? undef : 1,
            RequestAll => $$options{RequestAll} || 1,
            StrictDate => defined $$options{StrictDate}
            ? $$options{StrictDate}
            : 1,
            Struct => $structOpt,
        );
        if ( $$options{Geolocation} and not grep /\bGeolocation/i, @setTags ) {
            $self->VPrint( 0, '(resetting unnecessary Geolocation option)' );
            $$srcExifTool{OPTIONS}{Geolocation} = undef;
        }
        $$srcExifTool{GLOBAL_TIME_OFFSET} = $$self{GLOBAL_TIME_OFFSET};
        $$srcExifTool{ALT_EXIFTOOL}       = $$self{ALT_EXIFTOOL};
        foreach $tag (@setTags) {
            next if ref $tag;
            $tag =~ /^-(.*)/ and push( @exclude, $1 ), next;
            $_ = $tag;
            if (/(.+?)\s*(>|<)\s*(.+)/) {
                if ( $2 eq '>' ) {
                    $_ = $1;
                }
                else {
                    $_ = $3;
                    /\$/
                      and push( @reqTags, /\$\{?(?:[-\w]+:)*([-\w?*]+)/g ),
                      next;
                }
            }
            push @reqTags, $2 if /(^|:)([-\w?*]+)#?$/;
        }
        if (@exclude) {
            ExpandShortcuts( \@exclude, 1 );
            $srcExifTool->Options( Exclude => \@exclude );
        }
        $srcExifTool->Options( RequestTags => \@reqTags ) if @reqTags;
        $info = $srcExifTool->ImageInfo($srcFile);
    }
    return $info
      if $$info{Error}
      and $$info{Error} eq 'Error opening file'
      and not $$self{ALT_EXIFTOOL};
    delete $$srcExifTool{VALUE}{Error};

    my ( @tags, @prio );
    foreach (
        sort { $$srcExifTool{FILE_ORDER}{$a} <=> $$srcExifTool{FILE_ORDER}{$b} }
        keys %$info
      )
    {
        if (/ /) {
            push @tags, $_;
        }
        else {
            push @prio, $_;
        }
    }
    push @tags, @prio;
    unless (@setTags) {
        $$self{MAKER_NOTE_BYTE_ORDER} = $$srcExifTool{MAKER_NOTE_BYTE_ORDER};
        my $tagExtra = $$srcExifTool{TAG_EXTRA};
        foreach $tag (@tags) {
            next if $tag =~ /^(Error|Warning)\b/;
            if ( $opts{SrcType} and $opts{SrcType} ne $srcType ) {
                $$info{$tag} = $srcExifTool->GetValue( $tag, $opts{SrcType} );
            }
            my $fixup = $$tagExtra{$tag}{Fixup};
            $opts{Fixup} = $fixup if $fixup;
            my ( $n, $e ) = $self->SetNewValue( $tag, $$info{$tag}, %opts );
            $n or delete $$info{$tag};
            delete $opts{Fixup} if $fixup;
        }
        return $info;
    }
    my ( @setList, $set, %setMatches, $t, %altFiles );
    my $assign = 0;
    foreach $t (@setTags) {
        if ( ref $t eq 'HASH' ) {
            foreach $key ( keys %$t ) {
                $opts{$key} = $$t{$key};
            }
            next;
        }
        my $opts = {%opts};
        $tag = lc $t;
        my ( @fg, $grp, $dst, $dstGrp, $dstTag, $isExclude );
        if ( $tag =~ /(.+?)\s*(>|<|=)(\s*)(.*)/ ) {
            $dstGrp = '';
            my ( $opt, $op, $spc );
            if ( $2 eq '>' ) {
                ( $tag, $dstTag ) = ( $1, $4 );
                $opt = $1
                  if $tag =~ s/\s*([-+])$// or $dstTag =~ s/^([-+])\s*//;
            }
            else {
                ( $dstTag, $op, $spc, $tag ) = ( $1, $2, $3, $4 );
                $opt = $1 if $dstTag =~ s/\s*([-+])$//;
                if ( $op eq '=' ) {
                    $tag = $spc . $tag;
                    undef $tag unless $dstTag =~ s/\^$// or length $tag;
                    $$opts{ASSIGN} = ++$assign;
                }
                elsif ( $tag =~ /\$/ ) {
                    $tag = $t;

                    $tag =~ s/(.+?)\s*(>|<) ?//;
                    $$opts{EXPR} = 1;
                }
                else {
                    $opt = $1 if $tag =~ s/^([-+])\s*//;
                }
            }
            $$opts{Replace} = 0 if $dstTag =~ s/^\+//;
            unless ( $$opts{EXPR} or $$opts{ASSIGN} or ValidTagName($tag) ) {
                $self->Warn(
"Invalid tag name '${tag}'. Use '=' not '<' to assign a tag value"
                );
                next;
            }
            ValidTagName($dstTag)
              or $self->Warn("Invalid tag name '${dstTag}'"), next;
            if ($opt) {
                $$opts{ { '+' => 'AddValue', '-' => 'DelValue' }->{$opt} } = 1;
                $$opts{Shift} = 0;
            }
            ( $dstGrp, $dstTag ) = ( $1, $2 ) if $dstTag =~ /(.*):(.+)/;
            $$opts{Type} = 'ValueConv' if $dstTag =~ s/#$//;
            $dstTag = '*' if $dstTag eq 'all';
        }
        else {
            $$opts{Replace} = 0 if $tag =~ s/^\+//;
        }
        unless ( $$opts{EXPR} or $$opts{ASSIGN} ) {
            $isExclude = ( $tag =~ s/^-// );
            if ( $tag =~ /(.*):(.+)/ ) {
                ( $grp, $tag ) = ( $1, $2 );
                foreach ( split /:/, $grp ) {
                    next unless length($_) and /^(\d+)?(.*)/;
                    my ( $f, $g ) = ( $1, $2 );
                    $f = 7 if ( not $f or $f eq '7' ) and $g =~ s/^ID-//i;
                    if ( $g =~ /^file\d+$/i and ( not $f or $f eq '8' ) ) {
                        $f = 8;
                        my $g8 = ucfirst $g;
                        if ( $$srcExifTool{ALT_EXIFTOOL}{$g8} ) {
                            $$opts{GROUP8} = $g8;
                            $altFiles{$g8} or $altFiles{$g8} = [];
                            push @{ $altFiles{$g8} }, "$grp:$tag";
                        }
                    }
                    push @fg, [ $f, $g ] unless $g eq '*' or $g eq 'all';
                }
            }
            if ( $tag =~ s/#$// ) {
                $$opts{SrcType} = 'ValueConv';
                $$opts{Type}    = 'ValueConv' unless $dstTag;
            }
            $tag = '*' if $tag eq 'all';
            if ( $tag =~ /[?*]/ and $tag ne '*' ) {
                $$opts{WILD} = 1;
                $tag =~ s/\*/[-\\w]*/g;
                $tag =~ s/\?/[-\\w]/g;
            }
        }
        if ($dstTag) {
            $isExclude and return { Error => "Can't redirect excluded tag" };
            $dst = [ $dstGrp, $dstTag ];
        }
        elsif ($isExclude) {
            unshift @setList, [ [], '*', [ '', '*' ], $opts ] unless @setList;
        }
        else {
            $dst = [ $grp || '', $$opts{WILD} ? '*' : $tag ];
        }
        unshift @setList, [ \@fg, $tag, $dst, $opts ];
    }
    my $g8;
    foreach $g8 ( sort keys %altFiles ) {
        my $altInfo = $srcExifTool->GetInfo( $altFiles{$g8} );
        if (%$altInfo) {
            push @tags, 'Warning DUMMY', reverse sort keys %$altInfo;
            $$info{$_} = $$altInfo{$_} foreach keys %$altInfo;
        }
    }
    foreach $set (@setList) {
        $$set[2] and $setMatches{$set} = [];
    }
    undef @tags if $assign == @setList;
    my ( %rtnInfo, $isAlt );
    foreach $tag (@tags) {
        if ( $tag =~ /^(Error|Warning)( |$)/ ) {
            if ( $tag eq 'Warning DUMMY' ) {
                $isAlt = 1;
            }
            else {
                $rtnInfo{$tag} = $$info{$tag};
            }
            next;
        }
        my $lcTag = lc( GetTagName($tag) );
        my ( @grp, %grp );
      SET: foreach $set (@setList) {
            my $opts = $$set[3];
            next if $$opts{EXPR};
            next if $$opts{GROUP8} xor $isAlt;
            unless ( $$set[1] eq $lcTag or $$set[1] eq '*' ) {
                next unless $$opts{WILD} and $lcTag =~ /^$$set[1]$/;
            }
            if ( @{ $$set[0] } ) {
                unless (@grp) {
                    @grp = map( lc, $srcExifTool->GetGroup($tag) );
                    $grp{$_} = 1 foreach @grp;
                }
                foreach ( @{ $$set[0] } ) {
                    my ( $f, $g ) = @$_;
                    if ( not defined $f ) {
                        next SET unless $grp{$g};
                    }
                    elsif ( $f == 7 ) {
                        next SET
                          unless IsSameID( $srcExifTool->GetTagID($tag), $g );
                    }
                    else {
                        next SET unless defined $grp[$f] and $g eq $grp[$f];
                    }
                }
            }
            last unless $$set[2];

            push @{ $setMatches{$set} }, $tag;
        }
    }
    foreach $set ( reverse @setList ) {
        my $opts = $$set[3];
        if ( $$opts{EXPR} or $$opts{ASSIGN} ) {
            my $val;
            if ( $$opts{EXPR} ) {
                $val =
                  $srcExifTool->InsertTagValues( $$set[1], \@tags, 'Error' );
                my $err = $$srcExifTool{VALUE}{Error};
                if ($err) {
                    my $noWarn = $$srcExifTool{OPTIONS}{NoWarning};
                    unless (
                        $noWarn and (
                            eval { $err =~ /$noWarn/ }
                            or
                            (
                                $err =~ s/^\[minor\] //i
                                and eval { $err =~ /$noWarn/ }
                            )
                        )
                      )
                    {
                        $tag = NextFreeTagKey( \%rtnInfo, 'Warning' );
                        $rtnInfo{$tag} = $$srcExifTool{VALUE}{Error};
                    }
                    delete $$srcExifTool{VALUE}{Error};
                    next unless defined $val;
                }
            }
            else {
                $val = $$set[1];
            }
            my ( $dstGrp, $dstTag ) = @{ $$set[2] };
            $$opts{Protected} = 1 unless $dstTag =~ /[?*]/ and $dstTag ne '*';
            $$opts{Group}     = $dstGrp if $dstGrp;
            my @rtnVals = $self->SetNewValue( $dstTag, $val, %$opts );
            $rtnInfo{$dstTag} = $val if $rtnVals[0];

            $rtnInfo{ NextFreeTagKey( \%rtnInfo, 'Warning' ) } = $rtnVals[1]
              if $rtnVals[1];
            next;
        }
        foreach $tag ( @{ $setMatches{$set} } ) {
            my ( $val, $noWarn );
            if ( $$opts{SrcType} and $$opts{SrcType} ne $srcType ) {
                $val = $srcExifTool->GetValue( $tag, $$opts{SrcType} );
            }
            else {
                $val = $$info{$tag};
            }
            my ( $dstGrp, $dstTag ) = @{ $$set[2] };
            if ($dstGrp) {
                my @dstGrp = split /:/, $dstGrp;
                foreach (@dstGrp) {
                    next unless /^(\d*)(all|\*)$/i;
                    $_ =
                      $1 . $srcExifTool->GetGroup( $tag, length $1 ? $1 : 1 );
                    $noWarn = 1;
                }
                $$opts{Group} = join ':', @dstGrp;
            }
            else {
                delete $$opts{Group};
            }
            if ( $$srcExifTool{TAG_INFO}{$tag}{MakerNotes} ) {
                $$opts{Fixup} = $$srcExifTool{TAG_EXTRA}{$tag}{Fixup};
                $$self{MAKER_NOTE_BYTE_ORDER} =
                  $$srcExifTool{MAKER_NOTE_BYTE_ORDER};
            }
            if ( $dstTag eq '*' ) {
                $dstTag = $tag;
                $noWarn = 1;
            }
            if ( $$set[1] eq '*' or $$set[3]{WILD} ) {
                next
                  if $$srcExifTool{TAG_INFO}{$tag}{Protected}
                  and $$srcExifTool{TAG_INFO}{$tag}{Binary};
                delete $$opts{Protected};
                $$opts{NoFlat} = $structOpt eq '2' ? 1 : 0;
            }
            else {
                $$opts{Protected} = 1 unless $dstTag =~ /[?*]/;
                delete $$opts{NoFlat};
            }
            my ( $rtn, $wrn ) = $self->SetNewValue( $dstTag, $val, %$opts );
            if ( $wrn and not $noWarn ) {
                $rtnInfo{ NextFreeTagKey( \%rtnInfo, 'Warning' ) } = $wrn;
                $noWarn = 1;
            }
            delete $$opts{Fixup};
            $rtnInfo{$tag} = $val if $rtn;
        }
    }
    return \%rtnInfo;
}

sub GetNewValue($$;$) {
    local $_;
    my $self = shift;
    my $tag  = shift;
    my $nvHash;
    if ( ( ref $tag eq 'HASH' and $$tag{IsNVH} ) or not defined $tag ) {
        $nvHash = $tag;
    }
    else {
        my $newValueHashPt = shift;
        if ( $$self{NEW_VALUE} ) {
            my ( $group, $tagInfo );
            if ( ref $tag ) {
                $nvHash = $self->GetNewValueHash($tag);
            }
            elsif ( defined( $tagInfo = $Image::ExifTool::Extra{$tag} )
                and $$tagInfo{Writable} )
            {
                $nvHash = $self->GetNewValueHash($tagInfo);
            }
            else {
                my @groups;
                @groups = split ':', $1 if $tag =~ s/(.*)://;
                my @tagInfoList = FindTagInfo($tag);
              GNV_TagInfo: foreach $tagInfo (@tagInfoList) {
                    my $nvh = $self->GetNewValueHash($tagInfo) or next;
                    foreach (@groups) {
                        next if $_ eq $$nvh{WriteGroup};
                        my @grps = $self->GetGroup($tagInfo);
                        if ( $grps[0] eq $$nvh{WriteGroup} ) {
                            next if $_ eq $grps[1];
                        }
                        else {
                            next if $_ eq $grps[0];
                        }
                        next
                          if /^ID-(.*)/i and IsSameID( $$tagInfo{TagID}, $1 );
                        $nvh = $$nvh{Next} or next GNV_TagInfo;
                    }
                    $nvHash = $nvh;
                    last if defined $$nvHash{IsCreating};
                }
            }
        }
        $newValueHashPt and $$newValueHashPt = $nvHash;
    }
    unless ( $nvHash and $$nvHash{Value} ) {
        return () if wantarray;
        return undef;
    }
    my $vals = $$nvHash{Value};
    if ( $$nvHash{TagInfo}{RawConvInv} or $$nvHash{Shift} ) {
        my @copyVals = @$vals;
        $vals = \@copyVals;
        my $tagInfo = $$nvHash{TagInfo};
        my $conv    = $$tagInfo{RawConvInv};
        my $table   = $$tagInfo{Table};
        my ( $val, $checkProc );
        $checkProc = $$table{CHECK_PROC} if $$nvHash{Shift} and $table;
        local $SIG{'__WARN__'} = \&SetWarning;
        undef $evalWarning;

        foreach $val (@$vals) {
            if ($checkProc) {
                my $err = &$checkProc( $self, $tagInfo, \$val );
                if ( $err or not defined $val ) {
                    $err or $err = 'Error generating raw value';
                    $self->Warn("$err for $$tagInfo{Name}");
                    @$vals = ();
                    last;
                }
                next unless $conv;
            }
            else {
                last unless $conv;
            }
            if ( ref($conv) eq 'CODE' ) {
                $val = &$conv( $val, $self );
            }
            else {
                $val = eval $conv;
                $@ and $evalWarning = $@;
            }
            if ($evalWarning) {
                if ( $evalWarning ne "\n" ) {
                    my $err =
                      CleanWarning() . " in $$tagInfo{Name} (RawConvInv)";
                    $self->Warn($err);
                }
                @$vals = ();
                last;
            }
        }
    }
    if (wantarray) {
        if ( @$vals > 1 and $self->Options('NoDups') ) {
            my %seen;
            @$vals = grep { !$seen{$_}++ } @$vals;
        }
        return @$vals;
    }
    return $$vals[0];
}

sub CountNewValues($) {
    my $self   = shift;
    my $newVal = $$self{NEW_VALUE};
    my ( $num, $pseudo ) = ( 0, 0 );
    if ($newVal) {
        $num = scalar keys %$newVal;
        my $nv;
        foreach $nv ( values %$newVal ) {
            my $tagInfo = $$nv{TagInfo};
            $$tagInfo{WriteNothing} and --$num, next;
            $$tagInfo{WritePseudo} and ++$pseudo;
        }
    }
    $num += scalar keys %{ $$self{DEL_GROUP} };
    return $num unless wantarray;
    return ( $num, $pseudo );
}

sub SaveNewValues($) {
    my $self      = shift;
    my $newValues = $$self{NEW_VALUE};
    my $saveCount = ++$$self{SAVE_COUNT};
    my $key;
    foreach $key ( keys %$newValues ) {
        my $nvHash = $$newValues{$key};
        while ($nvHash) {
            $$nvHash{Save} or $$nvHash{Save} = $saveCount;
            $nvHash = $$nvHash{Next};
        }
    }
    $$self{SAVE_NEW_VALUE} = {};
    my %delGrp = %{ $$self{DEL_GROUP} };
    $$self{SAVE_DEL_GROUP} = \%delGrp;
    return $saveCount;
}

sub RestoreNewValues($) {
    my $self        = shift;
    my $newValues   = $$self{NEW_VALUE};
    my $savedValues = $$self{SAVE_NEW_VALUE};
    my $key;
    if ($newValues) {
        my @keys = keys %$newValues;
        foreach $key (@keys) {
            my $lastHash;
            my $nvHash = $$newValues{$key};
            while ($nvHash) {
                if ( $$nvHash{Save} ) {
                    $lastHash = $nvHash;
                }
                else {
                    if ($lastHash) {
                        $$lastHash{Next} = $$nvHash{Next};
                    }
                    elsif ( $$nvHash{Next} ) {
                        $$newValues{$key} = $$nvHash{Next};
                    }
                    else {
                        delete $$newValues{$key};
                    }
                }
                $nvHash = $$nvHash{Next};
            }
        }
    }
    if ($savedValues) {
        $newValues or $newValues = $$self{NEW_VALUE} = {};
        foreach $key ( keys %$savedValues ) {
            if ( $$newValues{$key} ) {
                my $nvHash = LastInList( $$newValues{$key} );
                $$nvHash{Next} = $$savedValues{$key};
            }
            else {
                $$newValues{$key} = $$savedValues{$key};
            }
        }
        $$self{SAVE_NEW_VALUE} = {};
    }
    my %delGrp = %{ $$self{SAVE_DEL_GROUP} };
    $$self{DEL_GROUP} = \%delGrp;
}

sub SetAlternateFile($$$) {
    my ( $self, $g8, $file ) = @_;
    $g8 = ucfirst lc $g8;
    return 0 unless $g8 =~ /^File\d+$/;
    if ( not defined $file ) {
        delete $$self{ALT_EXIFTOOL}{$g8};
    }
    elsif (
        not(    $$self{ALT_EXIFTOOL}{$g8}
            and $file !~ /\$/
            and $$self{ALT_EXIFTOOL}{$g8}{ALT_FILE} eq $file )
      )
    {
        my $altExifTool = Image::ExifTool->new;
        $$altExifTool{ALT_FILE} = $file;
        $$self{ALT_EXIFTOOL}{$g8} = $altExifTool;
    }
    return 1;
}

sub SetFileModifyDate($$;$$$) {
    my ( $self, $file, $originalTime, $tag, $isUnixTime ) = @_;
    my $nvHash;
    $tag = 'FileModifyDate' unless defined $tag;
    my $val = $self->GetNewValue( $tag, \$nvHash );
    return 0 unless defined $val;
    my $isOverwriting = $self->IsOverwriting($nvHash);
    return 0 unless $isOverwriting;
    return 0 if $tag eq 'FileCreateDate' and $^O ne 'MSWin32';

    if ( $isOverwriting < 0 ) {

        unless ( defined $originalTime ) {
            my ( $aTime, $mTime, $cTime ) = $self->GetFileTime($file);
            $originalTime = ( $tag eq 'FileCreateDate' ) ? $cTime : $mTime;
            return 0 unless defined $originalTime;
            $isUnixTime = 1;
        }
        $originalTime = int( $^T - $originalTime * ( 24 * 3600 ) + 0.5 )
          unless $isUnixTime;
        return 0 unless $self->IsOverwriting( $nvHash, $originalTime );
        $val = $$nvHash{Value}[0];
    }
    my ( $aTime, $mTime, $cTime );
    if ( $tag eq 'FileCreateDate' ) {
        eval { require Win32::API }
          or $self->Warn("Install Win32::API to set $tag"), return -1;
        eval { require Win32API::File }
          or $self->Warn("Install Win32API::File to set $tag"), return -1;
        $cTime = $val;
    }
    else {
        $aTime = $mTime = $val;
    }
    $self->SetFileTime( $file, $aTime, $mTime, $cTime, 1 )
      or $self->Warn("Error setting $tag"), return -1;
    ++$$self{CHANGED};
    $$self{WRITTEN}{$tag} = $val;
    $self->VerboseValue( "+ $tag", $val );
    return 1;
}

sub SetFileName($$;$$$) {
    my ( $self, $file, $newName, $opt, $usedFlag ) = @_;
    my ( $nvHash, $doName, $doDir );

    $opt or $opt = '';
    unless ( defined $newName ) {
        if ($opt) {
            if ( $opt eq 'HardLink' or $opt eq 'Link' ) {
                $newName = $self->GetNewValue('HardLink');
            }
            elsif ( $opt eq 'SymLink' ) {
                $newName = $self->GetNewValue('SymLink');
            }
            elsif ( $opt eq 'Test' ) {
                $newName = $self->GetNewValue('TestName');
            }
            return 0 unless defined $newName;
        }
        else {
            my $filename = $self->GetNewValue( 'FileName', \$nvHash );
            $doName = 1
              if defined $filename and $self->IsOverwriting( $nvHash, $file );
            my $dir = $self->GetNewValue( 'Directory', \$nvHash );
            $doDir = 1
              if defined $dir and $self->IsOverwriting( $nvHash, $file );
            return 0 unless $doName or $doDir;
            if ($doName) {
                $newName = GetNewFileName( $file,    $filename );
                $newName = GetNewFileName( $newName, $dir ) if $doDir;
            }
            else {
                $newName = GetNewFileName( $file, $dir );
            }
        }
    }
    if ( $^O eq 'MSWin32' ) {
        if ( $newName =~ /[\0-\x1f<>"|*]/ ) {
            $self->Warn(
'New file name not allowed in Windows (contains reserved characters)'
            );
            return -1;
        }
        if ( $newName =~ /:/ and $newName !~ /^[A-Z]:[^:]*$/i ) {
            $self->Warn("New file name not allowed in Windows (contains ':')");
            return -1;
        }
        if ( $newName =~ /\?/ and $newName !~ m{^[\\/]{2}\?[\\/][^?]*$} ) {
            $self->Warn("New file name not allowed in Windows (contains '?')");
            return -1;
        }
        if ( $newName =~
            m{(^|[\\/])(CON|PRN|AUX|NUL|COM[1-9]|LPT[1-9])(\.[^.]*)?$}i )
        {
            $self->Warn(
                'New file name not allowed in Windows (reserved device name)');
            return -1;
        }
        if ( $newName =~ /([. ])$/ ) {
            $self->Warn(
                "New file name not recommended for Windows (ends with '${1}')",
                2
            ) and return -1;
        }
        if ( length $newName > 259 and $newName !~ /\?/ ) {
            $self->Warn(
                'New file name not recommended for Windows (exceeds 260 chars)',
                2
            ) and return -1;
        }
    }
    else {
        $newName =~ tr/\0//d;
    }
    length $newName or $self->Warn('New file name is empty'), return -1;
    if ( $self->Exists( $newName, 1 )
        and ( not defined $usedFlag or $usedFlag ) )
    {
        if ( $file ne $newName or $opt =~ /Link$/ ) {
            if ( $opt =~ /Link$/ or not $self->IsSameFile( $file, $newName ) ) {
                $self->Warn("File '${newName}' already exists");
                return -1;
            }
        }
        else {
            $self->Warn('File name is unchanged');
            return 0;
        }
    }
    if ( $opt eq 'Test' ) {
        my $out = $$self{OPTIONS}{TextOut};
        print $out "'${file}' --> '${newName}'\n";
        return 1;
    }
    my $err = $self->CreateDirectory($newName);
    if ( defined $err ) {
        if ($err) {
            $self->Warn($err) unless $err =~ /^Error creating/;
            $self->Warn("Error creating directory for '${newName}'");
            return -1;
        }
        $self->VPrint( 0, "Created directory for '${newName}'\n" );
    }
    if ( $opt eq 'HardLink' or $opt eq 'Link' ) {
        unless ( link $file, $newName ) {
            $self->Warn("Error creating hard link '${newName}'");
            return -1;
        }
        ++$$self{CHANGED};
        $self->VerboseValue( '+ HardLink', $newName );
        return 1;
    }
    elsif ( $opt eq 'SymLink' ) {
        $^O eq 'MSWin32'
          and $self->Warn('SymLink not supported in Windows'), return -1;
        $newName =~ s(^\./)();

        if ( $file !~ m(^/) and $newName =~ m(/) ) {
            unless ( eval { require Cwd } ) {
                $self->Warn(
                    'Install Cwd to make symlinks to other directories');
                return -1;
            }
            $file = eval { Cwd::abs_path($file) };
            unless ( defined $file ) {
                $self->Warn('Error in Cwd::abs_path when creating symlink');
                return -1;
            }
        }
        unless ( eval { symlink $file, $newName } ) {
            $self->Warn("Error creating symbolic link '${newName}'");
            return -1;
        }
        ++$$self{CHANGED};
        $self->VerboseValue( '+ SymLink', $newName );
        return 1;
    }
    unless ( $self->Rename( $file, $newName ) ) {
        local ( *EXIFTOOL_SFN_IN, *EXIFTOOL_SFN_OUT );
        unless ( $self->Open( \*EXIFTOOL_SFN_IN, $file ) ) {
            $self->Error("Error opening '${file}'");
            return -1;
        }
        unless ( $self->Open( \*EXIFTOOL_SFN_OUT, $newName, '>' ) ) {
            close EXIFTOOL_SFN_IN;
            $self->Error("Error creating '${newName}'");
            return -1;
        }
        binmode EXIFTOOL_SFN_IN;
        binmode EXIFTOOL_SFN_OUT;
        my ( $buff, $err );
        while ( read EXIFTOOL_SFN_IN, $buff, 65536 ) {
            print EXIFTOOL_SFN_OUT $buff or $err = 1;
        }
        close EXIFTOOL_SFN_OUT or $err = 1;
        close EXIFTOOL_SFN_IN;
        if ($err) {
            $self->Unlink($newName);
            $self->Error("Error writing '${newName}'");
            return -1;
        }
        my ( $aTime, $mTime, $cTime ) = $self->GetFileTime($file);
        $self->SetFileTime( $newName, $aTime, $mTime, $cTime );
        $self->Unlink($file) or $self->Warn('Error removing old file');
    }
    $$self{NewName} = $newName;
    ++$$self{CHANGED};
    $self->VerboseValue( '+ FileName', $newName );
    return 1;
}

sub SetSystemTags($$) {
    my ( $self, $file ) = @_;
    my $result = 0;

    my $perm = $self->GetNewValue('FilePermissions');
    if ( defined $perm ) {
        if ( eval { chmod( $perm & 07777, $file ) } ) {
            $self->VerboseValue( '+ FilePermissions', $perm );
            $result = 1;
        }
        else {
            $self->Warn('Error setting FilePermissions');
            $result = -1;
        }
    }
    my $uid = $self->GetNewValue('FileUserID');
    my $gid = $self->GetNewValue('FileGroupID');
    if ( defined $uid or defined $gid ) {
        defined $uid or $uid = -1;
        defined $gid or $gid = -1;
        if ( eval { chown( $uid, $gid, $file ) } ) {
            $self->VerboseValue( '+ FileUserID',  $uid ) if $uid >= 0;
            $self->VerboseValue( '+ FileGroupID', $gid ) if $gid >= 0;
            $result = 1;
        }
        else {
            $self->Warn('Error setting FileGroup/UserID');
            $result = -1 unless $result;
        }
    }
    my $tag;
    foreach $tag (@writableMacOSTags) {
        my $nvHash;
        my $val = $self->GetNewValue( $tag, \$nvHash );
        next unless $nvHash;
        if ( $^O eq 'darwin' ) {
            ref $file
              and $self->Warn('Setting MDItem tags requires a file name'),
              last;
            require Image::ExifTool::MacOS;
            my $res = Image::ExifTool::MacOS::SetMacOSTags( $self, $file,
                \@writableMacOSTags );
            $result = $res if $res == 1 or not $result;
            last;
        }
        elsif ( $tag ne 'FileCreateDate' ) {
            $self->Warn('Can only set MDItem tags on MacOS');
            last;
        }
    }
    my $zhash =
      $self->GetNewValueHash( $Image::ExifTool::Extra{ZoneIdentifier} );
    if ($zhash) {
        my $res = -1;
        if ( $^O ne 'MSWin32' ) {
            $self->Warn('ZoneIdentifer is a Windows-only tag');
        }
        elsif ( ref $file ) {
            $self->Warn('Writing ZoneIdentifer requires a file name');
        }
        elsif ( defined $self->GetNewValue( 'ZoneIdentifier', \$zhash ) ) {
            $self->Warn('ZoneIndentifier may only be deleted');
        }
        elsif ( not eval { require Win32API::File } ) {
            $self->Warn('Install Win32API::File to write ZoneIdentifier');
        }
        else {
            my ( $wattr, $wide );
            my $zfile = "${file}:Zone.Identifier";
            if ( $self->EncodeFileName($zfile) ) {
                $wide  = 1;
                $wattr = eval { Win32API::File::GetFileAttributesW($zfile) };
            }
            else {
                $wattr = eval { Win32API::File::GetFileAttributes($zfile) };
            }
            if ( $wattr == Win32API::File::INVALID_FILE_ATTRIBUTES() ) {
                $res = 0;
            }
            elsif ( $wattr & Win32API::File::FILE_ATTRIBUTE_READONLY() ) {
                $self->Warn('Zone.Identifier stream is read-only');
            }
            else {
                if ($wide) {
                    $res = 1 if eval { Win32API::File::DeleteFileW($zfile) };
                }
                else {
                    $res = 1 if eval { Win32API::File::DeleteFile($zfile) };
                }
                if ( $res > 0 ) {
                    $self->VPrint( 0, "  Deleting Zone.Identifier stream\n" );
                }
                else {
                    $self->Warn('Error deleting Zone.Identifier stream');
                }
            }
        }
        $result = $res if $res == 1 or not $result;
    }
    return $result;
}

sub WriteInfo($$;$$) {
    local ( $_, *EXIFTOOL_FILE2, *EXIFTOOL_OUTFILE );
    my ( $self, $infile, $outfile, $outType ) = @_;
    my ( @fileTypeList, $fileType, $tiffType, $hdr, $seekErr, $type, $tmpfile );
    my (
        $inRef,   $outRef,  $closeIn, $closeOut, $outPos,
        $outBuff, $eraseIn, $raf,     $fileExt
    );
    my ( $hardLink, $symLink, $testName );
    my $oldRaf = $$self{RAF};
    my $rtnVal = 0;

    $self->Init();
    $$self{IsWriting} = 1;

    my ( $nvHash, $nvHash2, $originalTime, $createTime );
    my $setModDate = defined $self->GetNewValue( 'FileModifyDate', \$nvHash );
    my $setCreateDate =
      defined $self->GetNewValue( 'FileCreateDate', \$nvHash2 );
    my ( $aTime, $mTime, $cTime );
    if (    $setModDate
        and $self->IsOverwriting($nvHash) < 0
        and defined $infile
        and ref $infile ne 'SCALAR' )
    {
        ( $aTime, $mTime, $cTime ) = $self->GetFileTime($infile);
        $originalTime = $mTime;
    }
    if (    $setCreateDate
        and $self->IsOverwriting($nvHash2) < 0
        and defined $infile
        and ref $infile ne 'SCALAR' )
    {
        ( $aTime, $mTime, $cTime ) = $self->GetFileTime($infile)
          unless defined $cTime;
        $createTime = $cTime;
    }
    my ( $numNew, $numPseudo ) = $self->CountNewValues();
    if ( not defined $outfile and defined $infile ) {
        $hardLink = $self->GetNewValue('HardLink');
        $symLink  = $self->GetNewValue('SymLink');
        $testName = $self->GetNewValue('TestName');
        undef $hardLink if defined $hardLink and not length $hardLink;
        undef $symLink  if defined $symLink  and not length $symLink;
        undef $testName if defined $testName and not length $testName;
        my $newFileName = $self->GetNewValue( 'FileName', \$nvHash );
        my $newDir      = $self->GetNewValue('Directory');

        if ( defined $newDir and length $newDir ) {
            $newDir .= '/' unless $newDir =~ m{/$};
        }
        else {
            undef $newDir;
        }
        if ( $numNew == $numPseudo ) {
            $rtnVal = 2;
            if ( ( defined $newFileName or defined $newDir )
                and not ref $infile )
            {
                my $result = $self->SetFileName($infile);
                if ( $result > 0 ) {
                    $infile = $$self{NewName};
                    $rtnVal = 1;
                }
                elsif ( $result < 0 ) {
                    return 0;
                }
            }
            if ( not ref $infile or UNIVERSAL::isa( $infile, 'GLOB' ) ) {
                $self->SetFileModifyDate($infile) > 0 and $rtnVal = 1
                  if $setModDate;
                $self->SetFileModifyDate( $infile, undef, 'FileCreateDate' ) >
                  0 and $rtnVal = 1
                  if $setCreateDate;
                $self->SetSystemTags($infile) > 0 and $rtnVal = 1;
            }
            if ( defined $hardLink or defined $symLink or defined $testName ) {
                $hardLink
                  and $self->SetFileName( $infile, $hardLink, 'HardLink' )
                  and $rtnVal = 1;
                $symLink
                  and $self->SetFileName( $infile, $symLink, 'SymLink' )
                  and $rtnVal = 1;
                $testName
                  and $self->SetFileName( $infile, $testName, 'Test' )
                  and $rtnVal = 1;
            }
            return $rtnVal;
        }
        elsif ( defined $newFileName and length $newFileName ) {
            if ( ref $infile ) {
                $outfile = $newFileName;
            }
            elsif ( $self->IsOverwriting( $nvHash, $infile ) ) {
                $outfile = GetNewFileName( $infile, $newFileName );
                $eraseIn = 1;
            }
        }
        if ( defined $newDir ) {
            $outfile = $infile unless defined $outfile or ref $infile;
            if ( defined $outfile ) {
                $outfile = GetNewFileName( $outfile, $newDir );
                $eraseIn = 1 unless ref $infile;
            }
        }
    }
    if ( ref $infile ) {
        $inRef = $infile;
        if ( UNIVERSAL::isa( $inRef, 'GLOB' ) ) {
            seek( $inRef, 0, 0 );
        }
        elsif ( UNIVERSAL::isa( $inRef, 'File::RandomAccess' ) ) {
            $inRef->Seek(0);
            $raf = $inRef;
        }
        elsif (
            $] >= 5.006
            and (  $$self{OPTIONS}{EncodeHangs}
                or eval { require Encode; Encode::is_utf8($$inRef) }
                or $@ )
          )
        {
            local $SIG{'__WARN__'} = \&SetWarning;
            my $buff =
              ( $$self{OPTIONS}{EncodeHangs} or $@ )
              ? pack( 'C*', unpack( $] < 5.010000 ? 'U0C*' : 'C0C*', $$inRef ) )
              : Encode::encode( 'utf8', $$inRef );
            if ( defined $outfile ) {
                $inRef = \$buff;
            }
            else {
                $$inRef = $buff;
            }
        }
    }
    elsif ( defined $infile and $infile ne '' ) {
        $outfile = $tmpfile = "${infile}_exiftool_tmp" unless defined $outfile;
        if ( $self->Open( \*EXIFTOOL_FILE2, $infile ) ) {
            $fileExt      = GetFileExtension($infile);
            $fileType     = GetFileType($infile);
            @fileTypeList = GetFileType($infile);
            $tiffType     = $$self{FILE_EXT} = GetFileExtension($infile);
            $self->VPrint( 0, "Rewriting $infile...\n" );
            $inRef   = \*EXIFTOOL_FILE2;
            $closeIn = 1;
        }
        else {
            $self->Error('Error opening file');
            return 0;
        }
    }
    elsif ( not defined $outfile ) {
        $self->Error("WriteInfo(): Must specify infile or outfile\n");
        return 0;
    }
    else {
        $outType = GetFileExtension($outfile) unless $outType or ref $outfile;
        if ( CanCreate($outType) ) {
            if ( $$self{OPTIONS}{WriteMode} =~ /g/i ) {
                $fileType = $tiffType = $outType;
                $infile   = "$fileType file";
                $self->VPrint( 0, "Creating $infile...\n" );
                $inRef = \ '';
            }
            else {
                $self->Error(
                    "Not creating new $outType file (disallowed by WriteMode)");
                return 0;
            }
        }
        elsif ($outType) {
            $self->Error("Can't create $outType files");
            return 0;
        }
        else {
            $self->Error("Can't create file (unknown type)");
            return 0;
        }
    }
    unless (@fileTypeList) {
        if ($fileType) {
            @fileTypeList = ($fileType);
        }
        else {
            @fileTypeList = @fileTypes;
            $tiffType     = 'TIFF';
        }
    }
    if ( ref $outfile ) {
        $outRef = $outfile;
        if ( UNIVERSAL::isa( $outRef, 'GLOB' ) ) {
            binmode($outRef);
            $outPos = tell($outRef);
        }
        else {
            defined $$outRef or $$outRef = '';
            $outPos = length($$outRef);
        }
    }
    elsif ( not defined $outfile ) {
        if ($raf) {
            $self->Error("Can't edit File::RandomAccess object in place");
            return 0;
        }
        $outBuff = '';
        $outRef  = \$outBuff;
        $outPos  = 0;
    }
    elsif ( $self->Exists( $outfile, 1 ) ) {
        $self->Error("File already exists: $outfile");
    }
    elsif ( $self->Open( \*EXIFTOOL_OUTFILE, $outfile, '>' ) ) {
        $outRef   = \*EXIFTOOL_OUTFILE;
        $closeOut = 1;
        binmode($outRef);
        $outPos = 0;
    }
    else {
        my $tmp = $tmpfile ? ' temporary' : '';
        $self->Error("Error creating$tmp file: $outfile");
    }
    until ( $$self{VALUE}{Error} ) {
        $raf or $raf = File::RandomAccess->new( $inRef, 1 );
        $raf->BinMode();
        if ( $numNew == $numPseudo ) {
            $rtnVal = 1;
            my $buff;
            while ( $raf->Read( $buff, 65536 ) ) {
                Write( $outRef, $buff ) or $rtnVal = -1, last;
            }
            last;
        }
        elsif ( not ref $infile and ( $infile eq '-' or $infile =~ /\|$/ ) ) {
            $$raf{TESTED} = -1;
        }
        else {
            $raf->SeekTest();
        }
        my $inPos = $raf->Tell();
        $$self{RAF} = $raf;
        my %dirInfo = (
            RAF     => $raf,
            OutFile => $outRef,
        );
        $raf->Read( $hdr, 1024 ) or $hdr = '';
        $raf->Seek( $inPos, 0 ) or $seekErr = 1;
        my $wrongType;
        until ($seekErr) {
            $type = shift @fileTypeList;
            if (    $magicNumber{$type}
                and length($hdr)
                and $hdr !~ /^$magicNumber{$type}/s )
            {
                next if @fileTypeList;
                $wrongType = 1;
                last;
            }
            $dirInfo{Parent} = $$self{FILE_TYPE} = $$self{PATH}[0] = $type;
            $self->InitWriteDirs($type);
            if ( $type eq 'JPEG' or $type eq 'EXV' ) {
                $rtnVal = $self->WriteJPEG( \%dirInfo );
            }
            elsif ( $type eq 'TIFF' ) {
                if ( grep /^$tiffType$/, @{ $noWriteFile{TIFF} } ) {
                    $fileType = $tiffType;
                    undef $rtnVal;
                }
                else {
                    if ( $tiffType eq 'FFF' ) {
                        $self->Error(
'Phocus may not properly update previews of edited FFF images',
                            1
                        );
                    }
                    $dirInfo{Parent} = $tiffType;
                    $rtnVal = $self->ProcessTIFF( \%dirInfo );
                }
            }
            elsif ( exists $writableType{$type} ) {
                my ( $module, $func );
                if ( ref $writableType{$type} eq 'ARRAY' ) {
                    $module = $writableType{$type}[0] || $type;
                    $func   = $writableType{$type}[1];
                }
                else {
                    $module = $writableType{$type} || $type;
                }
                require "Image/ExifTool/$module.pm";
                $func =
                  "Image::ExifTool::${module}::" . ( $func || "Process$type" );
                no strict 'refs';
                $rtnVal = &$func( $self, \%dirInfo );
                use strict 'refs';
            }
            elsif ( $type eq 'ORF' or $type eq 'RAW' ) {
                $rtnVal = $self->ProcessTIFF( \%dirInfo );
            }
            elsif ( $type eq 'EXIF' ) {
                my $tagTablePtr = GetTagTable('Image::ExifTool::Exif::Main');
                my $buff =
                  $self->WriteDirectory( \%dirInfo, $tagTablePtr, \&WriteTIFF );
                if ( defined $buff ) {
                    $rtnVal = Write( $outRef, $buff ) ? 1 : -1;
                }
                else {
                    $rtnVal = 0;
                }
            }
            else {
                undef $rtnVal;
            }
            last if $rtnVal;
            last unless @fileTypeList;
            $raf->Seek( $inPos, 0 ) or $seekErr = 1, last;
            if ( UNIVERSAL::isa( $outRef, 'GLOB' ) ) {
                seek( $outRef, 0, $outPos );
            }
            else {
                $$outRef = substr( $$outRef, 0, $outPos );
            }
        }
        unless ($rtnVal) {
            my $err;
            if ($seekErr) {
                $err = 'Error seeking in file';
            }
            elsif ( $fileType and defined $rtnVal ) {
                if ( $$self{VALUE}{Error} ) {
                }
                elsif ( $fileType eq 'RAW' ) {
                    $err = 'Writing this type of RAW file is not supported';
                }
                else {
                    if ($wrongType) {
                        my $type = $fileExt
                          || ( $fileType eq 'TIFF' ? $tiffType : $fileType );
                        $err = "Not a valid $type";
                        foreach $type (@fileTypes) {
                            next unless $magicNumber{$type};
                            next unless $hdr =~ /^$magicNumber{$type}/s;
                            $err .= " (looks more like a $type)";
                            last;
                        }
                    }
                    else {
                        $err = 'Format error in file';
                    }
                }
            }
            elsif ($fileType) {
                $fileType = GetFileExtension($infile)
                  if $infile and GetFileType($infile);
                $err = "Writing of $fileType files is not yet supported";
            }
            else {
                $err = 'Writing of this type of file is not supported';
            }
            $self->Error($err) if $err;
            $rtnVal = 0;
        }
        last;
    }
    if ( $rtnVal > 0 ) {
        if ( $outType and $type and $outType ne $type ) {
            my @types = GetFileType($outType);
            unless ( grep /^$type$/, @types ) {
                $self->Error("Can't create $outType file from $type");
                $rtnVal = 0;
            }
        }
        if ( $rtnVal > 0 and not Tell($outRef) and not $$self{VALUE}{Error} ) {
            if ( defined $hdr and length $hdr ) {
                $type = '<unk>' unless defined $type;
                $self->Error(
                    "Can't delete all meta information from $type file");
            }
            else {
                $self->Error('Nothing to write');
            }
        }
        $rtnVal = 0 if $$self{VALUE}{Error};
    }

    if ( defined $outBuff ) {
        if ( $rtnVal <= 0 or not $$self{CHANGED} ) {
        }
        elsif ( UNIVERSAL::isa( $inRef, 'GLOB' ) ) {
            my $len = length($outBuff);
            my $size;
            $rtnVal = -1
              unless seek( $inRef, 0, 2 )
              and ( $size = tell $inRef ) >= 0
              and seek( $inRef, 0, 0 )
              and print $inRef $outBuff
              and ( $len >= $size
                or eval { truncate( $inRef, $len ) } );
        }
        else {
            $$inRef = $outBuff;
        }
        $outBuff = '';
    }
    if ($closeIn) {
        $rtnVal and $rtnVal = -1 unless close($inRef) or not defined $outBuff;
        if ( $rtnVal > 0 ) {
            if ( $^O eq 'darwin' and -s "$infile/..namedfork/rsrc" ) {
                if ( $$self{DEL_GROUP}{RSRC} ) {
                    $self->VPrint( 0, "Deleting Mac OS resource fork\n" );
                    ++$$self{CHANGED};
                }
                else {
                    $self->VPrint( 0, "Copying Mac OS resource fork\n" );
                    my ( $buf, $err );
                    local ( *SRC, *DST );
                    if ( $self->Open( \*SRC, "$infile/..namedfork/rsrc" ) ) {
                        if (
                            $self->Open(
                                \*DST, "$outfile/..namedfork/rsrc", '>'
                            )
                          )
                        {
                            binmode SRC;
                            binmode DST;
                            while ( read SRC, $buf, 65536 ) {
                                print DST $buf or $err = 'copying', last;
                            }
                            close DST or $err or $err = 'closing';
                        }
                        else {
                            $self->Warn('Error creating Mac OS resource fork');
                        }
                        close SRC;
                    }
                    else {
                        $err = 'opening';
                    }
                    $rtnVal = 0
                      if $err
                      and $self->Error( "Error $err Mac OS resource fork", 2 );
                }
            }
            $self->Unlink($infile)
              or $self->Warn('Error erasing original file')
              if $eraseIn;
        }
    }
    if ($closeOut) {
        $rtnVal and $rtnVal = -1 unless close($outRef);
        if ( $rtnVal <= 0 ) {
            $self->Unlink($outfile);
        }
        elsif ($tmpfile) {
            $self->CopyFileAttrs( $infile, $tmpfile );
            unless ( $self->Rename( $tmpfile, $infile ) ) {
                if ( not $self->Unlink($infile) ) {
                    $self->Unlink($tmpfile);
                    $self->Error('Error renaming temporary file');
                    $rtnVal = 0;
                }
                elsif ( not $self->Rename( $tmpfile, $infile ) ) {
                    $self->Error(
                        'Error renaming temporary file after deleting original'
                    );
                    $rtnVal = 0;
                }
            }
            $outfile = $infile if $rtnVal > 0;
        }
    }
    if (
        $rtnVal > 0
        and (
            $closeOut
            or ( defined $outBuff
                and ( $closeIn or UNIVERSAL::isa( $infile, 'GLOB' ) ) )
        )
      )
    {
        my $target = $closeOut ? $outfile : $infile;
        ++$$self{CHANGED} if $self->SetSystemTags($target) > 0;
        if ($closeIn) {
            ++$$self{CHANGED}
              if $setModDate
              and $self->SetFileModifyDate( $target, $originalTime, undef, 1 )
              > 0;
            ++$$self{CHANGED}
              if $setCreateDate
              and
              $self->SetFileModifyDate( $target, $createTime, 'FileCreateDate',
                1 ) > 0;
            ++$$self{CHANGED}
              if defined $hardLink
              and $self->SetFileName( $target, $hardLink, 'HardLink' );
            ++$$self{CHANGED}
              if defined $symLink
              and $self->SetFileName( $target, $symLink, 'SymLink' );
            defined $testName
              and $self->SetFileName( $target, $testName, 'Test' );
        }
    }
    if ( $rtnVal < 0 ) {
        $self->Error('Error writing output file') unless $$self{VALUE}{Error};
        $rtnVal = 0;
    }
    elsif ( $rtnVal > 0 ) {
        ++$rtnVal unless $$self{CHANGED};
    }
    $$self{RAF} = $oldRaf;

    return $rtnVal;
}

sub GetAllTags(;$) {
    local $_;
    my $group = shift;
    my ( %allTags, @groups );
    @groups = split ':', $group if $group;

    my $et = Image::ExifTool->new;
    LoadAllTables();
    my @tableNames = keys %allTables;

    while (@tableNames) {
        my $table = GetTagTable( pop @tableNames );
        if ( $$table{GROUPS} and $$table{GROUPS}{0} eq 'XMP' ) {
            Image::ExifTool::XMP::AddFlattenedTags($table);
        }
        my $tagID;
        foreach $tagID ( TagTableKeys($table) ) {
            my @infoArray = GetTagInfoList( $table, $tagID );
            my $tagInfo;
          GATInfo: foreach $tagInfo (@infoArray) {
                my $tag = $$tagInfo{Name};
                $tag or warn("no name for tag!\n"), next;
                next if $$tagInfo{SubDirectory} and not $$tagInfo{Writable};
                next if $$tagInfo{Hidden};
                if (@groups) {
                    my @tg = $et->GetGroup($tagInfo);
                    foreach $group (@groups) {
                        next GATInfo unless grep /^$group$/i, @tg;
                    }
                }
                $allTags{$tag} = 1;
            }
        }
    }
    return sort keys %allTags;
}

sub GetWritableTags(;$) {
    local $_;
    my $group = shift;
    my ( %writableTags, @groups );
    @groups = split ':', $group if $group;

    my $et = Image::ExifTool->new;
    LoadAllTables();
    my @tableNames = keys %allTables;

    while (@tableNames) {
        my $tableName = pop @tableNames;
        my $table     = GetTagTable($tableName);
        if ( $$table{GROUPS} and $$table{GROUPS}{0} eq 'XMP' ) {
            Image::ExifTool::XMP::AddFlattenedTags($table);
        }
        my @parts = split( /::/, $tableName );
        if ( @parts > 3 ) {
            my $i = $#parts - 1;
            $parts[$i] = "Write$parts[$i]";
            my $module = join( '::', @parts[ 0 .. $i ] );
            eval { require $module };
        }
        my $tagID;
        foreach $tagID ( TagTableKeys($table) ) {
            my @infoArray = GetTagInfoList( $table, $tagID );
            my $tagInfo;
          GWTInfo: foreach $tagInfo (@infoArray) {
                my $tag = $$tagInfo{Name};
                $tag or warn("no name for tag!\n"), next;
                my $writable = $$tagInfo{Writable};
                next
                  unless $writable
                  or (  $$table{WRITABLE}
                    and not defined $writable
                    and not $$tagInfo{SubDirectory} );
                next if $$tagInfo{Hidden};
                if (@groups) {
                    my @tg = $et->GetGroup($tagInfo);
                    foreach $group (@groups) {
                        next GWTInfo unless grep /^$group$/i, @tg;
                    }
                }
                $writableTags{$tag} = 1;
            }
        }
    }
    return sort keys %writableTags;
}

sub GetAllGroups($;$) {
    local $_;
    my $family = shift || 0;
    my $self;
    ref $family and $self = $family, $family = shift || 0;

    $family == 3 and return ( 'Doc#', 'Main' );
    $family == 4 and return ('Copy#');
    $family == 5 and return ('[too many possibilities to list]');
    if ( $family == 6 ) {
        my $fn = \%Image::ExifTool::Exif::formatNumber;
        return ( sort { $$fn{$a} <=> $$fn{$b} } keys %$fn );
    }
    $family == 8 and return ('File#');

    LoadAllTables();

    my @tableNames = keys %allTables;

    my %allGroups;
    no warnings;

    $family == 1
      and map { $allGroups{$_} = 1 }
      qw(Garmin AudioItemList AudioUserData
      VideoItemList VideoUserData Track#Keys Track#ItemList Track#UserData KFIX);
    use warnings;
    while (@tableNames) {
        my $table = GetTagTable( pop @tableNames );
        my ( $grps, $grp, $tag, $tagInfo );
        $allGroups{$grp} = 1
          if ( $grps = $$table{GROUPS} )
          and ( $grp = $$grps{$family} );
        foreach $tag ( TagTableKeys($table) ) {
            my @infoArray = GetTagInfoList( $table, $tag );
            if ( $family == 7 ) {
                foreach $tagInfo (@infoArray) {
                    my $id = $$tagInfo{TagID};
                    if ( not defined $id ) {
                        $id = '';
                    }
                    elsif ( $id =~ /^\d+$/ ) {
                        $id = sprintf( '0x%x', $id )
                          if $self and $$self{OPTIONS}{HexTagIDs};
                    }
                    else {
                        $id =~ s/([^-_A-Za-z0-9])/sprintf('%.2x',ord $1)/ge;
                    }
                    $allGroups{ 'ID-' . $id } = 1;
                }
            }
            else {
                foreach $tagInfo (@infoArray) {
                    next
                      unless ( $grps = $$tagInfo{Groups} )
                      and ( $grp = $$grps{$family} );
                    $allGroups{$grp} = 1;
                }
            }
        }
    }
    delete $allGroups{'*'};
    return sort { lc $a cmp lc $b } keys %allGroups;
}

sub GetNewGroups($) {
    my $self = shift;
    return @{ $$self{WRITE_GROUPS} };
}

sub GetDeleteGroups() {
    return sort { lc $a cmp lc $b } @delGroups, @delGroup2;
}

sub AddUserDefinedTags($%) {
    local $_;
    my ( $tableName, %addTags ) = @_;
    my $table = GetTagTable($tableName) or return 0;
    Image::ExifTool::TagLookup::AddTags( \%addTags, $tableName );
    my $tagID;
    my $num = 0;
    foreach $tagID ( keys %addTags ) {
        next if $specialTags{$tagID};
        delete $$table{$tagID};
        AddTagToTable( $table, $tagID, $addTags{$tagID}, 1 );
        ++$num;
    }
    return $num;
}

sub GetNewValues($$;$) {
    my ( $self, $tag, $nvHashPt ) = @_;
    return $self->GetNewValue( $tag, $nvHashPt );
}

sub Sanitize($$) {
    my ( $self, $valPt ) = @_;
    $$valPt = $$$valPt if ref $$valPt eq 'SCALAR';
    if (
        $] >= 5.006
        and (  $$self{OPTIONS}{EncodeHangs}
            or eval { require Encode; Encode::is_utf8($$valPt) }
            or $@ )
      )
    {
        local $SIG{'__WARN__'} = \&SetWarning;
        $$valPt =
          ( $$self{OPTIONS}{EncodeHangs} or $@ )
          ? pack( 'C*', unpack( $] < 5.010000 ? 'U0C*' : 'C0C*', $$valPt ) )
          : Encode::encode( 'utf8', $$valPt );
    }
    if ( $$self{OPTIONS}{Escape} ) {
        if ( $$self{OPTIONS}{Escape} eq 'XML' ) {
            $$valPt = Image::ExifTool::XMP::UnescapeXML($$valPt);
        }
        elsif ( $$self{OPTIONS}{Escape} eq 'HTML' ) {
            $$valPt = Image::ExifTool::HTML::UnescapeHTML( $$valPt,
                $$self{OPTIONS}{Charset} );
        }
    }
}

sub ConvInv($$$$$;$$) {
    my ( $self, $val, $tagInfo, $tag, $wgrp1, $convType, $wantGroup ) = @_;
    my ( $err, $type );

    $convType or $convType = $$self{ConvType} || 'PrintConv';

  Conv: for ( ; ; ) {
        if ( not defined $type ) {
            if ( $$tagInfo{List} ) {
                my $listSplit =
                  $$tagInfo{AutoSplit} || $$self{OPTIONS}{ListSplit};
                if (    defined $listSplit
                    and not $$tagInfo{Struct}
                    and ( $wantGroup or not defined $wantGroup ) )
                {
                    $listSplit = ',?\s+'
                      if $listSplit eq '1' and $$tagInfo{AutoSplit};
                    my @splitVal = split /$listSplit/, $val, -1;
                    $val =
                        @splitVal > 1 ? \@splitVal
                      : @splitVal     ? $splitVal[0]
                      :                 '';
                }
            }
            $type = $convType;
        }
        elsif ( $type eq 'PrintConv' ) {
            $type = 'ValueConv';
        }
        else {
            if ( $$tagInfo{RawJoin} and $$tagInfo{List} and not ref $val ) {
                my @splitVal = split ' ', $val;
                $val = \@splitVal if @splitVal > 1;
            }
            my ( $err2, $v );
            if ( $$tagInfo{WriteCheck} ) {
                $err2 = eval $$tagInfo{WriteCheck};
                $@ and warn($@), $err2 = 'Error evaluating WriteCheck';
            }
            unless ( defined $err2 ) {
                my $table = $$tagInfo{Table};
                if (    $table
                    and $$table{CHECK_PROC}
                    and not $$tagInfo{RawConvInv} )
                {
                    my $checkProc = $$table{CHECK_PROC};
                    if ( ref $val eq 'ARRAY' ) {
                        foreach $v (@$val) {
                            $err2 =
                              &$checkProc( $self, $tagInfo, \$v, $convType );
                            last if $err2;
                        }
                    }
                    else {
                        $err2 =
                          &$checkProc( $self, $tagInfo, \$val, $convType );
                    }
                }
            }
            if ( defined $err2 ) {
                if ($err2) {
                    $err = "$err2 for $wgrp1:$tag";
                    $self->VPrint( 2, "$err\n" );
                    undef $val;
                }
                else {
                    $err = $err2;
                }
            }
            last;
        }
        my $conv    = $$tagInfo{$type};
        my $convInv = $$tagInfo{"${type}Inv"};
        next unless defined $conv or defined $convInv;

        my ( @valList, $index, $convList, $convInvList );
        if ( ref $val eq 'ARRAY' ) {
            @valList = @$val;
            $val     = $valList[ $index = 0 ];
        }
        elsif ( ref $conv eq 'ARRAY' or ref $convInv eq 'ARRAY' ) {
            @valList = split /$listSep{$type}/, $val;
            $val     = $valList[ $index = 0 ];
            if ( ref $conv eq 'ARRAY' ) {
                $convList = $conv;
                $conv     = $$conv[0];
            }
            if ( ref $convInv eq 'ARRAY' ) {
                $convInvList = $convInv;
                $convInv     = $$convInv[0];
            }
        }
        for ( ; ; ) {
            if ($convInv) {
                local $SIG{'__WARN__'} = \&SetWarning;
                undef $evalWarning;
                if ( ref($convInv) eq 'CODE' ) {
                    $val = &$convInv( $val, $self );
                }
                else {
                    $val = eval $convInv;
                    $@ and $evalWarning = $@;
                }
                if ($evalWarning) {
                    if ( $evalWarning eq "\n" ) {
                        $err = '' unless defined $err;
                    }
                    else {
                        $err = CleanWarning() . " in $wgrp1:$tag (${type}Inv)";
                        $self->VPrint( 2, "$err\n" );
                    }
                    undef $val;
                    last Conv;
                }
                elsif ( not defined $val ) {
                    $err =
                      "Error converting value for $wgrp1:$tag (${type}Inv)";
                    $self->VPrint( 2, "$err\n" );
                    last Conv;
                }
            }
            elsif ($conv) {
                if ( ref $conv eq 'HASH'
                    and ( not exists $$tagInfo{"${type}Inv"} or $convInvList ) )
                {
                    my ( $multi, $lc );
                    if (    $$self{CUR_LANG}
                        and $type eq 'PrintConv'
                        and ref( $lc = $$self{CUR_LANG}{$tag} ) eq 'HASH'
                        and ( $lc = $$lc{PrintConv} ) )
                    {
                        my %newConv;
                        foreach ( keys %$conv ) {
                            my $val = $$conv{$_};
                            defined $$lc{$val} or $newConv{$_} = $val, next;
                            $newConv{$_} = $self->Decode( $$lc{$val}, 'UTF8' );
                        }
                        if ( $$conv{BITMASK} ) {
                            foreach ( keys %{ $$conv{BITMASK} } ) {
                                my $val = $$conv{BITMASK}{$_};
                                defined $$lc{$val}
                                  or $newConv{BITMASK}{$_} = $val, next;
                                $newConv{BITMASK}{$_} =
                                  $self->Decode( $$lc{$val}, 'UTF8' );
                            }
                        }
                        $conv = \%newConv;
                    }
                    undef $evalWarning;
                    if ( $$conv{BITMASK} ) {
                        my $lookupBits = $$conv{BITMASK};
                        my ( $wbits, $tbits ) =
                          @$tagInfo{ 'BitsPerWord', 'BitsTotal' };
                        my ( $val2, $err2 ) =
                          EncodeBits( $val, $lookupBits, $wbits, $tbits );
                        if ($err2) {
                            ( $val, $multi ) = ReverseLookup( $val, $conv );
                            unless ( defined $val ) {
                                $err = "Can't encode $wgrp1:$tag ($err2)";
                                $self->VPrint( 2, "$err\n" );
                                last Conv;
                            }
                        }
                        elsif ( defined $val2 ) {
                            $val = $val2;
                        }
                        else {
                            delete $$conv{BITMASK};
                            ( $val, $multi ) = ReverseLookup( $val, $conv );
                            $$conv{BITMASK} = $lookupBits;
                        }
                    }
                    else {
                        ( $val, $multi ) = ReverseLookup( $val, $conv );
                    }
                    if ( not defined $val ) {
                        my $prob =
                          $evalWarning
                          ? lcfirst CleanWarning()
                          : ( $multi ? 'matches more than one ' : 'not in ' )
                          . $type;
                        $err = "Can't convert $wgrp1:$tag ($prob)";
                        $self->VPrint( 2, "$err\n" );
                        last Conv;
                    }
                    elsif ($evalWarning) {
                        $self->VPrint( 2,
                            CleanWarning() . " for $wgrp1:$tag\n" );
                    }
                }
                elsif ( not $$tagInfo{WriteAlso} ) {
                    $err =
                      "Can't convert value for $wgrp1:$tag (no ${type}Inv)";
                    $self->VPrint( 2, "$err\n" );
                    undef $val;
                    last Conv;
                }
            }
            last unless @valList;
            $valList[$index] = $val;
            if ( ++$index >= @valList ) {
                $val = $$tagInfo{List} ? \@valList : join ' ', @valList;
                last;
            }
            $conv    = $$convList[$index]    if $convList;
            $convInv = $$convInvList[$index] if $convInvList;
            $val     = $valList[$index];
        }
    }

    return ( $val, $err );
}

sub PushValue($$$;$) {
    local $_;
    my ( $self, $val, $list, $missing ) = @_;
    if ( ref $val eq 'ARRAY' and ref $$val[0] ne 'HASH' ) {
        $self->PushValue( $_, $list, $missing ) foreach @$val;
    }
    elsif ( ref $val eq 'SCALAR' ) {
        if ( $$self{OPTIONS}{Binary} or $$val =~ /^Binary data/ ) {
            push @$list, $$val;
        }
        else {
            push @$list, 'Binary data ' . length($$val) . ' bytes';
        }
    }
    elsif ( ref $val eq 'HASH' or ref $val eq 'ARRAY' ) {
        require 'Image/ExifTool/XMPStruct.pl';
        push @$list, Image::ExifTool::XMP::SerializeStruct( $self, $val );
    }
    elsif ( not defined $val ) {
        my $mval = $$self{OPTIONS}{MissingTagValue};
        push @$list, $mval if $missing and defined $mval;
    }
    else {
        push @$list, $val;
    }
}

sub InsertTagValues($$;$$$$) {
    local $_;
    my ( $self, $line, $foundTags, $opt, $docGrp, $cache ) = @_;
    my $rtnStr = '';
    my ( $docNum, $tag );

    if ($docGrp) {
        $docNum = $docGrp =~ /(\d+(-\d+)*)$/ ? $1 : 0;
    }
    else {
        undef $cache;
    }
    $foundTags or $foundTags = $$self{FOUND_TAGS} || [];
    while ( $line =~ s/(.*?)\$(\{\s*)?([-\w]*\w|\$|\/)//s ) {
        my ( $pre, $bra, $var ) = ( $1, $2, $3 );
        my ( @tags, $tg, $val, @val, $type, $expr, $didExpr, $level, $asList );
        if ( $var eq '$' or $var eq '/' ) {
            $line =~ s/^\s*\}// if $bra;
            if ( $var eq '/' ) {
                $var = "\n";
            }
            elsif ( $line =~ /^self\b/ and not $rtnStr =~ /\$$/ ) {
                $var = '$$';
            }
            $rtnStr .= "$pre$var";
            next;
        }
        while ( $line =~ /^:([-\w]*\w)(.*)/s ) {
            my $group = $var;
            ( $var, $line ) = ( $1, $2 );
            $var = "$group:$var";
        }
        $type = 'ValueConv' if $line =~ s/^#//;
        if ( $bra and $line =~ s/^\@(#)?// ) {
            $asList = 1;
            $type   = 'ValueConv' if $1;
        }
        if ( $bra and $line !~ s/^\s*\}// and $line =~ s/^\s*;\s*(.*?)\s*\}//s )
        {
            my $part = $1;
            $expr = '';
            for ( $level = 0 ; ; --$level ) {
                ++$level while $part =~ /\{/g;
                $expr .= $part;
                last unless $level and $line =~ s/^(.*?)\s*\}//s;
                $part = $1;
                $expr .= '}';
            }
            $expr = 'tr(/\\\\?*:|"<>\\0)()d' unless length $expr;
        }
        push @tags, $var;
        ExpandShortcuts( \@tags );
        @tags or $rtnStr .= $pre, next;
        $$self{FMT_EXPR} = $expr;

        for ( ; ; ) {
            my $oldListJoin;
            $oldListJoin = $self->Options( ListJoin => undef ) if $asList;
            $tag         = shift @tags;
            my $lcTag = lc $tag;
            if ( $cache and $lcTag !~ /(^|:)all$/ ) {
                my $group;
                $tag =~ s/^(.*):// and $group = $1;
                my $cacheTag = $$cache{$lcTag};
                unless ($cacheTag) {
                    $cacheTag = $$cache{$lcTag} = {};
                    my $ex      = $$self{TAG_EXTRA};
                    my @matches = grep /^$tag(\s|$)/i, @$foundTags;
                    @matches = $self->GroupMatches( $group, \@matches )
                      if defined $group;
                    foreach (@matches) {
                        my $doc = $$ex{$_}{G3} || 0;
                        if ( defined $$cacheTag{$doc} ) {
                            next unless $$cacheTag{$doc} =~ / \((\d+)\)$/;
                            my $cur = $1;
                            next if / \((\d+)\)$/ and $1 < $cur;
                        }
                        $$cacheTag{$doc} = $_;
                    }
                }
                my $doc =
                  $lcTag =~ /\b(main|doc(\d+(-\d+)*)):/ ? ( $2 || 0 ) : $docNum;
                if ( $$cacheTag{$doc} ) {
                    $tag = $$cacheTag{$doc};
                    $val = $self->GetValue( $tag, $type );
                }
            }
            else {
                if ( $docGrp and $lcTag !~ /\b(main|doc\d+):/ ) {
                    $tag   = $docGrp . ':' . $tag;
                    $lcTag = lc $tag;
                }
                my ( $et, $fileTags ) = ( $self, $foundTags );
                if ( $tag =~ s/(\bfile\d+)://i ) {
                    $et = $$self{ALT_EXIFTOOL}{ ucfirst lc $1 };
                    if ($et) {
                        $fileTags = $$et{FoundTags};
                    }
                    else {
                        $et  = $self;
                        $tag = 'no_alt_file';
                    }
                }
                if ( $lcTag eq 'all' ) {
                    $val = 1;
                }
                elsif ( defined $$et{OPTIONS}{UserParam}{$lcTag} ) {
                    $val = $$et{OPTIONS}{UserParam}{$lcTag};
                }
                elsif ( $tag =~ /(.*):(.+)/ ) {
                    my ( $group, @matches );
                    ( $group, $tag ) = ( $1, $2 );
                    if ( $group =~ s/(^|:)(all|\*)(:|$)/$1 and $3/ei ) {
                        if ( lc $tag eq 'all' ) {
                            @matches =
                                $group
                              ? $et->GroupMatches( $group, $fileTags )
                              : @$fileTags;
                        }
                        else {
                            @matches = grep /^$tag(\s|$)/i, @$fileTags;
                            @matches = $et->GroupMatches( $group, \@matches )
                              if $group;
                        }
                        $self->PushValue( scalar $et->GetValue( $_, $type ),
                            \@val )
                          foreach @matches;
                    }
                    elsif ( lc $tag eq 'all' ) {
                        $val = $et->GroupMatches( $group, $fileTags ) ? 1 : 0;
                    }
                    else {
                        @matches = grep /^$tag(\s|$)/i, @$fileTags;
                        @matches = $et->GroupMatches( $group, \@matches );
                        foreach $tg (@matches) {
                            if ( defined $val and $tg =~ / \((\d+)\)$/ ) {
                                my $tagNum = $1;
                                next if $tag !~ / \((\d+)\)$/ or $1 > $tagNum;
                            }
                            $val = $et->GetValue( $tg, $type );
                            $tag = $tg;
                            last unless $tag =~ / /;
                        }
                    }
                }
                elsif ( $tag eq 'self' ) {
                    $val = $et;
                }
                else {
                    $val = $et->GetValue( $tag, $type );
                    unless ( defined $val ) {
                        ($tg) = grep /^$tag$/i, @$fileTags;
                        if ( defined $tg ) {
                            $val = $et->GetValue( $tg, $type );
                            $tag = $tg;
                        }
                    }
                }
            }
            $self->Options( ListJoin => $oldListJoin ) if $asList;
            $self->PushValue( $val, \@val, $asList );
            undef $val;
            last unless @tags;
        }
        if (@val) {
            $self->PushValue( $val, \@val ) if defined $val;
            $val = join $$self{OPTIONS}{ListSep}, @val;
        }
        elsif ( defined $val ) {
            $self->PushValue( $val, \@val );
        }
        if ( defined $expr and defined $val ) {
            local $SIG{'__WARN__'} = \&SetWarning;
            undef $evalWarning;
            $advFmtSelf = $self;
            if ($asList) {
                foreach (@val) {
                    eval $expr;
                    $@ and $evalWarning = $@;
                }
                @val = grep defined, @val;
                $val = @val ? join $$self{OPTIONS}{ListSep}, @val : undef;
            }
            else {
                $_ = $val;
                eval $expr;
                $@ and $evalWarning = $@;
                $val =
                  ref $_ eq 'ARRAY'
                  ? join( $$self{OPTIONS}{ListSep}, @$_ )
                  : $_;
            }
            if ($evalWarning) {
                my $g3 =
                  ( $docGrp and $var !~ /\b(main|doc\d+):/i )
                  ? $docGrp . ':'
                  : '';
                my $str = CleanWarning() . " for '$g3${var}'";
                if ($opt) {
                    if ( $opt eq 'Error' ) {
                        $self->Error($str);
                    }
                    elsif ( $opt ne 'Silent' ) {
                        $self->Warn($str);
                    }
                }
            }
            undef $advFmtSelf;
            $didExpr = 1;
        }
        unless ( defined $val or ( ref $opt and $$self{OPTIONS}{UndefTags} ) ) {
            $val = $$self{OPTIONS}{MissingTagValue};
            unless ( defined $val ) {
                my $g3 =
                  ( $docGrp and $var !~ /\b(main|doc\d+):/i )
                  ? $docGrp . ':'
                  : '';
                my $msg =
                  $didExpr
                  ? "Advanced formatting expression returned undef for '$g3${var}'"
                  : "Tag '$g3${var}' not defined";
                if ( ref $opt ) {
                    $val = '' if $$self{OPTIONS}{IgnoreMinorErrors};
                }
                elsif ($opt) {
                    no strict 'refs';
                    ( $opt eq 'Silent' or &$opt( $self, $msg, 2 ) )
                      and return $$self{FMT_EXPR} = undef;
                    $val = '';
                }
            }
        }
        if ( ref $opt eq 'HASH' ) {
            $var .= '#' if $type;
            if ( defined $expr ) {
                my $i = 1;
                ++$i while exists $$opt{"$var.expr$i"};
                $var .= '.expr' . $i;
            }
            $rtnStr .= "$pre\$info{'${var}'}";
            $$opt{$var} = $val;
        }
        else {
            $rtnStr .= "$pre$val";
        }
    }
    $$self{FMT_EXPR} = undef;
    return $rtnStr . $line;
}

sub DateFmt($) {
    my $et = bless { OPTIONS => { DateFormat => shift, StrictDate => 1 } };
    my $shift;
    if ( $advFmtSelf
        and defined( $shift = $$advFmtSelf{OPTIONS}{GlobalTimeShift} ) )
    {
        $$et{OPTIONS}{GlobalTimeShift} = $shift;
        $$et{GLOBAL_TIME_OFFSET} = $$advFmtSelf{GLOBAL_TIME_OFFSET};
    }
    $_ = $et->ConvertDateTime($_);
    defined $_ or warn "Error converting date/time\n";
    $$advFmtSelf{GLOBAL_TIME_OFFSET} = $$et{GLOBAL_TIME_OFFSET} if $shift;
    return $_;
}

sub NoDups {
    my %seen;
    my $sep = $advFmtSelf ? $$advFmtSelf{OPTIONS}{ListSep} : ', ';
    my $new = join $sep, grep { !$seen{$_}++ } split /\Q$sep\E/, $_;
    $_ = ( $_[0] and $new eq $_ ) ? undef : $new;
}

sub SetTags(@) {
    my $self = $advFmtSelf;
    my $et   = Image::ExifTool->new;
    $et->SetNewValuesFromFile( $self, @_ );
    return $et->WriteInfo( \$_ );
}

sub IsWritable($) {
    my $tag = shift;
    $tag =~ s/^(.*)://;
    my @tagInfo = FindTagInfo($tag);
    unless (@tagInfo) {
        return 0 if TagExists($tag);
        return undef;
    }
    my $tagInfo;
    foreach $tagInfo (@tagInfo) {
        return $$tagInfo{Writable} ? 1 : 0 if defined $$tagInfo{Writable};
        return 1                           if $$tagInfo{Table}{WRITABLE};
        my $writeProc = $$tagInfo{Table}{WRITE_PROC};
        if ($writeProc) {
            no strict 'refs';
            &$writeProc();
            return 1 if $$tagInfo{Writable};
        }
    }
    return 0;
}

sub IsSameFile($$$) {
    my ( $self, $file, $file2 ) = @_;
    return 0 unless lc $file eq lc $file2;
    my ( $isSame, $interrupted );
    my $tmp1 = "${file}_ExifTool_tmp_$$";
    my $tmp2 = "${file2}_ExifTool_tmp_$$";
    {
        local *TMP1;
        local $SIG{INT} = sub { $interrupted = 1 };
        if ( $self->Open( \*TMP1, $tmp1, '>' ) ) {
            close TMP1;
            $isSame = 1 if $self->Exists($tmp2);
            $self->Unlink($tmp1);
        }
    }
    if ( $interrupted and $SIG{INT} ) {
        no strict 'refs';
        &{ $SIG{INT} }();
    }
    return $isSame;
}

sub IsRawType($) {
    my $self = shift;
    return $rawType{ $$self{FileType} };
}

sub CopyFileAttrs($$$) {
    my ( $self, $src, $dst ) = @_;
    my ( $mode, $uid, $gid ) = ( stat($src) )[ 2, 4, 5 ];
    if ( defined $mode and not defined $self->GetNewValue('FilePermissions') ) {
        eval { chmod( $mode & 07777, $dst ) };
    }
    my $newUid = $self->GetNewValue('FileUserID');
    my $newGid = $self->GetNewValue('FileGroupID');
    if (    defined $uid
        and defined $gid
        and ( not defined $newUid or not defined $newGid ) )
    {
        defined $newGid and $gid = $newGid;
        defined $newUid and $uid = $newUid;
        eval { chown( $uid, $gid, $dst ) };
    }
}

sub GetNewFileName($$) {
    my ( $oldName, $newName ) = @_;
    my ( $dir,     $name )    = ( $oldName =~ m{(.*/)(.*)} );
    ( $dir, $name ) = ( '', $oldName ) unless defined $dir;
    if ( $newName =~ m{/$} ) {
        $newName = "$newName$name";
    }
    elsif ( $newName !~ m{/} ) {
        $newName = "$dir$newName";
    }
    return $newName;
}

sub NextFreeTagKey($$) {
    my ( $info, $tag ) = @_;
    return $tag unless exists $$info{$tag};
    my $i;
    for ( $i = 1 ; ; ++$i ) {
        my $key = "$tag ($i)";
        return $key unless exists $$info{$key};
    }
}

sub ReverseLookup($$) {
    my ( $val, $conv ) = @_;
    return undef unless defined $val;
    my $multi;
    if ( $val =~ /^Unknown\s*\((.*)\)$/i ) {
        $val = $1;
        if ( $val =~ /^0x([\da-fA-F]+)$/ ) {
            local $SIG{'__WARN__'} = sub { };
            $val = hex($val);
        }
    }
    else {
        my $qval = $val;
        $qval =~ s/\s+$//;
        $qval = quotemeta $qval;
        my @patterns =
          ( "^$qval\$", "^(?i)$qval\$", "^(?i)$qval", "(?i)$qval", );
        my ( $pattern, $found, $matches );
      PAT: foreach $pattern (@patterns) {
            $matches = scalar grep /$pattern/, values(%$conv);
            next unless $matches;
            if ( $matches > 1 and $pattern !~ /\$$/ ) {
                foreach ( keys %ignorePrintConv ) {
                    --$matches
                      if defined $$conv{$_} and $$conv{$_} =~ /$pattern/;
                }
                last if $matches > 1;
            }
            foreach ( sort keys %$conv ) {
                next if $$conv{$_} !~ /$pattern/ or $ignorePrintConv{$_};
                $val   = $_;
                $found = 1;
                last PAT;
            }
        }
        unless ($found) {
            if ( $$conv{OTHER} ) {
                local $SIG{'__WARN__'} = \&SetWarning;
                undef $evalWarning;
                $val = &{ $$conv{OTHER} }( $val, 1, $conv );
            }
            else {
                $val = undef;
            }
            $multi = 1 if $matches > 1;
        }
    }
    return ( $val, $multi ) if wantarray;
    return $val;
}

sub IsOverwriting($$;$) {
    my ( $self, $nvHash, $val ) = @_;
    return 0 unless $nvHash;
    return 1 unless $$nvHash{DelValue};
    my $shift = $$nvHash{Shift};
    return 0 unless @{ $$nvHash{DelValue} } or defined $shift;
    return -1 unless defined $val;
    my $tagInfo = $$nvHash{TagInfo};
    my $conv    = $$tagInfo{RawConv};

    if ($conv) {
        local $SIG{'__WARN__'} = \&SetWarning;
        undef $evalWarning;
        if ( ref $conv eq 'CODE' ) {
            $val = &$conv( $val, $self );
        }
        else {
            my ( $priority, @grps );
            my $tag = $$tagInfo{Name};
            $val = eval $conv;
            $@ and $evalWarning = $@;
        }
        return -1 unless defined $val;
    }
    return 0 if $$nvHash{CreateOnly};
    if ( defined $shift ) {
        my $shiftType = $$tagInfo{Shift};
        unless ( $shiftType and $shiftType eq 'Time' ) {
            unless ( IsFloat($val) ) {
                my $conv = $$tagInfo{ValueConv};
                if ( defined $conv ) {
                    local $SIG{'__WARN__'} = \&SetWarning;
                    undef $evalWarning;
                    if ( ref $conv eq 'CODE' ) {
                        $val = &$conv( $val, $self );
                    }
                    elsif ( not ref $conv ) {
                        $val = eval $conv;
                        $@ and $evalWarning = $@;
                    }
                    if ($evalWarning) {
                        $self->Warn(
                            "ValueConv $$tagInfo{Name}: " . CleanWarning() );
                        return 0;
                    }
                }
                unless ( defined $val and IsFloat($val) ) {
                    $self->Warn("Can't shift $$tagInfo{Name} (not a number)");
                    return 0;
                }
            }
            $shiftType = 'Number';
        }
        require 'Image/ExifTool/Shift.pl';
        my $err = $self->ApplyShift( $shiftType, $shift, $val, $nvHash );
        if ($err) {
            $self->Warn("$err when shifting $$tagInfo{Name}");
            return 0;
        }
        my $checkVal = $self->GetNewValue($nvHash);
        return 0 unless defined $checkVal;
        return 0 if $val eq $$nvHash{Value}[0];
        return 1;
    }
    my $delVal;
    foreach $delVal ( @{ $$nvHash{DelValue} } ) {
        return 1 if $val eq $delVal;
    }
    return 0;
}

sub GetWriteGroup($) {
    return $_[0]{WriteGroup};
}

sub GetWriteGroup1($$) {
    my ( $self, $tagInfo, $writeGroup ) = @_;
    return $writeGroup
      unless $writeGroup =~ /^(MakerNotes|XMP|Composite|QuickTime)$/;
    return $self->GetGroup( $tagInfo, 1 );
}

sub GetGeolocateTags($$;$) {
    my ( $self, $wantGroup, $writeGPS ) = @_;
    my @grps = $wantGroup ? map lc, split( /:/, $wantGroup ) : ();
    my %grps = map { $_ => $_ } @grps;
    $grps{exif} and not $grps{gps} and $grps{gps} = 'gps', push( @grps, 'gps' );
    my %tagGroups = (
        'xmp-iptcext' => [
            qw(LocationShownCity LocationShownProvinceState LocationShownCountryCode
              LocationShownCountryName LocationShownGPSLatitude LocationShownGPSLongitude)
        ],
        'xmp-photoshop' => [qw(City State Country)],
        'xmp-iptccore'  => ['CountryCode'],
        'iptc'          => [
            qw(City Province-State Country-PrimaryLocationCode Country-PrimaryLocationName)
        ],
        'gps' => [qw(GPSLatitude GPSLongitude GPSLatitudeRef GPSLongitudeRef)],
        'xmp-exif' => [qw(GPSLatitude GPSLongitude)],
        'itemlist' => ['GPSCoordinates'],
        'userdata' => ['GPSCoordinates'],
    );
    my ( @tags, $grp );
    foreach $grp (@grps) {
        $tagGroups{$grp}
          and push @tags, map( "$grp:$_", @{ $tagGroups{$grp} } );
    }
    if ( not $writeGPS ) {
        push @tags, 'Keys:LocationName' if $grps{'keys'};
        if ( $grps{xmp} or ( not @tags and not $grps{quicktime} ) ) {
            push @tags,
              qw(XMP:City XMP:State XMP:CountryCode XMP:Country Keys:LocationName);
        }
    }
    $writeGPS = 1 unless defined $writeGPS;
    push @tags, 'Keys:GPSCoordinates' if $writeGPS and $grps{'keys'};
    my $didQT = grep /GPSCoordinates$/, @tags;
    if (   ( $grps{quicktime} and not $didQT )
        or ( $writeGPS and not @tags and not $grps{xmp} ) )
    {
        push @tags, 'QuickTime:GPSCoordinates';
    }
    if ($writeGPS) {
        push @tags, qw(XMP:GPSLatitude XMP:GPSLongitude)
          if $grps{xmp} and not $grps{'xmp-exif'};
        push @tags, qw(GPSLatitude GPSLongitude GPSLatitudeRef GPSLongitudeRef)
          if not $wantGroup;
    }
    return @tags;
}

sub GetNewValueHash($$;$$$$) {
    my ( $self, $tagInfo, $writeGroup, $opts ) = @_;
    return undef unless $tagInfo;
    my $nvHash = $$self{NEW_VALUE}{$tagInfo};

    my %opts;
    $opts and $opts{$opts} = 1;
    $writeGroup = '' unless defined $writeGroup;

    if ($writeGroup) {
        while ( $nvHash and $$nvHash{WriteGroup} ne $writeGroup ) {
            last if $$nvHash{WriteGroup} =~ /^(QuickTime|All)$/;
            last
              if $writeGroup eq 'All'
              or $$nvHash{WriteGroup} eq 'EXIF' and $writeGroup =~ /IFD/;
            $nvHash = $$nvHash{Next};
        }
    }
    if ( defined $nvHash
        and ( $opts{'delete'} or ( $opts{'create'} and $$nvHash{Save} ) ) )
    {
        my $protect =
          (       defined $_[4]
              and defined $$nvHash{Save}
              and $$nvHash{Save} > $_[4] );
        if (
            $protect
            and not(
                $opts{create}
                and (  $$nvHash{NoReplace}
                    or $_[5]
                    or ( $$nvHash{DelValue} and not defined $$nvHash{Shift} ) )
            )
          )
        {
            return undef;
        }
        elsif ( $opts{'delete'} ) {
            $self->RemoveNewValueHash( $nvHash, $tagInfo );
            undef $nvHash;
        }
        else {
            my %copy = %$nvHash;
            my $key;
            foreach $key ( keys %copy ) {
                next unless ref $copy{$key} eq 'ARRAY';
                $copy{$key} = [ @{ $copy{$key} } ];
            }
            my $saveHash = $$self{SAVE_NEW_VALUE};
            $copy{Next} = $$saveHash{$tagInfo};
            $$saveHash{$tagInfo} = \%copy;
            delete $$nvHash{Save};
            $$nvHash{AddBefore} = scalar @{ $$nvHash{Value} }
              if $protect and $$nvHash{Value};
        }
    }
    if ( not defined $nvHash and $opts{'create'} ) {
        $nvHash = {
            TagInfo    => $tagInfo,
            WriteGroup => $writeGroup,
            IsNVH      => 1,
            Order      => $$self{NV_COUNT}++,
        };
        if ( $$self{NEW_VALUE}{$tagInfo} ) {
            my $lastHash = LastInList( $$self{NEW_VALUE}{$tagInfo} );
            $$lastHash{Next} = $nvHash;
        }
        else {
            $$self{NEW_VALUE}{$tagInfo} = $nvHash;
        }
    }
    return $nvHash;
}

sub LoadAllTables() {
    return if $loadedAllTables;

    my $table;
    foreach $table (@loadAllTables) {
        my $tableName = "Image::ExifTool::$table";
        $tableName .= '::Main' unless $table =~ /:/;
        GetTagTable($tableName);
    }
    GetTagTable('Image::ExifTool::Extra');
    GetTagTable('Image::ExifTool::Composite');
    my @tableNames = keys %allTables;
    my %pushedTables;
    while (@tableNames) {
        $table = GetTagTable( shift @tableNames );
        my $writeProc = $$table{WRITE_PROC};
        if ($writeProc) {
            no strict 'refs';
            &$writeProc();
        }
        foreach ( TagTableKeys($table) ) {
            my @infoArray = GetTagInfoList( $table, $_ );
            my $tagInfo;
            foreach $tagInfo (@infoArray) {
                my $subdir    = $$tagInfo{SubDirectory} or next;
                my $tableName = $$subdir{TagTable}      or next;
                next if $allTables{$tableName} or $pushedTables{$tableName};
                push @tableNames, $tableName;
                $pushedTables{$tableName} = 1;
            }
        }
    }
    $loadedAllTables = 1;
}

sub RemoveNewValueHash($$$) {
    my ( $self, $nvHash, $tagInfo ) = @_;
    my $firstHash = $$self{NEW_VALUE}{$tagInfo};
    if ( $nvHash eq $firstHash ) {
        if ( $$nvHash{Next} ) {
            $$self{NEW_VALUE}{$tagInfo} = $$nvHash{Next};
        }
        else {
            delete $$self{NEW_VALUE}{$tagInfo};
        }
    }
    else {
        $firstHash = $$firstHash{Next} while $$firstHash{Next} ne $nvHash;
        $$firstHash{Next} = $$nvHash{Next};
    }
    if ( $$nvHash{Save} ) {
        my $saveHash = $$self{SAVE_NEW_VALUE};
        $$nvHash{Next} = $$saveHash{$tagInfo};
        $$saveHash{$tagInfo} = $nvHash;
    }
}

sub RemoveNewValuesForGroup($$) {
    my ( $self, $group ) = @_;

    return unless $$self{NEW_VALUE};

    my @groups = ($group);
    push @groups, @{ $removeGroups{$group} } if $removeGroups{$group};

    my ( $out, @keys, $hashKey );
    $out = $$self{OPTIONS}{TextOut} if $$self{OPTIONS}{Verbose} > 1;

    @keys = keys %{ $$self{NEW_VALUE} };
    foreach $hashKey (@keys) {
        my $nvHash = $$self{NEW_VALUE}{$hashKey};
        for ( ; ; ) {
            my $nextHash = $$nvHash{Next};
            my $tagInfo  = $$nvHash{TagInfo};
            my ( $grp0, $grp1 ) = $self->GetGroup($tagInfo);
            my $wgrp = $$nvHash{WriteGroup};
            $wgrp = $grp1 if $wgrp eq $grp0;
            if (   $grp0 eq '*'
                or $wgrp eq '*'
                or grep /^($grp0|$wgrp)$/i, @groups )
            {
                $out
                  and print $out
                  "Removed new value for $wgrp:$$tagInfo{Name}\n";
                $self->RemoveNewValueHash( $nvHash, $tagInfo );
            }
            $nvHash = $nextHash or last;
        }
    }
}

sub GetNewTagInfoList($;$) {
    my ( $self, $tagTablePtr ) = @_;
    my @tagInfoList;
    my $nv = $$self{NEW_VALUE};
    if ($nv) {
        my $hashKey;
        foreach $hashKey ( keys %$nv ) {
            my $tagInfo = $$nv{$hashKey}{TagInfo};
            next if $tagTablePtr and $tagTablePtr ne $$tagInfo{Table};
            push @tagInfoList, $tagInfo;
        }
    }
    return @tagInfoList;
}

sub GetNewTagInfoHash($@) {
    my $self = shift;
    my ( %tagInfoHash, $hashKey );
    my $nv = $$self{NEW_VALUE};
    while ($nv) {
        my $tagTablePtr = shift || last;
        foreach $hashKey ( keys %$nv ) {
            my $tagInfo = $$nv{$hashKey}{TagInfo};
            next if $tagTablePtr and $tagTablePtr ne $$tagInfo{Table};
            $tagInfoHash{ $$tagInfo{TagID} } = $tagInfo;
        }
    }
    return \%tagInfoHash;
}

sub GetAddDirHash($$;$) {
    my ( $self, $tagTablePtr, $parent ) = @_;
    $parent or $parent = $$tagTablePtr{GROUPS}{0};
    my $tagID;
    my %addDirHash;
    my %editDirHash;
    my $addDirs  = $$self{ADD_DIRS};
    my $editDirs = $$self{EDIT_DIRS};
    foreach $tagID ( TagTableKeys($tagTablePtr) ) {
        my @infoArray = GetTagInfoList( $tagTablePtr, $tagID );
        my $tagInfo;
        foreach $tagInfo (@infoArray) {
            next unless $$tagInfo{SubDirectory};
            my $dirName = $$tagInfo{SubDirectory}{DirName};
            unless ($dirName) {
                $dirName = $$tagInfo{Name};
                $$tagInfo{SubDirectory}{DirName} = $dirName;
            }
            if ( $$editDirs{$dirName} and $$editDirs{$dirName} eq $parent ) {
                $editDirHash{$tagID} = $tagInfo;
                $addDirHash{$tagID}  = $tagInfo if $$addDirs{$dirName};
            }
        }
    }
    return ( \%addDirHash, \%editDirHash ) if wantarray;
    return \%addDirHash;
}

sub GetLangInfo($$) {
    my ( $tagInfo, $langCode ) = @_;
    my $table    = $$tagInfo{Table};
    my $tagID    = $$tagInfo{TagID} . '-' . $langCode;
    my $langInfo = $$table{$tagID};
    unless ($langInfo) {
        $langInfo = {
            %$tagInfo,
            Name        => $$tagInfo{Name} . '-' . $langCode,
            Description => Image::ExifTool::MakeDescription( $$tagInfo{Name} )
              . " ($langCode)",
            LangCode   => $langCode,
            SrcTagInfo => $tagInfo,
        };
        AddTagToTable( $table, $tagID, $langInfo );
    }
    return $langInfo;
}

sub InitWriteDirs($$;$$) {
    my ( $self, $fileType, $preferredGroup, $altGroup ) = @_;
    my $editDirs = $$self{EDIT_DIRS} = {};
    my $addDirs  = $$self{ADD_DIRS}  = {};
    my $fileDirs = $dirMap{$fileType};
    unless ($fileDirs) {
        return unless ref $fileType eq 'HASH';
        $fileDirs = $fileType;
    }
    my @tagInfoList = $self->GetNewTagInfoList();
    my ( $tagInfo, $nvHash );

    $$self{PreferredGroup} = $preferredGroup;

    foreach $tagInfo (@tagInfoList) {
        for (
            $nvHash = $self->GetNewValueHash($tagInfo) ;
            $nvHash ;
            $nvHash = $$nvHash{Next}
          )
        {
            my $isCreating = $$nvHash{IsCreating};
            if ($preferredGroup) {
                my $g0 = $self->GetGroup( $tagInfo, 0 );
                if ($isCreating) {
                    $isCreating = 0
                      if $preferredGroup ne $g0
                      and $$nvHash{CreateGroups}{$preferredGroup}
                      and ( not $altGroup or $altGroup ne $g0 );
                }
                else {
                    $isCreating = 1
                      if $$nvHash{Value}
                      and $preferredGroup eq $g0
                      and not $$nvHash{EditOnly}
                      and $$self{OPTIONS}{WriteMode} =~ /g/;
                }
            }
            my $dirName = $$nvHash{WriteGroup};
            if ( $dirName =~ /^MIE\d*(-[a-z]+)?\d*$/i ) {
                $dirName = 'MIE' . ( $1 || '' );
            }
            my @dirNames;
            if ( $dirName eq '*' and $$nvHash{Value} ) {
                my $val = $$nvHash{Value}[0];
                if ($val) {
                    foreach (qw(EXIF IPTC XMP PNG FixBase)) {
                        next unless $val =~ /\b($_|All)\b/i;
                        push @dirNames, $_;
                        push @dirNames, 'EXIF' if $_ eq 'FixBase';
                        $$self{FORCE_WRITE}{$_} = 1;
                    }
                }
                $dirName = shift @dirNames;
            }
            elsif ( $dirName eq 'QuickTime' ) {
                $dirName = $self->GetGroup( $tagInfo, 1 );
            }
            while ($dirName) {
                my $parent = $$fileDirs{$dirName};
                if ( ref $parent ) {
                    push @dirNames, reverse @$parent;
                    $parent = pop @dirNames;
                }
                $$editDirs{$dirName} = $parent;
                $$addDirs{$dirName}  = $parent
                  if $isCreating and $isCreating != 2;
                $dirName = $parent || shift @dirNames;
            }
        }
    }
    if ( %{ $$self{DEL_GROUP} } ) {
        foreach ( keys %{ $$self{DEL_GROUP} } ) {
            next if /^-/;
            my $dirName = $_;
            $dirName = $translateWriteGroup{$dirName}
              if $translateWriteGroup{$dirName};
            $dirName = 'XMP' if $dirName =~ /^XMP-/;
            my @dirNames;
            while ($dirName) {
                my $parent = $$fileDirs{$dirName};
                if ( ref $parent ) {
                    push @dirNames, reverse @$parent;
                    $parent = pop @dirNames;
                }
                $$editDirs{$dirName} = $parent;
                $dirName = $parent || shift @dirNames;
            }
        }
    }
    if ( $$editDirs{IFD0} and $$fileDirs{JFIF} ) {
        $$editDirs{JFIF} = 'IFD1';
        $$editDirs{APP0} = undef;
    }

    if ( $$self{OPTIONS}{Verbose} ) {
        my $out = $$self{OPTIONS}{TextOut};
        print $out "  Editing tags in: ";
        foreach ( sort keys %$editDirs ) { print $out "$_ "; }
        print $out "\n";
        return unless $$self{OPTIONS}{Verbose} > 1;
        print $out "  Creating tags in: ";
        foreach ( sort keys %$addDirs ) { print $out "$_ "; }
        print $out "\n";
    }
}

sub WriteDirectory($$$;$) {
    my ( $self, $dirInfo, $tagTablePtr, $writeProc ) = @_;
    my ( $out, $nvHash, $delFlag );

    $tagTablePtr or return undef;
    $out = $$self{OPTIONS}{TextOut} if $$self{OPTIONS}{Verbose};
    my $dirName = $$dirInfo{DirName};
    my $parent  = $$dirInfo{Parent} || '';
    my $dataPt  = $$dirInfo{DataPt};
    my $grp0    = $$tagTablePtr{GROUPS}{0};
    $dirName or $dirName = $$dirInfo{DirName} = $grp0;
    if ( %{ $$self{DEL_GROUP} } ) {
        my $delGroup = $$self{DEL_GROUP};
        my $grp1 = $dirName;
        $delFlag = ( $$delGroup{$grp0} or $$delGroup{$grp1} );
        if ( $permanentDir{$grp0}
            and not( $$dirInfo{TagInfo} and $$dirInfo{TagInfo}{Deletable} ) )
        {
            undef $delFlag;
        }
        if ($delFlag) {
            if ( $$dirInfo{Permanent} ) {
                $self->Warn("Not deleting permanent $dirName directory");
                undef $grp1;
            }
            elsif (
                (
                       $grp0 =~ /^(MakerNotes)$/
                    or $grp1 =~ /^(IFD0|ExifIFD|MakerNotes)$/
                )
                and $self->IsRawType()
                and
                (
                       not $$dirInfo{TagInfo}
                    or not defined $$dirInfo{TagInfo}{Permanent}
                    or $$dirInfo{TagInfo}{Permanent}
                )
                and
                not( $self->IsRawType() == 2 and $parent eq 'ExifIFD' )
              )
            {
                $self->Warn( "Can't delete $1 from $$self{FileType}", 1 );
                undef $grp1;
            }
            elsif ( not $blockExifTypes{ $$self{FILE_TYPE} } ) {
                if ( $$self{FILE_TYPE} eq 'PSD' ) {
                    undef $grp1 if $grp0 eq 'Photoshop';
                }
                elsif ( $$self{FILE_TYPE} =~ /^(EPS|PS)$/ ) {
                }
                elsif ( $grp1 eq 'IFD0' ) {
                    my $type = $$self{TIFF_TYPE} || $$self{FILE_TYPE};
                    $$delGroup{IFD0}
                      and $self->Warn( "Can't delete IFD0 from $type", 1 );
                    undef $grp1;
                }
                elsif ( $grp0 eq 'EXIF' and $$delGroup{$grp0} ) {
                    undef $grp1 unless $$delGroup{$grp1} or $grp1 eq 'ExifIFD';
                }
            }
            if ($grp1) {
                if ( $dataPt or $$dirInfo{RAF} ) {
                    ++$$self{CHANGED};
                    $out and print $out "  Deleting $grp1\n";
                    $self->Warn(
                        'ICC_Profile deleted. Image colors may be affected')
                      if $grp1 eq 'ICC_Profile';
                    delete $$self{TIFF_END} if $dirName =~ /IFD/;
                }
                my $right = $$self{ADD_DIRS}{$grp1};
                $right = $$self{ADD_DIRS}{IFD0}
                  if not $right and $grp1 eq 'EXIF';
                if ( $delFlag == 2 and $right ) {
                    my $right2 = $$self{ADD_DIRS}{$right} || '';
                    if (   not $parent
                        or $parent eq $right
                        or $parent eq $right2 )
                    {
                        my $path = join '-', @{ $$self{PATH} }, $dirName;
                        $$self{Recreated} or $$self{Recreated} = {};
                        if ( $$self{Recreated}{$path} ) {
                            my $p = $parent ? " in $parent" : '';
                            $self->Warn( "Not recreating duplicate $grp1$p",
                                1 );
                            return '';
                        }
                        $$self{Recreated}{$path} = 1;
                        my $data = '';
                        $$dirInfo{DataPt}   = \$data;
                        $$dirInfo{DataLen}  = 0;
                        $$dirInfo{DirStart} = 0;
                        $$dirInfo{DirLen}   = 0;
                        delete $$dirInfo{RAF};
                        delete $$dirInfo{Base};
                        delete $$dirInfo{DataPos};
                    }
                    else {
                        $self->Warn(
"Not recreating $grp1 in $parent (should be in $right)",
                            1
                        );
                        return '';
                    }
                }
                else {
                    return '' unless $$dirInfo{NoDelete};
                }
            }
        }
    }
    $writeProc or $writeProc = $$tagTablePtr{WRITE_PROC} or return undef;

    my $isRewriting = (
             $$dirInfo{DirLen}
          or ( defined $dataPt and length $$dataPt )
          or $$dirInfo{RAF}
    );

    my $blockName = $dirName;
    $blockName = 'EXIF' if $blockName eq 'IFD0';
    my $tagInfo = $Image::ExifTool::Extra{$blockName} || $$dirInfo{TagInfo};
    while ( $tagInfo
        and ( $nvHash = $$self{NEW_VALUE}{$tagInfo} )
        and $self->IsOverwriting($nvHash)
        and not( $$nvHash{CreateOnly} and $isRewriting ) )
    {
        if ( $blockName eq 'EXIF' ) {
            unless ( $blockExifTypes{ $$self{FILE_TYPE} } ) {
                $self->Warn(
                    "Can't write EXIF as a block to $$self{FILE_TYPE} file");
                last;
            }
            last unless $writeProc eq \&Image::ExifTool::WriteTIFF;
        }
        last unless $self->IsOverwriting( $nvHash, $dataPt ? $$dataPt : '' );
        my $verb   = 'Writing';
        my $newVal = $self->GetNewValue($nvHash);
        if ( defined $newVal and length $newVal ) {
            if ( $$tagInfo{Name} eq 'MakerNoteCanon' ) {
                require Image::ExifTool::Canon;
                if ( $tagInfo eq $Image::ExifTool::Canon::uuid{CMT3} ) {
                    my $hdr;
                    if ( substr( $newVal, 0, 1 ) eq "\0" ) {
                        $hdr = "MM\0\x2a" . pack( 'N', 8 );
                    }
                    else {
                        $hdr = "II\x2a\0" . pack( 'V', 8 );
                    }
                    $newVal = $hdr . $newVal;
                }
            }
        }
        else {
            return '' unless $dataPt or $$dirInfo{RAF};

            if (
                    $blockName eq 'MakerNotes'
                and $self->IsRawType()
                and
                not( $self->IsRawType() == 2 and $parent eq 'ExifIFD' )
              )
            {
                $self->Warn( "Can't delete MakerNotes from $$self{FileType}",
                    1 );
                return undef;
            }
            $verb   = 'Deleting';
            $newVal = '';
        }
        $$dirInfo{BlockWrite} = 1;
        $out and print $out "  $verb $blockName as a block\n";
        ++$$self{CHANGED};
        return $newVal;
    }
    if (    defined $dataPt
        and defined $$dirInfo{DirStart}
        and defined $$dirInfo{DataPos}
        and not $$dirInfo{NoRefTest} )
    {
        my $addr =
          $$dirInfo{DirStart} +
          $$dirInfo{DataPos} +
          ( $$dirInfo{Base} || 0 ) +
          $$self{BASE};
        if ( $$self{PROCESSED}{$addr}
            and ( $dirName ne 'ICC_Profile' or $$self{TIFF_TYPE} ne 'IIQ' ) )
        {
            if (    defined $$dirInfo{DirLen}
                and not $$dirInfo{DirLen}
                and $dirName ne $$self{PROCESSED}{$addr} )
            {
            }
            elsif (
                $self->Error(
"$dirName pointer references previous $$self{PROCESSED}{$addr} directory",
                    2
                )
              )
            {
                return undef;
            }
            else {
                $self->Warn("Deleting duplicate $dirName directory");
                $out and print $out "  Deleting $dirName\n";
                return '';
            }
        }
        else {
            $$self{PROCESSED}{$addr} = $dirName;
        }
    }
    my $oldDir = $$self{DIR_NAME};
    my @save   = @$self{ 'Compression', 'SubfileType' };
    my $name;
    if ($out) {
        $name =
          ( $dirName eq 'MakerNotes' and $$dirInfo{TagInfo} )
          ? $$dirInfo{TagInfo}{Name}
          : $dirName;
        if ( not defined $oldDir or $oldDir ne $name ) {
            my $verb = $isRewriting ? 'Rewriting' : 'Creating';
            print $out "  $verb $name\n";
        }
    }
    my $saveOrder  = GetByteOrder();
    my $oldChanged = $$self{CHANGED};
    $$self{DIR_NAME} = $dirName;
    push @{ $$self{PATH} }, $dirName;
    $$dirInfo{IsWriting} = 1;
    my $newData;
    {
        no strict 'refs';
        $newData = &$writeProc( $self, $dirInfo, $tagTablePtr );
    }
    pop @{ $$self{PATH} };
    $$self{CHANGED} = $oldChanged
      unless defined $newData and ( length($newData) or $isRewriting );
    $$self{DIR_NAME} = $oldDir;
    @$self{ 'Compression', 'SubfileType' } = @save;
    SetByteOrder($saveOrder);
    if ($out) {
        print $out "  Deleting $name\n"
          if defined $newData and not length $newData;
        if ( $$self{CHANGED} == $oldChanged and $$self{OPTIONS}{Verbose} > 2 ) {
            print $out "$$self{INDENT}  [nothing changed in $name]\n";
        }
    }
    return $newData;
}

sub Get64s($$) {
    my ( $dataPt, $pos ) = @_;
    my $pt = GetByteOrder() eq 'MM' ? 0 : 4;
    my $hi = Get32s( $dataPt, $pos + $pt );
    my $lo = Get32u( $dataPt, $pos + 4 - $pt );
    return $hi * 4294967296 + $lo;
}

sub Get64u($$) {
    my ( $dataPt, $pos ) = @_;
    my $pt = GetByteOrder() eq 'MM' ? 0 : 4;
    my $hi = Get32u( $dataPt, $pos + $pt );
    my $lo = Get32u( $dataPt, $pos + 4 - $pt );
    return $hi * 4294967296 + $lo;
}

sub GetFixed64s($$) {
    my ( $dataPt, $pos ) = @_;
    my $val = Get64s( $dataPt, $pos ) / 4294967296;
    return int( $val * 1e10 + ( $val > 0 ? 0.5 : -0.5 ) ) / 1e10;
}

sub GetExtended($$) {
    my ( $dataPt, $pos ) = @_;
    my $pt   = GetByteOrder() eq 'MM' ? 0 : 2;
    my $exp  = Get16u( $dataPt, $pos + $pt );
    my $sig  = Get64u( $dataPt, $pos + 2 - $pt );
    my $sign = $exp & 0x8000 ? -1 : 1;
    $exp = ( $exp & 0x7fff ) - 16383 - 63;
    return $sign * $sig * 2**$exp;
}

sub HexDump($;$%) {
    my $dataPt = shift;
    my $len    = shift;
    my %opts   = @_;
    my $start  = $opts{Start} || 0;
    my $addr   = $opts{Addr};
    my $wid    = $opts{Width}  || 16;
    my $prefix = $opts{Prefix} || '';
    my $out    = $opts{Out}    || \*STDOUT;
    my $maxLen = $opts{MaxLen};
    my $datLen = length($$dataPt) - $start;
    my $more;
    $len = $opts{Len} if defined $opts{Len};

    $addr = $start + ( $opts{DataPos} || 0 ) + ( $opts{Base} || 0 )
      unless defined $addr;
    $len = $datLen unless defined $len;
    if ( $maxLen and $len > $maxLen ) {
        $maxLen = int( ( $maxLen - 1 ) / $wid ) * $wid;
        $more   = $len - $maxLen;
        $len    = $maxLen;
    }
    if ( $len > $datLen ) {
        print $out "$prefix    Warning: Attempted dump outside data\n";
        print $out
          "$prefix    ($len bytes specified, but only $datLen available)\n";
        $len = $datLen;
    }
    my $format = sprintf( "%%-%ds", $wid * 3 );
    my $tmpl   = 'H2' x $wid;
    my $i;
    for ( $i = 0 ; $i < $len ; $i += $wid ) {
        $wid > $len - $i and $wid = $len - $i, $tmpl = 'H2' x $wid;
        printf $out "$prefix%8.4x: ", $addr + $i;
        my $dat = substr( $$dataPt, $i + $start, $wid );
        my $s   = join( ' ', unpack( $tmpl, $dat ) );
        printf $out $format, $s;
        $dat =~ tr /\x00-\x1f\x7f-\xff/./;
        print $out "[$dat]\n";
    }
    $more and print $out "$prefix    [snip $more bytes]\n";
}

sub VerboseInfo($$$%) {
    my ( $self, $tagID, $tagInfo, %parms ) = @_;
    my $verbose = $$self{OPTIONS}{Verbose};
    my $out     = $$self{OPTIONS}{TextOut};
    my ( $tag, $line, $hexID );

    if ( defined $tagID ) {
        $tagID =~ /^\d+$/ and $hexID = sprintf( "0x%.4x", $tagID );
    }
    else {
        $tagID = 'Unknown';
    }
    if ( $tagInfo and $$tagInfo{Name} ) {
        $tag = $$tagInfo{Name};
    }
    elsif ( $parms{Name} ) {
        $tag = $parms{Name};
        undef $hexID;
    }
    else {
        my $prefix;
        $prefix = $parms{Table}{TAG_PREFIX} if $parms{Table};
        if ( $prefix or $hexID ) {
            $prefix = 'Unknown' unless $prefix;
            $tag    = $prefix . '_' . ( $hexID ? $hexID : $tagID );
        }
        else {
            $tag = $tagID;
        }
    }
    my $dataPt = $parms{DataPt};
    my $size   = $parms{Size};
    $size = length $$dataPt unless defined $size or not $dataPt;
    my $indent = $$self{INDENT};

    $line = $indent;
    my $index = $parms{Index};
    if ( defined $index ) {
        $line   .= $index . ') ';
        $line   .= ' ' if length($index) < 2;
        $indent .= '    ';
    }
    $line .= $tag;
    if ( $tagInfo and $$tagInfo{SubDirectory} ) {
        $line .= ' (SubDirectory) -->';
    }
    else {
        my $maxLen = 90 - length($line);
        my $val    = $parms{Value};
        if ( defined $val ) {
            $val = '[' . join( ',', @$val ) . ']' if ref $val eq 'ARRAY';
            $line .= ' = ' . $self->Printable( $val, $maxLen );
        }
        elsif ($dataPt) {
            my $start = $parms{Start} || 0;
            $line .= ' = '
              . $self->Printable( substr( $$dataPt, $start, $size ), $maxLen );
        }
    }
    print $out "$line\n";

    if (
        $verbose > 1
        and (  $parms{Extra}
            or $parms{Format}
            or $parms{DataPt}
            or defined $size
            or $tagID =~ /\// )
      )
    {
        $line = $indent . '- Tag ';
        if ($hexID) {
            $line .= $hexID;
        }
        else {
            $tagID =~ s/([\0-\x1f\x7f-\xff])/sprintf('\\x%.2x',ord $1)/ge;
            $line .= "'${tagID}'";
        }
        $line .= $parms{Extra} if defined $parms{Extra};
        my $format = $parms{Format};
        if ( $format or defined $size ) {
            $line .= ' (';
            if ( defined $size ) {
                $line .= "$size bytes";
                $line .= ', ' if $format;
            }
            if ($format) {
                $line .= $format;
                $line .= '[' . $parms{Count} . ']' if $parms{Count};
            }
            $line .= ')';
        }
        $line .= ':' if $verbose > 2 and $parms{DataPt};
        print $out "$line\n";
    }

    if (    $verbose > 2
        and $parms{DataPt}
        and ( not $tagInfo or not $$tagInfo{ReadFromRAF} ) )
    {
        $parms{Out}    = $out;
        $parms{Prefix} = $indent;
        $parms{MaxLen} = $verbose == 3 ? 96 : 2048 if $verbose < 5;
        HexDump( $dataPt, $size, %parms );
    }
}

sub DumpTrailer($$) {
    my ( $self, $dirInfo ) = @_;
    my $raf      = $$dirInfo{RAF};
    my $curPos   = $raf->Tell();
    my $trailer  = $$dirInfo{DirName} || 'Unknown';
    my $pos      = $$dirInfo{DataPos};
    my $verbose  = $$self{OPTIONS}{Verbose};
    my $htmlDump = $$self{HTML_DUMP};
    my ( $buff, $buf2 );
    my $size = $$dirInfo{DirLen};
    $pos = $curPos unless defined $pos;

    for ( ; ; ) {
        unless ($size) {
            $raf->Seek( 0, 2 ) or last;
            $size = $raf->Tell() - $pos;
            last unless $size;
        }
        $raf->Seek( $pos, 0 ) or last;
        if ($htmlDump) {
            my $num  = $raf->Read( $buff, $size ) or return;
            my $desc = "$trailer trailer";
            $desc = "[$desc]" if $trailer eq 'Unknown';
            $self->HDump( $pos, $num, $desc, undef, 0x08 );
            last;
        }
        my $out = $$self{OPTIONS}{TextOut};
        printf $out "$trailer trailer (%d bytes at offset 0x%.4x):\n", $size,
          $pos;
        last unless $verbose > 2;
        my $num = $size;

        if ( $verbose < 5 ) {
            my $limit = $verbose < 4 ? 96 : 512;
            $num = $limit if $num > $limit;
        }
        $raf->Read( $buff, $num ) == $num or return;
        if ( $size > 2 * $num ) {
            $raf->Seek( $pos + $size - $num, 0 );
            $raf->Read( $buf2, $num );
        }
        elsif ( $size > $num ) {
            $raf->Seek( $pos + $num, 0 );
            $raf->Read( $buf2, $size - $num );
            $buff .= $buf2;
            undef $buf2;
        }
        HexDump( \$buff, undef, Addr => $pos, Out => $out );
        if ( defined $buf2 ) {
            print $out "    [snip ", $size - $num * 2, " bytes]\n";
            HexDump( \$buf2, undef, Addr => $pos + $size - $num, Out => $out );
        }
        last;
    }
    $raf->Seek( $curPos, 0 );
}

sub DumpUnknownTrailer($$) {
    my ( $self, $dirInfo ) = @_;
    my $pos    = $$dirInfo{DataPos};
    my $endPos = $pos + $$dirInfo{DirLen};
    my $value      = $$self{VALUE};
    my $prePos     = $$value{PreviewImageStart}  || $$self{PreviewImageStart};
    my $preLen     = $$value{PreviewImageLength} || $$self{PreviewImageLength};
    my $hidPos     = $$value{HiddenDataOffset};
    my $hidLen     = $$value{HiddenDataLength};
    my $tag        = 'PreviewImage';
    my $mpImageNum = 0;
    my ( %image, $lastOne );

    if ( $hidPos and $hidLen ) {
        require Image::ExifTool::Sony;
        my $datPt =
          Image::ExifTool::Sony::ReadHiddenData( $self, $hidPos, $hidLen );
        $image{$hidPos} = [ 'HiddenData', $hidLen ] if $datPt;
    }
    for ( ; ; ) {
        $image{$prePos} = [ $tag, $preLen ]
          if $prePos
          and $preLen
          and $prePos + $preLen > $pos;
        last if $lastOne;

        ++$mpImageNum;
        $prePos = $$value{"MPImageStart ($mpImageNum)"};
        if ( defined $prePos ) {
            $preLen = $$value{"MPImageLength ($mpImageNum)"};
        }
        else {
            $prePos  = $$value{MPImageStart};
            $preLen  = $$value{MPImageLength};
            $lastOne = 1;
        }
        $tag = "MPImage$mpImageNum";
    }
    $image{$endPos} = [ '', 0 ];
    foreach $prePos ( sort { $a <=> $b } keys %image ) {
        if ( $pos < $prePos ) {
            $$dirInfo{DirName} = 'Unknown';
            $$dirInfo{DataPos} = $pos;
            $$dirInfo{DirLen}  = $prePos - $pos;
            $self->DumpTrailer($dirInfo);
        }
        ( $tag, $preLen ) = @{ $image{$prePos} };
        last unless $preLen;
        if ( $$self{OPTIONS}{Verbose} ) {
            $$dirInfo{DirName} = $tag;
            $$dirInfo{DataPos} = $prePos;
            $$dirInfo{DirLen}  = $preLen;
            $self->DumpTrailer($dirInfo);
        }
        $pos = $prePos + $preLen;
    }
}

sub LastInList($) {
    my $element = shift;
    while ( $$element{Next} ) {
        $element = $$element{Next};
    }
    return $element;
}

sub VerboseValue($$$;$) {
    return unless $_[0]{OPTIONS}{Verbose} > 1;
    my ( $self, $str, $val, $xtra ) = @_;
    my $out = $$self{OPTIONS}{TextOut};
    $xtra or $xtra = '';
    my $maxLen = 81 - length($str) - length($xtra);
    $val = $self->Printable( $val, $maxLen );
    print $out "    $str = '${val}'$xtra\n";
}

sub PackUTF8(@) {
    my @out;
    while (@_) {
        my $ch = pop;
        unshift( @out, $ch ), next if $ch < 0x80;
        unshift( @out, 0x80 | ( $ch & 0x3f ) );
        $ch >>= 6;
        unshift( @out, 0xc0 | $ch ), next if $ch < 0x20;
        unshift( @out, 0x80 | ( $ch & 0x3f ) );
        $ch >>= 6;
        unshift( @out, 0xe0 | $ch ), next if $ch < 0x10;
        unshift( @out, 0x80 | ( $ch & 0x3f ) );
        $ch >>= 6;
        unshift( @out, 0xf0 | ( $ch & 0x07 ) );
    }
    return pack( 'C*', @out );
}

sub UnpackUTF8($) {
    my ( @out, $pos );
    pos( $_[0] ) = $pos = 0;
    for ( ; ; ) {
        my ( $ch, $newPos, $val, $byte );
        if ( $_[0] =~ /([\x80-\xff])/g ) {
            $ch     = ord($1);
            $newPos = pos( $_[0] ) - 1;
        }
        else {
            $newPos = length $_[0];
        }
        my $len = $newPos - $pos;
        push @out, unpack( "x${pos}C$len", $_[0] ) if $len;
        last unless defined $ch;
        $pos = $newPos + 1;
        if ( $ch < 0xc2 or $ch >= 0xf8 ) {
            push @out, ord('?');
            $evalWarning = 'Bad UTF-8';
            next;
        }
        my $n = 1;
        if ( $ch < 0xe0 ) {
            $val = $ch & 0x1f;
        }
        elsif ( $ch < 0xf0 ) {
            $val = $ch & 0x0f;
            ++$n;
        }
        else {
            $val = $ch & 0x07;
            $n += 2;
        }
        unless ( $_[0] =~ /\G([\x80-\xbf]{$n})/g ) {
            pos( $_[0] ) = $pos;
            push @out, ord('?');
            $evalWarning = 'Bad UTF-8';
            next;
        }
        foreach $byte ( unpack 'C*', $1 ) {
            $val = ( $val << 6 ) | ( $byte & 0x3f );
        }
        push @out, $val;
        $pos += $n;
    }
    return @out;
}

my $guidCount;

sub NewGUID() {
    my @tm = localtime time;
    $guidCount = 0 unless defined $guidCount and ++$guidCount < 0x100;
    return sprintf(
        '%.4d%.2d%.2d%.2d%.2d%.2d%.2X%.4X%.4X%.4X%.4X',
        $tm[5] + 1900, $tm[4] + 1,    $tm[3],     $tm[2],
        $tm[1],        $tm[0],        $guidCount, $$ & 0xffff,
        rand(0x10000), rand(0x10000), rand(0x10000)
    );
}

sub MakeTiffHeader($$$$;$$) {
    my ( $w, $h, $cols, $bits, $res, $cmap ) = @_;
    $res or $res = 72;
    my $saveOrder = GetByteOrder();
    SetByteOrder('II');
    if ( not $cmap ) {
        $cmap = '';
    }
    elsif ( length $cmap == 3 * 2**$bits ) {
        $cmap = pack 'v*', map { $_ | ( $_ << 8 ) } unpack 'C*', $cmap;
    }
    elsif ( length $cmap != 6 * 2**$bits ) {
        $cmap = '';
    }
    my $cmo = $cmap ? 12 : 0;
    my $hdr =
        "\x49\x49\x2a\0\x08\0\0\0\x0e\0"
      . "\xfe\x00\x04\0\x01\0\0\0\x00\0\0\0"
      . "\x00\x01\x04\0\x01\0\0\0"
      . Set32u($w)
      . "\x01\x01\x04\0\x01\0\0\0"
      . Set32u($h)
      . "\x02\x01\x03\0"
      . Set32u($cols)
      . Set32u( $cols == 1 ? $bits : 0xb6 + $cmo )
      . "\x03\x01\x03\0\x01\0\0\0\x01\0\0\0"
      . "\x06\x01\x03\0\x01\0\0\0"
      . Set32u( $cmap ? 3 : $cols == 1 ? 1 : 2 )
      . "\x11\x01\x04\0\x01\0\0\0"
      . Set32u( 0xcc + $cmo + length($cmap) )
      . "\x15\x01\x03\0\x01\0\0\0"
      . Set32u($cols)
      . "\x16\x01\x04\0\x01\0\0\0"
      . Set32u($h)
      . "\x17\x01\x04\0\x01\0\0\0"
      . Set32u( $w * $h * $cols * int( ( $bits + 7 ) / 8 ) )
      . "\x1a\x01\x05\0\x01\0\0\0"
      . Set32u( 0xbc + $cmo )
      . "\x1b\x01\x05\0\x01\0\0\0"
      . Set32u( 0xc4 + $cmo )
      . "\x1c\x01\x03\0\x01\0\0\0\x01\0\0\0"
      . "\x28\x01\x03\0\x01\0\0\0\x02\0\0\0"
      . (
        $cmap ? "\x40\x01\x03\0" . Set32u( 3 * 2**$bits ) . "\xd8\0\0\0" : '' )
      . "\0\0\0\0"
      . ( Set16u($bits) x 3 )
      . Set32u($res)
      . "\x01\0\0\0"
      . Set32u($res)
      . "\x01\0\0\0"
      . $cmap;
    SetByteOrder($saveOrder);
    return $hdr;
}

sub TimeNow(;$$) {
    my ( $self, $tzFlag ) = @_;
    my $timeNow;
    ref $self or $tzFlag = $self, $self = {};
    if ( $$self{Now} ) {
        $timeNow = $$self{Now}[0];
    }
    else {
        my $time = time();
        my @tm   = localtime $time;
        my $tz   = TimeZoneString( \@tm, $time );
        $timeNow = sprintf(
            "%4d:%.2d:%.2d %.2d:%.2d:%.2d",
            $tm[5] + 1900,
            $tm[4] + 1,
            $tm[3], $tm[2], $tm[1], $tm[0]
        );
        $$self{Now} = [ $timeNow, $tz ];
    }
    $timeNow .= $$self{Now}[1] if $tzFlag or not defined $tzFlag;
    return $timeNow;
}

my $strptimeLib;

sub InverseDateTime($$;$$) {
    my ( $self, $val, $tzFlag, $dateOnly ) = @_;
    my ( $rtnVal, $tz, $fs );
    my $fmt = $$self{OPTIONS}{DateFormat};
    if ( not $fmt and $val =~ s/([-+])(\d{1,2}):?(\d{2})\s*(DST)?$//i ) {
        $tz = sprintf( "$1%.2d:$3", $2 );
    }
    elsif ( not $fmt and $val =~ s/Z$//i ) {
        $tz = 'Z';
    }
    else {
        $tz = '';
        return $self->TimeNow($tzFlag) if lc($val) eq 'now';
    }
    if ($fmt) {
        unless ( defined $strptimeLib ) {
            if ( eval { require POSIX::strptime } ) {
                $strptimeLib = 'POSIX::strptime';
            }
            elsif ( eval { require Time::Piece } ) {
                $strptimeLib = 'Time::Piece';
                eval { Time::Piece->use_locale() };
            }
            else {
                $strptimeLib = '';
            }
        }
        ( $fs, $tz ) = ( '', '' );
        if ( $fmt =~ /%(f|:?z)/ ) {
            if ( $fmt =~ s/(.*[^%])%f/$1/ ) {
                $fs = $2 if $val =~ s/(.*)(\.\d+)/$1/;
            }
            if ( $fmt =~ s/(.*[^%])%(:?)z/$1/ ) {
                my $colon = $2;
                $tz = "$2:$3" if $val =~ s/(.*)([-+]\d{2})$colon(\d{2})/$1/;
            }
        }
        my ( $lib, $wrn, @a );
      TryLib: for ( $lib = $strptimeLib ; ; $lib = '' ) {
            if ( $fmt eq '%s' ) {
                $val = ConvertUnixTime( $val, 1 );
                last;
            }
            if ( not $lib ) {
                last unless $$self{OPTIONS}{StrictDate};
                warn $wrn
                  || "Install POSIX::strptime or Time::Piece for inverse date/time conversions\n";
                return undef;
            }
            elsif ( $lib eq 'POSIX::strptime' ) {
                @a = eval { POSIX::strptime( $val, $fmt ) };
            }
            else {
                if ( $^O eq 'MSWin32' and $fmt =~ /%s/ and $val =~ /-\d/ ) {
                    warn "Can't convert negative epoch time\n";
                    return undef;
                }
                @a = eval {
                    my $t = Time::Piece->strptime( $val, $fmt );
                    return (
                        $t->sec,  $t->min,  $t->hour,
                        $t->mday, $t->_mon, $t->_year
                    );
                };
            }
            if ( defined $a[5] and length $a[5] ) {
                $a[5] += 1900;
            }
            else {
                $wrn = "Invalid date/time (no year) using $lib\n";
                next;
            }
            ++$a[4] if defined $a[4] and length $a[4];
            my $i;
            foreach $i ( 0 .. 4 ) {
                if ( not defined $a[$i] or not length $a[$i] ) {
                    if ( $i < 2 or $dateOnly ) {
                        $a[$i] = '  ';
                    }
                    else {
                        $wrn =
                          "Incomplete date/time specification using $lib\n";
                        next TryLib;
                    }
                }
                elsif ( length( $a[$i] ) < 2 ) {
                    $a[$i] = "0$a[$i]";
                }
            }
            $val =
                join( ':', @a[ 5, 4, 3 ] ) . ' '
              . join( ':', @a[ 2, 1, 0 ] )
              . $fs
              . $tz;
            last;
        }
    }
    if ( $val =~ /(\d{4})/g ) {
        my $yr = $1;
        my @a  = ( $val =~ /\d{1,2}/g );
        length($_) < 2 and $_ = "0$_" foreach @a;
        if ( @a >= 3 ) {
            my $ss = $a[4];
            push @a, '00' while @a < 5;

            unless ($fmt) {
                $fs = ( @a > 5 and $val =~ /(\.\d+)\s*$/ ) ? $1 : '';
            }
            if ($tzFlag) {
                if ( not $tz ) {
                    if ( eval { require Time::Local } ) {
                        my @args =
                          ( $a[4], $a[3], $a[2], $a[1], $a[0] - 1, $yr );
                        my $diff =
                          Time::Local::timegm(@args) - TimeLocal(@args);
                        $tz = TimeZoneString( $diff / 60 );
                    }
                    else {
                        $tz = 'Z';
                    }
                }
            }
            elsif ( defined $tzFlag ) {
                $tz = $fs = '';
            }
            if ( defined $ss and $ss < 60 ) {
                $ss = ":$ss";
            }
            elsif ($dateOnly) {
                $ss = '';
            }
            else {
                $ss = ':00';
            }
            if ( $a[0] < 1 or $a[0] > 12 ) {
                warn "Month '$a[0]' out of range 1..12\n";
                return undef;
            }
            if ( $a[1] < 1 or $a[1] > 31 ) {
                warn "Day '$a[1]' out of range 1..31\n";
                return undef;
            }
            $a[2] > 24
              and warn("Hour '$a[2]' out of range 0..24\n"), return undef;
            $a[3] > 59
              and warn("Minutes '$a[3]' out of range 0..59\n"), return undef;
            $rtnVal = "$yr:$a[0]:$a[1] $a[2]:$a[3]$ss$fs$tz";
        }
        elsif ($dateOnly) {
            $rtnVal = join ':', $yr, @a;
        }
    }
    $rtnVal
      or warn "Invalid date/time (use YYYY:mm:dd HH:MM:SS[.ss][+/-HH:MM|Z])\n";
    return $rtnVal;
}

sub SetPreferredByteOrder($;$) {
    my ( $self, $default ) = @_;
    my $byteOrder =
         $self->Options('ByteOrder')
      || $self->GetNewValue('ExifByteOrder')
      || $default
      || $$self{MAKER_NOTE_BYTE_ORDER}
      || 'MM';
    unless ( SetByteOrder($byteOrder) ) {
        warn "Invalid byte order '${byteOrder}'\n" if $self->Options('Verbose');
        $byteOrder = $$self{MAKER_NOTE_BYTE_ORDER} || 'MM';
        SetByteOrder($byteOrder);
    }
    return GetByteOrder();
}

sub AssembleRational($$@) {
    @_ < 3 and return @_;
    my ( $num, $denom, $frac ) = splice( @_, 0, 3 );
    return AssembleRational( $frac * $num + $denom, $num, @_ );
}

sub Rationalize($;$) {
    my $val = shift;
    return ( 1,  0 )  if $val eq 'inf';
    return ( 0,  0 )  if $val eq 'undef';
    return ( $1, $2 ) if $val =~ m{^([-+]?\d+)/(\d+)$};

    return ( 0, 1 ) if $val == 0;
    my $sign = $val < 0 ? ( $val = -$val, -1 ) : 1;
    my ( $num, $denom, @fracs );
    my $frac   = $val;
    my $maxInt = shift || 0x7fffffff;
    for ( ; ; ) {
        my ( $n, $d ) = AssembleRational( int( $frac + 0.5 ), 1, @fracs );
        if ( $n > $maxInt or $d > $maxInt ) {
            last if defined $num;
            return ( $sign, $maxInt ) if $val < 1;
            return ( $sign * $maxInt, 1 );
        }
        ( $num, $denom ) = ( $n, $d );
        my $err = ( $n / $d - $val ) / $val;
        last if abs($err) < 1e-8;
        my $int = int($frac);
        unshift @fracs, $int;
        last unless $frac -= $int;
        $frac = 1 / $frac;
    }
    return ( $num * $sign, $denom );
}

sub Set16s(@) {
    my $val = shift;
    $val < 0 and $val += 0x10000;
    return Set16u( $val, @_ );
}

sub Set32s(@) {
    my $val = shift;
    $val < 0 and $val += 0xffffffff, ++$val;
    return Set32u( $val, @_ );
}

sub Set64u(@) {
    my $val = $_[0];
    my $hi  = int( $val / 4294967296 );
    my $lo  = Set32u( $val - $hi * 4294967296 );
    $hi  = Set32u($hi);
    $val = GetByteOrder() eq 'MM' ? $hi . $lo : $lo . $hi;
    $_[1] and substr( ${ $_[1] }, $_[2], length($val) ) = $val;
    return $val;
}

sub Set64s(@) {
    my $val = shift;
    $val < 0 and $val += 4294967296 * 4294967296;
    return Set64u( $val, @_ );
}

sub SetRational64u(@) {
    my ( $numer, $denom ) = Rationalize( $_[0], 0xffffffff );
    my $val = Set32u($numer) . Set32u($denom);
    $_[1] and substr( ${ $_[1] }, $_[2], length($val) ) = $val;
    return $val;
}

sub SetRational64s(@) {
    my ( $numer, $denom ) = Rationalize( $_[0] );
    my $val = Set32s($numer) . Set32u($denom);
    $_[1] and substr( ${ $_[1] }, $_[2], length($val) ) = $val;
    return $val;
}

sub SetRational32u(@) {
    my ( $numer, $denom ) = Rationalize( $_[0], 0xffff );
    my $val = Set16u($numer) . Set16u($denom);
    $_[1] and substr( ${ $_[1] }, $_[2], length($val) ) = $val;
    return $val;
}

sub SetRational32s(@) {
    my ( $numer, $denom ) = Rationalize( $_[0], 0x7fff );
    my $val = Set16s($numer) . Set16u($denom);
    $_[1] and substr( ${ $_[1] }, $_[2], length($val) ) = $val;
    return $val;
}

sub SetFixed16u(@) {
    my $val = int( shift() * 0x100 + 0.5 );
    return Set16u( $val, @_ );
}

sub SetFixed16s(@) {
    my $val = shift;
    return Set16s( int( $val * 0x100 + ( $val < 0 ? -0.5 : 0.5 ) ), @_ );
}

sub SetFixed32u(@) {
    my $val = int( shift() * 0x10000 + 0.5 );
    return Set32u( $val, @_ );
}

sub SetFixed32s(@) {
    my $val = shift;
    return Set32s( int( $val * 0x10000 + ( $val < 0 ? -0.5 : 0.5 ) ), @_ );
}

sub SetFloat(@) {
    my $val = SwapBytes( pack( 'f', $_[0] ), 4 );
    $_[1] and substr( ${ $_[1] }, $_[2], length($val) ) = $val;
    return $val;
}

sub SetDouble(@) {
    my $val = SwapBytes( SwapWords( pack( 'd', $_[0] ) ), 8 );
    $_[1] and substr( ${ $_[1] }, $_[2], length($val) ) = $val;
    return $val;
}
my %writeValueProc = (
    int8s       => \&Set8s,
    int8u       => \&Set8u,
    int16s      => \&Set16s,
    int16u      => \&Set16u,
    int16uRev   => \&Set16uRev,
    int32s      => \&Set32s,
    int32u      => \&Set32u,
    int64s      => \&Set64s,
    int64u      => \&Set64u,
    rational32s => \&SetRational32s,
    rational32u => \&SetRational32u,
    rational64s => \&SetRational64s,
    rational64u => \&SetRational64u,
    fixed16u    => \&SetFixed16u,
    fixed16s    => \&SetFixed16s,
    fixed32u    => \&SetFixed32u,
    fixed32s    => \&SetFixed32s,
    float       => \&SetFloat,
    double      => \&SetDouble,
    ifd         => \&Set32u,
);
{
    my %writeTest = (
        float  => [ -3.14159, 'c0490fd0' ],
        double => [ -3.14159, 'c00921f9f01b866e' ],
    );
    my $format;
    my $oldOrder = GetByteOrder();
    SetByteOrder('MM');
    foreach $format ( keys %writeTest ) {
        my ( $val, $hex ) = @{ $writeTest{$format} };
        next if unpack( 'H*', &{ $writeValueProc{$format} }($val) ) eq $hex;
        delete $writeValueProc{$format};
    }
    SetByteOrder($oldOrder);
}

sub WriteValue($$;$$$$) {
    my ( $val, $format, $count, $dataPt, $offset ) = @_;
    my $proc = $writeValueProc{$format};
    my $packed;

    if ($proc) {
        my @vals = split( ' ', $val );
        if ($count) {
            $count = @vals if $count < 0;
        }
        else {
            $count = 1;
        }
        $packed = '';
        while ( $count-- ) {
            $val = shift @vals;
            return undef unless defined $val;
            if ( $format =~ /^int/ ) {
                unless ( IsInt($val) or IsHex($val) ) {
                    return undef unless IsFloat($val);
                    $val = int( $val + ( $val < 0 ? -0.5 : 0.5 ) );
                    $_[0] = $val;
                }
            }
            elsif ( not IsFloat($val) ) {
                return undef
                  unless $format =~ /^rational/
                  and ($val eq 'inf'
                    or $val eq 'undef'
                    or IsRational($val) );
            }
            $packed .= &$proc($val);
        }
    }
    elsif ( $format eq 'string' or $format eq 'undef' ) {
        $format eq 'string' and $val .= "\0";
        if ( $count and $count > 0 ) {
            my $diff = $count - length($val);
            if ($diff) {
                if ( $diff < 0 ) {
                    if ( $format eq 'string' ) {
                        return undef unless $count;
                        $val = substr( $val, 0, $count - 1 ) . "\0";
                    }
                    else {
                        $val = substr( $val, 0, $count );
                    }
                }
                else {
                    $val .= "\0" x $diff;
                }
            }
        }
        else {
            $count = length($val);
        }
        $dataPt and substr( $$dataPt, $offset, $count ) = $val;
        return $val;
    }
    else {
        warn "Sorry, Can't write $format values on this platform\n";
        return undef;
    }
    $dataPt and substr( $$dataPt, $offset, length($packed) ) = $packed;
    return $packed;
}

sub EncodeBits($$;$$) {
    my ( $val, $lookup, $bits, $num ) = @_;
    $bits or $bits = 32;
    $num  or $num  = $bits;
    my $words  = int( ( $num + $bits - 1 ) / $bits );
    my @outVal = (0) x $words;
    if ( $val ne '(none)' ) {
        my @vals = split /\s*,\s*/, $val;
        foreach $val (@vals) {
            my $bit;
            if ($lookup) {
                $bit = ReverseLookup( $val, $lookup );
                unless ( defined $bit ) {
                    if ( $val =~ /\[(\d+)\]/ ) {
                        $bit = $1;
                    }
                    else {
                        return undef unless @vals > 1 and wantarray;
                        return ( undef, "no match for '${val}'" );
                    }
                }
            }
            else {
                $bit = $val;
            }
            unless ( IsInt($bit) and $bit < $num ) {
                return undef unless wantarray;
                return ( undef,
                    IsInt($bit) ? 'bit number too high' : 'not an integer' );
            }
            my $word = int( $bit / $bits );
            $outVal[$word] |= ( 1 << ( $bit - $word * $bits ) );
        }
    }
    return "@outVal";
}

sub Tell($) {
    my $outfile = shift;
    if ( UNIVERSAL::isa( $outfile, 'GLOB' ) ) {
        return tell($outfile);
    }
    else {
        return length($$outfile);
    }
}

sub Write($@) {
    my $outfile = shift;
    if ( UNIVERSAL::isa( $outfile, 'GLOB' ) ) {
        return print $outfile @_;
    }
    elsif ( ref $outfile eq 'SCALAR' ) {
        $$outfile .= join( '', @_ );
        return 1;
    }
    return 0;
}

sub WriteTrailerBuffer($$$) {
    my ( $self, $trailInfo, $outfile ) = @_;
    if ( $$self{DEL_GROUP}{Trailer} ) {
        $self->VPrint( 0, "  Deleting trailer ($$trailInfo{Offset} bytes)\n" );
        ++$$self{CHANGED};
        return 1;
    }
    my $pos     = Tell($outfile);
    my $trailPt = $$trailInfo{OutFile};
    if ( $$trailInfo{Fixup} ) {
        if ( $pos > 0 ) {
            $$trailInfo{Fixup}{Shift} += $pos;
            $$trailInfo{Fixup}->ApplyFixup($trailPt);
        }
        else {
            $self->Error( "Can't get file position for trailer offset fixup",
                1 );
        }
    }
    return Write( $outfile, $$trailPt );
}

sub AddNewTrailers($;@) {
    my ( $self, @types ) = @_;
    my $trailPt;
    ref $types[0] and $trailPt = shift @types;
    $types[0] or shift @types;

    @types or @types = qw(CanonVRD CanonDR4);
    my $type;
    foreach $type (@types) {
        next unless $$self{NEW_VALUE}{ $Image::ExifTool::Extra{$type} };
        next if $$self{"Did$type"};
        my $val = $self->GetNewValue($type) or next;
        if ( $type eq 'CanonDR4' ) {
            next if $$self{DidCanonVRD};
            require Image::ExifTool::CanonVRD;
            $val = Image::ExifTool::CanonVRD::WrapDR4($val);
            $$self{DidCanonVRD} = 1;
        }
        my $verb = $trailPt ? 'Writing' : 'Adding';
        $self->VPrint( 0, "  $verb $type as a block\n" );
        if ($trailPt) {
            $$trailPt .= $val;
        }
        else {
            $trailPt = \$val;
        }
        $$self{"Did$type"} = 1;
        ++$$self{CHANGED};
    }
    return $trailPt;
}

sub WriteMultiSegment($$$$;$) {
    my ( $outfile, $marker, $header, $dataPt, $type ) = @_;
    $type or $type = '';
    my $len    = length($$dataPt);
    my $hdr    = "\xff" . chr($marker);
    my $count  = 0;
    my $maxLen = $maxSegmentLen - length($header);
    $maxLen -= 2 if $type eq 'ICC';
    my $num = int( ( $len + $maxLen - 1 ) / $maxLen );
    my $n   = 0;

    for ( ; ; ) {
        ++$count;
        my $size = $len - $n;
        if ( $size > $maxLen ) {
            $size = $maxLen;
            --$size
              if $type eq 'EXIF'
              and $n + $maxLen <= $len - 4
              and substr( $$dataPt, $n + $maxLen, 4 ) =~ /^(MM\0\x2a|II\x2a\0)/;
        }
        my $buff = substr( $$dataPt, $n, $size );
        $n    += $size;
        $size += length($header);
        if ( $type eq 'ICC' ) {
            $buff = pack( 'CC', $count, $num ) . $buff;
            $size += 2;
        }
        my $segHdr = $hdr . pack( 'n', $size + 2 );
        Write( $outfile, $segHdr, $header, $buff ) or return 0;
        last if $n >= $len;
    }
    return $count;
}

sub WriteMultiXMP($$$$$) {
    my ( $self, $outfile, $dataPt, $extPt, $guid ) = @_;
    my $success = 1;

    my $size = length($$dataPt) + length($xmpAPP1hdr);
    if ( $size > $maxXMPLen ) {
        $self->Error( "XMP block too large for JPEG segment! ($size bytes)",
            1 );
        return 1;
    }
    my $app1hdr = "\xff\xe1" . pack( 'n', $size + 2 );
    Write( $outfile, $app1hdr, $xmpAPP1hdr, $$dataPt ) or $success = 0;
    if ( defined $guid ) {
        $size = length($$extPt);
        my $maxLen = $maxXMPLen - 75;
        my $off;
        for ( $off = 0 ; $off < $size ; $off += $maxLen ) {
            my $len = $size - $off;
            $len     = $maxLen if $len > $maxLen;
            $app1hdr = "\xff\xe1" . pack( 'n', $len + 75 + 2 );
            $self->VPrint( 0, "Writing extended XMP segment ($len bytes)\n" );
            Write(
                $outfile, $app1hdr, $xmpExtAPP1hdr, $guid,
                pack( 'N2', $size, $off ),
                substr( $$extPt, $off, $len )
            ) or $success = 0;
        }
    }
    return $success;
}

sub WriteJPEG($$) {
    my ( $self, $dirInfo ) = @_;
    my $outfile = $$dirInfo{OutFile};
    my $raf     = $$dirInfo{RAF};
    my ( $ch, $s, $length, $err, %doneDir, $isEXV, $creatingEXV );
    my $verbose = $$self{OPTIONS}{Verbose};
    my $out     = $$self{OPTIONS}{TextOut};
    my $rtnVal  = 0;
    my ( $writeBuffer, $oldOutfile );

    unless ( $raf->Read( $s, 2 ) == 2 and $s eq "\xff\xd8" ) {
        if ( defined $s and length $s ) {
            return 0
              unless $s eq "\xff\x01"
              and $raf->Read( $s, 5 ) == 5
              and $s eq 'Exiv2';
        }
        else {
            return 0 unless $$self{FILE_TYPE} eq 'EXV';
            $s           = 'Exiv2';
            $creatingEXV = 1;
        }
        Write( $outfile, "\xff\x01" ) or $err = 1;
        $isEXV = 1;
    }

    delete $$self{PREVIEW_INFO};
    delete $$self{DEL_PREVIEW};

    Write( $outfile, $s ) or $err = 1;
    my $addDirs  = $$self{ADD_DIRS};
    my $editDirs = $$self{EDIT_DIRS};
    my $delGroup = $$self{DEL_GROUP};
    my $path     = $$self{PATH};
    my $pn       = scalar @$path;

    local $/ = "\xff";
    my $pos = $raf->Tell();
    my ( $marker, @dirOrder, %dirCount );
  Prescan: for ( ; ; ) {
        $raf->ReadLine($s) or last;
        for ( ; ; ) {
            $raf->Read( $ch, 1 ) or last Prescan;
            $marker = ord($ch);
            last unless $marker == 0xff;
        }
        my $dirName;
        if ( $marker == 0xda or $marker == 0xd9 ) {
            $dirName = $jpegMarker{$marker};
            push( @dirOrder, $dirName );
            $dirCount{$dirName} = 1;
            last;
        }
        if (    ( $marker & 0xf0 ) == 0xc0
            and ( $marker == 0xc0 or $marker & 0x03 ) )
        {
            last unless $raf->Seek( 7, 1 );
        }
        elsif ( $marker != 0x00
            and $marker != 0x01
            and ( $marker < 0xd0 or $marker > 0xd7 ) )
        {
            last unless $raf->Read( $s, 2 ) == 2;
            my $len = unpack( 'n', $s );
            last unless defined($len) and $len >= 2;
            $len -= 2;
            if ( ( $marker & 0xf0 ) == 0xe0 ) {
                my $n = $len < 64 ? $len : 64;
                $raf->Read( $s, $n ) == $n or last;
                $len -= $n;
                if ( $marker == 0xe0 ) {
                    $s =~ /^JFIF\0/               and $dirName = 'JFIF';
                    $s =~ /^JFXX\0\x10/           and $dirName = 'JFXX';
                    $s =~ /^(II|MM).{4}HEAPJPGM/s and $dirName = 'CIFF';
                }
                elsif ( $marker == 0xe1 ) {
                    if ( $s =~ /^(.{0,4})Exif\0.(.{1,4})/is ) {
                        $dirName = 'IFD0';
                        my ( $junk, $bytes ) = ( $1, $2 );
                        if (    @dirOrder
                            and $dirOrder[-1] =~ /^(IFD0|ExtendedEXIF)$/
                            and not length $junk
                            and $bytes !~ /^(MM\0\x2a|II\x2a\0)/ )
                        {
                            $dirName = 'ExtendedEXIF';
                        }
                    }
                    $s =~ /^$xmpAPP1hdr/    and $dirName = 'XMP';
                    $s =~ /^$xmpExtAPP1hdr/ and $dirName = 'XMP';
                }
                elsif ( $marker == 0xe2 ) {
                    $s =~ /^ICC_PROFILE\0/ and $dirName = 'ICC_Profile';
                    $s =~ /^FPXR\0/        and $dirName = 'FlashPix';
                    $s =~ /^MPF\0/         and $dirName = 'MPF';
                }
                elsif ( $marker == 0xe3 ) {
                    $s =~ /^(Meta|META|Exif)\0\0/ and $dirName = 'Meta';
                }
                elsif ( $marker == 0xe5 ) {
                    $s =~ /^RMETA\0/ and $dirName = 'RMETA';
                }
                elsif ( $marker == 0xea ) {
                    $s =~ /^AROT\0\0/ and $dirName = 'AROT';
                }
                elsif ( $marker == 0xeb ) {
                    $s =~ /^JP/ and $dirName = 'JUMBF';
                }
                elsif ( $marker == 0xec ) {
                    $s =~ /^Ducky/ and $dirName = 'Ducky';
                }
                elsif ( $marker == 0xed ) {
                    $s =~ /^$psAPP13hdr/ and $dirName = 'Photoshop';
                }
                elsif ( $marker == 0xee ) {
                    $s =~ /^Adobe/ and $dirName = 'Adobe';
                }
                $doneDir{$dirName} = 0
                  if defined $dirName and not $$delGroup{$dirName};
            }
            $raf->Seek( $len, 1 ) or last;
        }
        $dirName or $dirName = JpegMarkerName($marker);
        $dirCount{$dirName} = ( $dirCount{$dirName} || 0 ) + 1;
        push @dirOrder, $dirName;
    }
    unless ( $marker and $marker == 0xda ) {
        $isEXV or $self->Error('Corrupted JPEG image'), return 1;
        $marker
          and $marker != 0xd9
          and $self->Error('Corrupted EXV file'), return 1;
    }
    $raf->Seek( $pos, 0 ) or $self->Error('Seek error'), return 1;
    my ( $combinedSegData, $segPos, $firstSegPos, %extendedXMP );
    my ( @iccChunk, $iccChunkCount, $iccChunksTotal );
  Marker: for ( ; ; ) {

        my $segJunk;
        $raf->ReadLine($segJunk) or $segJunk = '';
        chomp($segJunk);
        Write( $outfile, $segJunk ) if length $segJunk;
        for ( ; ; ) {
            if ( $raf->Read( $ch, 1 ) ) {
                $marker = ord($ch);
                last unless $marker == 0xff;
            }
            elsif ($creatingEXV) {
                $marker = 0xd9;
                push @dirOrder, 'EOI';
                $dirCount{EOI} = 1;
                last;
            }
            else {
                $self->Error('Format error');
                return 1;
            }
        }
        my $segData;
        if (    ( $marker & 0xf0 ) == 0xc0
            and ( $marker == 0xc0 or $marker & 0x03 ) )
        {
            last unless $raf->Read( $segData, 7 ) == 7;
        }
        elsif ( $marker != 0x00
            and $marker != 0x01
            and $marker != 0xd9
            and ( $marker < 0xd0 or $marker > 0xd7 ) )
        {
            last unless $raf->Read( $s, 2 ) == 2;
            my $len = unpack( 'n', $s );
            last unless defined($len) and $len >= 2;
            $segPos = $raf->Tell();
            $len -= 2;
            last unless $raf->Read( $segData, $len ) == $len;
        }
        my $hdr        = "\xff" . chr($marker);
        my $markerName = JpegMarkerName($marker);
        my $dirName    = shift @dirOrder;
        while ( $markerName ne 'SOI' ) {
            if ( exists $$addDirs{JFIF} and not defined $doneDir{JFIF} ) {
                $doneDir{JFIF} = 1;
                if ( defined $doneDir{Adobe} ) {
                    $self->Warn('Not creating JFIF in JPEG with Adobe APP14');
                }
                else {
                    if ($verbose) {
                        print $out "Creating APP0:\n";
                        print $out "  Creating JFIF with default values\n";
                    }
                    my $jfif = "\x01\x02\x01\0\x48\0\x48\0\0";
                    SetByteOrder('MM');
                    my $tagTablePtr =
                      GetTagTable('Image::ExifTool::JFIF::Main');
                    my %dirInfo = (
                        DataPt   => \$jfif,
                        DirStart => 0,
                        DirLen   => length $jfif,
                        Parent   => 'JFIF',
                    );
                    my $delJFIF = $$delGroup{JFIF};
                    delete $$delGroup{JFIF};
                    $$path[$pn] = 'JFIF';
                    my $newData =
                      $self->WriteDirectory( \%dirInfo, $tagTablePtr );
                    $$delGroup{JFIF} = $delJFIF if defined $delJFIF;

                    if ( defined $newData and length $newData ) {
                        my $app0hdr =
                          "\xff\xe0" . pack( 'n', length($newData) + 7 );
                        Write( $outfile, $app0hdr, "JFIF\0", $newData )
                          or $err = 1;
                    }
                }
            }
            last
              if $markerName eq 'APP0'
              or $dirCount{IFD0}
              or $dirCount{ExtendedEXIF};
            if ( exists $$addDirs{IFD0} and not defined $doneDir{IFD0} ) {
                $doneDir{IFD0} = 1;
                $verbose and print $out "Creating APP1:\n";
                $$self{TIFF_TYPE} = 'APP1';
                my $tagTablePtr = GetTagTable('Image::ExifTool::Exif::Main');
                my %dirInfo     = (
                    DirName => 'IFD0',
                    Parent  => 'APP1',
                );
                $$path[$pn] = 'APP1';
                my $buff =
                  $self->WriteDirectory( \%dirInfo, $tagTablePtr, \&WriteTIFF );
                if ( defined $buff and length $buff ) {
                    if ( length($buff) + length($exifAPP1hdr) > $maxSegmentLen )
                    {
                        if ( $self->Options('NoMultiExif') ) {
                            $self->Error('EXIF is too large for JPEG segment');
                        }
                        else {
                            $self->Warn( 'Creating multi-segment EXIF', 1 );
                        }
                    }
                    if (
                        (
                               $$self{PREVIEW_INFO}
                            or $$self{LeicaTrailer}
                            or $$self{HiddenData}
                        )
                        and not $oldOutfile
                      )
                    {
                        $writeBuffer = '';
                        $oldOutfile  = $outfile;
                        $outfile     = \$writeBuffer;
                        foreach (qw(PREVIEW_INFO LeicaTrailer HiddenData)) {
                            $$self{$_}{Fixup}{Start} += 18 if $$self{$_};
                        }
                    }
                    my $n =
                      WriteMultiSegment( $outfile, 0xe1, $exifAPP1hdr, \$buff,
                        'EXIF' );
                    if ( not $n ) {
                        $err = 1;
                    }
                    elsif ( $n > 1 and $oldOutfile ) {
                        $self->Error(
"Can't write multi-segment EXIF with external pointers"
                        );
                    }
                    ++$$self{CHANGED};
                }
            }
            last if $dirCount{Photoshop};
            if ( exists $$addDirs{Photoshop}
                and not defined $doneDir{Photoshop} )
            {
                $doneDir{Photoshop} = 1;
                $verbose and print $out "Creating APP13:\n";
                my $tagTablePtr =
                  GetTagTable('Image::ExifTool::Photoshop::Main');
                my %dirInfo = ( Parent => 'APP13', );
                $$path[$pn] = 'APP13';
                my $buff = $self->WriteDirectory( \%dirInfo, $tagTablePtr );
                if ( defined $buff and length $buff ) {
                    WriteMultiSegment( $outfile, 0xed, $psAPP13hdr, \$buff )
                      or $err = 1;
                    ++$$self{CHANGED};
                }
            }
            last if $dirCount{XMP};
            if ( exists $$addDirs{XMP} and not defined $doneDir{XMP} ) {
                $doneDir{XMP} = 1;
                $verbose and print $out "Creating APP1:\n";
                my $tagTablePtr = GetTagTable('Image::ExifTool::XMP::Main');
                my %dirInfo     = (
                    Parent => 'APP1',
                    MaxDataLen => $maxXMPLen - length($xmpAPP1hdr),
                );
                $$path[$pn] = 'APP1';
                my $buff = $self->WriteDirectory( \%dirInfo, $tagTablePtr );
                if ( defined $buff and length $buff ) {
                    WriteMultiXMP( $self, $outfile, \$buff,
                        $dirInfo{ExtendedXMP}, $dirInfo{ExtendedGUID} )
                      or $err = 1;
                }
            }
            last if $dirCount{ICC_Profile};
            if ( exists $$addDirs{ICC_Profile}
                and not defined $doneDir{ICC_Profile} )
            {
                $doneDir{ICC_Profile} = 1;
                next
                  if $$delGroup{ICC_Profile} and $$delGroup{ICC_Profile} != 2;
                $verbose and print $out "Creating APP2:\n";
                my $tagTablePtr =
                  GetTagTable('Image::ExifTool::ICC_Profile::Main');
                my %dirInfo = ( Parent => 'APP2', );
                $$path[$pn] = 'APP2';
                my $buff = $self->WriteDirectory( \%dirInfo, $tagTablePtr );
                if ( defined $buff and length $buff ) {
                    WriteMultiSegment( $outfile, 0xe2, "ICC_PROFILE\0", \$buff,
                        'ICC' )
                      or $err = 1;
                    ++$$self{CHANGED};
                }
            }
            last if $dirCount{Ducky};
            if ( exists $$addDirs{Ducky} and not defined $doneDir{Ducky} ) {
                $doneDir{Ducky} = 1;
                $verbose and print $out "Creating APP12 Ducky:\n";
                my $tagTablePtr = GetTagTable('Image::ExifTool::APP12::Ducky');
                my %dirInfo     = ( Parent => 'APP12', );
                $$path[$pn] = 'APP12';
                my $buff = $self->WriteDirectory( \%dirInfo, $tagTablePtr );
                if ( defined $buff and length $buff ) {
                    my $size = length($buff) + 5;
                    if ( $size <= $maxSegmentLen ) {
                        my $app12hdr = "\xff\xec" . pack( 'n', $size + 2 );
                        Write( $outfile, $app12hdr, 'Ducky', $buff )
                          or $err = 1;
                    }
                    else {
                        $self->Warn(
                            "APP12 Ducky segment too large! ($size bytes)");
                    }
                }
            }
            last if $dirCount{Adobe};
            if ( exists $$addDirs{Adobe} and not defined $doneDir{Adobe} ) {
                $doneDir{Adobe} = 1;
                my $buff = $self->GetNewValue('Adobe');
                if ($buff) {
                    $verbose
                      and print $out
                      "Creating APP14:\n  Creating Adobe segment\n";
                    my $size = length($buff);
                    if ( $size <= $maxSegmentLen ) {
                        my $app14hdr = "\xff\xee" . pack( 'n', $size + 2 );
                        Write( $outfile, $app14hdr, $buff ) or $err = 1;
                        ++$$self{CHANGED};
                    }
                    else {
                        $self->Warn(
                            "APP14 Adobe segment too large! ($size bytes)");
                    }
                }
            }
            last if $dirCount{COM};
            if ( exists $$addDirs{COM} and not defined $doneDir{COM} ) {
                $doneDir{COM} = 1;
                next if $$delGroup{File} and $$delGroup{File} != 2;
                my $newComment = $self->GetNewValue('Comment');
                if ( defined $newComment ) {
                    if ($verbose) {
                        print $out "Creating COM:\n";
                        $self->VerboseValue( '+ Comment', $newComment );
                    }
                    WriteMultiSegment( $outfile, 0xfe, '', \$newComment )
                      or $err = 1;
                    ++$$self{CHANGED};
                }
            }
            last;
        }
        $$path[$pn] = $markerName;
        --$dirCount{$dirName};
        if (    ( $marker & 0xf0 ) == 0xc0
            and ( $marker == 0xc0 or $marker & 0x03 ) )
        {
            $verbose and print $out "JPEG $markerName:\n";
            Write( $outfile, $hdr, $segData ) or $err = 1;
            next;
        }
        elsif ( $marker == 0xda ) {
            pop @$path;
            $verbose and print $out "JPEG SOS\n";
            $s = pack( 'n', length($segData) + 2 );
            Write( $outfile, $hdr, $s, $segData ) or $err = 1;
            my ( $buff, $endPos, $trailInfo );
            my $delPreview = $$self{DEL_PREVIEW};
            $trailInfo = $self->IdentifyTrailer($raf)
              unless $$delGroup{Trailer};
            my $nvTrail =
              $self->GetNewValueHash( $Image::ExifTool::Extra{Trailer} );

            unless ( $oldOutfile
                or $delPreview
                or $trailInfo
                or $$delGroup{Trailer}
                or $nvTrail
                or $$self{HiddenData} )
            {
                while ( $raf->Read( $buff, 65536 ) ) {
                    Write( $outfile, $buff ) or $err = 1, last;
                }
                $rtnVal = 1;
                last;
            }
            my $endedWithFF;
            for ( ; ; ) {
                my $n = $raf->Read( $buff, 65536 ) or last Marker;
                if ( ( $endedWithFF and $buff =~ m/^\xd9/sg )
                    or $buff =~ m/\xff\xd9/sg )
                {
                    $rtnVal = 1;

                    my $pos = pos($buff);
                    Write( $outfile, substr( $buff, 0, $pos ) ) or $err = 1;
                    $buff = substr( $buff, $pos );
                    last;
                }
                unless ( $n == 65536 ) {
                    $self->Error('JPEG EOI marker not found');
                    last Marker;
                }
                Write( $outfile, $buff ) or $err = 1;
                $endedWithFF = substr( $buff, 65535, 1 ) eq "\xff" ? 1 : 0;
            }
            $endPos = $$self{TrailerStart} = $raf->Tell() - length($buff);
            if ($nvTrail) {
                if ( $$nvTrail{Value} and $$nvTrail{Value}[0] ) {
                    $self->VPrint( 0, '  Writing new trailer' );
                    Write( $outfile, $$nvTrail{Value}[0] ) or $err = 1;
                    ++$$self{CHANGED};
                }
                elsif ( $raf->Seek( 0, 2 ) and $raf->Tell() != $endPos ) {
                    $self->VPrint(
                        0,
                        '  Deleting trailer (',
                        $raf->Tell() - $endPos,
                        ' bytes)'
                    );
                    ++$$self{CHANGED};
                }
                last;
            }
            if ($trailInfo) {
                my $tbuf = '';
                $raf->Seek( -length($buff), 1 );
                $$trailInfo{OutFile}        = \$tbuf;
                $$trailInfo{ScanForTrailer} = 1;
                $self->ProcessTrailers($trailInfo) or undef $trailInfo;
            }
            if ($oldOutfile) {
                my $previewInfo;
                if ( $$self{HiddenData} ) {
                    my $pad;
                    my $hd    = $$self{HiddenData};
                    my $hdOff = $$hd{Offset} + $$hd{Base};
                    require Image::ExifTool::Sony;
                    my $dataPt =
                      Image::ExifTool::Sony::ReadHiddenData( $self, $hdOff,
                        $$hd{Size} );
                    if ($dataPt) {
                        my $padLen = $hdOff - $endPos;
                        unless ($padLen >= 0
                            and $raf->Seek( $endPos, 0 )
                            and $raf->Read( $pad, $padLen ) == $padLen )
                        {
                            $self->Error( 'Error reading HiddenData padding',
                                1 );
                            $pad = '';
                        }
                        $endPos += length($pad) + length($$dataPt);
                    }
                    else {
                        $$dataPt = $pad = '';
                    }
                    my $fixup = $$self{HiddenData}{Fixup};
                    $fixup->SetMarkerPointers( $outfile, 'HiddenData',
                        length($$outfile) + length($pad) - 10 );
                    $writeBuffer .= $pad . $$dataPt;
                }
                if ( $$self{LeicaTrailer} ) {
                    my $trailLen;
                    if ($trailInfo) {
                        $trailLen = $$trailInfo{DataPos} - $endPos;
                    }
                    else {
                        $raf->Seek( 0, 2 ) or $err = 1;
                        $trailLen = $raf->Tell() - $endPos;
                    }
                    my $fixup = $$self{LeicaTrailer}{Fixup};
                    $$self{LeicaTrailer}{TrailPos} = $endPos;
                    $$self{LeicaTrailer}{TrailLen} = $trailLen;
                    my $absPos = Tell($oldOutfile) + length($$outfile);
                    require Image::ExifTool::Panasonic;
                    my $dat =
                      Image::ExifTool::Panasonic::ProcessLeicaTrailer( $self,
                        $absPos );
                    my $junk = $$self{LeicaTrailerPos} - $endPos;
                    $fixup->SetMarkerPointers( $outfile, 'LeicaTrailer',
                        length($$outfile) - 10 + $junk );
                    my $trailSize =
                      defined($dat)
                      ? length($dat) - $junk
                      : $$self{LeicaTrailer}{Size};
                    $$fixup{Start} -= 4;
                    $$fixup{Shift} += 4;
                    $fixup->SetMarkerPointers( $outfile, 'LeicaTrailer',
                        $trailSize )
                      if defined $trailSize;
                    $$fixup{Start} += 4;
                    $$fixup{Shift} -= 4;

                    if ( defined $dat ) {
                        Write( $outfile, $dat ) or $err = 1;
                        $delPreview = 1;
                    }
                }
                if ( $$self{PREVIEW_INFO} ) {
                    my $scanLen = $$self{Make} =~ /^SONY/i ? 65536 : 1024;
                    if ( length($buff) < $scanLen ) {
                        my $buf2;
                        $buff .= $buf2
                          if $raf->Read( $buf2, $scanLen - length($buff) );
                    }
                    my $newPos = length($$outfile) - 10;
                    my $junkLen;
                    if ( $buff =~ /(\xff\xd8\xff.|.\xd8\xff\xdb)(..)/sg ) {
                        my ( $jpegHdr, $segLen ) = ( $1, $2 );
                        $junkLen = pos($buff) - 6;
                        if ( $$self{Make} =~ /^SONY/i and $junkLen > 32 ) {
                            if ( $jpegHdr eq "\xff\xd8\xff\xe1" ) {
                                $segLen = unpack( 'n', $segLen );

                                if ( length($buff) > $junkLen + $segLen + 6
                                    and
                                    substr( $buff, $junkLen + $segLen + 3, 3 )
                                    eq "\xd8\xff\xdb" )
                                {
                                    $junkLen += $segLen + 2;
                                }
                            }
                            $junkLen -= 32;
                        }
                        $newPos += $junkLen;
                    }
                    $previewInfo = $$self{PREVIEW_INFO};
                    delete $$self{PREVIEW_INFO};
                    my $fixup = $$previewInfo{Fixup};
                    $newPos += ( $$previewInfo{BaseShift} || 0 );
                    $newPos += Tell($oldOutfile) + 10
                      if $$previewInfo{Absolute};
                    if ( $$previewInfo{Relative} ) {
                        $newPos -= (
                            $fixup->GetMarkerPointers( $outfile,
                                'PreviewImage' )
                              || 0
                        );
                    }
                    elsif ( $$previewInfo{ChangeBase} ) {
                        my $makerOffset =
                          $fixup->GetMarkerPointers( $outfile, 'LeicaTrailer' );
                        $newPos -= $makerOffset if $makerOffset;
                    }
                    $fixup->SetMarkerPointers( $outfile, 'PreviewImage',
                        $newPos );
                    if ( $$previewInfo{Data} ne 'LOAD_PREVIEW' ) {
                        $$previewInfo{Junk} = substr( $buff, 0, $junkLen )
                          if $junkLen;
                    }
                }
                $outfile = $oldOutfile;
                undef $oldOutfile;
                Write( $outfile, $writeBuffer ) or $err = 1;
                undef $writeBuffer;
                if ( $previewInfo and $$previewInfo{Data} ne 'LOAD_PREVIEW' ) {
                    Write( $outfile, $$previewInfo{Junk} )
                      or $err = 1
                      if defined $$previewInfo{Junk};
                    Write( $outfile, $$previewInfo{Data} ) or $err = 1;
                    delete $$previewInfo{Data};
                    $delPreview = 1;
                }
            }
            unless ($delPreview) {
                my $extra;
                if ($trailInfo) {
                    $extra =
                      defined $$trailInfo{DataPos}
                      ? ( $$trailInfo{DataPos} - $endPos )
                      : 0;
                }
                else {
                    $raf->Seek( 0, 2 ) or $err = 1;
                    $extra = $raf->Tell() - $endPos;
                }
                if ( $extra > 0 ) {
                    if ( $$delGroup{Trailer} ) {
                        $verbose
                          and print $out
                          "  Deleting unknown trailer ($extra bytes)\n";
                        ++$$self{CHANGED};
                    }
                    else {
                        $verbose
                          and print $out
                          "  Preserving unknown trailer ($extra bytes)\n";
                        $raf->Seek( $endPos, 0 ) or $err = 1;
                        CopyBlock( $raf, $outfile, $extra ) or $err = 1;
                    }
                }
            }
            if ($trailInfo) {
                $self->WriteTrailerBuffer( $trailInfo, $outfile ) or $err = 1;
                undef $trailInfo;
            }
            last;

        }
        elsif ( $marker == 0xd9 and $isEXV ) {
            Write( $outfile, "\xff\xd9" ) or $err = 1;
            $rtnVal = 1;
            last;

        }
        elsif ($marker == 0x00
            or $marker == 0x01
            or ( $marker >= 0xd0 and $marker <= 0xd7 ) )
        {
            $verbose and $marker and print $out "JPEG $markerName:\n";
            Write( $outfile, $hdr ) or $err = 1;
            next;
        }
        my $segDataPt = \$segData;
        $length = length($segData);
        print $out "JPEG $markerName ($length bytes)\n" if $verbose;
        if ( $$delGroup{$dirName} ) {
            $verbose and print $out "  Deleting $dirName segment\n";
            $self->Warn('ICC_Profile deleted. Image colors may be affected')
              if $dirName eq 'ICC_Profile';
            ++$$self{CHANGED};
            next Marker;
        }
        my ( $segType, $del );
        while ( exists $$editDirs{$markerName} or $$delGroup{'*'} ) {
            if ( $marker == 0xe0 ) {
                if ( $$segDataPt =~ /^JFIF\0/ ) {
                    $segType = 'JFIF';
                    $$delGroup{JFIF} and $del = 1, last;
                    last unless $$editDirs{JFIF};
                    SetByteOrder('MM');
                    my $tagTablePtr =
                      GetTagTable('Image::ExifTool::JFIF::Main');
                    my %dirInfo = (
                        DataPt   => $segDataPt,
                        DataPos  => $segPos,
                        DataLen  => $length,
                        DirStart => 5,
                        DirLen   => $length - 5,
                        Parent   => $markerName,
                    );
                    my $newData =
                      $self->WriteDirectory( \%dirInfo, $tagTablePtr );
                    if ( defined $newData and length $newData ) {
                        $$segDataPt = "JFIF\0" . $newData;
                    }
                }
                elsif ( $$segDataPt =~ /^JFXX\0\x10/ ) {
                    $segType = 'JFXX';
                    $$delGroup{JFIF} and $del = 1;
                }
                elsif ( $$segDataPt =~ /^(II|MM).{4}HEAPJPGM/s ) {
                    $segType = 'CIFF';
                    $$delGroup{CIFF} and $del = 1, last;
                    last unless $$editDirs{CIFF};
                    my $newData = '';
                    my %dirInfo = (
                        RAF     => File::RandomAccess->new($segDataPt),
                        OutFile => \$newData,
                    );
                    require Image::ExifTool::CanonRaw;
                    if ( Image::ExifTool::CanonRaw::WriteCRW( $self, \%dirInfo )
                        > 0 )
                    {
                        if ( length $newData ) {
                            $$segDataPt = $newData;
                        }
                        else {
                            undef $segDataPt;
                            $del = 1;
                        }
                    }
                }
            }
            elsif ( $marker == 0xe1 ) {

                if ( $$segDataPt =~ /^(.{0,4})Exif\0./is ) {
                    my $hdrLen = length $exifAPP1hdr;
                    if ( length $1 ) {
                        $hdrLen += length $1;
                        $self->Error(
                            'Unknown garbage at start of EXIF segment', 1 );
                    }
                    elsif ( $$segDataPt !~ /^Exif\0/ ) {
                        $self->Error( 'Incorrect EXIF segment identifier', 1 );
                    }
                    $segType = 'EXIF';
                    last unless $$editDirs{IFD0};
                    if ( defined $combinedSegData ) {
                        $combinedSegData .= substr( $$segDataPt, $hdrLen );
                        $segDataPt = \$combinedSegData;
                        $segPos    = $firstSegPos;
                        $length    = length $combinedSegData;
                    }
                    if ( $dirOrder[0] eq 'ExtendedEXIF' ) {
                        unless ( defined $combinedSegData ) {
                            $combinedSegData = $$segDataPt;
                            $firstSegPos     = $segPos;
                            $self->Warn( 'File contains multi-segment EXIF',
                                1 );
                        }
                        next Marker;
                    }
                    $doneDir{IFD0}
                      and $self->Warn('Multiple APP1 EXIF records');
                    $doneDir{IFD0} = 1;
                    if ( $$delGroup{IFD0} or $$delGroup{EXIF} ) {
                        delete $doneDir{IFD0};
                        $del = 1;
                        last;
                    }
                    my %dirInfo = (
                        DataPt   => $segDataPt,
                        DataPos  => -$hdrLen,
                        DirStart => $hdrLen,
                        Base     => $segPos + $hdrLen,
                        Parent   => $markerName,
                        DirName  => 'IFD0',
                    );
                    my $tagTablePtr =
                      GetTagTable('Image::ExifTool::Exif::Main');
                    my $buff = $self->WriteDirectory( \%dirInfo, $tagTablePtr,
                        \&WriteTIFF );
                    if ( defined $buff ) {
                        undef $$segDataPt;
                        $segDataPt = \$buff;
                    }
                    else {
                        last Marker unless $self->Options('IgnoreMinorErrors');
                    }
                    length $$segDataPt or $del = 1, last;
                    if (
                        length($$segDataPt) + length($exifAPP1hdr) >
                        $maxSegmentLen )
                    {
                        if ( $self->Options('NoMultiExif') ) {
                            $self->Error('EXIF is too large for JPEG segment');
                        }
                        else {
                            $self->Warn( 'Writing multi-segment EXIF', 1 );
                        }
                    }
                    if (
                        (
                               $$self{PREVIEW_INFO}
                            or $$self{LeicaTrailer}
                            or $$self{HiddenData}
                        )
                        and not $oldOutfile
                      )
                    {
                        $writeBuffer = '';
                        $oldOutfile  = $outfile;
                        $outfile     = \$writeBuffer;
                        foreach (qw(PREVIEW_INFO LeicaTrailer HiddenData)) {
                            $$self{$_}{Fixup}{Start} += 18 if $$self{$_};
                        }
                    }
                    my $n = WriteMultiSegment( $outfile, $marker, $exifAPP1hdr,
                        $segDataPt, 'EXIF' );
                    if ( not $n ) {
                        $err = 1;
                    }
                    elsif ( $n > 1 and $oldOutfile ) {
                        $self->Error(
"Can't write multi-segment EXIF with external pointers"
                        );
                    }
                    undef $combinedSegData;
                    undef $$segDataPt;
                    next Marker;
                }
                elsif ( $$segDataPt =~ /^($xmpAPP1hdr|$xmpExtAPP1hdr)/ ) {
                    $segType                 = 'XMP';
                    $$delGroup{XMP} and $del = 1, last;
                    $doneDir{XMP}            = ( $doneDir{XMP} || 0 ) + 1;
                    last unless $$editDirs{XMP};
                    if ( $doneDir{XMP} + $dirCount{XMP} > 1 ) {
                        my ( $guid, $extXMP );
                        if ( $$segDataPt =~ /^$xmpExtAPP1hdr/ ) {
                            if ( length $$segDataPt < 75 ) {
                                $extendedXMP{Error} = 'Truncated data';
                            }
                            else {
                                my ( $size, $off ) =
                                  unpack( 'x67N2', $$segDataPt );
                                $guid = substr( $$segDataPt, 35, 32 );
                                if ( $guid =~ /[^A-Za-z0-9]/ ) {
                                    $extendedXMP{Error} = 'Invalid GUID';
                                }
                                else {
                                    $extXMP = $extendedXMP{$guid};
                                    if ($extXMP) {
                                        $size == $$extXMP{Size}
                                          or $extendedXMP{Error} =
                                          'Inconsistent size';
                                    }
                                    else {
                                        $extXMP = $extendedXMP{$guid} = {};
                                    }
                                    $$extXMP{Size} = $size;
                                    $$extXMP{$off} = substr( $$segDataPt, 75 );
                                }
                            }
                        }
                        else {
                            $extendedXMP{Main} = [] unless $extendedXMP{Main};
                            push @{ $extendedXMP{Main} },
                              substr( $$segDataPt, length $xmpAPP1hdr );
                        }
                        next Marker if $dirCount{XMP};
                        $$segDataPt = $xmpAPP1hdr;
                        my $goodGuid = '';
                        foreach ( @{ $extendedXMP{Main} } ) {
                            if (/:HasExtendedXMP\s*(=\s*['"]|>)(\w{32})/) {
                                if ( $goodGuid and $goodGuid ne $2 ) {
                                    $self->Warn(
'Multiple XMP segments specifying different extended XMP GUID'
                                    );
                                }
                                $goodGuid = $2;
                            }
                            $$segDataPt .= $_;
                        }
                        my $readGuid = $$self{OPTIONS}{ExtendedXMP} || 0;
                        $readGuid = $goodGuid if $readGuid eq '1';
                        foreach $guid ( sort keys %extendedXMP ) {
                            next unless length $guid == 32;
                            if ( $guid ne $readGuid and $readGuid ne '2' ) {
                                my $non = $guid eq $goodGuid ? '' : 'non-';
                                $self->Warn(
"Ignored ${non}standard extended XMP (GUID $guid)"
                                );
                                next;
                            }
                            if ( $guid ne $goodGuid ) {
                                $self->Warn(
"Reading non-standard extended XMP (GUID $guid)"
                                );
                            }
                            $extXMP = $extendedXMP{$guid};
                            next unless ref $extXMP eq 'HASH';
                            my $size = $$extXMP{Size};
                            my ( @offsets, $off );
                            for ( $off = 0 ; $off < $size ; ) {
                                last unless defined $$extXMP{$off};
                                push @offsets, $off;
                                $off += length $$extXMP{$off};
                            }
                            if ( $off == $size ) {
                                $$segDataPt .= $$extXMP{$_} foreach @offsets;
                            }
                            else {
                                $self->Error(
                                    "Incomplete extended XMP (GUID $guid)", 1 );
                            }
                        }
                        $self->Error( "$extendedXMP{Error} in extended XMP", 1 )
                          if $extendedXMP{Error};
                    }
                    my $start       = length $xmpAPP1hdr;
                    my $tagTablePtr = GetTagTable('Image::ExifTool::XMP::Main');
                    my %dirInfo     = (
                        DataPt   => $segDataPt,
                        DirStart => $start,
                        Parent   => $markerName,
                        MaxDataLen => $maxXMPLen - length($xmpAPP1hdr),
                    );
                    my $newData =
                      $self->WriteDirectory( \%dirInfo, $tagTablePtr );
                    if ( defined $newData ) {
                        undef %extendedXMP;
                        if ( length $newData ) {
                            WriteMultiXMP( $self, $outfile, \$newData,
                                $dirInfo{ExtendedXMP}, $dirInfo{ExtendedGUID} )
                              or $err = 1;
                            undef $$segDataPt;
                            next Marker;
                        }
                        else {
                            $$segDataPt = '';
                        }
                    }
                    else {
                        $verbose
                          and print $out
                          "    [XMP rewritten with no changes]\n";
                        if ( $doneDir{XMP} > 1 ) {
                            my ( $dat, $guid, $extXMP, $off );
                            foreach $dat ( @{ $extendedXMP{Main} } ) {
                                next unless length $dat;
                                $s = pack( 'n',
                                    length($xmpAPP1hdr) + length($dat) + 2 );
                                Write( $outfile, $hdr, $s, $xmpAPP1hdr, $dat )
                                  or $err = 1;
                            }
                            foreach $guid ( sort keys %extendedXMP ) {
                                next unless length $guid == 32;
                                $extXMP = $extendedXMP{$guid};
                                next unless ref $extXMP eq 'HASH';
                                my $size = $$extXMP{Size} or next;
                                for (
                                    $off = 0 ;
                                    defined $$extXMP{$off} ;
                                    $off += length $$extXMP{$off}
                                  )
                                {
                                    $s = pack( 'n',
                                        length($xmpExtAPP1hdr) +
                                          length( $$extXMP{$off} ) +
                                          42 );
                                    Write(
                                        $outfile,
                                        $hdr,
                                        $s,
                                        $xmpExtAPP1hdr,
                                        $guid,
                                        pack( 'N2', $size, $off ),
                                        $$extXMP{$off}
                                    ) or $err = 1;
                                }
                            }
                            undef $$segDataPt;
                            undef %extendedXMP;
                            next Marker;
                        }
                    }
                    $del = 1 unless length $$segDataPt;
                }
                elsif ( $$segDataPt =~ /^http/ or $$segDataPt =~ /<exif:/ ) {
                    $self->Warn(
                        'Ignored APP1 XMP segment with non-standard header',
                        1 );
                }
            }
            elsif ( $marker == 0xe2 ) {
                if ( $$segDataPt =~ /^ICC_PROFILE\0/ and $length >= 14 ) {
                    $segType = 'ICC_Profile';
                    $$delGroup{ICC_Profile} and $del = 1, last;
                    my $chunkNum  = Get8u( $segDataPt, 12 );
                    my $chunksTot = Get8u( $segDataPt, 13 );
                    if ( defined $iccChunksTotal ) {
                        if ( $chunksTot != $iccChunksTotal
                            and defined $iccChunkCount )
                        {
                            $self->Error(
                                'Inconsistent ICC_Profile chunk count', 1 );
                            undef $iccChunkCount;
                            undef $chunkNum;
                            ++$$self{CHANGED};
                        }
                    }
                    else {
                        $iccChunkCount  = 0;
                        $iccChunksTotal = $chunksTot;
                        $self->Warn('ICC_Profile chunk count is zero')
                          if !$chunksTot;
                    }
                    if ( defined $iccChunkCount ) {
                        if ( defined $iccChunk[$chunkNum] ) {
                            $self->Warn(
                                "Duplicate ICC_Profile chunk number $chunkNum");
                            $iccChunk[$chunkNum] .= substr( $$segDataPt, 14 );
                        }
                        else {
                            $iccChunk[$chunkNum] = substr( $$segDataPt, 14 );
                        }
                        next Marker unless ++$iccChunkCount >= $iccChunksTotal;
                        undef $iccChunkCount;
                        $doneDir{ICC_Profile} = 1;
                        my $icc_profile = '';
                        defined $_ and $icc_profile .= $_ foreach @iccChunk;
                        undef @iccChunk;
                        $segDataPt = \$icc_profile;
                        $length    = length $icc_profile;
                        my $tagTablePtr =
                          GetTagTable('Image::ExifTool::ICC_Profile::Main');
                        my %dirInfo = (
                            DataPt   => $segDataPt,
                            DataPos  => $segPos + 14,
                            DataLen  => $length,
                            DirStart => 0,
                            DirLen   => $length,
                            Parent   => $markerName,
                        );
                        my $newData =
                          $self->WriteDirectory( \%dirInfo, $tagTablePtr );

                        if ( defined $newData ) {
                            undef $$segDataPt;
                            $segDataPt = \$newData;
                        }
                        length $$segDataPt or $del = 1, last;
                        WriteMultiSegment( $outfile, $marker, "ICC_PROFILE\0",
                            $segDataPt, 'ICC' )
                          or $err = 1;
                        undef $$segDataPt;
                        next Marker;
                    }
                    elsif ( defined $chunkNum ) {
                        $self->Warn(
                            'Invalid or extraneous ICC_Profile chunk(s)');
                    }
                }
                elsif ( $$segDataPt =~ /^FPXR\0/ ) {
                    $segType = 'FPXR';
                    $$delGroup{FlashPix} and $del = 1;
                }
                elsif ( $$segDataPt =~ /^MPF\0/ ) {
                    $segType = 'MPF';
                    $$delGroup{MPF} and $del = 1;
                }
            }
            elsif ( $marker == 0xe3 ) {
                if ( $$segDataPt =~ /^(Meta|META|Exif)\0\0/ ) {
                    $segType = 'Kodak Meta';
                    $$delGroup{Meta} and $del = 1, last;
                    $doneDir{Meta}
                      and $self->Warn('Multiple APP3 Meta segments');
                    $doneDir{Meta} = 1;
                    last unless $$editDirs{Meta};
                    my %dirInfo = (
                        DataPt   => $segDataPt,
                        DataPos  => -6,
                        DirStart =>  6,
                        Base     => $segPos + 6,
                        Parent   => $markerName,
                        DirName  => 'Meta',
                    );
                    my $tagTablePtr =
                      GetTagTable('Image::ExifTool::Kodak::Meta');
                    my $buff = $self->WriteDirectory( \%dirInfo, $tagTablePtr,
                        \&WriteTIFF );

                    if ( defined $buff ) {
                        $$segDataPt = substr( $$segDataPt, 0, 6 ) . $buff;
                    }
                    else {
                        last Marker unless $self->Options('IgnoreMinorErrors');
                    }
                    $del = 1 unless length($$segDataPt) > 6;
                }
            }
            elsif ( $marker == 0xe5 ) {
                if ( $$segDataPt =~ /^RMETA\0/ ) {
                    $segType = 'Ricoh RMETA';
                    $$delGroup{RMETA} and $del = 1;
                }
            }
            elsif ( $marker == 0xe8 or $marker == 0xe9 ) {
                if ( $$segDataPt =~ /^SEAL\0/ ) {
                    $segType = 'SEAL';
                    $$delGroup{SEAL} and $del = 1;
                }
            }
            elsif ( $marker == 0xea ) {
                if ( $$segDataPt =~ /^AROT\0\0/ ) {
                    $segType = 'AROT';
                    $$delGroup{AROT} and $del = 1;
                }
            }
            elsif ( $marker == 0xeb ) {
                if ( $$segDataPt =~ /^JP/ ) {
                    $segType = 'JUMBF';
                    $$delGroup{JUMBF} and $del = 1;
                }
            }
            elsif ( $marker == 0xec ) {
                if ( $$segDataPt =~ /^Ducky/ ) {
                    $segType = 'Ducky';
                    $$delGroup{Ducky} and $del = 1, last;
                    $doneDir{Ducky}
                      and $self->Warn('Multiple APP12 Ducky segments');
                    $doneDir{Ducky} = 1;
                    last unless $$editDirs{Ducky};
                    my $tagTablePtr =
                      GetTagTable('Image::ExifTool::APP12::Ducky');
                    my %dirInfo = (
                        DataPt   => $segDataPt,
                        DataPos  => $segPos,
                        DataLen  => $length,
                        DirStart => 5,
                        DirLen   => $length - 5,
                        Parent   => $markerName,
                    );
                    my $newData =
                      $self->WriteDirectory( \%dirInfo, $tagTablePtr );

                    if ( defined $newData ) {
                        undef $$segDataPt;

                        $newData   = 'Ducky' . $newData if length $newData;
                        $segDataPt = \$newData;
                    }
                    $del = 1 unless length $$segDataPt;
                }
            }
            elsif ( $marker == 0xed ) {
                if ( $$segDataPt =~ /^$psAPP13hdr/ ) {
                    $segType = 'Photoshop';
                    if ( defined $combinedSegData ) {
                        $combinedSegData .=
                          substr( $$segDataPt, length($psAPP13hdr) );
                        $segDataPt = \$combinedSegData;
                        $length    = length $combinedSegData;
                    }
                    if ( $dirOrder[0] eq 'Photoshop' ) {
                        $combinedSegData = $$segDataPt
                          unless defined $combinedSegData;
                        next Marker;
                    }
                    if ( $doneDir{Photoshop} ) {
                        $self->Warn('Multiple Photoshop records');
                        $$delGroup{Photoshop} and $del = 1, last;
                    }
                    $doneDir{Photoshop} = 1;
                    my $tagTablePtr =
                      GetTagTable('Image::ExifTool::Photoshop::Main');
                    my %dirInfo = (
                        DataPt   => $segDataPt,
                        DataPos  => $segPos,
                        DataLen  => $length,
                        DirStart => 14,
                        DirLen   => $length - 14,
                        Parent   => $markerName,
                    );
                    my $newData =
                      $self->WriteDirectory( \%dirInfo, $tagTablePtr );
                    if ( defined $newData ) {
                        undef $$segDataPt;
                        $segDataPt = \$newData;
                    }
                    length $$segDataPt or $del = 1, last;
                    WriteMultiSegment( $outfile, $marker, $psAPP13hdr,
                        $segDataPt )
                      or $err = 1;
                    undef $combinedSegData;
                    undef $$segDataPt;
                    next Marker;
                }
            }
            elsif ( $marker == 0xee ) {
                if ( $$segDataPt =~ /^Adobe/ ) {
                    $segType = 'Adobe';
                    if ( $$delGroup{Adobe} or $$editDirs{Adobe} ) {
                        $del = 1;
                        undef $doneDir{Adobe};
                    }
                }
            }
            elsif ( $marker == 0xfe ) {
                my $newComment;
                unless ( $doneDir{COM} ) {
                    $doneDir{COM} = 1;
                    unless ( $$delGroup{File} and $$delGroup{File} != 2 ) {
                        my $tagInfo = $Image::ExifTool::Extra{Comment};
                        my $nvHash  = $self->GetNewValueHash($tagInfo);
                        my $val     = $segData;
                        $val =~ s/\0+$//;
                        if (   $self->IsOverwriting( $nvHash, $val )
                            or $$delGroup{File} )
                        {
                            $newComment = $self->GetNewValue($nvHash);
                        }
                        else {
                            delete $$editDirs{COM};
                            last;
                        }
                    }
                }
                $self->VerboseValue( '- Comment', $$segDataPt );
                if ( defined $newComment ) {
                    $self->VerboseValue( '+ Comment', $newComment );
                    WriteMultiSegment( $outfile, 0xfe, '', \$newComment )
                      or $err = 1;
                }
                else {
                    $verbose and print $out "  Deleting COM segment\n";
                }
                ++$$self{CHANGED};
                undef $segDataPt;
            }
            last;
        }

        if (
            $del
            or (    $$delGroup{'*'}
                and not $segType
                and $marker >= 0xe0
                and $marker <= 0xef )
          )
        {
            $segType = 'unknown' unless $segType;
            $verbose and print $out "  Deleting $markerName $segType segment\n";
            ++$$self{CHANGED};
            next Marker;
        }
        if ( defined $segDataPt and defined $$segDataPt ) {
            my $size = length($$segDataPt);
            if ( $size > $maxSegmentLen ) {
                $segType or $segType = 'Unknown';
                $self->Error(
                    "$segType $markerName segment too large! ($size bytes)");
                $err = 1;
            }
            else {
                $s = pack( 'n', length($$segDataPt) + 2 );
                Write( $outfile, $hdr, $s, $$segDataPt ) or $err = 1;
            }
            undef $$segDataPt;
            undef $segDataPt;
        }
    }
    $self->Error( 'Incomplete ICC_Profile record', 1 )
      if defined $iccChunkCount;
    pop @$path if @$path > $pn;
    $oldOutfile and return 0;
    if ($rtnVal) {
        my $trailPt = $self->AddNewTrailers();
        Write( $outfile, $$trailPt ) or $err = 1 if $trailPt;
    }
    $rtnVal = -1 if $rtnVal and $err;
    if ( $creatingEXV and $rtnVal > 0 and not $$self{CHANGED} ) {
        $self->Error('Nothing written');
        $rtnVal = -1;
    }
    return $rtnVal;
}

sub CheckImage($$) {
    my ( $self, $valPtr ) = @_;
    if (    length($$valPtr)
        and $$valPtr !~ /^\xff\xd8/
        and not $self->Options('IgnoreMinorErrors') )
    {
        return '[Minor] Not a valid image';
    }
    return undef;
}

sub CheckValue($$;$) {
    my ( $valPtr, $format, $count ) = @_;
    my ( @vals, $val, $n );

    if ( $format eq 'string' or $format eq 'undef' ) {
        return undef unless $count and $count > 0;
        my $len = length($$valPtr);
        if ( $format eq 'string' ) {
            $len >= $count and return 'String too long';
        }
        else {
            $len > $count and return 'Data too long';
        }
        if ( $len < $count ) {
            $$valPtr .= "\0" x ( $count - $len );
        }
        return undef;
    }
    if ( $count and $count != 1 ) {
        @vals = split( ' ', $$valPtr );
        $count < 0 and ( $count = @vals or return undef );
    }
    else {
        $count = 1;
        @vals  = ($$valPtr);
    }
    if ( @vals != $count ) {
        my $str = @vals > $count ? 'Too many' : 'Not enough';
        return "$str values specified ($count required)";
    }
    for ( $n = 0 ; $n < $count ; ++$n ) {
        $val = shift @vals;
        if ( $format =~ /^int/ ) {
            unless ( IsInt($val) ) {
                if ( IsHex($val) ) {
                    $val = $$valPtr = hex($val);
                }
                else {
                    return 'Not an integer'
                      unless IsFloat($val)
                      and $count == 1;
                    $val = $$valPtr = int( $val + ( $val < 0 ? -0.5 : 0.5 ) );
                }
            }
            my $rng = $intRange{$format} or return "Bad int format: $format";
            return "Value below $format minimum" if $val < $$rng[0];
            return "Value above $format maximum"
              if $val > $$rng[1] and $val != 0xfeedfeed;
        }
        elsif ($format =~ /^rational/
            or $format eq 'float'
            or $format eq 'double' )
        {
            unless ( IsFloat($val) ) {
                if ( $format =~ /^rational/ ) {
                    next if $val eq 'inf' or $val eq 'undef';
                    if ( $val =~ m{^([-+]?\d+)/(\d+)$} ) {
                        next unless $1 < 0 and $format =~ /u$/;
                        return 'Must be an unsigned rational';
                    }
                }
                return 'Not a floating point number';
            }
            if ( $format =~ /^rational\d+u$/ and $val < 0 ) {
                return 'Must be a positive number';
            }
        }
    }
    return undef;
}

sub CheckBinaryData($$$) {
    my ( $self, $tagInfo, $valPtr ) = @_;
    my $format = $$tagInfo{Format};
    unless ($format) {
        my $table = $$tagInfo{Table};
        if ( $table and $$table{FORMAT} ) {
            $format = $$table{FORMAT};
        }
        else {
            $format = 'int8u';
        }
    }
    my $count;
    if ( $format =~ /(.*)\[(.*)\]/ ) {
        $format = $1;
        $count  = $2;
        $count = -1 if $count =~ /\$size/;
    }
    return CheckValue( $valPtr, $format, $count );
}

sub Rename($$$) {
    my ( $self, $old, $new ) = @_;
    my ( $result, $try, $winUni );

    if ( $self->EncodeFileName($old) ) {
        $self->EncodeFileName( $new, 1 );
        $winUni = 1;
    }
    elsif ( $self->EncodeFileName($new) ) {
        $old = $_[1];
        $self->EncodeFileName( $old, 1 );
        $winUni = 1;
    }
    for ( ; ; ) {
        if ($winUni) {
            $result = eval {
                Win32API::File::MoveFileExW( $old, $new,
                    Win32API::File::MOVEFILE_REPLACE_EXISTING() |
                      Win32API::File::MOVEFILE_COPY_ALLOWED() );
            };
        }
        else {
            $result = rename( $old, $new );
        }
        last if $result or $^O ne 'MSWin32';
        $try = ( $try || 1 ) + 1;
        last if $try > 50;
        select( undef, undef, undef, 0.01 );
    }
    return $result;
}

sub Unlink($@) {
    my $self   = shift;
    my $result = 0;
    while (@_) {
        my $file = shift;
        if ( $self->EncodeFileName($file) ) {
            ++$result if eval { Win32API::File::DeleteFileW($file) };
        }
        else {
            ++$result if unlink $file;
        }
    }
    return $result;
}

my $k32SetFileTime;

sub SetFileTime($$;$$$$) {
    my ( $self, $file, $atime, $mtime, $ctime, $noWarn ) = @_;
    my $saveFile;
    local *FH;

    unless ( ref $file ) {
        unless ( $self->Open( \*FH, $file, '+<' ) ) {
            my $success;
            if ( defined $atime or defined $mtime ) {
                my ( $a, $m, $c ) = $self->GetFileTime($file);
                $atime   = $a unless defined $atime;
                $mtime   = $m unless defined $mtime;
                $success = eval { utime( $atime, $mtime, $file ) }
                  if defined $atime and defined $mtime;
            }
            $self->Warn('Error updating file time') unless $success;
            return $success;
        }
        $saveFile = $file;
        $file     = \*FH;
    }
    if ( $^O eq 'MSWin32' ) {
        if ( not eval { require Win32::API } ) {
            $self->Warn(
                'Install Win32::API for proper handling of Windows file times');
        }
        elsif ( not eval { require Win32API::File } ) {
            $self->Warn(
'Install Win32API::File for proper handling of Windows file times'
            );
        }
        else {
            my $win32Handle = eval { Win32API::File::GetOsFHandle($file) };
            unless ($win32Handle) {
                $self->Warn(
                    'Win32API::File GetOsFHandle returned invalid handle');
                return 0;
            }
            my $time;
            foreach $time ( $atime, $mtime, $ctime ) {
                defined $time or $time = 0, next;
                my $wt =
                  ( $time + ( ( ( 1970 - 1601 ) * 365 + 89 ) * 24 * 3600 ) ) *
                  1e7;
                my $hi = int( $wt / 4294967296 );
                $time = pack 'LL', int( $wt - $hi * 4294967296 ), $hi;
            }
            unless ($k32SetFileTime) {
                return 0 if defined $k32SetFileTime;
                $k32SetFileTime =
                  Win32::API->new( 'KERNEL32', 'SetFileTime', 'NPPP', 'I' );
                unless ($k32SetFileTime) {
                    $self->Warn('Error loading Win32::API SetFileTime');
                    $k32SetFileTime = 0;
                    return 0;
                }
            }
            unless (
                $k32SetFileTime->Call( $win32Handle, $ctime, $atime, $mtime ) )
            {
                $self->Warn( 'Win32::API SetFileTime returned '
                      . Win32::GetLastError() );
                return 0;
            }
            return 1;
        }
    }
    if ( defined $atime and defined $mtime ) {
        my $success;
        local $SIG{'__WARN__'} = \&SetWarning;
        for ( ; ; ) {
            undef $evalWarning;
            $success = eval { utime( $atime, $mtime, $file ) };
            last if $success or not defined $saveFile;
            close $file;
            $file = $saveFile;
            undef $saveFile;
        }
        unless ($noWarn) {
            if ( $@ or $evalWarning ) {
                $self->Warn( CleanWarning( $@ || $evalWarning ) );
            }
            elsif ( not $success ) {
                $self->Warn('Error setting file time');
            }
        }
        return $success;
    }
    return 1;
}

sub ImageDataHash($$$;$$) {
    my ( $self, $raf, $size, $type, $noMsg ) = @_;
    my $hash = $$self{ImageDataHash} or return;
    my ( $bytesRead, $n ) = ( 0, 65536 );
    my $buff;
    for ( ; ; ) {
        if ( defined $size ) {
            last unless $size;
            $n = $size > 65536 ? 65536 : $size;
            $size -= $n;
        }
        unless ( $raf->Read( $buff, $n ) ) {
            $self->Warn("Error reading $type data") if $type and defined $size;
            last;
        }
        $hash->add($buff);
        $bytesRead += length $buff;
    }
    if ( $$self{OPTIONS}{Verbose} and $bytesRead and $type and not $noMsg ) {
        $self->VPrint( 0,
            "$$self{INDENT}(ImageDataHash: $bytesRead bytes of $type data)\n" );
    }
    return $bytesRead;
}

sub CopyBlock($$$) {
    my ( $raf, $outfile, $size ) = @_;
    my $buff;
    for ( ; ; ) {
        last unless $size > 0;
        my $n = $size > 65536 ? 65536 : $size;
        $raf->Read( $buff, $n ) == $n or return 0;
        Write( $outfile, $buff )      or return undef;
        $size -= $n;
    }
    return 1;
}

sub CopyImageData($$$) {
    my ( $self, $imageDataBlocks, $outfile ) = @_;
    my $raf = $$self{RAF};
    my ( $dataBlock, $err );
    my $num = @$imageDataBlocks;
    $self->VPrint( 0, "  Copying $num image data blocks\n" ) if $num;
    foreach $dataBlock (@$imageDataBlocks) {
        my ( $pos, $size, $pad ) = @$dataBlock;
        $raf->Seek( $pos, 0 ) or $err = 'read', last;
        my $result = CopyBlock( $raf, $outfile, $size );
        $result or $err = defined $result ? 'read' : 'writ';
        Write( $outfile, "\0" x $pad ) or $err = 'writ' if $pad;
        last if $err;
    }
    if ($err) {
        $self->Error("Error ${err}ing image data");
        return 0;
    }
    return 1;
}

sub WriteBinaryData($$$) {
    my ( $self, $dirInfo, $tagTablePtr ) = @_;
    $self or return 1;

    my $dataPt        = $$dirInfo{DataPt} or return undef;
    my $dataLen       = length $$dataPt;
    my $defaultFormat = $$tagTablePtr{FORMAT} || 'int8u';
    my $increment     = FormatSize($defaultFormat);
    unless ($increment) {
        warn "Unknown format $defaultFormat\n";
        return undef;
    }
    my @varOffsets;
    if ( $$tagTablePtr{DATAMEMBER} ) {
        $$dirInfo{DataMember}    = $$tagTablePtr{DATAMEMBER};
        $$dirInfo{VarFormatData} = \@varOffsets;
        $self->ProcessBinaryData( $dirInfo, $tagTablePtr );
        delete $$dirInfo{DataMember};
        delete $$dirInfo{VarFormatData};
    }
    my $dirStart = $$dirInfo{DirStart} || 0;
    my $dirLen   = $$dirInfo{DirLen};
    $dirLen = $dataLen - $dirStart
      if not defined $dirLen
      or $dirLen > $dataLen - $dirStart;
    my $newData = substr( $$dataPt, $dirStart, $dirLen ) or return undef;
    my $dirName = $$dirInfo{DirName};
    my $varSize = 0;
    my @varInfo = @varOffsets;
    my $tagInfo;
    $dataPt = \$newData;

    foreach $tagInfo ( sort { $$a{TagID} <=> $$b{TagID} }
        $self->GetNewTagInfoList($tagTablePtr) )
    {
        my $tagID = $$tagInfo{TagID};
        if ( ref $$tagTablePtr{$tagID} eq 'ARRAY' or $$tagInfo{Condition} ) {
            my $writeInfo = $self->GetTagInfo( $tagTablePtr, $tagID );
            next unless $writeInfo and $writeInfo eq $tagInfo;
        }
        while ( @varInfo and $varInfo[0][0] < $tagID ) {
            $varSize = $varInfo[0][1];
            shift @varInfo;
        }
        my $count  = 1;
        my $format = $$tagInfo{Format};
        my $entry  = int($tagID) * $increment + $varSize;
        if ($format) {
            if ( $format =~ /(.*)\[(.*)\]/ ) {
                $format = $1;
                $count  = $2;
                my $size = $dirLen;

                $count = eval $count;
                $@ and warn($@), next;
            }
            elsif ( $format eq 'string' ) {
                $count = ( $dirLen > $entry ) ? $dirLen - $entry : 0;
            }
        }
        else {
            $format = $defaultFormat;
        }
        $format = $varInfo[0][2] if @varInfo and $varInfo[0][0] == $tagID;
        my $val =
          ReadValue( $dataPt, $entry, $format, $count, $dirLen - $entry );
        next unless defined $val;
        my $nvHash =
          $self->GetNewValueHash( $tagInfo, $$self{CUR_WRITE_GROUP} );
        next unless $self->IsOverwriting( $nvHash, $val ) > 0;
        my $newVal = $self->GetNewValue($nvHash);
        next unless defined $newVal;

        $$self{ $$tagInfo{DataMember} } = $newVal if $$tagInfo{DataMember};
        my $mask = $$tagInfo{Mask};
        $newVal =
          ( ( $newVal << $$tagInfo{BitShift} ) & $mask ) | ( $val & ~$mask )
          if $mask;
        if ( $$tagInfo{DataTag} and not $$tagInfo{IsOffset} ) {
            warn 'Internal error' unless $newVal == 0xfeedfeed;
            my $data = $self->GetNewValue( $$tagInfo{DataTag} );
            $newVal = length($data) if defined $data;
            my $format = $$tagInfo{Format} || $$tagTablePtr{FORMAT} || 'int32u';
            if ( $format =~ /^int16/ and $newVal > 0xffff ) {
                $self->Error(
"$$tagInfo{DataTag} is too large (64 KiB max. for this file)"
                );
            }
        }
        my $rtnVal = WriteValue( $newVal, $format, $count, $dataPt, $entry );
        if ( defined $rtnVal ) {
            $self->VerboseValue( "- $dirName:$$tagInfo{Name}", $val );
            $self->VerboseValue( "+ $dirName:$$tagInfo{Name}", $newVal );
            ++$$self{CHANGED};
        }
        else {
            $self->Warn("Error packing $$tagInfo{Name} value");
        }
    }
    if ( $$tagTablePtr{IS_OFFSET} and $$dirInfo{Fixup} ) {
        $varSize = 0;
        @varInfo = @varOffsets;
        my $fixup = $$dirInfo{Fixup};
        my $tagID;
        foreach $tagID ( @{ $$tagTablePtr{IS_OFFSET} } ) {
            $tagInfo = $self->GetTagInfo( $tagTablePtr, $tagID ) or next;
            while ( @varInfo and $varInfo[0][0] < $tagID ) {
                $varSize = $varInfo[0][1];
                shift @varInfo;
            }
            my $entry = $tagID * $increment + $varSize;
            next unless $entry <= $dirLen - 4;
            my $format = $$tagInfo{Format} || $$tagTablePtr{FORMAT} || 'int32u';
            my $offset =
              ReadValue( $dataPt, $entry, $format, 1, $dirLen - $entry );
            next unless $offset;
            $fixup->AddFixup( $entry, $$tagInfo{DataTag}, $format );
            next unless $$tagInfo{DataTag} and defined $$tagInfo{OffsetPair};
            $entry = $$tagInfo{OffsetPair} * $increment + $varSize;
            my $size =
              ReadValue( $dataPt, $entry, $format, 1, $dirLen - $entry );
            next unless defined $size;

            if ( $$tagInfo{DataTag} eq 'HiddenData' ) {
                $$self{HiddenData} = {
                    Offset => $offset,
                    Size   => $size,
                    Fixup  => Image::ExifTool::Fixup->new,
                    Base   => $$dirInfo{Base},
                };
                next;
            }
            next
              unless $$tagInfo{DataTag} eq 'PreviewImage'
              and $$self{FILE_TYPE} eq 'JPEG';
            my $previewInfo = $$self{PREVIEW_INFO};
            $previewInfo
              or $previewInfo = $$self{PREVIEW_INFO} =
              { Fixup => Image::ExifTool::Fixup->new, };
            $$previewInfo{IsShort}  = 1 unless $format eq 'int32u';
            $$previewInfo{Absolute} = 1
              if $$tagInfo{IsOffset} and $$tagInfo{IsOffset} eq '3';
            $$previewInfo{Data} =
              $self->GetNewValue( GetCompositeTagInfo('PreviewImage') );
            unless ( defined $$previewInfo{Data} ) {
                if ( $offset >= 0 and $offset + $size <= $$dirInfo{DataLen} ) {
                    $$previewInfo{Data} =
                      substr( ${ $$dirInfo{DataPt} }, $offset, $size );
                }
                else {
                    $$previewInfo{Data} = 'LOAD_PREVIEW';
                }
            }
        }
    }
    if ( $$tagTablePtr{IS_SUBDIR} ) {
        $varSize = 0;
        @varInfo = @varOffsets;
        my $tagID;
        foreach $tagID ( @{ $$tagTablePtr{IS_SUBDIR} } ) {
            my $tagInfo = $self->GetTagInfo( $tagTablePtr, $tagID );
            next unless defined $tagInfo;
            while ( @varInfo and $varInfo[0][0] < $tagID ) {
                $varSize = $varInfo[0][1];
                shift @varInfo;
            }
            my $entry = int($tagID) * $increment + $varSize;
            last if $entry >= $dirLen;
            unless ($tagInfo) {
                my $more = $dirLen - $entry;
                $more = 128 if $more > 128;
                my $v = substr( $newData, $entry, $more );
                $tagInfo = $self->GetTagInfo( $tagTablePtr, $tagID, \$v );
                next unless $tagInfo;
            }
            my $subdir = $$tagInfo{SubDirectory} or next;
            my $start  = $$subdir{Start};
            my $len;
            if ( not $start ) {
                $start = $entry;
                $len   = $dirLen - $start;
            }
            elsif ( $start =~ /\$/ ) {
                my $count  = 1;
                my $format = $$tagInfo{Format} || $defaultFormat;
                $format =~ /(.*)\[(.*)\]/ and ( $format, $count ) = ( $1, $2 );
                my $val = ReadValue( $dataPt, $entry, $format, $count,
                    $dirLen - $entry );
                next unless $val;
                my $dirStart = 0;
                $start = eval($start);
                next if $start < $dirStart or $start > $dataLen;
                $len = $$subdir{DirLen};
                $len = $dataLen - $start
                  unless $len and $len <= $dataLen - $start;
            }
            my %subdirInfo = (
                DataPt   => \$newData,
                DirStart => $start,
                DirLen   => $len,
                TagInfo  => $tagInfo,
            );
            my $dat = $self->WriteDirectory( \%subdirInfo,
                GetTagTable( $$subdir{TagTable} ) );
            substr( $newData, $start, $len ) = $dat
              if defined $dat and length $dat;
        }
    }
    return $newData;
}

sub WriteTIFF($$$) {
    my ( $self, $dirInfo, $tagTablePtr ) = @_;
    $self or return 1;
    my $buff = '';
    $$dirInfo{OutFile} = \$buff;
    return $buff if $self->ProcessTIFF( $dirInfo, $tagTablePtr ) > 0;
    return undef;
}

1;

__END__

