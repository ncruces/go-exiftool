
package Image::ExifTool::AAC;

use strict;
use vars            qw($VERSION);
use Image::ExifTool qw(:DataAccess :Utils);
use Image::ExifTool::FLAC;

$VERSION = '1.00';

my %convSampleRate = (
    0  => 96000,
    7  => 22050,
    1  => 88200,
    8  => 16000,
    2  => 64000,
    9  => 12000,
    3  => 48000,
    10 => 11025,
    4  => 44100,
    11 => 8000,
    5  => 32000,
    12 => 7350,
    6  => 24000,
);

%Image::ExifTool::AAC::Main = (
    PROCESS_PROC => \&Image::ExifTool::FLAC::ProcessBitStream,
    GROUPS       => { 2 => 'Audio' },
    NOTES        => 'Tags extracted from Advanced Audio Coding (AAC) files.',
    'Bit016-017' => {
        Name      => 'ProfileType',
        PrintConv => {
            0 => 'Main',
            1 => 'Low Complexity',
            2 => 'Scalable Sampling Rate',
        },
    },
    'Bit018-021' => {
        Name      => 'SampleRate',
        ValueConv => \%convSampleRate,
    },
    'Bit023-025' => {
        Name      => 'Channels',
        PrintConv => {
            0 => '?',
            1 => 1,
            2 => 2,
            3 => 3,
            4 => 4,
            5 => 5,
            6 => '5+1',
            7 => '7+1',
        },
    },
    Encoder => {
        Name  => 'Encoder',
        Notes => 'taken from filler payload of first frame',
    },
);

sub ProcessAAC($$) {
    my ( $et, $dirInfo ) = @_;
    my $raf = $$dirInfo{RAF};
    my ( $buff, $buf2 );

    $raf->Read( $buff, 7 ) == 7 or return 0;
    return 0 unless $buff =~ /^\xff[\xf0\xf1]/;
    my @t = unpack( 'NnC', $buff );
    return 0 if ( ( $t[0] >> 16 ) & 0x03 ) == 3;
    return 0 if ( ( $t[0] >> 12 ) & 0x0f ) > 12;
    my $len = ( ( $t[0] << 11 ) & 0x1800 ) | ( ( $t[1] >> 5 ) & 0x07ff );
    return 0 if $len < 7;

    $et->SetFileType();

    my $tagTablePtr = GetTagTable('Image::ExifTool::AAC::Main');
    $et->ProcessDirectory( { DataPt => \$buff }, $tagTablePtr );

    while ( $len > 8 and $raf->Read( $buff, $len - 7 ) == $len - 7 ) {
        my $noCRC  = ( $t[0] & 0x00010000 );
        my $blocks = ( $t[2] & 0x03 );
        my $pos    = 0;
        $pos += 2 + 2 * $blocks unless $noCRC;
        last if $pos + 2 > length($buff);
        my $tmp = unpack( "x${pos}n", $buff );
        my $id  = $tmp >> 13;
        if ( $id == 6 ) {
            my $cnt = ( $tmp >> 9 ) & 0x0f;
            ++$pos;
            if ( $cnt == 15 ) {
                $cnt += ( ( $tmp >> 1 ) & 0xff ) - 1;
                ++$pos;
            }
            if ( $pos + $cnt <= length($buff) ) {
                my $dat = substr( $buff, $pos, $cnt );
                $dat =~ s/^\0+//;
                $dat =~ s/\0+$//;
                $et->HandleTag( $tagTablePtr, Encoder => $dat )
                  if $dat =~ /^[\x20-\x7e]+$/;
            }
        }
        last;
    }

    return 1;
}

1;

__END__


