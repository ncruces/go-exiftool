
package Image::ExifTool::Fixup;

use strict;
use Image::ExifTool qw(GetByteOrder SetByteOrder Get32u Get32s Set32u
  Get16u Get16s Set16u);
use vars qw($VERSION);

$VERSION = '1.06';

sub AddFixup($$;$$);
sub ApplyFixup($$);
sub Dump($;$);

sub new {
    local $_;
    my $that  = shift;
    my $class = ref($that) || $that || 'Image::ExifTool::Fixup';
    my $self  = bless {}, $class;

    $self->{Start} = 0;
    $self->{Shift} = 0;

    return $self;
}

sub Clone($) {
    my $self  = shift;
    my $clone = Image::ExifTool::Fixup->new;
    $clone->{Start} = $self->{Start};
    $clone->{Shift} = $self->{Shift};
    my $phash = $self->{Pointers};
    if ($phash) {
        $clone->{Pointers} = {};
        my $byteOrder;
        foreach $byteOrder ( keys %$phash ) {
            my @pointers = @{ $phash->{$byteOrder} };
            $clone->{Pointers}->{$byteOrder} = \@pointers;
        }
    }
    if ( $self->{Fixups} ) {
        $clone->{Fixups} = [];
        my $subFixup;
        foreach $subFixup ( @{ $self->{Fixups} } ) {
            push @{ $clone->{Fixups} }, $subFixup->Clone();
        }
    }
    return $clone;
}

sub AddFixup($$;$$) {
    my ( $self, $pointer, $marker, $format ) = @_;
    if ( ref $pointer ) {
        $self->{Fixups} or $self->{Fixups} = [];
        push @{ $self->{Fixups} }, $pointer;
    }
    else {
        my $byteOrder = GetByteOrder();
        if ( defined $format ) {
            if ( $format eq 'int16u' ) {
                $byteOrder .= '2';
            }
            elsif ( $format ne 'int32u' ) {
                warn "Bad Fixup pointer format $format\n";
            }
        }
        $byteOrder .= "_$marker" if defined $marker;
        my $phash = $self->{Pointers};
        $phash               or $phash               = $self->{Pointers} = {};
        $phash->{$byteOrder} or $phash->{$byteOrder} = [];
        push @{ $phash->{$byteOrder} }, $pointer;
    }
}

sub ApplyFixup($$) {
    my ( $self, $dataPt ) = @_;

    my $start = $self->{Start};
    my $shift = $self->{Shift} + $start;
    my $phash = $self->{Pointers};

    if ( $phash and ( $start or $shift ) ) {
        my $saveOrder = GetByteOrder();
        my ( $byteOrder, $ptr );
        foreach $byteOrder ( keys %$phash ) {
            SetByteOrder( substr( $byteOrder, 0, 2 ) );
            my ( $get, $set ) =
                ( $byteOrder =~ /^(II2|MM2)/ )
              ? ( \&Get16s, \&Set16u )
              : ( \&Get32s, \&Set32u );
            foreach $ptr ( @{ $phash->{$byteOrder} } ) {
                $ptr += $start;
                next unless $shift;
                &$set( &$get( $dataPt, $ptr ) + $shift, $dataPt, $ptr );
            }
        }
        SetByteOrder($saveOrder);
    }
    if ( $self->{Fixups} ) {
        $phash or $phash = $self->{Pointers} = {};
        my $subFixup;
        foreach $subFixup ( @{ $self->{Fixups} } ) {
            $subFixup->{Start} += $start;
            $subFixup->{Shift} += $shift - $start;
            ApplyFixup( $subFixup, $dataPt );
            my $shash = $subFixup->{Pointers} or next;
            my $byteOrder;
            foreach $byteOrder ( keys %$shash ) {
                $phash->{$byteOrder} or $phash->{$byteOrder} = [];
                push @{ $phash->{$byteOrder} }, @{ $shash->{$byteOrder} };
                delete $shash->{$byteOrder};
            }
            delete $subFixup->{Pointers};
        }
        delete $self->{Fixups};
    }
    $self->{Start} = $self->{Shift} = 0;
}

sub IsEmpty($) {
    my $self  = shift;
    my $phash = $self->{Pointers};
    if ($phash) {
        my $key;
        foreach $key ( keys %$phash ) {
            next unless ref $$phash{$key} eq 'ARRAY';
            return 0 if @{ $$phash{$key} };
        }
    }
    return 1;
}

sub HasMarker($$) {
    my ( $self, $marker ) = @_;
    my $phash = $self->{Pointers};
    return 0 unless $phash;
    return 1 if grep /_$marker$/, keys %$phash;
    return 0 unless $self->{Fixups};
    my $subFixup;
    foreach $subFixup ( @{ $self->{Fixups} } ) {
        return 1 if $subFixup->HasMarker($marker);
    }
    return 0;
}

sub SetMarkerPointers($$$$;$) {
    my ( $self, $dataPt, $marker, $value, $startOffset ) = @_;
    my $start = $self->{Start} + ( $startOffset || 0 );
    my $phash = $self->{Pointers};

    if ($phash) {
        my $saveOrder = GetByteOrder();
        my ( $byteOrder, $ptr );
        foreach $byteOrder ( keys %$phash ) {
            next unless $byteOrder =~ /^(II|MM)(2?)_$marker$/;
            SetByteOrder($1);
            my $set = $2 ? \&Set16u : \&Set32u;
            foreach $ptr ( @{ $phash->{$byteOrder} } ) {
                &$set( $value, $dataPt, $ptr + $start );
            }
        }
        SetByteOrder($saveOrder);
    }
    if ( $self->{Fixups} ) {
        my $subFixup;
        foreach $subFixup ( @{ $self->{Fixups} } ) {
            $subFixup->SetMarkerPointers( $dataPt, $marker, $value, $start );
        }
    }
}

sub GetMarkerPointers($$$;$) {
    my ( $self, $dataPt, $marker, $startOffset ) = @_;
    my $start = $self->{Start} + ( $startOffset || 0 );
    my $phash = $self->{Pointers};
    my @pointers;

    if ($phash) {
        my $saveOrder = GetByteOrder();
        my ( $byteOrder, $ptr );
        foreach $byteOrder ( grep /_$marker$/, keys %$phash ) {
            SetByteOrder( substr( $byteOrder, 0, 2 ) );
            my $get = ( $byteOrder =~ /^(II2|MM2)/ ) ? \&Get16u : \&Get32u;
            foreach $ptr ( @{ $phash->{$byteOrder} } ) {
                push @pointers, &$get( $dataPt, $ptr + $start );
            }
        }
        SetByteOrder($saveOrder);
    }
    if ( $self->{Fixups} ) {
        my $subFixup;
        foreach $subFixup ( @{ $self->{Fixups} } ) {
            push @pointers,
              $subFixup->GetMarkerPointers( $dataPt, $marker, $start );
        }
    }
    return @pointers if wantarray;
    return $pointers[0];
}

sub Dump($;$) {
    my ( $self, $indent ) = @_;
    $indent or $indent = '';
    printf "${indent}Fixup start=0x%x shift=0x%x\n", $self->{Start},
      $self->{Shift};
    my $phash = $self->{Pointers};
    if ($phash) {
        my $byteOrder;
        foreach $byteOrder ( sort keys %$phash ) {
            print "$indent  $byteOrder: ",
              join( ' ', @{ $phash->{$byteOrder} } ), "\n";
        }
    }
    if ( $self->{Fixups} ) {
        my $subFixup;
        foreach $subFixup ( @{ $self->{Fixups} } ) {
            Dump( $subFixup, $indent . '  ' );
        }
    }
}

1;

__END__

