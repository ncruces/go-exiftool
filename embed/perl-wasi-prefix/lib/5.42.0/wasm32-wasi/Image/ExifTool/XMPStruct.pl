
package Image::ExifTool::XMP;

use strict;
use vars qw(%specialStruct %stdXlatNS);

use Image::ExifTool qw(:Utils);
use Image::ExifTool::XMP;

sub SerializeStruct($$;$);
sub InflateStruct($$;$);
sub DumpStruct($;$);
sub CheckStruct($$$);
sub AddNewStruct($$$$$$);
sub ConvertStruct($$$$;$);
sub EscapeJSON($;$);

my %jsonChar = (
    '"'  => '"',
    '\\' => '\\',
    "\b" => 'b',
    "\f" => 'f',
    "\n" => 'n',
    "\r" => 'r',
    "\t" => 't'
);
my %jsonEsc = (
    '"'  => '"',
    '\\' => '\\',
    'b'  => "\b",
    'f'  => "\f",
    'n'  => "\n",
    'r'  => "\r",
    't'  => "\t"
);

sub SerializeStruct($$;$) {
    my ( $et, $obj, $ket ) = @_;
    my ( $key, $val, @vals, $rtnVal );
    my $sfmt = $et->Options('StructFormat');

    if ( ref $obj eq 'HASH' ) {
        foreach $key ( Image::ExifTool::OrderedKeys($obj) ) {
            my $hdr = $sfmt ? EscapeJSON($key) . ':' : $key . '=';
            push @vals, $hdr . SerializeStruct( $et, $$obj{$key}, '}' );
        }
        $rtnVal = '{' . join( ',', @vals ) . '}';
    }
    elsif ( ref $obj eq 'ARRAY' ) {
        foreach $val (@$obj) {
            push @vals, SerializeStruct( $et, $val, ']' );
        }
        $rtnVal = '[' . join( ',', @vals ) . ']';
    }
    elsif ( defined $obj ) {
        $obj = $$obj if ref $obj eq 'SCALAR';
        if ($sfmt) {
            $rtnVal = EscapeJSON( $obj, $sfmt eq 'JSONQ' );
        }
        else {
            my $pat = $ket ? "\\$ket|,|\\|" : ',|\\|';
            ( $rtnVal = $obj ) =~ s/($pat)/|$1/g;
            $rtnVal =~ s/^([\s\[\{])/|$1/;
        }
    }
    elsif ($sfmt) {
        $rtnVal = 'null';
    }
    else {
        $rtnVal = '';
    }
    return $rtnVal;
}

sub InflateStruct($$;$) {
    my ( $et, $obj, $delim ) = @_;
    my ( $val, $warn, $part );
    my $sfmt = $et->Options('StructFormat');

    if ( $$obj =~ s/^\s*\{// ) {
        my %struct;
        for ( ; ; ) {
            last
              unless $sfmt
              ? $$obj =~ s/^\s*"(.*?)"\s*://s
              : $$obj =~ s/^\s*([-\w:.]+#?)\s*=//s;
            my $tag = $1;
            my ( $v, $w ) = InflateStruct( $et, $obj, '}' );
            $warn = $w if $w and not $warn;
            return ( undef, $warn ) unless defined $v;
            $struct{$tag} = $v;
            last unless $$obj =~ s/^\s*,//s;
        }
        unless ( $$obj =~ s/^\s*\}//s or $warn ) {
            if ( length $$obj ) {
                ( $part = $$obj ) =~ s/^\s*//s;
                $part =~ s/[\x0d\x0a].*//s;
                $part = substr( $part, 0, 27 ) . '...' if length($part) > 30;
                $warn = "Invalid structure field at '${part}'";
            }
            else {
                $warn = 'Missing closing brace for structure';
            }
        }
        $val = \%struct;
    }
    elsif ( $$obj =~ s/^\s*\[// ) {
        my @list;
        for ( ; ; ) {
            my ( $v, $w ) = InflateStruct( $et, $obj, ']' );
            $warn = $w if $w and not $warn;
            return ( undef, $warn ) unless defined $v;
            push @list, $v;
            last unless $$obj =~ s/^\s*,//s;
        }
        $$obj =~ s/^\s*\]//s
          or $warn
          or $warn = 'Missing closing bracket for list';
        $val = \@list;
    }
    else {
        $$obj =~ s/^\s+//s;
        if ($sfmt) {
            if ( $$obj =~ s/^"// ) {
                $val = '';
                while ( $$obj =~ s/(.*?)"// ) {
                    $val .= $1;
                    last unless $val =~ /([\\]+)$/ and length($1) & 0x01;
                    substr( $val, -1, 1 ) = '"';
                }
                if ( $val =~ s/^base64:// ) {
                    $val = DecodeBase64($val);
                }
                else {
                    $val =~ s/\\(.)/$jsonEsc{$1}||'\\'.$1/egs;
                }
            }
            elsif ( $$obj =~ s/^(true|false)\b// ) {
                $val = '"' . ucfirst($1) . '"';
            }
            elsif (
                $$obj =~ s/^([+-]?(?=\d|\.\d)\d*(\.\d*)?([Ee]([+-]?\d+))?)// )
            {
                $val = $1;
            }
            else {
                $warn or $warn = 'Unknown JSON object';
                $val = '""';
            }
        }
        else {
            $delim = $delim ? "\\$delim|,|\\||\$" : ',|\\||$';
            $val   = '';
            for ( ; ; ) {
                $$obj =~ s/^(.*?)($delim)//s or last;
                $val .= $1;
                last unless $2;
                $2 eq '|' or $$obj = $2 . $$obj, last;
                $$obj =~ s/^(.)//s and $val .= $1;
            }
        }
    }
    return ( $val, $warn );
}

sub EscapeJSON($;$) {
    my ( $str, $quote ) = @_;
    unless ($quote) {
        return 'null' unless defined $str;
        return lc($str) if $str =~ /^(true|false)$/i;
        return $str
          if $str =~ /^-?(\d|[1-9]\d{1,14})(\.\d{1,16})?(e[-+]?\d{1,3})?$/i;
    }
    return '""' unless defined $str;
    return '"base64:' . EncodeBase64( $str, 1 ) . '"'
      if Image::ExifTool::IsUTF8( \$str ) < 0;
    $str =~ s/(["\t\n\r\\])/\\$jsonChar{$1}/sg;
    $str =~ tr/\0//d;

    $str =~ s/([\0-\x1f])/sprintf("\\u%.4X",ord $1)/sge;
    return '"' . $str . '"';
}

sub GetLangCode($) {
    my $tag = shift;
    if ( $tag =~
        /^(\w+)[-_]([a-z]{2,3}|[xi])([-_][a-z\d]{2,8}([-_][a-z\d]{1,8})*)?$/i )
    {
        my ( $tg, $langCode ) = ( $1, lc($2) );
        $langCode .= ( length($3) == 3 ? uc($3) : lc($3) ) if $3;
        $langCode =~ tr/_/-/;
        $langCode = '' if lc($langCode) eq 'x-default';
        return ( $tg, $langCode );
    }
    else {
        return ( $tag, undef );
    }
}

sub DumpStruct($;$) {
    local $_;
    my ( $obj, $indent ) = @_;

    $indent or $indent = '';
    if ( ref $obj eq 'HASH' ) {
        print "{\n";
        foreach ( Image::ExifTool::OrderedKeys($obj) ) {
            print "$indent  $_ = ";
            DumpStruct( $$obj{$_}, "$indent  " );
        }
        print $indent, "},\n";
    }
    elsif ( ref $obj eq 'ARRAY' ) {
        print "[\n";
        foreach (@$obj) {
            print "$indent  ";
            DumpStruct( $_, "$indent  " );
        }
        print $indent, "],\n",;
    }
    else {
        print "\"$obj\",\n";
    }
}

sub CheckStruct($$$) {
    my ( $et, $struct, $strTable ) = @_;

    my $strName =
      $$strTable{STRUCT_NAME} || ( 'XMP ' . RegisterNamespace($strTable) );
    ref $struct eq 'HASH'
      or return wantarray ? ( undef, "Expecting $strName structure" ) : undef;

    my ( $key, $err, $warn, %copy, $rtnVal, $val );
    $copy{_ordered_keys_} = [] if $$struct{_ordered_keys_};
  Key:
    foreach $key ( Image::ExifTool::OrderedKeys($struct) ) {
        my $tag = $key;
        my ( $type, $fieldInfo );
        $type      = 'ValueConv' if $tag =~ s/#$//;
        $fieldInfo = $$strTable{$tag} unless $specialStruct{$tag};
        unless ($fieldInfo) {
            my ($fix) = reverse sort grep /^$tag$/i, keys %$strTable;
            $fieldInfo = $$strTable{ $tag = $fix }
              if $fix and not $specialStruct{$fix};
        }
        until ( ref $fieldInfo eq 'HASH' ) {
            unless ( $$strTable{NAMESPACE} ) {
                my ( $grp, $tg, $langCode );
                ( $grp, $tg ) =
                  $tag =~ /^(.+):(.+)/ ? ( lc $1, $2 ) : ( '', $tag );
                undef $grp if $grp eq 'XMP';
                require Image::ExifTool::TagLookup;
                my @matches = Image::ExifTool::TagLookup::FindTagInfo($tg);
                unless (@matches) {
                    ( $tg, $langCode ) = GetLangCode($tg);
                    @matches = Image::ExifTool::TagLookup::FindTagInfo($tg)
                      if defined $langCode;
                }
                my ( $tagInfo, $priority, $ti, $g1 );
                foreach $ti (@matches) {
                    my @grps = $et->GetGroup($ti);
                    next unless $grps[0] eq 'XMP';
                    next if $grp and $grp ne lc $grps[1];
                    next
                      if defined $langCode
                      and
                      not( $$ti{Writable} and $$ti{Writable} eq 'lang-alt' );
                    my $pri = $$ti{Priority} || 1;
                    $pri -= 10 if $$ti{Avoid};
                    next       if defined $priority and $priority >= $pri;
                    $priority = $pri;
                    $tagInfo  = $ti;
                    $g1       = $grps[1];
                }
                $tagInfo
                  or $warn = "'${tag}' is not a writable XMP tag", next Key;
                GetPropertyPath($tagInfo);
                $tag = $$tagInfo{Name};
                $tag = "$g1:$tag" if $grp;
                $tag .= "-$langCode" if $langCode;
                $fieldInfo = $$strTable{$tag};
                $fieldInfo
                  or $fieldInfo = $$strTable{$tag} = {
                    %$tagInfo,
                    Namespace => $$tagInfo{Namespace}
                      || $$tagInfo{Table}{NAMESPACE},
                    LangCode => $langCode,
                  };
                delete $$fieldInfo{Description};
                delete $$fieldInfo{Groups};
                last;
            }
            my ( $tg, $langCode ) = GetLangCode($tag);
            if ( defined $langCode ) {
                $fieldInfo = $$strTable{$tg} unless $specialStruct{$tg};
                unless ($fieldInfo) {
                    my ($fix) = reverse sort grep /^$tg$/i, keys %$strTable;
                    $fieldInfo = $$strTable{ $tg = $fix }
                      if $fix and not $specialStruct{$fix};
                }
                if (    ref $fieldInfo eq 'HASH'
                    and $$fieldInfo{Writable}
                    and $$fieldInfo{Writable} eq 'lang-alt' )
                {
                    my $srcInfo = $fieldInfo;
                    $tag       = $tg . '-' . $langCode if $langCode;
                    $fieldInfo = $$strTable{$tag};
                    $fieldInfo
                      or $fieldInfo = $$strTable{$tag} = {
                        %$srcInfo,
                        TagID    => $tg,
                        LangCode => $langCode,
                      };
                    last;
                }
            }
            $warn = "'${tag}' is not a field of $strName";
            next Key;
        }
        if ( ref $$struct{$key} eq 'HASH' ) {
            $$fieldInfo{Struct}
              or $warn = "$tag is not a structure in $strName", next Key;
            ( $val, $err ) =
              CheckStruct( $et, $$struct{$key}, $$fieldInfo{Struct} );
            $err and $warn = $err, next Key;
            $copy{$tag}    = $val;
        }
        elsif ( ref $$struct{$key} eq 'ARRAY' ) {
            $$fieldInfo{List}
              or $warn = "$tag is not a list in $strName", next Key;
            my ( $item, @copy );
            my $i = 0;
            foreach $item ( @{ $$struct{$key} } ) {
                if ( not ref $item ) {
                    $item = '' unless defined $item;
                    if ( $$fieldInfo{Struct} ) {
                        $item =~ /^\s*$/
                          or $warn = "$tag items are not valid structures",
                          next Key;
                        $copy[$i] = {};
                    }
                    else {
                        $et->Sanitize( \$item );
                        ( $copy[$i], $err ) =
                          $et->ConvInv( $item, $fieldInfo, $tag, $strName,
                            $type, '' );
                        $copy[$i]      = '' unless defined $copy[$i];
                        $err and $warn = $err, next Key;
                        $err = CheckXMP( $et, $fieldInfo, \$copy[$i] );
                        $err and $warn = "$err in $strName $tag", next Key;
                    }
                }
                elsif ( ref $item eq 'HASH' ) {
                    $$fieldInfo{Struct}
                      or $warn = "$tag is not a structure in $strName",
                      next Key;
                    ( $copy[$i], $err ) =
                      CheckStruct( $et, $item, $$fieldInfo{Struct} );
                    $err and $warn = $err, next Key;
                }
                else {
                    $warn = "Invalid value for $tag in $strName";
                    next Key;
                }
                ++$i;
            }
            $copy{$tag} = \@copy;
        }
        elsif ( $$fieldInfo{Struct} ) {
            $warn = "Improperly formed structure in $strName $tag";
            next;
        }
        else {
            $et->Sanitize( \$$struct{$key} );
            ( $val, $err ) =
              $et->ConvInv( $$struct{$key}, $fieldInfo, $tag, $strName, $type,
                '' );
            $err and $warn = $err, next Key;
            next Key unless defined $val;
            $err = CheckXMP( $et, $fieldInfo, \$val );
            $err and $warn = "$err in $strName $tag", next Key;
            $copy{$tag} = $$fieldInfo{List} ? [$val] : $val;
        }
        push @{ $copy{_ordered_keys_} }, $tag if $copy{_ordered_keys_};
    }
    if ( %copy or not $warn ) {
        $rtnVal = \%copy;
        undef $err;
        $$et{CHECK_WARN} = $warn if $warn;
    }
    else {
        $err = $warn;
    }
    return wantarray ? ( $rtnVal, $err ) : $rtnVal;
}

sub DeleteStruct($$$$$) {
    my ( $et,          $capture,       $pathPt,  $nvHash, $changed ) = @_;
    my ( $deleted,     $added,         $existed, $p, $pp, $val, $delPath );
    my ( @structPaths, @matchingPaths, @delPaths );

    ( $pp = $$pathPt ) =~ s/ \d+/ \\d\+/g;
    @structPaths = sort grep( /^$pp(\/|$)/, keys %$capture );
    $existed     = 1 if @structPaths;
    if ( $$nvHash{DelValue} ) {
        if ( @{ $$nvHash{DelValue} } ) {
            my $strTable = $$nvHash{TagInfo}{Struct};
            foreach $val ( @{ $$nvHash{DelValue} } ) {
                next unless ref $val eq 'HASH';
                my ( %cap, $p2, %match );
                next
                  unless AddNewStruct( undef, undef, \%cap, $$pathPt, $val,
                    $strTable );
                foreach $p ( keys %cap ) {
                    if ( $p =~ / / ) {
                        ( $p2 = $p ) =~ s/ \d+/ \\d\+/g;
                        @matchingPaths = sort grep( /^$p2$/, @structPaths );
                    }
                    else {
                        push @matchingPaths, $p;
                    }
                    foreach $p2 (@matchingPaths) {
                        $p2 =~ /^($pp)/ or next;
                        my $attr = $cap{$p}[1];
                        if ( $$attr{'xml:lang'} ) {
                            my $a2 = $$capture{$p2}[1];
                            next
                              unless $$a2{'xml:lang'}
                              and $$a2{'xml:lang'} eq $$attr{'xml:lang'};
                        }
                        if (    $$capture{$p2}
                            and $$capture{$p2}[0] eq $cap{$p}[0] )
                        {
                            $match{$1} = ( $match{$1} || 0 ) + 1;
                        }
                    }
                }
                my $num = scalar( keys %cap );
                foreach $p ( keys %match ) {
                    next unless $match{$p} == $num;
                    foreach $p2 (@structPaths) {
                        push @delPaths, $p2 if $p2 =~ /^$p/;
                    }
                    $delPath = $p if not $delPath or $delPath gt $p;
                }
            }
        }
    }
    elsif (@structPaths) {
        @delPaths = @structPaths;
        $structPaths[0] =~ /^($pp)/;
        $delPath = $1;
    }
    if (@delPaths) {
        my $verbose = $et->Options('Verbose');
        @delPaths = sort @delPaths if $verbose > 1;
        foreach $p (@delPaths) {
            if ( $verbose > 1 ) {
                my $p2 = $p;
                $p2 =~ s/^(\w+)/$stdXlatNS{$1} || $1/e;
                $et->VerboseValue( "- XMP-$p2", $$capture{$p}[0] );
            }
            delete $$capture{$p};
            $deleted = 1;
            ++$$changed;
        }
        $delPath
          or warn("Internal error 1 in DeleteStruct\n"),
          return ( undef, undef, $existed );
        $$pathPt = $delPath;
    }
    elsif ( $$nvHash{TagInfo}{List} ) {
        if (@structPaths) {
            $structPaths[-1] =~ /^($pp)/
              or warn("Internal error 2 in DeleteStruct\n"),
              return ( undef, undef, $existed );
            my $path = $1;
            if ( $$capture{$path} ) {
                my $cap = $$capture{$path};
                $et->Error( "Improperly structured XMP ($path)", 1 )
                  if ref $cap ne 'ARRAY' or $$cap[0];
                delete $$capture{$path};
            }
            $path =~ m/.* (\d+)/g
              or warn("Internal error 3 in DeleteStruct\n"),
              return ( undef, undef, $existed );
            $added = $1;
            my $len = length $added;
            my $pos = pos($path) - $len;
            my $nxt = substr( $added, 1 ) + 1;
            substr( $path, $pos, $len ) = length($nxt) . $nxt;
            $$pathPt = $path;
        }
        else {
            $added = '10';
        }
    }
    return ( $deleted, $added, $existed );
}

sub AddNewTag($$$$$$) {
    my ( $et, $tagInfo, $capture, $path, $valPtr, $langIdx ) = @_;
    my $val = EscapeXML($$valPtr);
    my %attrs;
    if ( $$tagInfo{Resource} ) {
        $attrs{'rdf:resource'} = $val;
        $val = '';
    }
    if ( $$tagInfo{Writable} and $$tagInfo{Writable} eq 'lang-alt' ) {
        my $langCode = $$tagInfo{LangCode};
        my $i = $$langIdx{$path} || 0;
        $$langIdx{$path} = $i + 1;
        if ($i) {
            my $idx = length($i) . $i;
            $path =~ s/(.*) \d+/$1 $idx/;
        }
        $attrs{'xml:lang'} = $langCode || 'x-default';
    }
    $$capture{$path} = [ $val, \%attrs ];
    if ( $et and $et->Options('Verbose') > 1 ) {
        my $p = $path;
        $p =~ s/^(\w+)/$stdXlatNS{$1} || $1/e;
        $et->VerboseValue( "+ XMP-$p", $val );
    }
}

sub AddNewStruct($$$$$$) {
    my ( $et, $tagInfo, $capture, $basePath, $struct, $strTable ) = @_;
    my $verbose = $et ? $et->Options('Verbose') : 0;
    my ( $tag, %langIdx );

    my $ns      = $$strTable{NAMESPACE} || '';
    my $changed = 0;

    %$struct or $$struct{'~dummy~'} = '';

    foreach $tag ( Image::ExifTool::OrderedKeys($struct) ) {
        my $fieldInfo = $$strTable{$tag};
        unless ($fieldInfo) {
            next unless $tag eq '~dummy~';
            $fieldInfo = {};
        }
        my $val      = $$struct{$tag};
        my $propPath = $$fieldInfo{PropertyPath};
        unless ($propPath) {
            $propPath = ( $$fieldInfo{Namespace} || $ns ) . ':'
              . ( $$fieldInfo{TagID} || $tag );
            if ( $$fieldInfo{List} ) {
                $propPath .= "/rdf:$$fieldInfo{List}/rdf:li 10";
            }
            if ( $$fieldInfo{Writable} and $$fieldInfo{Writable} eq 'lang-alt' )
            {
                $propPath .= "/rdf:Alt/rdf:li 10";
            }
            $$fieldInfo{PropertyPath} = $propPath;
        }
        my $path = $basePath . '/' . ConformPathToNamespace( $et, $propPath );
        my $addedTag;
        if ( ref $val eq 'HASH' ) {
            my $subStruct = $$fieldInfo{Struct} or next;
            $changed +=
              AddNewStruct( $et, $tagInfo, $capture, $path, $val, $subStruct );
        }
        elsif ( ref $val eq 'ARRAY' ) {
            next unless $$fieldInfo{List};
            my $i = 0;
            my ( $item, $p );
            my $level = scalar( () = ( $propPath =~ / \d+/g ) );
            foreach $item ( @{$val} ) {
                if ($i) {
                    $p = ConformPathToNamespace( $et, $propPath );
                    my $idx = length($i) . $i;
                    $p =~ s/ \d+/ $idx/;
                    $p = "$basePath/$p";
                }
                else {
                    $p = $path;
                }
                if ( ref $item eq 'HASH' ) {
                    my $subStruct = $$fieldInfo{Struct} or next;
                    AddNewStruct( $et, $tagInfo, $capture, $p, $item,
                        $subStruct )
                      or next;
                }
                elsif ( length $item or ( defined $item and $level == 1 ) ) {
                    AddNewTag( $et, $fieldInfo, $capture, $p, \$item,
                        \%langIdx );
                    $addedTag = 1;
                }
                ++$changed;
                ++$i;
            }
        }
        else {
            AddNewTag( $et, $fieldInfo, $capture, $path, \$val, \%langIdx );
            $addedTag = 1;
            ++$changed;
        }
        if ( $addedTag and $$fieldInfo{StructType} and $$fieldInfo{Table} ) {
            AddStructType( $et, $$fieldInfo{Table}, $capture, $propPath,
                $basePath );
        }
    }
    if ( $$strTable{TYPE} and $changed ) {
        my $path = $basePath . '/' . ConformPathToNamespace( $et, "rdf:type" );
        unless ( $$capture{$path} ) {
            $$capture{$path} = [ '', { 'rdf:resource' => $$strTable{TYPE} } ];
            if ( $verbose > 1 ) {
                my $p = $path;
                $p =~ s/^(\w+)/$stdXlatNS{$1} || $1/e;
                $et->VerboseValue( "+ XMP-$p", $$strTable{TYPE} );
            }
        }
    }
    return $changed;
}

sub ConvertStruct($$$$;$) {
    my ( $et, $tagInfo, $value, $type, $parentID ) = @_;
    if ( ref $value eq 'HASH' ) {
        my ( %struct, $key );
        my $table = $$tagInfo{Table};
        $parentID = $$tagInfo{TagID} unless $parentID;
        $struct{_ordered_keys_} = [] if $$value{_ordered_keys_};
        foreach $key ( Image::ExifTool::OrderedKeys($value) ) {
            my $tagID    = $parentID . ucfirst($key);
            my $flatInfo = $$table{$tagID};
            unless ($flatInfo) {
                if ( $key =~ /^XMP-(.*?:)(.*)/ ) {
                    $tagID    = $1 . $parentID . ucfirst($2);
                    $flatInfo = $$table{$tagID};
                }
                $flatInfo or $flatInfo = $tagInfo;
            }
            my $v = $$value{$key};
            if ( ref $v ) {
                $v = ConvertStruct( $et, $flatInfo, $v, $type, $tagID );
            }
            else {
                $v = $et->GetValue( $flatInfo, $type, $v );
            }
            if ( defined $v ) {
                $struct{$key} = $v;

                push @{ $struct{_ordered_keys_} }, $key
                  if $struct{_ordered_keys_};
            }
        }
        return \%struct;
    }
    elsif ( ref $value eq 'ARRAY' ) {
        if ( defined $$et{OPTIONS}{ListItem} ) {
            my $li = $$et{OPTIONS}{ListItem};
            return undef unless defined $$value[$li];
            undef $$et{OPTIONS}{ListItem};
            my $val =
              ConvertStruct( $et, $tagInfo, $$value[$li], $type, $parentID );
            $$et{OPTIONS}{ListItem} = $li;
            return $val;
        }
        else {
            my ( @list, $val );
            foreach $val (@$value) {
                my $v = ConvertStruct( $et, $tagInfo, $val, $type, $parentID );
                push @list, $v if defined $v;
            }
            return \@list;
        }
    }
    else {
        return $et->GetValue( $tagInfo, $type, $value );
    }
}

sub RestoreStruct($;$) {
    local $_;
    my ( $et, $keepFlat ) = @_;
    my ( $key, %structs, %var, %lists, $si, %listKeys, @siList );
    my $valueHash = $$et{VALUE};
    my $fileOrder = $$et{FILE_ORDER};
    my $tagExtra  = $$et{TAG_EXTRA};
    foreach $key ( keys %{ $$et{TAG_INFO} } ) {
        my $structProps = $$tagExtra{$key}{Struct} or next;
        delete $$tagExtra{$key}{Struct};
        my $tagInfo = $$et{TAG_INFO}{$key};
        my $table   = $$tagInfo{Table};
        my $prop    = shift @$structProps;
        my $tag     = $$prop[0];
        my $strInfo = @$structProps ? $$table{$tag} : $tagInfo;
        if ($strInfo) {
            ref $strInfo eq 'HASH' or next;
            if ( @$structProps and not $$strInfo{Struct} ) {
                $et->Warn("$$strInfo{Name} is not a structure!")
                  unless $$et{NO_STRUCT_WARN};
                next;
            }
        }
        else {
            my $g1   = $$table{GROUPS}{0} || 'XMP';
            my $name = $tag;
            if ( $tag =~ /(.+):(.+)/ ) {
                my $ns;
                ( $ns, $name ) = ( $1, $2 );
                $ns =~ s/^XMP-//;
                $ns = $stdXlatNS{$ns} if $stdXlatNS{$ns};
                $g1 .= "-$ns";
            }
            $strInfo = {
                Name   => ucfirst $name,
                Groups => { 1 => $g1 },
                Struct => 'Unknown',
            };
            if (@$structProps) {
                $$strInfo{Struct} = { STRUCT_NAME => 'XMP Unknown' }
                  if @$structProps;
            }
            elsif ( $$tagInfo{LangCode} ) {
                $tag = $tag . '-' . $$tagInfo{LangCode};
                $$strInfo{LangCode} = $$tagInfo{LangCode};
            }
            AddTagToTable( $table, $tag, $strInfo );
        }
        $tag = $strInfo;
        my $struct    = \%structs;
        my $oldStruct = $structs{$strInfo};
        my $writable = $$tagInfo{Writable} || '';
        my ( $err, $i );
        for ( ; ; ) {
            my $index = $$prop[1];
            if ( $index and not @$structProps ) {
                if ( $writable eq 'lang-alt' ) {
                    pop @$prop;
                    undef $index if @$prop < 2;
                }
                if ( $$tagInfo{LangCode} and not ref $tag ) {
                    $tag = $tag . '-' . $$tagInfo{LangCode};
                }
            }
            my $nextStruct = $$struct{$tag};
            if ( defined $index ) {
                $index = substr $index, 1;
                if ($nextStruct) {
                    ref $nextStruct eq 'ARRAY' or $err = 2, last;
                    $struct = $nextStruct;
                }
                else {
                    $struct = $$struct{$tag} = [];
                }
                $nextStruct = $$struct[$index];
                for ( $i = 2 ; $$prop[$i] ; ++$i ) {
                    if ($nextStruct) {
                        ref $nextStruct eq 'ARRAY' or last;
                        $struct = $nextStruct;
                    }
                    else {
                        $lists{$struct} = $struct;
                        $struct = $$struct[$index] = [];
                    }
                    $nextStruct = $$struct[$index];
                    $index      = substr $$prop[$i], 1;
                }
                if ( ref $nextStruct eq 'HASH' ) {
                    $struct = $nextStruct;
                }
                elsif (@$structProps) {
                    $lists{$struct} = $struct;
                    $struct = $$struct[$index] = {};
                }
                else {
                    $lists{$struct} = $struct;
                    $$struct[$index] = $$valueHash{$key};
                    last;
                }
            }
            else {
                if ($nextStruct) {
                    ref $nextStruct eq 'HASH' or $err = 3, last;
                    $struct = $nextStruct;
                }
                elsif (@$structProps) {
                    $struct = $$struct{$tag} = {};
                }
                else {
                    $$struct{$tag} = $$valueHash{$key};
                    last;
                }
            }
            $prop = shift @$structProps or last;
            $tag  = $$prop[0];
            if ( $tag =~ /(.+):(.+)/ ) {
                my ( $ns, $name ) = ( $1, $2 );
                $ns  = $stdXlatNS{$ns} if $stdXlatNS{$ns};
                $tag = "XMP-$ns:" . ucfirst $name;
            }
            else {
                $tag = ucfirst $tag;
            }
        }
        if ($err) {
            unless ( $$et{NO_STRUCT_WARN} ) {
                my $ns =
                  $$tagInfo{Namespace} || $$tagInfo{Table}{NAMESPACE} || '';
                $et->Warn(
"Error $err placing $ns:$$tagInfo{TagID} in structure or list",
                    1
                );
            }
            delete $structs{$strInfo} unless $oldStruct;
        }
        elsif ( $tagInfo eq $strInfo ) {
            if ($oldStruct) {
                my $k = $listKeys{$oldStruct};
                if ($k) {
                    if ( $k lt $key ) {
                        $$fileOrder{$k} = $$fileOrder{$key}
                          if $$fileOrder{$k} > $$fileOrder{$key};
                        $et->DeleteTag($key);
                        next;
                    }
                    $$fileOrder{$key} = $$fileOrder{$k}
                      if $$fileOrder{$key} > $$fileOrder{$k};
                    $et->DeleteTag($k);
                }
            }
            $$valueHash{$key} = $structs{$strInfo};
            $listKeys{ $structs{$strInfo} } = $key;
        }
        else {
            if ( $var{$strInfo} ) {
                if ( $var{$strInfo}[1] > $$fileOrder{$key} ) {
                    $var{$strInfo}[1] = $$fileOrder{$key} - 0.5;
                }
            }
            else {
                $var{$strInfo} = [ $strInfo, $$fileOrder{$key} - 0.5 ];
            }
            if ($keepFlat) {
                my $extra = $$tagExtra{$key};
                if ( $$extra{NoList} ) {
                    $$valueHash{$key} = $$extra{NoList};
                    delete $$extra{NoList};
                }
                elsif ( $$extra{NoListDel} ) {
                    $et->DeleteTag($key);
                }
            }
            else {
                $et->DeleteTag($key);
            }
        }
    }
    foreach $si ( keys %lists ) {
        defined $_ or $_ = '' foreach @{ $lists{$si} };
    }
    $var{$_} and push @siList, $_ foreach keys %structs;
    foreach $si ( sort { $var{$a}[1] <=> $var{$b}[1] } @siList ) {
        $key = $var{$si}[0]{Name};
        my $found;
        if ( $$valueHash{$key} ) {
            my @keys = grep /^$key( \(\d+\))?$/, keys %$valueHash;
            foreach $key (@keys) {
                next unless $$valueHash{$key} eq $structs{$si};
                $found = 1;
                last;
            }
        }
        unless ($found) {
            $key = $et->FoundTag( $var{$si}[0], '' );
            $$valueHash{$key} = $structs{$si};
        }
        $$fileOrder{$key} = $var{$si}[1];
    }
}

1;

__END__

