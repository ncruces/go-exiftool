
package Image::ExifTool::OOXML;

use strict;
use vars            qw($VERSION);
use Image::ExifTool qw(:DataAccess :Utils);
use Image::ExifTool::XMP;
use Image::ExifTool::ZIP;

$VERSION = '1.10';

my %isOOXML = (
    DOCX => 1,
    DOCM => 1,
    DOTX => 1,
    DOTM => 1,
    POTX => 1,
    POTM => 1,
    PPAX => 1,
    PPAM => 1,
    PPSX => 1,
    PPSM => 1,
    PPTX => 1,
    PPTM => 1,
    THMX => 1,
    XLAM => 1,
    XLSX => 1,
    XLSM => 1,
    XLSB => 1,
    XLTX => 1,
    XLTM => 1,
    VSDX => 1,
);

my %fileType;
{
    my $type;
    foreach $type ( keys %isOOXML ) {
        $fileType{ $Image::ExifTool::mimeType{$type} } = $type;
    }
}

my %queuedAttrs;
my %queueAttrs = (
    fmtid => 1,
    pid   => 1,
    name  => 1,
);

my $vectorCount;
my @vectorVals;

%Image::ExifTool::OOXML::Main = (
    GROUPS       => { 0 => 'XML', 1 => 'XML', 2 => 'Document' },
    PROCESS_PROC => \&Image::ExifTool::XMP::ProcessXMP,
    VARS         => { ID_FMT => 'none' },
    NOTES        => q{
        The Office Open XML (OOXML) format was introduced with Microsoft Office 2007
        and is used by file types such as DOCX, PPTX, XLSX and VSDX.  These are
        essentially ZIP archives containing XML files.  The table below lists some
        tags which have been observed in OOXML documents, but ExifTool will extract
        any tags found from XML files of the OOXML document properties ("docProps")
        directory.

        B<Tips:>

        1) Structural ZIP tags may be ignored (if desired) with C<--ZIP:all> on the
        command line.

        2) Tags may be grouped by their document number in the ZIP archive with the
        C<-g3> or C<-G3> option.
    },
    Application          => {},
    AppVersion           => {},
    category             => {},
    Characters           => {},
    CharactersWithSpaces => {},
    CheckedBy            => {},
    Client               => {},
    Company              => {},
    created              => {
        Name      => 'CreateDate',
        Groups    => { 2 => 'Time' },
        Format    => 'date',
        PrintConv => '$self->ConvertDateTime($val)',
    },
    createdType   => { Hidden => 1, RawConv => 'undef' },
    DateCompleted => {
        Groups    => { 2 => 'Time' },
        Format    => 'date',
        PrintConv => '$self->ConvertDateTime($val)',
    },
    Department  => {},
    Destination => {},
    Disposition => {},
    Division    => {},
    DocSecurity => {
        PrintConv => {
            0 => 'None',
            1 => 'Password protected',
            2 => 'Read-only recommended',
            4 => 'Read-only enforced',
            8 => 'Locked for annotations',
        },
    },
    DocumentNumber    => {},
    Editor            => { Groups => { 2 => 'Author' } },
    ForwardTo         => {},
    Group             => {},
    HeadingPairs      => {},
    HiddenSlides      => {},
    HyperlinkBase     => {},
    HyperlinksChanged => { PrintConv => { 'false' => 'No', 'true' => 'Yes' } },
    keywords          => {},
    Language          => {},
    lastModifiedBy    => { Groups => { 2 => 'Author' } },
    lastPrinted       => {
        Groups    => { 2 => 'Time' },
        Format    => 'date',
        PrintConv => '$self->ConvertDateTime($val)',
    },
    Lines         => {},
    LinksUpToDate => { PrintConv => { 'false' => 'No', 'true' => 'Yes' } },
    Mailstop      => {},
    Manager       => {},
    Matter        => {},
    MMClips       => {},
    modified      => {
        Name      => 'ModifyDate',
        Groups    => { 2 => 'Time' },
        Format    => 'date',
        PrintConv => '$self->ConvertDateTime($val)',
    },
    modifiedType       => { Hidden => 1, RawConv => 'undef' },
    Notes              => {},
    Office             => {},
    Owner              => { Groups => { 2 => 'Author' } },
    Pages              => {},
    Paragraphs         => {},
    PresentationFormat => {},
    Project            => {},
    Publisher          => {},
    Purpose            => {},
    ReceivedFrom       => {},
    RecordedBy         => {},
    RecordedDate       => {
        Groups    => { 2 => 'Time' },
        Format    => 'date',
        PrintConv => '$self->ConvertDateTime($val)',
    },
    Reference       => {},
    revision        => { Name      => 'RevisionNumber' },
    ScaleCrop       => { PrintConv => { 'false' => 'No', 'true' => 'Yes' } },
    SharedDoc       => { PrintConv => { 'false' => 'No', 'true' => 'Yes' } },
    Slides          => {},
    Source          => {},
    Status          => {},
    TelephoneNumber => {},
    Template        => {},
    TitlesOfParts   => {},
    TotalTime       => {
        Name      => 'TotalEditTime',
        PrintConv => 'ConvertTimeSpan($val, 60)',
    },
    Typist => {},
    Words  => {},
);

sub GetTagID($) {
    my $props = shift;
    my ( $tag, $prop, $namespace );
    foreach $prop (@$props) {
        my ( $ns, $nm ) =
          ( $prop =~ /(.*?):(.*)/ ) ? ( $1, $2 ) : ( '', $prop );
        next if $ns eq 'vt';
        if ( defined $tag ) {
            $tag .= ucfirst($nm);
        }
        elsif ( $prop ne 'Properties'
            and $prop ne 'cp:coreProperties'
            and $prop ne 'property' )
        {
            $tag = $nm;
            $namespace = $ns unless $namespace;
        }
    }
    return ( $tag, $namespace || '' );
}

sub FoundTag($$$$;$) {
    my ( $et, $tagTablePtr, $props, $val, $attrs ) = @_;
    return 0 unless @$props;
    my $verbose = $et->Options('Verbose');

    my $tag = $$props[-1];
    $et->VPrint( 0, "  | - Tag '", join( '/', @$props ), "'\n" )
      if $verbose > 1;

    $val = Image::ExifTool::XMP::UnescapeXML($val);
    $val =~ s/_x([0-9a-f]{4})_/Image::ExifTool::PackUTF8(hex($1))/gie;
    $val = $et->Decode( $val, 'UTF8' );
    if ( $queueAttrs{$tag} ) {
        $queuedAttrs{$tag} = $val;
        return 0;
    }
    my $ns;
    ( $tag, $ns ) = GetTagID($props);
    if ( not $tag ) {
        my $name = $queuedAttrs{name} or return 0;
        $name =~ s/(^| )([a-z])/$1\U$2/g;
        ( $tag = $name ) =~ tr/-_a-zA-Z0-9//dc;
        return 0 unless length $tag;
        unless ( $$tagTablePtr{$tag} ) {
            my %tagInfo = (
                Name        => $tag,
                Description => $name,
            );
            if ( $$props[-1] eq 'vt:filetime' ) {
                $tagInfo{Groups} = { 2 => 'Time' },
                  $tagInfo{Format} = 'date',
                  $tagInfo{PrintConv} = '$self->ConvertDateTime($val)';
            }
            $et->VPrint( 0, "  | [adding $tag]\n" ) if $verbose;
            AddTagToTable( $tagTablePtr, $tag, \%tagInfo );
        }
    }
    elsif ( $tag eq 'xmlns' ) {
        return 0;
    }
    elsif ( ref $Image::ExifTool::XMP::Main{$ns} eq 'HASH'
        and $Image::ExifTool::XMP::Main{$ns}{SubDirectory} )
    {
        my $table = $Image::ExifTool::XMP::Main{$ns}{SubDirectory}{TagTable};
        no strict 'refs';
        if ( $table and %$table ) {
            $tagTablePtr = Image::ExifTool::GetTagTable($table);
        }
    }
    elsif ( @$props > 2 and grep /^vt:vector$/, @$props ) {
        if ( $$props[-1] eq 'vt:size' ) {
            $vectorCount = $val;
            undef @vectorVals;
            return 0;
        }
        elsif ( $$props[-1] eq 'vt:baseType' ) {
            return 0;
        }
        elsif ($vectorCount) {
            --$vectorCount;
            if ($vectorCount) {
                push @vectorVals, $val;
                return 0;
            }
            $val = [ @vectorVals, $val ] if @vectorVals;
        }
    }
    if ( $$tagTablePtr{$tag} ) {
        my $tagInfo = $$tagTablePtr{$tag};
        if ( ref $tagInfo eq 'HASH' ) {
            my $fmt = $$tagInfo{Format} || $$tagInfo{Writable} || '';
            $val = Image::ExifTool::XMP::ConvertXMPDate($val) if $fmt eq 'date';
        }
    }
    else {
        $et->VPrint( 0, "  [adding $tag]\n" ) if $verbose;
        AddTagToTable( $tagTablePtr, $tag, { Name => ucfirst $tag } );
    }
    $et->HandleTag( $tagTablePtr, $tag, $val );

    undef $vectorCount;
    undef %queuedAttrs;

    return 1;
}

sub ProcessDOCX($$) {
    my ( $et, $dirInfo ) = @_;
    my $zip         = $$dirInfo{ZIP};
    my $tagTablePtr = GetTagTable('Image::ExifTool::OOXML::Main');
    my $mime        = $$dirInfo{MIME} || $Image::ExifTool::mimeType{DOCX};

    my $fileType = $fileType{$mime};
    if ($fileType) {
        if (    $fileType eq 'PPTX'
            and $$et{FILE_EXT}
            and $$et{FILE_EXT} eq 'THMX' )
        {
            $fileType = 'THMX';
        }
    }
    else {
        $et->VPrint( 0, "Unrecognized MIME type: $mime\n" );
        $fileType = $$et{FILE_EXT};
        $fileType = 'DOCX' unless $fileType and $isOOXML{$fileType};
    }
    $et->SetFileType($fileType);

    local $SIG{'__WARN__'} = \&Image::ExifTool::ZIP::WarnProc;
    my $docNum  = 0;
    my @members = $zip->members();
    my $member;
    foreach $member (@members) {
        my $file = $member->fileName();
        next unless defined $file;
        $et->VPrint( 0, "File: $file\n" );
        $$et{DOC_NUM} = ++$docNum;
        Image::ExifTool::ZIP::HandleMember( $et, $member );
        next unless $file =~ m{^docProps/(.*\.xml|(thumbnail\.(jpe?g|wmf)))$}i;
        my ( $buff, $status ) = $zip->contents($member);
        $status and $et->Warn("Error extracting $file"), next;

        if ( $file =~ /\.(jpe?g|wmf)$/i ) {
            my $tag = $file =~ /\.wmf$/i ? 'PreviewWMF' : 'PreviewImage';
            $et->FoundTag( $tag, \$buff );
            next;
        }
        my %dirInfo = (
            DataPt => \$buff,
            DirLen => length $buff,
            DirStart     => ( $buff =~ /<\?xml\s+.*?\?>/g ? pos($buff) : 0 ),
            DataLen      => length $buff,
            XMPParseOpts => { FoundProc => \&FoundTag },
        );
        $et->ProcessDirectory( \%dirInfo, $tagTablePtr );
        undef $buff;
    }
    delete $$et{DOC_NUM};
    return 1;
}

1;

__END__


