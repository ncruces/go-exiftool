
package Image::ExifTool::Charset;

use strict;
use vars            qw($VERSION %csType);
use Image::ExifTool qw(:DataAccess :Utils);

$VERSION = '1.11';

my %charsetTable;

my %unicode2byte = (
    Latin => {
        0x20ac => 0x80,
        0x0160 => 0x8a,
        0x2013 => 0x96,
        0x201a => 0x82,
        0x2039 => 0x8b,
        0x2014 => 0x97,
        0x0192 => 0x83,
        0x0152 => 0x8c,
        0x02dc => 0x98,
        0x201e => 0x84,
        0x017d => 0x8e,
        0x2122 => 0x99,
        0x2026 => 0x85,
        0x2018 => 0x91,
        0x0161 => 0x9a,
        0x2020 => 0x86,
        0x2019 => 0x92,
        0x203a => 0x9b,
        0x2021 => 0x87,
        0x201c => 0x93,
        0x0153 => 0x9c,
        0x02c6 => 0x88,
        0x201d => 0x94,
        0x017e => 0x9e,
        0x2030 => 0x89,
        0x2022 => 0x95,
        0x0178 => 0x9f,
    },
);

%csType = (
    UTF8         => 0x100,
    ASCII        => 0x100,
    Arabic       => 0x101,
    Baltic       => 0x101,
    Cyrillic     => 0x101,
    Greek        => 0x101,
    Hebrew       => 0x101,
    Latin        => 0x101,
    Latin2       => 0x101,
    DOSLatinUS   => 0x101,
    DOSLatin1    => 0x101,
    DOSCyrillic  => 0x101,
    MacCroatian  => 0x101,
    MacCyrillic  => 0x101,
    MacGreek     => 0x101,
    MacIceland   => 0x101,
    MacLatin2    => 0x101,
    MacRoman     => 0x101,
    MacRomanian  => 0x101,
    MacTurkish   => 0x101,
    Thai         => 0x101,
    Turkish      => 0x101,
    Vietnam      => 0x101,
    MacArabic    => 0x103,
    PDFDoc       => 0x181,
    Unicode      => 0x200,
    UCS2         => 0x200,
    UTF16        => 0x200,
    Symbol       => 0x201,
    JIS          => 0x201,
    UCS4         => 0x400,
    MacChineseCN => 0x803,
    MacChineseTW => 0x803,
    MacHebrew    => 0x803,
    MacKorean    => 0x803,
    MacRSymbol   => 0x803,
    MacThai      => 0x803,
    MacJapanese  => 0x883,
    ShiftJIS     => 0x883,
);

sub LoadCharset($) {
    my $charset = shift;
    my $conv    = $charsetTable{$charset};
    unless ($conv) {
        my $module = "Image::ExifTool::Charset::$charset";
        no strict 'refs';
        if ( %$module or eval "require $module" ) {
            $conv = $charsetTable{$charset} = \%$module;
        }
    }
    return $conv;
}

sub IsUTF16($) {
    local $_;
    my $uni = shift;
    my $surrogate;
    foreach (@$uni) {
        my $hiBits = ( $_ & 0xfc00 );
        if ( $hiBits == 0xfc00 ) {
            return 0
              if $_ == 0xffff
              or $_ == 0xfffe
              or ( $_ >= 0xfdd0 and $_ <= 0xfdef );
        }
        elsif ($surrogate) {
            return 0 if $hiBits != 0xdc00;
            $surrogate = 0;
        }
        else {
            return 0       if $hiBits == 0xdc00;
            $surrogate = 1 if $hiBits == 0xd800;
        }
    }
    return 1 if not defined $surrogate;
    return 2 unless $surrogate;
    return 0;
}

sub Decompose($$$;$) {
    local $_;
    my ( $et, $val, $charset ) = @_;
    my $type = $csType{$charset};
    my ( @uni, $conv );

    if ( $type & 0x001 ) {
        $conv = LoadCharset($charset);
        unless ($conv) {
            $et->Warn("Invalid character set $charset") if $et;
            return \@uni;
        }
    }
    elsif ( $type == 0x100 ) {
        if ( $] < 5.006001 ) {
            @uni = Image::ExifTool::UnpackUTF8($val);
        }
        else {
            undef $Image::ExifTool::evalWarning;
            local $SIG{'__WARN__'} = \&Image::ExifTool::SetWarning;
            @uni = unpack( $] < 5.010000 ? 'U0U*' : 'C0U*', $val );
            if (    $Image::ExifTool::evalWarning
                and $et
                and not $$et{WarnBadUTF8} )
            {
                $et->Warn('Malformed UTF-8 character(s)');
                $$et{WarnBadUTF8} = 1;
            }
        }
        return \@uni;
    }
    if ( $type & 0x100 ) {
        @uni = unpack( 'C*', $val );
        foreach (@uni) {
            $_ = $$conv{$_} if defined $$conv{$_};
        }
    }
    elsif ( $type & 0x600 ) {
        my $unknown;
        my $byteOrder = $_[3];
        if ( not $byteOrder ) {
            $byteOrder = GetByteOrder();
        }
        elsif ( $byteOrder eq 'Unknown' ) {
            $byteOrder = GetByteOrder();
            $unknown   = 1;
        }
        my $fmt = $byteOrder eq 'MM' ? 'n*' : 'v*';
        if ( $type & 0x400 ) {
            $fmt = uc $fmt;

            $val =~ s/^(\0\0\xfe\xff|\xff\xfe\0\0)//
              and $fmt = $1 eq "\0\0\xfe\xff" ? 'N*' : 'V*';
            undef $unknown;
        }
        elsif ( $val =~ s/^(\xfe\xff|\xff\xfe)// ) {
            $fmt = $1 eq "\xfe\xff" ? 'n*' : 'v*';
            undef $unknown;
        }
        @uni = unpack( $fmt, $val );

        if ( not $conv ) {
            if ($unknown) {
                my ( %bh, %bl );
                my ( $zh, $zl ) = ( 0, 0 );
                foreach (@uni) {
                    $bh{ $_ >> 8 } = 1;
                    $bl{ $_ & 0xff } = 1;
                    ++$zh unless $_ & 0xff00;
                    ++$zl unless $_ & 0x00ff;
                }
                my ( $bh, $bl ) = ( scalar( keys %bh ), scalar( keys %bl ) );
                if ( $bh > $bl or ( $bh == $bl and $zl > $zh ) ) {
                    $fmt =~ tr/nvNV/vnVN/;
                    @uni = unpack( $fmt, $val );
                    $$et{WrongByteOrder} = 1;
                }
            }
            if ( $charset eq 'UTF16' ) {
                my $i;
                for ( $i = 0 ; $i < $#uni ; ++$i ) {
                    next
                      unless ( $uni[$i] & 0xfc00 ) == 0xd800
                      and ( $uni[ $i + 1 ] & 0xfc00 ) == 0xdc00;
                    my $cp = 0x10000 + ( ( $uni[$i] & 0x3ff ) << 10 ) +
                      ( $uni[ $i + 1 ] & 0x3ff );
                    splice( @uni, $i, 2, $cp );
                }
            }
        }
        elsif ($unknown) {
            my $e1 = 0;
            foreach (@uni) {
                defined $$conv{$_} and $_ = $$conv{$_}, next;
                ++$e1;
            }
            if ($e1) {
                $fmt = $byteOrder eq 'MM' ? 'v*' : 'n*';
                my @try = unpack( $fmt, $val );
                my $e2  = 0;
                foreach (@try) {
                    defined $$conv{$_} and $_ = $$conv{$_}, next;
                    ++$e2;
                }
                if ( $e2 < $e1 ) {
                    $$et{WrongByteOrder} = 1;
                    return \@try;
                }
            }
        }
        else {
            foreach (@uni) {
                $_ = $$conv{$_} if defined $$conv{$_};
            }
        }
    }
    else {

        my @bytes = unpack( 'C*', $val );
        while (@bytes) {
            my $ch = shift @bytes;
            my $cv = $$conv{$ch};
            $cv or push( @uni, $ch ), next;
            ref $cv or push( @uni, $cv ), next;
            ref $cv eq 'ARRAY' and push( @uni, @$cv ), next;
            $ch = shift @bytes;
            if ( defined $ch ) {
                if ( $$cv{$ch} ) {
                    $cv = $$cv{$ch};
                    ref $cv or push( @uni, $cv ), next;
                    push @uni, @$cv;
                }
                else {
                    push @uni, ord('?');
                    unshift @bytes, $ch;
                }
            }
            else {
                push @uni, ord('?');
            }
        }
    }
    return \@uni;
}

sub Recompose($$;$$) {
    local $_;
    my ( $et, $uni, $charset ) = @_;
    my ( $outVal, $conv, $inv );
    $charset or $charset = $$et{OPTIONS}{Charset};
    my $csType = $csType{$charset};
    if ( $csType == 0x100 ) {
        if ( $] >= 5.006001 ) {
            $outVal = pack( 'C0U*', @$uni );
        }
        else {
            $outVal = Image::ExifTool::PackUTF8(@$uni);
        }
        $outVal =~ s/\0.*//s;
        return $outVal;
    }
    if ( $csType & 0x801 ) {
        $conv = LoadCharset($charset);
        unless ($conv) {
            $et->Warn("Missing charset $charset") if $et;
            return '';
        }
        $inv = $unicode2byte{$charset};
        unless ($inv) {
            if ( not $csType or $csType & 0x802 ) {
                $et->Warn("Invalid destination charset $charset") if $et;
                return '';
            }
            my ( $char, %inv );
            foreach $char ( keys %$conv ) {
                $inv{ $$conv{$char} } = $char;
            }
            $inv = $unicode2byte{$charset} = \%inv;
        }
    }
    if ( $csType & 0x100 ) {

        foreach (@$uni) {
            next if $_ < 0x80;
            $$inv{$_} and $_ = $$inv{$_}, next;
            next if $_ < 0x100 and not $$conv{$_};
            $_ = ord('?');
            if ( $et and not $$et{EncodingError} ) {
                $et->Warn("Some character(s) could not be encoded in $charset");
                $$et{EncodingError} = 1;
            }
        }
        $outVal = pack( 'C*', @$uni );
        $outVal =~ s/\0.*//s;
    }
    else {

        if ($inv) {
            $$inv{$_} and $_ = $$inv{$_} foreach @$uni;
        }
        if ( $charset eq 'UTF16' ) {
            my $i;
            for ( $i = 0 ; $i < @$uni ; ++$i ) {
                next unless $$uni[$i] >= 0x10000 and $$uni[$i] < 0x10ffff;
                my $t  = $$uni[$i] - 0x10000;
                my $w1 = 0xd800 + ( ( $t >> 10 ) & 0x3ff );
                my $w2 = 0xdc00 + ( $t & 0x3ff );
                splice( @$uni, $i, 1, $w1, $w2 );
                ++$i;
            }
        }
        my $byteOrder = $_[3] || GetByteOrder();
        my $fmt       = $byteOrder eq 'MM' ? 'n*' : 'v*';
        $fmt    = uc($fmt) if $csType & 0x400;
        $outVal = pack( $fmt, @$uni );
    }
    return $outVal;
}

1;

__END__

