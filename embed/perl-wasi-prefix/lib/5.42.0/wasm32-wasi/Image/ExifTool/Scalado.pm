
package Image::ExifTool::Scalado;

use strict;
use vars            qw($VERSION);
use Image::ExifTool qw(:DataAccess :Utils);
use Image::ExifTool::PLIST;

$VERSION = '1.01';

sub ProcessScalado($$$);

%Image::ExifTool::Scalado::Main = (
    GROUPS       => { 0 => 'APP4', 1 => 'Scalado', 2 => 'Image' },
    PROCESS_PROC => \&ProcessScalado,
    TAG_PREFIX   => 'Scalado',
    FORMAT       => 'int32s',
    NOTES        => q{
        Tags extracted from the JPEG APP4 "SCALADO" segment found in images from
        HTC, LG and Samsung phones.  (Presumably written by Scalado mobile software,
        L<http://www.scalado.com/>.)
    },
    SPMO => {
        Name    => 'DataLength',
        Unknown => 1,
    },
    WDTH => {
        Name      => 'PreviewImageWidth',
        ValueConv => '$val ? abs($val) : undef',
    },
    HGHT => {
        Name      => 'PreviewImageHeight',
        ValueConv => '$val ? abs($val) : undef',
    },
    QUAL => {
        Name      => 'PreviewQuality',
        ValueConv => '$val ? abs($val) : undef',
    },
);

sub ProcessScalado($$$) {
    my ( $et, $dirInfo, $tagTablePtr ) = @_;
    my $dataPt  = $$dirInfo{DataPt};
    my $pos     = 0;
    my $end     = length $$dataPt;
    my $unknown = $et->Options('Unknown');

    $et->VerboseDir( 'APP4 SCALADO', undef, $end );
    SetByteOrder('MM');

    for ( ; ; ) {
        last if $pos + 12 > $end;
        my $tag = substr( $$dataPt, $pos, 4 );
        my $ver = Get32u( $dataPt, $pos + 4 );
        if ( not $$tagTablePtr{$tag} and $unknown ) {
            my $name = $tag;
            $name =~ tr/-A-Za-z0-9_//dc;
            last unless length $name;
            AddTagToTable(
                $tagTablePtr,
                $tag,
                {
                    Name        => "Scalado_$name",
                    Description => "Scalado $name",
                    Unknown     => 1,
                }
            );
        }
        $et->HandleTag(
            $tagTablePtr, $tag, undef,
            DataPt => $dataPt,
            Start  => $pos + 8,
            Size   => 4,
            Extra  => ", ver $ver",
        );
        if ( $tag eq 'SPMO' ) {
            my $val = Get32u( $dataPt, $pos + 8 );
            if ( $ver < 5 ) {
                $end -= $val;
            }
            else {
                $end = $val + 12;
            }
        }
        $pos += 12;
    }
    return 1;
}

1;

__END__

