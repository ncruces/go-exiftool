package Image::ExifTool::QuickTime;

use strict;

use Image::ExifTool qw(:DataAccess :Utils);
use Image::ExifTool::QuickTime;

sub Process_tx3g($$$);
sub Process_mebx($$$);
sub Process_text($$$;$);
sub ProcessFreeGPS($$$);
sub Process360Fly($$$);
sub ProcessFMAS($$$);
sub ProcessWolfbox($$$);
sub ProcessCAMM($$$);
sub OrderCipherDigits($$$;$);

my %qtFmt = (
    0 => 'undef',
    1 => 'string',

    23 => 'float',
    24 => 'double',
    65 => 'int8s',
    66 => 'int16s',
    67 => 'int32s',
    70 => 'float',
    71 => 'float',
    72 => 'float',
    74 => 'int64s',
    75 => 'int8u',
    76 => 'int16u',
    77 => 'int32u',
    78 => 'int64u',
    79 => 'float',
    80 => 'float',
);

my @dateMax = ( 24, 59, 59, 2200, 12, 31 );

my $gpsBlockSize = 0x8000;

my $knotsToKph = 1.852;
my $mpsToKph   = 3.6;
my $mphToKph   = 1.60934;

my %processByMetaFormat = (
    meta => 1,
    data => 1,
    sbtl => 1,
    ctbx => 1,
);

my %insvDataLen = (
    0x000 => 0,
    0x200 => 0,
    0x300 => 0,
    0x400 => 16,
    0x600 => 8,
    0x700 => 53,

);

my %insvLimit = ( 0x300 => [ 'accelerometer', 20000 ], );

%Image::ExifTool::QuickTime::Stream = (
    GROUPS => { 2 => 'Location' },
    NOTES  => q{
        The tags below are extracted from timed metadata in QuickTime and other
        formats of video files when the ExtractEmbedded option is used.  Although
        most of these tags are combined into the single table below, ExifTool
        currently reads 121 different types of timed GPS metadata from video files.
    },
    GPSLatitude => {
        PrintConv => 'Image::ExifTool::GPS::ToDMS($self, $val, 1, "N")',
        RawConv   => '$$self{FoundGPSLatitude} = 1; $val'
    },
    GPSLongitude =>
      { PrintConv => 'Image::ExifTool::GPS::ToDMS($self, $val, 1, "E")' },
    GPSLatitude2 =>
      { PrintConv => 'Image::ExifTool::GPS::ToDMS($self, $val, 1, "N")' },
    GPSLongitude2 =>
      { PrintConv => 'Image::ExifTool::GPS::ToDMS($self, $val, 1, "E")' },
    GPSAltitude => { PrintConv => '(sprintf("%.4f", $val) + 0) . " m"' },
    GPSSpeed    => {
        PrintConv => 'sprintf("%.4f", $val) + 0',
        Notes     => 'in km/h unless GPSSpeedRef says otherwise'
    },
    GPSSpeedRef => { PrintConv => { K => 'km/h', M => 'mph', N => 'knots' } },
    GPSTrack    => {
        PrintConv => 'sprintf("%.4f", $val) + 0',
        Notes     => 'relative to true north unless GPSTrackRef says otherwise'
    },
    GPSTrackRef =>
      { PrintConv => { M => 'Magnetic North', T => 'True North' } },
    GPSDateTime => {
        Groups      => { 2 => 'Time' },
        Description => 'GPS Date/Time',
        RawConv     => '$$self{FoundGPSDateTime} = 1; $val',
        PrintConv   => '$self->ConvertDateTime($val)',
    },
    DateTimeOriginal => {
        Groups      => { 2 => 'Time' },
        Description => 'Date/Time Original',
        PrintConv   => '$self->ConvertDateTime($val)',
    },
    GPSTimeStamp => {
        PrintConv => 'Image::ExifTool::GPS::PrintTimeStamp($val)',
        Groups    => { 2 => 'Time' }
    },
    GPSSatellites => {},
    GPSDOP        => { Description => 'GPS Dilution Of Precision' },
    Distance      => { PrintConv   => '"$val m"' },
    VerticalSpeed => { PrintConv   => '"$val m/s"' },
    CameraModel   => { Groups      => { 2 => 'Camera' } },
    FNumber       => {
        PrintConv => 'Image::ExifTool::Exif::PrintFNumber($val)',
        Groups    => { 2 => 'Camera' }
    },
    ExposureTime => {
        PrintConv => 'Image::ExifTool::Exif::PrintExposureTime($val)',
        Groups    => { 2 => 'Camera' }
    },
    ExposureCompensation => {
        PrintConv => 'Image::ExifTool::Exif::PrintFraction($val)',
        Groups    => { 2 => 'Camera' }
    },
    ISO            => { Groups => { 2 => 'Camera' } },
    CameraDateTime => {
        PrintConv => '$self->ConvertDateTime($val)',
        Groups    => { 2 => 'Time' }
    },
    DateTimeStamp => {
        PrintConv => '$self->ConvertDateTime($val)',
        Groups    => { 2 => 'Time' }
    },
    VideoTimeStamp => { Groups => { 2 => 'Video' } },
    Accelerometer  => { Notes => '3-axis acceleration, usually in units of g' },
    AccelerometerData => {},
    AngularVelocity   => {},
    GSensor           => {},
    Car               => {},
    RawGSensor        => {
        ValueConv => 'my @a=split " ",$val; $_/=1000 foreach @a; "@a"',
    },
    Text        => { Groups => { 2 => 'Other' } },
    TimeCode    => { Groups => { 2 => 'Video' } },
    FrameNumber => { Groups => { 2 => 'Video' } },
    SampleTime  => {
        Groups    => { 2 => 'Video' },
        PrintConv => 'ConvertDuration($val)',
        Notes     => 'sample decoding time'
    },
    SampleDuration =>
      { Groups => { 2 => 'Video' }, PrintConv => 'ConvertDuration($val)' },
    UserLabel      => { Groups => { 2 => 'Other' } },
    KiloCalories   => { Groups => { 2 => 'Other' } },
    SampleDateTime => {
        Groups    => { 2 => 'Time' },
        ValueConv => 'ConvertUnixTime($val, 0, -6)',
        PrintConv => '$self->ConvertDateTime($val)',
    },
    mebx => {
        Name         => 'mebx',
        SubDirectory => {
            TagTable    => 'Image::ExifTool::QuickTime::Keys',
            ProcessProc => \&Process_mebx,
        },
    },
    gpmd => [
        {
            Name         => 'gpmd_Kingslim',
            Condition    => '$$valPt =~ /^.{21}\0\0\0A[NS][EW]/s',
            SubDirectory => {
                TagTable    => 'Image::ExifTool::QuickTime::Stream',
                ProcessProc => \&ProcessFreeGPS,
            },
        },
        {
            Name         => 'gpmd_Rove',
            Condition    => '$$valPt =~ /^\0\0\xf2\xe1\xf0\xeeTT/',
            SubDirectory => {
                TagTable    => 'Image::ExifTool::QuickTime::Stream',
                ProcessProc => \&Process_text,
            },
        },
        {
            Name         => 'gpmd_FMAS',
            Condition    => '$$valPt =~ /^FMAS\0\0\0\0/',
            SubDirectory => {
                TagTable    => 'Image::ExifTool::QuickTime::Stream',
                ProcessProc => \&ProcessFMAS,
            },
        },
        {
            Name      => 'gpmd_Wolfbox',
            Condition =>
              '$$valPt =~ /^.{136}(0{16}[A-Z]{4}|https:\/\/www.redtiger\0)/s',
            SubDirectory => {
                TagTable    => 'Image::ExifTool::QuickTime::Stream',
                ProcessProc => \&ProcessWolfbox,
            },
        },
        {
            Name         => 'gpmd_GoPro',
            SubDirectory => { TagTable => 'Image::ExifTool::GoPro::GPMF' },
        }
    ],
    fdsc => {
        Name      => 'fdsc',
        Condition => '$$valPt =~ /^GPRO/',
        SubDirectory => { TagTable => 'Image::ExifTool::GoPro::fdsc' },
    },
    rtmd => {
        Name         => 'rtmd',
        SubDirectory => { TagTable => 'Image::ExifTool::Sony::rtmd' },
    },
    marl => {
        Name         => 'marl',
        SubDirectory => { TagTable => 'Image::ExifTool::GM::marl' },
    },
    CTMD => {
        Name         => 'CTMD',
        SubDirectory => { TagTable => 'Image::ExifTool::Canon::CTMD' },
    },
    tx3g => {
        Name         => 'tx3g',
        SubDirectory => { TagTable => 'Image::ExifTool::QuickTime::tx3g' },
    },
    RVMI => [
        {
            Name         => 'RVMI_gReV',
            Condition    => '$$valPt =~ /^gReV/',
            SubDirectory => {
                TagTable  => 'Image::ExifTool::QuickTime::RVMI_gReV',
                ByteOrder => 'Little-endian',
            },
        },
        {
            Name         => 'RVMI_sReV',
            Condition    => '$$valPt =~ /^sReV/',
            SubDirectory => {
                TagTable  => 'Image::ExifTool::QuickTime::RVMI_sReV',
                ByteOrder => 'Little-endian',
            },
        }
    ],
    camm => [
        {
            Name => 'camm0',
            Condition    => '$$valPt =~ /^..\0\0/s',
            SubDirectory => {
                TagTable  => 'Image::ExifTool::QuickTime::camm0',
                ByteOrder => 'Little-Endian',
            },
        },
        {
            Name         => 'camm1',
            Condition    => '$$valPt =~ /^..\x01\0/s',
            SubDirectory => {
                TagTable  => 'Image::ExifTool::QuickTime::camm1',
                ByteOrder => 'Little-Endian',
            },
        },
        {
            Name         => 'camm2',
            Condition    => '$$valPt =~ /^..\x02\0/s',
            SubDirectory => {
                TagTable  => 'Image::ExifTool::QuickTime::camm2',
                ByteOrder => 'Little-Endian',
            },
        },
        {
            Name         => 'camm3',
            Condition    => '$$valPt =~ /^..\x03\0/s',
            SubDirectory => {
                TagTable  => 'Image::ExifTool::QuickTime::camm3',
                ByteOrder => 'Little-Endian',
            },
        },
        {
            Name         => 'camm4',
            Condition    => '$$valPt =~ /^..\x04\0/s',
            SubDirectory => {
                TagTable  => 'Image::ExifTool::QuickTime::camm4',
                ByteOrder => 'Little-Endian',
            },
        },
        {
            Name         => 'camm5',
            Condition    => '$$valPt =~ /^..\x05\0/s',
            SubDirectory => {
                TagTable  => 'Image::ExifTool::QuickTime::camm5',
                ByteOrder => 'Little-Endian',
            },
        },
        {
            Name         => 'camm6',
            Condition    => '$$valPt =~ /^..\x06\0/s',
            SubDirectory => {
                TagTable  => 'Image::ExifTool::QuickTime::camm6',
                ByteOrder => 'Little-Endian',
            },
        },
        {
            Name         => 'camm7',
            Condition    => '$$valPt =~ /^..\x07\0/s',
            SubDirectory => {
                TagTable  => 'Image::ExifTool::QuickTime::camm7',
                ByteOrder => 'Little-Endian',
            },
        }
    ],
    mett => {
        Name         => 'mett',
        SubDirectory => { TagTable => 'Image::ExifTool::Parrot::mett' },
    },
    JPEG => {
        Name    => 'JpgFromRaw',
        Groups  => { 2 => 'Preview' },
        RawConv => '$self->ValidateImage(\$val,$tag)',
    },
    text => {
        Name      => 'PreviewInfo',
        Condition =>
'length $$valPt > 12 and Get32u($valPt,4) == length($$valPt) and $$valPt =~ /^.{8}\xff\xd8\xff/s',
        SubDirectory =>
          { TagTable => 'Image::ExifTool::QuickTime::PreviewInfo' },
    },
    INSV => {
        Groups       => { 0 => 'Trailer', 1 => 'Insta360' },
        SubDirectory =>
          { TagTable => 'Image::ExifTool::QuickTime::INSV_MakerNotes' },
    },
    ssmd => [
        {
            Name => 'RoveGPS',

            Condition =>
'length $$valPt == 32 and $$valPt !~ /^\0\0\xe0\xff\xff\xff\xef\x41/',
            SubDirectory => {
                TagTable  => 'Image::ExifTool::QuickTime::RoveGPS',
                ByteOrder => 'Little-Endian',
            },
        },
        {
            Name      => 'Accelerometer',
            Condition => 'length $$valPt == 12',
            Format    => 'float',
            ByteOrder => 'Little-Endian',
        },
        {
            Name      => 'PreviewImage',
            Condition => '$$valPt =~ /^\xff\xd8\xff/',
            Groups    => { 2 => 'Preview' },
            RawConv   => '$self->ValidateImage(\$val,$tag)',
        }
    ],
    djmd => {
        Name         => 'DJIMetadata',
        SubDirectory => { TagTable => 'Image::ExifTool::DJI::Protobuf' },
    },
    dbgi => {
        Name         => 'DJIDebug',
        Unknown      => 2,
        Notes        => 'extracted only if Unknown option is 2 or greater',
        SubDirectory => { TagTable => 'Image::ExifTool::DJI::Protobuf' },
    },
    Unknown00         => { Unknown => 1 },
    Unknown01         => { Unknown => 1 },
    Unknown02         => { Unknown => 1 },
    Unknown03         => { Unknown => 1 },
    MagneticVariation => {},
);

%Image::ExifTool::QuickTime::RoveGPS = (
    PROCESS_PROC => \&Image::ExifTool::ProcessBinaryData,
    GROUPS       => { 2 => 'Location' },
    FIRST_ENTRY  => 0,
    0            => {
        Name      => 'GPSLatitude',
        Format    => 'double',
        ValueConv =>
          'my $deg = int($val/100); $val = $deg + ($val - $deg * 100) / 60',
        PrintConv => 'Image::ExifTool::GPS::ToDMS($self, $val, 1, "N")',
    },
    8 => {
        Name      => 'GPSLongitude',
        Format    => 'double',
        ValueConv =>
          'my $deg = int($val/100); $val = $deg + ($val - $deg * 100) / 60',
        PrintConv => 'Image::ExifTool::GPS::ToDMS($self, $val, 1, "E")',
    },
    20 => {
        Name      => 'GPSSpeed',
        Format    => 'int16u',
        ValueConv => '$val * 1.852',
    },
    22 => {
        Name        => 'GPSDateTime',
        Description => 'GPS Date/Time',
        Groups      => { 2 => 'Time' },
        Format      => 'int8u[6]',
        ValueConv   => q{
            my @v = split ' ', $val;
            $v[0] += 2000;
            sprintf('%.4d:%.2d:%.2d %.2d:%.2d:%.2d', @v);
        },
        PrintConv => '$self->ConvertDateTime($val)',
    },
);

%Image::ExifTool::QuickTime::camm0 = (
    PROCESS_PROC => \&ProcessCAMM,
    GROUPS       => { 2 => 'Location' },
    FIRST_ENTRY  => 0,
    NOTES        => q{
        The camm0 through camm7 tables define tags extracted from the Google Street
        View Camera Motion Metadata of MP4 videos.  See
        L<https://developers.google.com/streetview/publish/camm-spec> for the
        specification.
    },
    4 => {
        Name  => 'AngleAxis',
        Notes => 'angle axis orientation in radians in local coordinate system',
        Format => 'float[3]',
    },
);

%Image::ExifTool::QuickTime::camm1 = (
    PROCESS_PROC => \&ProcessCAMM,
    GROUPS       => { 2 => 'Camera' },
    FIRST_ENTRY  => 0,
    4            => {
        Name      => 'PixelExposureTime',
        Format    => 'int32s',
        ValueConv => '$val * 1e-9',
        PrintConv => 'sprintf("%.4g ms", $val * 1000)',
    },
    8 => {
        Name      => 'RollingShutterSkewTime',
        Format    => 'int32s',
        ValueConv => '$val * 1e-9',
        PrintConv => 'sprintf("%.4g ms", $val * 1000)',
    },
);

%Image::ExifTool::QuickTime::camm2 = (
    PROCESS_PROC => \&ProcessCAMM,
    GROUPS       => { 2 => 'Location' },
    FIRST_ENTRY  => 0,
    4            => {
        Name   => 'AngularVelocity',
        Notes  => 'gyro angular velocity about X, Y and Z axes in rad/s',
        Format => 'float[3]',
    },
);

%Image::ExifTool::QuickTime::camm3 = (
    PROCESS_PROC => \&ProcessCAMM,
    GROUPS       => { 2 => 'Location' },
    FIRST_ENTRY  => 0,
    4            => {
        Name   => 'Acceleration',
        Notes  => 'acceleration in the X, Y and Z directions in m/s^2',
        Format => 'float[3]',
    },
);

%Image::ExifTool::QuickTime::camm4 = (
    PROCESS_PROC => \&ProcessCAMM,
    GROUPS       => { 2 => 'Location' },
    FIRST_ENTRY  => 0,
    4            => {
        Name   => 'Position',
        Notes  => 'X, Y, Z position in local coordinate system',
        Format => 'float[3]',
    },
);

%Image::ExifTool::QuickTime::camm5 = (
    PROCESS_PROC => \&ProcessCAMM,
    GROUPS       => { 2 => 'Location' },
    FIRST_ENTRY  => 0,
    4            => {
        Name      => 'GPSLatitude',
        Format    => 'double',
        RawConv   => '$$self{FoundGPSLatitude} = 1; $val',
        ValueConv => 'Image::ExifTool::GPS::ToDegrees($val, 1)',
        PrintConv => 'Image::ExifTool::GPS::ToDMS($self, $val, 1, "N")',
    },
    12 => {
        Name      => 'GPSLongitude',
        Format    => 'double',
        ValueConv => 'Image::ExifTool::GPS::ToDegrees($val, 1)',
        PrintConv => 'Image::ExifTool::GPS::ToDMS($self, $val, 1, "E")',
    },
    20 => {
        Name      => 'GPSAltitude',
        Format    => 'double',
        PrintConv => '$_ = sprintf("%.6f", $val); s/\.?0+$//; "$_ m"',
    },
);

%Image::ExifTool::QuickTime::camm6 = (
    PROCESS_PROC => \&ProcessCAMM,
    GROUPS       => { 2 => 'Location' },
    FIRST_ENTRY  => 0,
    0x04         => {
        Name        => 'GPSDateTime',
        Description => 'GPS Date/Time',
        Groups      => { 2 => 'Time' },
        Format      => 'double',
        RawConv     => '$$self{FoundGPSDateTime} = 1; $val',
        ValueConv => q{
            my $offset = 315964800;
            if ($$self{CreateDate} and $$self{CreateDate} - $val > 24 * 3600 * 365 * 5) {
                $val += $offset;
            }
            my $str = ConvertUnixTime($val, 0, -6);
            return $str . 'Z';
        },
        PrintConv => '$self->ConvertDateTime($val)',
    },
    0x0c => {
        Name      => 'GPSMeasureMode',
        Format    => 'int32u',
        PrintConv => {
            0 => 'No Measurement',
            2 => '2-Dimensional Measurement',
            3 => '3-Dimensional Measurement',
        },
    },
    0x10 => {
        Name      => 'GPSLatitude',
        Format    => 'double',
        RawConv   => '$$self{FoundGPSLatitude} = 1; $val',
        ValueConv => 'Image::ExifTool::GPS::ToDegrees($val, 1)',
        PrintConv => 'Image::ExifTool::GPS::ToDMS($self, $val, 1, "N")',
    },
    0x18 => {
        Name      => 'GPSLongitude',
        Format    => 'double',
        ValueConv => 'Image::ExifTool::GPS::ToDegrees($val, 1)',
        PrintConv => 'Image::ExifTool::GPS::ToDMS($self, $val, 1, "E")',
    },
    0x20 => {
        Name      => 'GPSAltitude',
        Format    => 'float',
        PrintConv => '$_ = sprintf("%.3f", $val); s/\.?0+$//; "$_ m"',
    },
    0x24 =>
      { Name => 'GPSHorizontalAccuracy', Format => 'float', Notes => 'metres' },
    0x28 => { Name => 'GPSVerticalAccuracy', Format => 'float' },
    0x2c => { Name => 'GPSVelocityEast',  Format => 'float', Notes => 'm/s' },
    0x30 => { Name => 'GPSVelocityNorth', Format => 'float' },
    0x34 => { Name => 'GPSVelocityUp',    Format => 'float' },
    0x38 => { Name => 'GPSSpeedAccuracy', Format => 'float' },
);

%Image::ExifTool::QuickTime::camm7 = (
    PROCESS_PROC => \&ProcessCAMM,
    GROUPS       => { 2 => 'Location' },
    FIRST_ENTRY  => 0,
    4            => {
        Name   => 'MagneticField',
        Format => 'float[3]',
        Notes  => 'microtesla',
    },
);

%Image::ExifTool::QuickTime::PreviewInfo = (
    PROCESS_PROC => \&Image::ExifTool::ProcessBinaryData,
    FIRST_ENTRY  => 0,
    NOTES        => 'Preview stored by TomTom Bandit ActionCam.',
    8            => {
        Name   => 'PreviewImage',
        Groups => { 2 => 'Preview' },
        Binary => 1,
        Format => 'undef[$size-8]',
    },
);

%Image::ExifTool::QuickTime::RVMI_gReV = (
    PROCESS_PROC => \&Image::ExifTool::ProcessBinaryData,
    GROUPS       => { 2 => 'Location' },
    FIRST_ENTRY  => 0,
    NOTES => 'GPS information extracted from the RVMI box of MOV videos.',
    4     => {
        Name      => 'GPSLatitude',
        Format    => 'int32s',
        RawConv   => '$$self{FoundGPSLatitude} = 1; $val',
        ValueConv => 'Image::ExifTool::GPS::ToDegrees($val/1e6, 1)',
        PrintConv => 'Image::ExifTool::GPS::ToDMS($self, $val, 1, "N")',
    },
    8 => {
        Name      => 'GPSLongitude',
        Format    => 'int32s',
        ValueConv => 'Image::ExifTool::GPS::ToDegrees($val/1e6, 1)',
        PrintConv => 'Image::ExifTool::GPS::ToDMS($self, $val, 1, "E")',
    },
    16 => {
        Name      => 'GPSSpeed',
        Format    => 'int16s',
        ValueConv => '$val / 10',
    },
    18 => {
        Name      => 'GPSTrack',
        Format    => 'int16u',
        ValueConv => '$val * 2',
    },
);

%Image::ExifTool::QuickTime::RVMI_sReV = (
    PROCESS_PROC => \&Image::ExifTool::ProcessBinaryData,
    GROUPS       => { 2 => 'Location' },
    FIRST_ENTRY  => 0,
    NOTES        => q{
        G-sensor information extracted from the RVMI box of MOV videos.
    },
    4 => {
        Name      => 'GSensor',
        Format    => 'int16s[3]',
        ValueConv => 'my @a=split " ",$val; $_/=1000 foreach @a; "@a"',
    },
);

%Image::ExifTool::QuickTime::tx3g = (
    PROCESS_PROC => \&Process_tx3g,
    GROUPS       => { 2 => 'Location' },
    FIRST_ENTRY  => 0,
    NOTES        => q{
        Tags extracted from the tx3g sbtl timed metadata of Yuneec and Autel drones,
        and subtitle text in some other videos.
    },
    Lat => {
        Name      => 'GPSLatitude',
        RawConv   => '$$self{FoundGPSLatitude} = 1; $val',
        PrintConv => 'Image::ExifTool::GPS::ToDMS($self, $val, 1, "N")',
    },
    Lon => {
        Name      => 'GPSLongitude',
        PrintConv => 'Image::ExifTool::GPS::ToDMS($self, $val, 1, "E")',
    },
    Alt => {
        Name      => 'GPSAltitude',
        ValueConv => '$val =~ s/\s*m$//; $val',
        PrintConv => '"$val m"',
    },
    Yaw      => 'Yaw',
    Pitch    => 'Pitch',
    Roll     => 'Roll',
    GimYaw   => 'GimbalYaw',
    GimPitch => 'GimbalPitch',
    GimRoll  => 'GimbalRoll',
    DateTime => {
        Groups    => { 2 => 'Time' },
        PrintConv => '$self->ConvertDateTime($val)',
    },
    Text => { Groups => { 2 => 'Other' } },
    GPSDateTime => {
        Groups      => { 2 => 'Time' },
        Description => 'GPS Date/Time',
        PrintConv   => '$self->ConvertDateTime($val)',
    },
    HomeLat => {
        Name      => 'GPSHomeLatitude',
        RawConv   => '$$self{FoundGPSLatitude} = 1; $val',
        PrintConv => 'Image::ExifTool::GPS::ToDMS($self, $val, 1, "N")',
    },
    HomeLon => {
        Name      => 'GPSHomeLongitude',
        PrintConv => 'Image::ExifTool::GPS::ToDMS($self, $val, 1, "E")',
    },
    ISO     => {},
    SHUTTER => {
        Name      => 'ExposureTime',
        ValueConv => '1 / $val',
        PrintConv => 'Image::ExifTool::Exif::PrintExposureTime($val)',
    },
    'F-NUM' => {
        Name      => 'FNumber',
        PrintConv => 'Image::ExifTool::Exif::PrintFNumber($val)',
    },
    EV => 'ExposureCompensation',
);

%Image::ExifTool::QuickTime::INSV_MakerNotes = (
    GROUPS => { 1 => 'MakerNotes', 2 => 'Camera' },
    0x0a   => 'SerialNumber',
    0x12   => 'Model',
    0x1a   => 'Firmware',
    0x2a   => {
        Name => 'Parameters',
        Notes =>
          'number of lenses, 6-axis orientation of each lens, raw resolution',
        ValueConv => '$val =~ tr/_/ /; $val',
    },
);

%Image::ExifTool::QuickTime::Tags360Fly = (
    PROCESS_PROC => \&Process360Fly,
    NOTES        => 'Timed metadata found in MP4 videos from the 360Fly.',
    1            => {
        Name         => 'Accel360Fly',
        SubDirectory =>
          { TagTable => 'Image::ExifTool::QuickTime::Accel360Fly' },
    },
    2 => {
        Name         => 'Gyro360Fly',
        SubDirectory =>
          { TagTable => 'Image::ExifTool::QuickTime::Gyro360Fly' },
    },
    3 => {
        Name         => 'Mag360Fly',
        SubDirectory => { TagTable => 'Image::ExifTool::QuickTime::Mag360Fly' },
    },
    5 => {
        Name         => 'GPS360Fly',
        SubDirectory => { TagTable => 'Image::ExifTool::QuickTime::GPS360Fly' },
    },
    6 => {
        Name         => 'Rot360Fly',
        SubDirectory => { TagTable => 'Image::ExifTool::QuickTime::Rot360Fly' },
    },
    250 => {
        Name         => 'Fusion360Fly',
        SubDirectory =>
          { TagTable => 'Image::ExifTool::QuickTime::Fusion360Fly' },
    },
);

%Image::ExifTool::QuickTime::Accel360Fly = (
    PROCESS_PROC => \&Image::ExifTool::ProcessBinaryData,
    GROUPS       => { 2    => 'Location' },
    1            => { Name => 'AccelMode', Unknown => 1 },
    2            => {
        Name      => 'SampleTime',
        Groups    => { 2 => 'Video' },
        Format    => 'int64u',
        ValueConv => '$val / 1e6',
        PrintConv => 'ConvertDuration($val)',
    },
    10 => { Name => 'AccelYPR', Format => 'float[3]' },
);

%Image::ExifTool::QuickTime::Gyro360Fly = (
    PROCESS_PROC => \&Image::ExifTool::ProcessBinaryData,
    GROUPS       => { 2    => 'Location' },
    1            => { Name => 'GyroMode', Unknown => 1 },
    2            => {
        Name      => 'SampleTime',
        Groups    => { 2 => 'Video' },
        Format    => 'int64u',
        ValueConv => '$val / 1e6',
        PrintConv => 'ConvertDuration($val)',
    },
    10 => { Name => 'GyroYPR', Format => 'float[3]' },
);

%Image::ExifTool::QuickTime::Mag360Fly = (
    PROCESS_PROC => \&Image::ExifTool::ProcessBinaryData,
    GROUPS       => { 2    => 'Location' },
    1            => { Name => 'MagMode', Unknown => 1 },
    2            => {
        Name      => 'SampleTime',
        Groups    => { 2 => 'Video' },
        Format    => 'int64u',
        ValueConv => '$val / 1e6',
        PrintConv => 'ConvertDuration($val)',
    },
    10 => { Name => 'MagnetometerXYZ', Format => 'float[3]' },
);

%Image::ExifTool::QuickTime::GPS360Fly = (
    PROCESS_PROC => \&Image::ExifTool::ProcessBinaryData,
    GROUPS       => { 2    => 'Location' },
    1            => { Name => 'GPSMode', Unknown => 1 },
    2            => {
        Name      => 'SampleTime',
        Groups    => { 2 => 'Video' },
        Format    => 'int64u',
        ValueConv => '$val / 1e6',
        PrintConv => 'ConvertDuration($val)',
    },
    10 => {
        Name      => 'GPSLatitude',
        Format    => 'float',
        PrintConv => 'Image::ExifTool::GPS::ToDMS($self, $val, 1, "N")'
    },
    14 => {
        Name      => 'GPSLongitude',
        Format    => 'float',
        PrintConv => 'Image::ExifTool::GPS::ToDMS($self, $val, 1, "E")'
    },
    18 => { Name => 'GPSAltitude', Format => 'float', PrintConv => '"$val m"' },
    22 => {
        Name      => 'GPSSpeed',
        Notes     => 'converted to km/hr',
        Format    => 'int16u',
        ValueConv => '$val * 0.036',
        PrintConv => 'sprintf("%.1f",$val)',
    },
    24 => { Name => 'GPSTrack', Format => 'int16u', ValueConv => '$val / 100' },
    26 => {
        Name      => 'Acceleration',
        Format    => 'int16u',
        ValueConv => '$val / 1000'
    },
);

%Image::ExifTool::QuickTime::Rot360Fly = (
    PROCESS_PROC => \&Image::ExifTool::ProcessBinaryData,
    GROUPS       => { 2    => 'Location' },
    1            => { Name => 'RotMode', Unknown => 1 },
    2            => {
        Name      => 'SampleTime',
        Groups    => { 2 => 'Video' },
        Format    => 'int64u',
        ValueConv => '$val / 1e6',
        PrintConv => 'ConvertDuration($val)',
    },
    10 => { Name => 'RotationXYZ', Format => 'float[3]' },
);

%Image::ExifTool::QuickTime::Fusion360Fly = (
    PROCESS_PROC => \&Image::ExifTool::ProcessBinaryData,
    GROUPS       => { 2    => 'Location' },
    1            => { Name => 'FusionMode', Unknown => 1 },
    2            => {
        Name      => 'SampleTime',
        Groups    => { 2 => 'Video' },
        Format    => 'int64u',
        ValueConv => '$val / 1e6',
        PrintConv => 'ConvertDuration($val)',
    },
    10 => { Name => 'FusionYPR', Format => 'float[3]' },
);

sub SignedInt32() {
    return $_ < 0x80000000 ? $_ : $_ - 4294967296;
}

sub SaveMetaKeys($$$) {
    local $_;
    my ( $et, $dirInfo, $tagTbl ) = @_;
    my $dataPt = $$dirInfo{DataPt};
    my $dirLen = length $$dataPt;
    return 0 unless $dirLen > 8;
    my $pos       = 0;
    my $verbose   = $$et{OPTIONS}{Verbose};
    my $oldIndent = $$et{INDENT};
    my $ee        = $$et{ee};
    $ee or $ee = $$et{ee} = {};

    $verbose and $et->VerboseDir( $$dirInfo{DirName}, undef, $dirLen );

    while ( $pos + 8 < $dirLen ) {
        my $size = Get32u( $dataPt, $pos );
        my $id   = substr( $$dataPt, $pos + 4, 4 );
        my $end  = $pos + $size;
        $end = $dirLen if $end > $dirLen;
        $pos += 8;
        my ( $tagID, $format, $pid );
        if ($verbose) {
            $pid = PrintableTagID( $id, 1 );
            $et->VPrint( 0,
                "$oldIndent+ [Metadata Key entry, Local ID=$pid, $size bytes]\n"
            );
            $$et{INDENT} .= '| ';
        }

        while ( $pos + 4 < $end ) {
            my $len = unpack( "x${pos}N", $$dataPt );
            last if $len < 8 or $pos + $len > $end;
            my $tag = substr( $$dataPt, $pos + 4, 4 );
            $pos += 8;
            $len -= 8;
            my $val = substr( $$dataPt, $pos, $len );
            $pos += $len;
            my $str;

            if ( $tag eq 'keyd' ) {
                ( $tagID = $val ) =~ s/^(mdta|fiel)com\.apple\.quicktime\.//;
                $tagID = "Tag_$val" unless $tagID;
                ( $str = $val ) =~ s/(.{4})/$1 / if $verbose;
            }
            elsif ( $tag eq 'dtyp' ) {
                next if length $val < 4;
                if ( length $val >= 4 ) {
                    my $ns = unpack( 'N', $val );
                    if ( $ns == 0 ) {
                        length $val >= 8 or $et->Warn('Short dtyp data'), next;
                        $str    = unpack( 'x4N', $val );
                        $format = $qtFmt{$str} || 'undef';
                    }
                    elsif ( $ns == 1 ) {
                        $str    = substr( $val, 4 );
                        $format = 'undef';
                    }
                    else {
                        $format = 'undef';
                    }
                    $str .= " ($format)" if $verbose and defined $str;
                }
            }
            if ( $verbose > 1 ) {
                if ( defined $str ) {
                    $str =~ tr/\x00-\x1f\x7f-\xff/./;
                    $str = " = $str";
                }
                else {
                    $str = '';
                }
                $et->VPrint( 1,
                        $$et{INDENT}
                      . "- Tag '"
                      . PrintableTagID( $tag, 2 )
                      . "' ($len bytes)$str\n" );
                $et->VerboseDump( \$val );
            }
        }
        if ( defined $tagID and defined $format ) {
            if ($verbose) {
                my $t2 = PrintableTagID($tagID);
                $et->VPrint( 0,
                    "$$et{INDENT}Added Local ID $pid = $t2 ($format)\n" );
            }
            $$ee{'keys'}{$id} = { TagID => $tagID, Format => $format };
        }
        $$et{INDENT} = $oldIndent;
    }
    return 1;
}

sub FoundSomething($$;$$) {
    my ( $et, $tagTbl, $time, $dur ) = @_;
    $$et{DOC_NUM} = ++$$et{DOC_COUNT};
    $et->HandleTag( $tagTbl, SampleTime     => $time ) if defined $time;
    $et->HandleTag( $tagTbl, SampleDuration => $dur )  if defined $dur;
}

sub SetGPSDateTime($$$;$) {
    my ( $et, $tagTbl, $sampleTime, $isUTC ) = @_;
    my $value = $$et{VALUE};
    if ( defined $sampleTime and $$value{CreateDate} ) {
        $sampleTime += $$value{CreateDate};
        if ( $$et{CreateDateAtEnd} ) {
            return unless $$value{TimeScale} and $$value{Duration};
            $sampleTime -= $$value{Duration} / $$value{TimeScale};
            $et->Warn(
'Approximating GPSDateTime as CreateDate - Duration + SampleTime',
                1
            );
        }
        else {
            $et->Warn( 'Approximating GPSDateTime as CreateDate + SampleTime',
                1 );
        }
        my $utc = $et->Options('QuickTimeUTC');
        $utc = $isUTC unless defined $utc;
        unless ($utc) {
            my $tzOff = $$et{tzOff};
            unless ( defined $tzOff ) {
                my @tm = localtime $$value{CreateDate};
                my @gm = gmtime $$value{CreateDate};
                $tzOff = $$et{tzOff} =
                  Image::ExifTool::GetTimeZone( \@tm, \@gm ) * 60;
            }
            $sampleTime -= $tzOff;
        }
        $$et{SET_GROUP0} = 'Composite';
        $et->HandleTag( $tagTbl,
            GPSDateTime => Image::ExifTool::ConvertUnixTime( $sampleTime, 0, 3 )
              . 'Z' );
        delete $$et{SET_GROUP0};
    }
}

sub HandleTextTags($$$) {
    my ( $et, $tagTbl, $tags ) = @_;
    my $tag;
    delete $$tags{done};
    delete $$tags{GPSTimeStamp} if $$tags{GPSDateTime};
    foreach $tag ( sort keys %$tags ) {
        $et->HandleTag( $tagTbl, $tag => $$tags{$tag} );
    }
    $$et{UnknownTextCount} = 0;
    undef %$tags;
}

sub HandleNewTime($$$$) {
    my ( $et, $time, $tagTbl, $tags ) = @_;
    if ( $$et{LastTime} ) {
        if ( $$et{LastTime} eq $time ) {
            $$et{DOC_NUM} = $$et{LastDoc};
        }
        elsif (%$tags) {
            HandleTextTags( $et, $tagTbl, $tags );
            $$et{DOC_COUNT} < ++$$et{DOC_NUM}
              and $$et{DOC_COUNT} = $$et{DOC_NUM};
        }
    }
    $$et{LastTime} = $time;
    $$et{LastDoc}  = $$et{DOC_NUM};
}

sub Process_text($$$;$) {
    my ( $et, $dataPt, $tagTbl, $handled ) = @_;
    my %tags;

    return if $$et{NoMoreTextDecoding};

    if ( ref $dataPt eq 'HASH' ) {
        my $dirName = $$dataPt{DirName};
        $dataPt = $$dataPt{DataPt};
        $et->VerboseDir( $dirName, undef, length($$dataPt) );
    }

    while ( $$dataPt =~ /\$(\w+)([^\$\0]*)/g ) {
        my ( $tag, $dat ) = ( $1, $2 );
        if ( $tag =~ /^[A-Z]{2}RMC$/ ) {
            unless ( $dat =~
/^,(\d{2})(\d{2})(\d+(?:\.\d*)),A?,(\d*?)(\d{1,2}\.\d+),([NS]),(\d*?)(\d{1,2}\.\d+),([EW]),(\d*\.?\d*),(\d*\.?\d*),(\d{2})(\d{2})(\d+)/
              )
            {
                $tags{Text} =
                  defined $tags{Text}
                  ? $tags{Text} . "\$$tag$dat"
                  : "\$$tag$dat"
                  unless $handled;
                $dat =~ /^,\d+\.?\d*,V,/ and $$et{UnknownTextCount} = 0;
                next;
            }
            my $time = "$1:$2:$3";
            HandleNewTime( $et, $time, $tagTbl, \%tags );
            my $year = $14 + ( $14 >= 70 ? 1900 : 2000 );
            my $date = sprintf( '%.4d:%.2d:%.2d', $year, $13, $12 );
            $$et{LastDate}     = $date;
            $tags{GPSDateTime} = "$date ${time}Z";
            $tags{GPSLatitude} =
              ( ( $4 || 0 ) + $5 / 60 ) * ( $6 eq 'N' ? 1 : -1 );
            $tags{GPSLongitude} =
              ( ( $7 || 0 ) + $8 / 60 ) * ( $9 eq 'E' ? 1 : -1 );
            $tags{GPSSpeed} = $10 * $knotsToKph if length $10;
            $tags{GPSTrack} = $11               if length $11;
        }
        elsif ( $tag =~ /^[A-Z]{2}GGA$/
            and $dat =~
/^,(\d{2})(\d{2})(\d+(?:\.\d*)?),(\d*?)(\d{1,2}\.\d+),([NS]),(\d*?)(\d{1,2}\.\d+),([EW]),[1-6]?,(\d+)?,(\.\d+|\d+\.?\d*)?,(-?\d+\.?\d*)?,M?/s
          )
        {
            my $time = "$1:$2:$3";
            HandleNewTime( $et, $time, $tagTbl, \%tags );
            $tags{GPSTimeStamp} = $time;
            $tags{GPSLatitude} =
              ( ( $4 || 0 ) + $5 / 60 ) * ( $6 eq 'N' ? 1 : -1 );
            $tags{GPSLongitude} =
              ( ( $7 || 0 ) + $8 / 60 ) * ( $9 eq 'E' ? 1 : -1 );
            $tags{GPSSatellites} = $10 if defined $10;
            $tags{GPSDOP}        = $11 if defined $11;
            $tags{GPSAltitude}   = $12 if defined $12;
        }
        elsif ( $tag eq 'G'
            and $dat =~
/:(\d{4})-(\d{2})-(\d{2}) (\d{2}:\d{2}:\d{2})-([NS])(\d+\.\d+)-([EW])(\d+\.\d+)-S(\d+)/
          )
        {
            $tags{GPSDateTime}  = "$1:$2:$3 $4";
            $tags{GPSLatitude}  = $6 * ( $5 eq 'S' ? -1 : 1 );
            $tags{GPSLongitude} = $8 * ( $7 eq 'W' ? -1 : 1 );
            $tags{GPSSpeed}     = $9;
        }
        elsif ( $tag eq 'GS' and $dat =~ /:([-+]?\d+),([-+]?\d+),([-+]?\d+)/ ) {
            my @acc = (
                ( $2 + 2432 ) / 1000,
                ( $3 + 361 ) / 1000,
                ( $1 - 3708 ) / 1000
            );
            $tags{Accelerometer} = "@acc";
        }
        elsif ( $tag eq 'BEGINGSENSOR'
            and $dat =~ /^:([-+]\d+\.\d+):([-+]\d+\.\d+):([-+]\d+\.\d+)/ )
        {
            $tags{Accelerometer} = "$1 $2 $3";
        }
        elsif ( $tag eq 'TIME' and $dat =~ /^:(\d+)/ ) {
            $tags{TimeCode} = $1 / ( $$et{MediaTS} || 1 );
        }
        elsif ( $tag eq 'BEGIN' ) {
            $tags{Text} = $dat if length $dat;
            $tags{done} = 1;
        }
        elsif ( $tag ne 'END' and not $handled ) {
            $tags{Text} =
              defined $tags{Text} ? $tags{Text} . "\$$tag$dat" : "\$$tag$dat";
        }
    }
    if (%tags) {
        unless ( $tags{Accelerometer} ) {

            if ( $$dataPt =~
                /^\0{4}(\d{4})(\d{2})(\d{2})(\d{2})(\d{2})(\d{2})\0\0.{2}/s )
            {
                $tags{DateTimeStamp} = "$1:$2:$2 $4:$5:$6";
                my $num = unpack( 'x20v', $$dataPt );
                if ( $num and $num * 12 + 22 < length $$dataPt ) {
                    $num *= 6;
                    my @acc = unpack( "x22v$num", $$dataPt );
                    map { $_ = $_ - 0x10000 if $_ >= 0x8000 } @acc;
                    $tags{AccelerometerData} = "@acc";
                }
            }
        }
        if ( $tags{GPSTimeStamp} and not $tags{GPSDateTime} and $$et{LastDate} )
        {
            $tags{GPSDateTime} = "$$et{LastDate} $tags{GPSTimeStamp}Z";
        }
        HandleTextTags( $et, $tagTbl, \%tags );
        return;
    }
    if ( $$dataPt =~ /^\0\0(..\xaa\xaa|\xf2\xe1\xf0\xee)/s
        and length $$dataPt >= 282 )
    {
        my $val = pack( 'C*',
            map { $_ ^ 0xaa } unpack( 'C*', substr( $$dataPt, 8, 14 ) ) );
        if ( $val =~ /^(\d{4})(\d{2})(\d{2})(\d{2})(\d{2})(\d{2})$/ ) {
            $tags{GPSDateTime} = "$1:$2:$3 $4:$5:$6";
            $val = pack( 'C*',
                map { $_ ^ 0xaa } unpack( 'C*', substr( $$dataPt, 38, 9 ) ) );
            if ( $val =~ /^([NS])(\d{2})(\d+$)$/ ) {
                $tags{GPSLatitude} =
                  ( $2 + $3 / 600000 ) * ( $1 eq 'S' ? -1 : 1 );
            }
            $val = pack( 'C*',
                map { $_ ^ 0xaa } unpack( 'C*', substr( $$dataPt, 47, 10 ) ) );
            if ( $val =~ /^([EW])(\d{3})(\d+$)$/ ) {
                $tags{GPSLongitude} =
                  ( $2 + $3 / 600000 ) * ( $1 eq 'W' ? -1 : 1 );
            }
            $val = pack( 'C*',
                map { $_ ^ 0xaa } unpack( 'C*', substr( $$dataPt, 0x39, 5 ) ) );
            $tags{GPSAltitude} = $val + 0 if $val =~ /^[-+]\d+$/;
            $val = pack( 'C*',
                map { $_ ^ 0xaa } unpack( 'C*', substr( $$dataPt, 0x3e, 3 ) ) );
            $tags{GPSSpeed} = $val + 0 if $val =~ /^\d+$/;
            if ( $$dataPt =~ /^\0\0..\xaa\xaa/s ) {
                $val = pack( 'C*',
                    map { $_ ^ 0xaa }
                      unpack( 'C*', substr( $$dataPt, 0xad, 12 ) ) );
                if ( $val =~ /^([-+]\d{3})([-+]\d{3})([-+]\d{3})$/ ) {
                    $tags{Accelerometer} = "$1 $2 $3";
                    $val = pack( 'C*',
                        map { $_ ^ 0xaa }
                          unpack( 'C*', substr( $$dataPt, 0xba, 96 ) ) );
                    my $order = GetByteOrder();
                    SetByteOrder('II');
                    $val = ReadValue( \$val, 0, 'float' );
                    SetByteOrder($order);
                    $tags{AccelerometerData} = $val;
                }
            }
            else {
                my @acc;
                $val = pack( 'C*',
                    map { $_ ^ 0xaa }
                      unpack( 'C*', substr( $$dataPt, 0x41, 195 ) ) );
                push @acc, $1, $2, $3
                  while $val =~ /\G([-+]\d{3})([-+]\d{3})([-+]\d{3})/g;
                $tags{Accelerometer} = "@acc" if @acc;
            }
        }
        %tags and HandleTextTags( $et, $tagTbl, \%tags ), return;
    }

    if ( $$dataPt =~ /GPS \(([-+]?\d*\.\d+),\s*([-+]?\d*\.\d+)/ ) {
        $$et{CreateDateAtEnd} = 1;
        $tags{GPSLatitude}    = $2;
        $tags{GPSLongitude}   = $1;
        $tags{GPSAltitude}    = $1 if $$dataPt =~ /,\s*H\s+([-+]?\d+\.?\d*)m/;
        $tags{GPSSpeed}       = $1 * $mpsToKph
          if $$dataPt =~ /,\s*H.S\s+([-+]?\d+\.?\d*)/;
        $tags{Distance} = $1 * $mpsToKph if $$dataPt =~ /,\s*D\s+(\d+\.?\d*)m/;
        $tags{VerticalSpeed} = $1 if $$dataPt =~ /,\s*V.S\s+([-+]?\d+\.?\d*)/;
        $tags{FNumber}       = $1 if $$dataPt =~ /\bF\/(\d+\.?\d*)/;
        $tags{ExposureTime}  = 1 / $1 if $$dataPt =~ /\bSS\s+(\d+\.?\d*)/;
        $tags{ExposureCompensation} = ( $1 / ( $2 || 1 ) )
          if $$dataPt =~ /\bEV\s+([-+]?\d+\.?\d*)(\/\d+)?/;
        $tags{ISO} = $1 if $$dataPt =~ /\bISO\s+(\d+\.?\d*)/;
        HandleTextTags( $et, $tagTbl, \%tags );
        return;
    }

    if ( $$dataPt =~ /^A,(\d{2})(\d{2})(\d{2}),(\d{2})(\d{2})(\d{2}(\.\d+)?)/ )
    {
        $tags{GPSDateTime} = "20$3:$2:$1 $4:$5:$6Z";
        if ( $$dataPt =~ /^A,.*?,.*?,(\d{2})(\d+\.\d+),([NS])/ ) {
            $tags{GPSLatitude} = ( $1 + $2 / 60 ) * ( $3 eq 'S' ? -1 : 1 );
        }
        if ( $$dataPt =~ /^A,.*?,.*?,.*?,.*?,(\d{3})(\d+\.\d+),([EW])/ ) {
            $tags{GPSLongitude} = ( $1 + $2 / 60 ) * ( $3 eq 'W' ? -1 : 1 );
        }
        my @a = split ',', $$dataPt;
        $tags{GPSAltitude}   = $a[8] if $a[8] and $a[8] =~ s/M$//;
        $tags{GPSSpeed}      = $a[7] if $a[7] and $a[7] =~ /^\d+\.\d+$/;
        $tags{Accelerometer} = "$a[9] $a[10] $a[11]"
          if $a[11] and $a[11] =~ s/;\s*$//;
        HandleTextTags( $et, $tagTbl, \%tags );
        return;
    }

    if ( $$dataPt =~ /\*[0-9A-F]{2}~$/ ) {
        my @decode = unpack 'C*', '-I8XQWRVNZOYPUTA0B1C2SJ9K.L,M$D3E4F5G6H7';
        my @chars  = unpack 'C*', substr( $$dataPt, 0, -4 );
        foreach (@chars) {
            my $n = $_ - 43;
            $_ = $decode[$n] if $n >= 0 and defined $decode[$n];
        }
        my $buff = pack 'C*', @chars;
        if ( $buff =~ /X(.*?)Y(.*?)Z(.*?)G(.*?)\$/ ) {
            $tags{Accelerometer} = "$1 $2 $3 $4";
            $$dataPt = $buff;
        }
    }

    if (
        $$dataPt =~
/[A-Z]{2}RMC,(\d{2})(\d{2})(\d+(\.\d*)?),A?,(\d*?)(\d{1,2}\.\d+),([NS]),(\d*?)(\d{1,2}\.\d+),([EW]),(\d*\.?\d*),(\d*\.?\d*),(\d{2})(\d{2})(\d+)/
        and
        $13 <= 31 and $14 <= 12 and $15 <= 99
      )
    {
        my $year = $15 + ( $15 >= 70 ? 1900 : 2000 );
        $tags{GPSDateTime} = sprintf( '%.4d:%.2d:%.2d %.2d:%.2d:%.2dZ',
            $year, $14, $13, $1, $2, $3 );
        $tags{GPSLatitude} = ( ( $5 || 0 ) + $6 / 60 ) * ( $7 eq 'N' ? 1 : -1 );
        $tags{GPSLongitude} =
          ( ( $8 || 0 ) + $9 / 60 ) * ( $10 eq 'E' ? 1 : -1 );
        $tags{GPSSpeed} = $11 * $knotsToKph if length $11;
        $tags{GPSTrack} = $12               if length $12;
    }
    $tags{GSensor} = $1 if $$dataPt =~ /\bgsensori,(.*?)(;|$)/;
    $tags{Car}     = $1 if $$dataPt =~ /\bCAR,(.*?)(;|$)/;

    if (%tags) {
        HandleTextTags( $et, $tagTbl, \%tags );
    }
    else {
        $$et{UnknownTextCount} = ( $$et{UnknownTextCount} || 0 ) + 1;
        $$et{NoMoreTextDecoding} = 1 if $$et{UnknownTextCount} > 100;
    }
}

sub ProcessSamples($) {
    my $et = shift;
    my ( $raf,        $ee ) = @$et{qw(RAF ee)};
    my ( $i,          $pos, $hdrLen, $hdrFmt, @time, @dur, $oldIndent, $hash );
    my ( $mdatOffset, $mdatSize );

    return unless $ee;
    delete $$et{ee};

    my $eeOpt = $et->Options('ExtractEmbedded') || 0;
    my $type  = $$et{HandlerType}               || '';
    if ( $type eq 'vide' ) {
        $hash = $$et{ImageDataHash};
        if ($eeOpt) {
            if    ( $$ee{avcC} ) { $type = 'avcC' }
            elsif ( $$ee{JPEG} ) { $type = 'JPEG' }
            else                 { return unless $hash }
        }
    }
    elsif ( $type eq 'soun' ) {
        $hash = $$et{ImageDataHash};
        return unless $hash;
    }
    else {
        return unless $eeOpt;
    }

    my $hashSize = 0;
    my ( $start, $size ) = @$ee{qw(start size)};
    unless ( $start and $size ) {
        return unless $size;
        my ( $stco, $stsc, $stts ) = @$ee{qw(stco stsc stts)};
        return unless $stco and $stsc and @$stsc;
        $start = [];
        my ( $nextChunk, $iChunk ) = ( 0, 1 );
        my ( $chunkStart, $startChunk, $samplesPerChunk, $descIdx, $timeCount,
            $timeDelta, $time );
        if ( $stts and @$stts > 1 ) {
            $time      = 0;
            $timeCount = shift @$stts;
            $timeDelta = shift @$stts;
        }
        my $ts = $$et{MediaTS} || 1;
        my @chunkSize;
        foreach $chunkStart (@$stco) {
            if ( $iChunk >= $nextChunk and @$stsc ) {
                ( $startChunk, $samplesPerChunk, $descIdx ) = @{ shift @$stsc };
                $nextChunk = $$stsc[0][0] if @$stsc;
            }
            @$size < @$start + $samplesPerChunk
              and $et->Warn('Sample size error'), last;
            last unless defined $chunkStart and length $chunkStart;
            my $sampleStart = $chunkStart;
            my $chunkSize   = 0;
          Sample: for ( $i = 0 ; ; ) {
                push @$start, $sampleStart;
                if ( defined $time ) {
                    until ($timeCount) {
                        if ( @$stts < 2 ) {
                            undef $time;
                            last Sample;
                        }
                        $timeCount = shift @$stts;
                        $timeDelta = shift @$stts;
                    }
                    push @time, $time / $ts;
                    push @dur,  $timeDelta / $ts;
                    $time += $timeDelta;
                    --$timeCount;
                }
                $chunkSize += $$size[$#$start];
                last if ++$i >= $samplesPerChunk;
                $sampleStart += $$size[$#$start];
            }
            push @chunkSize, $chunkSize;
            ++$iChunk;
        }
        @$start == @$size
          or $et->Warn('Incorrect sample start/size count'), return;
        if ( $type eq 'soun' or $type eq 'vide' ) {
            $start = $stco;
            $size  = \@chunkSize;
        }
    }
    my $tagTbl     = GetTagTable('Image::ExifTool::QuickTime::Stream');
    my $verbose    = $et->Options('Verbose');
    my $metaFormat = $$et{MetaFormat} || '';
    my $tell       = $raf->Tell();

    if ($verbose) {
        $et->VPrint( 0, "---- Extract Embedded ----\n" );
        $oldIndent = $$et{INDENT};
        $$et{INDENT} = '';
    }
    if ($hash) {
        $mdatSize   = $$et{MediaDataSize};
        $mdatOffset = $$et{MediaDataOffset} if defined $mdatSize;
    }
    if ( $type eq 'avcC' ) {
        $hdrLen = ( Get8u( \$$ee{avcC}, 4 ) & 0x03 ) + 1;
        $hdrFmt = ( $hdrLen == 4 ? 'N' : $hdrLen == 2 ? 'n' : 'C' );
        require Image::ExifTool::H264;
    }

    for ( $i = 0 ; $i < @$start and $i < @$size ; ++$i ) {

        delete $$et{FoundGPSLatitude};
        delete $$et{FoundGPSDateTime};

        my $size = $$size[$i];
        if ( defined $mdatOffset ) {
            if ( $$start[$i] < $mdatOffset ) {
                $et->Warn(
                    "Sample $i for '${type}' data is before start of mdat");
            }
            elsif ( $$start[$i] + $size > $mdatOffset + $mdatSize ) {
                $et->Warn("Sample $i for '${type}' data runs off end of mdat");
                $size = $mdatOffset + $mdatSize - $$start[$i];
                $size = 0 if $size < 0;
            }
        }
        $raf->Seek( $$start[$i], 0 )
          or $et->Warn("Seek error in $type data"), next;
        my $buff;
        my $n = $raf->Read( $buff, $size );
        unless ( $n == $size ) {
            $et->Warn("Error reading $type data");
            next unless $n;
            $size = $n;
        }
        if ($hash) {
            $hash->add($buff);
            $hashSize += length $buff;
        }
        if ( $type eq 'avcC' ) {
            next if length($buff) <= $hdrLen;
            for ( $pos = 0 ; ; ) {
                my $len = unpack( "x$pos$hdrFmt", $buff );
                last if $pos + $hdrLen + $len > length($buff);
                my $tmp = "\0\0\0\x01" . substr( $buff, $pos + $hdrLen, $len );
                Image::ExifTool::H264::ParseH264Video( $et, \$tmp );
                $pos += $hdrLen + $len;
                last if $pos + $hdrLen >= length($buff);
            }
            last if $$et{GotNAL06} and $eeOpt < 3;
            next;
        }
        if ( $verbose > 1 ) {
            my $hdr =
              $$et{SET_GROUP1}
              ? "$$et{SET_GROUP1} Type='${type}' Format='${metaFormat}'"
              : "Type='${type}'";
            $et->VPrint( 1,
                    "${hdr}, Sample "
                  . ( $i + 1 ) . ' of '
                  . scalar(@$start)
                  . " ($size bytes)\n" );
            $et->VerboseDump( \$buff, Addr => $$start[$i] );
        }
        if (
            $type eq 'text'
            or
            (
                    $type eq 'sbtl'
                and $metaFormat eq 'tx3g'
                and $buff =~ /^..PNDM/s
            )
          )
        {

            my $handled;
            FoundSomething( $et, $tagTbl, $time[$i], $dur[$i] );
            unless ( $buff =~ /^\$BEGIN/ ) {
                $buff =~ s/\0\0\0\x0cencd\0\0\x01\0$// and $size -= 12;
                if ( $size >= 2 and unpack( 'n', $buff ) == $size - 2 ) {
                    next if $size == 2;
                    $buff = substr( $buff, 2 );
                }
                my $val;
                if ( $buff =~ /^\0/ and $buff =~ /\x0a$/ and length($buff) > 5 )
                {
                    my $dif = ord('*') - ord( substr( $buff, -4, 1 ) );
                    my $tmp = pack 'C*',
                      map { $_ = ( $_ + $dif ) & 0xff } unpack 'C*',
                      substr $buff, 1, -1;
                    if ( $verbose > 2 ) {
                        $et->VPrint( 0, "[decrypted text]\n" );
                        $et->VerboseDump( \$tmp );
                    }
                    if ( $tmp =~ /^(.*?)(\$[A-Z]{2}RMC.*)/s ) {
                        ( $val, $buff ) = ( $1, $2 );
                        $val =~ tr/\t/ /;
                        $et->HandleTag( $tagTbl, RawGSensor => $val )
                          if length $val;
                    }
                }
                elsif ( $buff =~ /^(\0.{3})?PNDM/s ) {
                    my $n = $1 ? 4 : 0;
                    next if length($buff) < 20 + $n;
                    $et->HandleTag( $tagTbl,
                        GPSLatitude => Get32s( \$buff, 12 + $n ) * 180 /
                          0x80000000 );
                    $et->HandleTag( $tagTbl,
                        GPSLongitude => Get32s( \$buff, 16 + $n ) * 180 /
                          0x80000000 );
                    $et->HandleTag( $tagTbl,
                        GPSSpeed => Get16u( \$buff, 8 + $n ) * $mphToKph );
                    SetGPSDateTime( $et, $tagTbl, $time[$i], 1 );
                    next;
                }
                unless ( defined $val or $buff =~ /\0[^\0]/ ) {
                    $et->HandleTag( $tagTbl, Text => $buff );
                    $handled = 1;
                }
            }
            Process_text( $et, \$buff, $tagTbl, $handled );

        }
        elsif ( $processByMetaFormat{$type} ) {

            if ( $$tagTbl{$metaFormat} ) {
                my $tagInfo = $et->GetTagInfo( $tagTbl, $metaFormat, \$buff );
                if (
                    $tagInfo
                    and ( not $$tagInfo{Unknown}
                        or $$et{OPTIONS}{Unknown} >= $$tagInfo{Unknown} )
                  )
                {
                    FoundSomething( $et, $tagTbl, $time[$i], $dur[$i] );
                    $$et{ee} = $ee;
                    $et->HandleTag(
                        $tagTbl, $metaFormat, undef,
                        DataPt  => \$buff,
                        Base    => $$start[$i],
                        TagInfo => $tagInfo,
                    );
                    delete $$et{ee};
                    if ( $metaFormat eq 'djmd' ) {
                        if (    defined $$et{GPSLatitude}
                            and defined $$et{GPSLongitude}
                            and not $$et{GPSDateTime} )
                        {
                            SetGPSDateTime( $et, $tagTbl, $time[$i], 1 );
                        }
                        delete $$et{GPSLatitude};
                        delete $$et{GPSLongitude};
                        delete $$et{GPSDateTime};
                    }
                }
                elsif ( $metaFormat eq 'camm' and $buff =~ /^X/ ) {
                    FoundSomething( $et, $tagTbl, $time[$i], $dur[$i] );
                    $et->HandleTag( $tagTbl, Accelerometer => "$1 $2 $3 $4" )
                      if $buff =~ /X(.*?)Y(.*?)Z(.*?)G(.*?)\$/;
                    Process_text( $et, \$buff, $tagTbl );
                }
            }
            elsif ($verbose) {
                $et->VPrint( 0, "Unknown $type format ($metaFormat)" );
            }

        }
        elsif ( $type eq 'gps ' ) {

            if ( $buff =~ /^....freeGPS /s ) {
                last if $$et{FoundGPSByScan};
                ProcessFreeGPS(
                    $et,
                    {
                        DataPt         => \$buff,
                        DataPos        => $$start[$i],
                        SampleTime     => $time[$i],
                        SampleDuration => $dur[$i],
                    },
                    $tagTbl
                );
            }

        }
        elsif ( $$tagTbl{$type} ) {

            my $tagInfo = $et->GetTagInfo( $tagTbl, $type, \$buff );
            if ($tagInfo) {
                FoundSomething( $et, $tagTbl, $time[$i], $dur[$i] );
                $et->HandleTag(
                    $tagTbl, $type, undef,
                    DataPt  => \$buff,
                    Base    => $$start[$i],
                    TagInfo => $tagInfo,
                );
            }
        }
        SetGPSDateTime( $et, $tagTbl, $time[$i] )
          if $$et{FoundGPSLatitude} and not $$et{FoundGPSDateTime};
    }
    if ($verbose) {
        my $str = $type eq 'soun' ? 'Audio' : 'Video';
        $et->VPrint( 0,
            "$$et{INDENT}(ImageDataHash: $hashSize bytes of $str data)\n" )
          if $hashSize;
        $$et{INDENT} = $oldIndent;
        $et->VPrint( 0, "--------------------------\n" );
    }
    $raf->Seek( $tell, 0 );
    delete $$et{DOC_NUM};
    $$et{HandlerType} = '';
}

sub ConvertLatLon($$) {
    my $deg = int( $_[0] / 100 );
    $_[0] = $deg + ( $_[0] - $deg * 100 ) / 60;
    $deg  = int( $_[1] / 100 );
    $_[1] = $deg + ( $_[1] - $deg * 100 ) / 60;
}

my @luckyKeys = ( 'luckychip gps', 'customer ## gps' );

sub DecryptLucky($$) {
    my ( $str, $key ) = @_;
    my @str = unpack( 'C*', $str );
    my @key = unpack( 'C*', $key );
    my @enc = ( 0 .. 255 );
    my ( $i, $j, $k ) = ( 0, 0, 0 );
    do {
        $j = ( $j + $enc[$i] + $key[ $i % length($key) ] ) & 0xff;
        @enc[ $i, $j ] = @enc[ $j, $i ];
    } while ( ++$i < 256 );
    ( $i, $j, $k ) = ( 0, 0, 0 );
    do {
        $j = ( $j + 1 ) & 0xff;
        $k = ( $k + $enc[$j] ) & 0xff;
        @enc[ $j, $k ] = @enc[ $k, $j ];
        $str[$i] ^= $enc[ ( $enc[$j] + $enc[$k] ) & 0xff ];
    } while ( ++$i < @str );
    return pack( 'C*', @str );
}

sub ProcessFreeGPS($$$) {
    my ( $et, $dirInfo, $tagTbl ) = @_;
    my $dataPt = $$dirInfo{DataPt};
    my $dirLen = length $$dataPt;
    my ( $yr, $mon, $day, $hr, $min, $sec, $ss, $stat, $lbl, $ddd, $done );
    my ( $lat, $latRef, $lon, $lonRef, $spd, $trk, $alt, @acc, @xtra );

    return 0 if $dirLen < 82;

    my $debug    = $et->Options('Debug');
    my $oldOrder = GetByteOrder();
    SetByteOrder('II');
    $$et{FoundEmbedded} = 1;

    if ( substr( $$dataPt, 18, 8 ) eq "\xaa\xaa\xf2\xe1\xf0\xee\x54\x54" ) {

        $debug and $et->FoundTag( GPSType => 1 );
        my $n = $dirLen - 18;
        $n = 0x101 if $n > 0x101;
        my $buf2 = pack 'C*', map { $_ ^ 0xaa } unpack 'C*',
          substr( $$dataPt, 18, $n );
        if ( $et->Options('Verbose') > 1 ) {
            $et->VPrint( 1, '[decrypted freeGPS data]' );
            $et->VerboseDump( \$buf2 );
        }
        if ( $buf2 =~
/^.{8}(\d{4})(\d{2})(\d{2})(\d{2})(\d{2})(\d{2}).(.{15})([NS])(\d{8})([EW])(\d{9})(\d{8})?/s
          )
        {
            (
                $yr,  $mon,    $day, $hr,     $min, $sec,
                $lbl, $latRef, $lat, $lonRef, $lon, $spd
              )
              = (
                $1, $2, $3,       $4,  $5,        $6,
                $7, $8, $9 / 1e4, $10, $11 / 1e4, $12
              );
            if ( defined $spd ) {
                $spd += 0;
            }
            elsif ( $buf2 =~ /^.{57}([-+]\d{4})(\d{3})/s ) {

                $spd = $2 + 0;
            }
        }
        if ( $buf2 =~ /^.{65}(([-+]\d{3})([-+]\d{3})([-+]\d{3})([-+]\d{3})*)/s )
        {
            $_   = $1;
            @acc = ( $2 / 100, $3 / 100, $4 / 100 );
            s/([-+])/ $1/g;
            s/^ //;
            push @xtra, AccelerometerData => $_;
        }
        elsif ( $buf2 =~ /^.{173}([-+]\d{3})([-+]\d{3})([-+]\d{3})/s ) {

            @acc = ( $1 / 100, $2 / 100, $3 / 100 );
            if ( not defined $yr
                and $buf2 =~
                /^.{8}(\d{4})(\d{2})(\d{2})(\d{2})(\d{2})(\d{2}).(.{15})/s )
            {
                ( $yr, $mon, $day, $hr, $min, $sec, $lbl ) =
                  ( $1, $2, $3, $4, $5, $6, $7 );
            }
        }
        if ( defined $lbl ) {
            $lbl =~ s/\0.*//s;
            $lbl =~ s/\s+$//;
            push @xtra, UserLabel => $lbl if length $lbl;
        }

    }
    elsif ( $$dataPt =~ /^.{52}(\d{4})(\d{2})(\d{2})(\d{2})(\d{2})(\d{2})/s ) {

        $debug and $et->FoundTag( GPSType => 2 );
        push @xtra, CameraDateTime => "$1:$2:$3 $4:$5:$6";
        if ( $$dataPt =~
/\$[A-Z]{2}RMC,(\d{2})(\d{2})(\d+(\.\d*)?),A?,(\d+\.\d+),([NS]),(\d+\.\d+),([EW]),(\d*\.?\d*),(\d*\.?\d*),(\d{2})(\d{2})(\d+)/s
          )
        {
            ( $lat, $latRef, $lon, $lonRef ) = ( $5, $6, $7, $8 );
            $yr = $13 + ( $13 >= 70 ? 1900 : 2000 );
            ( $mon, $day, $hr, $min, $sec ) = ( $12, $11, $1, $2, $3 );
            $spd = $9 * $knotsToKph if length $9;
            $trk = $10              if length $10;
        }
        if ( $$dataPt =~
/\$[A-Z]{2}GGA,(\d{2})(\d{2})(\d+(\.\d*)?),(\d+\.\d+),([NS]),(\d+\.\d+),([EW]),[1-6]?,(\d+)?,(\.\d+|\d+\.?\d*)?,(-?\d+\.?\d*)?,M?/s
          )
        {
            ( $hr, $min, $sec, $lat, $latRef, $lon, $lonRef ) =
              ( $1, $2, $3, $5, $6, $7, $8 )
              unless defined $yr;
            $alt = $11;
            unshift @xtra, GPSSatellites => $9;
            unshift @xtra, GPSDOP        => $10;
        }
        if ( defined $lat ) {
            @acc = map { SignedInt32 / 256 } unpack( 'x68V3', $$dataPt );
        }

    }
    elsif ( $$dataPt =~ /^(.{37}|.{85})\0\0\0A([NS])([EW])\0/s ) {

        if ( length($1) == 85 ) {
            $$dataPt = substr( $$dataPt, 48 );
        }
        ( $latRef, $lonRef ) = ( $2, $3 );
        ( $hr, $min, $sec, $yr, $mon, $day ) = unpack( 'x16V6', $$dataPt );
        my ( $notEnc, $notStr, $lt, $ln );
        if ( length($$dataPt) < 0x78 ) {
            $notEnc = $notStr = 1;
        }
        else {
            $lt = substr( $$dataPt, 0x2c, 20 ),
              $ln = substr( $$dataPt, 0x40, 20 ),
              /^[A-Za-z0-9+\/]{8,20}={0,2}\0*$/
              or $notEnc = 1, last
              foreach ( $lt, $ln );
            /^\d{1,5}\.\d+\0*$/ or $notStr = 1, last foreach ( $lt, $ln );
        }
        if ( $notEnc and $notStr ) {

            $debug and $et->FoundTag( GPSType => 3 );
            if ( $yr >= 2000 ) {
                require Time::Local;
                my $time =
                  Image::ExifTool::TimeLocal( $sec, $min, $hr, $day, $mon - 1,
                    $yr );
                ( $sec, $min, $hr, $day, $mon, $yr ) = gmtime($time);
                $yr += 1900;
                ++$mon;
                $et->Warn(
                    'Converting GPSDateTime to UTC based on local time zone',
                    1 );
            }
            $lat = GetFloat( $dataPt, 0x2c );
            $lon = GetFloat( $dataPt, 0x30 );
            $spd = GetFloat( $dataPt, 0x34 ) * $knotsToKph;
            $trk = GetFloat( $dataPt, 0x38 );
            my $tmp = substr( $$dataPt, 60, 12 );
            if (    $tmp ne "\0\0\0\0\0\0\0\0\0\0\0\0"
                and $tmp ne "\x01\0\x02\0\x03\0\x04\0\x05\0\x06\0" )
            {
                @acc = map { SignedInt32 / 256 } unpack( 'V3', $tmp );
            }

        }
        else {

            $debug and $et->FoundTag( GPSType => 4 );
            $spd = GetFloat( $dataPt, 0x54 ) * $knotsToKph;
            $trk = GetFloat( $dataPt, 0x58 );
            @acc = map SignedInt32, unpack( 'x92V3', $$dataPt );
            if ($notEnc) {
                ( $lat = $lt ) =~ s/\0+$//;
                ( $lon = $ln ) =~ s/\0+$//;
            }
            else {
                require Image::ExifTool::XMP;
                $_ = ${ Image::ExifTool::XMP::DecodeBase64($_) }
                  foreach ( $lt, $ln );
                my ( $i, $ch, $key ) = ( 0, 'a', $luckyKeys[0] );
                for ( ; $i < 20 ; ++$i ) {
                    $i and ( $key = $luckyKeys[1] ) =~ s/#/$ch/g, ++$ch;
                    ( $lat = DecryptLucky( $lt, $key ) ) =~ /^\d{1,4}\.\d+$/
                      or undef($lat), next;
                    ( $lon = DecryptLucky( $ln, $key ) ) =~ /^\d{1,5}\.\d+$/
                      or undef($lon), next;
                    last;
                }
                $lon or $et->Warn('Unknown encryption for latitude/longitude');
            }
        }

    }
    elsif ( $$dataPt =~ /^(.{16}|.{48}|.{80})LIGOGPSINFO\0/s
        and length($$dataPt) >= length($1) + 0x84 )
    {

        $debug and $et->FoundTag( GPSType => 5 );
        my $pos = length $1;
        my %dirInfo =
          ( DataPt => $dataPt, DirStart => $pos, DirName => "LigoGPS_$pos" );
        $$et{LigoGPSScale} = 3
          if $pos == 16 and $$dataPt =~ /^.{12}\xf0\x03\0\0.{16}\0{4}/s;
        Image::ExifTool::LigoGPS::ProcessLigoGPS( $et, \%dirInfo, $tagTbl );
        $done = 1;

    }
    elsif ( $$dataPt =~ /^.{60}A\0{3}.{4}([NS])\0{3}.{4}([EW])\0{3}/s ) {

        $debug and $et->FoundTag( GPSType => 6 );
        ( $latRef, $lonRef ) = ( $1, $2 );
        ( $hr, $min, $sec, $yr, $mon, $day, @acc ) =
          unpack( 'x48V3x28V6', $$dataPt );
        $lat = GetFloat( $dataPt, 0x40 );
        $lon = GetFloat( $dataPt, 0x48 );
        $spd = GetFloat( $dataPt, 0x50 );
        $trk = GetFloat( $dataPt, 0x54 );
        if ( substr( $$dataPt, 16, 4 ) eq 'x.xx' ) {
            $trk += 180;
            $trk -= 360 if $trk >= 360;
            undef @acc;
        }
        else {
            @acc = map { SignedInt32 / 1000 } @acc;
        }

    }
    elsif ( $$dataPt =~ /^.{60}4W`b]S</s and length($$dataPt) >= 140 ) {

        $debug and $et->FoundTag( GPSType => 7 );
        $_ = pack 'C*',
          map { $_ >= 16 and $_ -= 16 } unpack( 'x60C80', $$dataPt );
        if (
/[A-Z]{2}RMC,(\d{2})(\d{2})(\d+(\.\d*)?),A?,(\d*?\d{1,2}\.\d+),([NS]),(\d*?\d{1,2}\.\d+),([EW]),(\d*\.?\d*),(\d*\.?\d*),(\d{2})(\d{2})(\d+)/
          )
        {
            ( $yr, $mon, $day, $hr, $min, $sec, $lat, $latRef, $lon, $lonRef )
              = ( $13, $12, $11, $1, $2, $3, $5, $6, $7, $8 );
            $yr += ( $yr >= 70 ? 1900 : 2000 );
            $spd = $9 * $knotsToKph if length $9;
            $trk = $10              if length $10;
        }
        else {
            $done = 1;
        }

    }
    elsif (
        $$dataPt =~ /^.{64}[\x01-\x0c]\0{3}[\x01-\x1f]\0{3}A[NS][EW]\0{5}/s )
    {

        $debug and $et->FoundTag( GPSType => 8 );
        ( $hr, $min, $sec, $yr, $mon, $day, $stat, $latRef, $lonRef ) =
          unpack( 'x48V6a1a1a1x1', $$dataPt );

        $et->Warn(
'GPSLatitude/Longitude encryption is not yet known, so these will be wrong'
        );

        $spd = GetFloat( $dataPt, 0x60 );
        $trk = GetFloat( $dataPt, 0x64 ) + 180;
        $lat = GetDouble( $dataPt, 0x50 );
        $lon = GetDouble( $dataPt, 0x58 );
        $ddd = 1;

    }
    elsif ( $$dataPt =~ /^.{12}\xac\0\0\0.{44}(.{72})/s ) {

        $debug and $et->FoundTag( GPSType => 9 );

        $et->Warn( "Can't yet decrypt EACHPAI timed GPS", 1 );
        $done = 1;

    }
    elsif ( $$dataPt =~ /^.{64}A([NS])([EW])\0/s ) {

        $debug and $et->FoundTag( GPSType => 10 );
        ( $latRef, $lonRef ) = ( $1, $2 );
        ( $yr, $mon, $day, $hr, $min, $sec, @acc ) =
          unpack( 'x68V6x20V3', $$dataPt );
        if ( $mon >= 1 and $mon <= 12 and $day >= 1 and $day <= 31 ) {
            @acc = map { SignedInt32 / 1000 } @acc;
            $lon = GetFloat( $dataPt, 0x5c );
            $lat = GetFloat( $dataPt, 0x60 );
            $spd = GetFloat( $dataPt, 0x64 ) * $knotsToKph;
            $trk = GetFloat( $dataPt, 0x68 );
            $alt = GetFloat( $dataPt, 0x6c );
        }
        else {
            $done = 1;
        }

    }
    elsif ( substr( $$dataPt, 0x45, 3 ) eq 'ATC' ) {

        $debug and $et->FoundTag( GPSType => 11 );

        my ( $recPos, $lastRecPos, $foundNew );
        my $verbose = $et->Options('Verbose');
        my $dataPos = $$dirInfo{DataPos};
        my $then    = $$et{FreeGPS2}{Then};
        $then or $then = $$et{FreeGPS2}{Then} = [ (0) x 6 ];
      ATCRec:
        for ( $recPos = 0x30 ; $recPos + 52 < $dirLen ; $recPos += 52 ) {

            my $a = substr( $$dataPt, $recPos, 52 );

            my @a = unpack( 'C*', $a );
            my ( $key1, $key2 ) = @a[ 0x14, 0x1c ];
            $a[$_] ^= $key1 foreach 0x00 .. 0x14, 0x18 .. 0x1b;
            $a[$_] ^= $key2 foreach 0x1c, 0x20 .. 0x32;
            my $b = pack 'C*', @a;
            my @now = unpack 'x13C3x28vC2', $b;
            $now[0] = ( $now[0] + 1 ) & 0xff;
            my $i;

            for ( $i = 0 ; $i < @dateMax ; ++$i ) {
                next if $now[$i] <= $dateMax[$i];
                $et->Warn('Invalid GPS date/time');
                next ATCRec;
            }
            foreach $i ( 3 .. 5, 0 .. 2 ) {
                if ( $now[$i] < $$then[$i] ) {
                    last ATCRec if $foundNew;
                    last;
                }
                next if $now[$i] == $$then[$i];
                if ($verbose) {
                    $et->VPrint( 2, "  + [encrypted GPS record]\n" );
                    $et->VerboseDump( \$a, DataPos => $dataPos + $recPos );
                    $et->VPrint( 2, "  + [decrypted GPS record]\n" );
                    $et->VerboseDump( \$b );
                }
                @$then        = @now;
                $$et{DOC_NUM} = ++$$et{DOC_COUNT};
                $trk          = Get16s( \$b, 0x24 ) / 100;
                $trk += 360 if $trk < 0;
                my $time = sprintf( '%.4d:%.2d:%.2d %.2d:%.2d:%.2dZ',
                    @now[ 3 .. 5, 0 .. 2 ] );
                $et->HandleTag( $tagTbl, GPSDateTime => $time );
                $et->HandleTag( $tagTbl,
                    GPSLatitude => Get32s( \$b, 0x10 ) / 1e7 );
                $et->HandleTag( $tagTbl,
                    GPSLongitude => Get32s( \$b, 0x18 ) / 1e7 );
                $et->HandleTag( $tagTbl,
                    GPSSpeed => Get32s( \$b, 0x20 ) / 100 * $mpsToKph );
                $et->HandleTag( $tagTbl, GPSTrack => $trk );
                $et->HandleTag( $tagTbl,
                    GPSAltitude => Get32s( \$b, 0x28 ) / 1000 );
                $lastRecPos = $recPos;
                $foundNew   = 1;
                delete $$et{FreeGPS2}{RecentRecPos};
                last;
            }
            my $recentRecPos = $$et{FreeGPS2}{RecentRecPos};
            $recPos = $recentRecPos
              if $recentRecPos and $recPos < $recentRecPos;
        }
        $$et{FreeGPS2}{RecentRecPos} = $lastRecPos;
        $done = 1;

    }
    elsif ( $$dataPt =~ /^.{60}A\0.{10}([NS])\0.{14}([EW])\0/s
        and $dirLen >= 0x88 )
    {

        $debug and $et->FoundTag( GPSType => 12 );

        ( $latRef, $lonRef ) = ( $1, $2 );
        ( $hr, $min, $sec, $yr, $mon, $day, @acc ) =
          unpack( 'x48V3x52V6', $$dataPt );
        @acc = map { SignedInt32 / 1000 } @acc;
        $lat = GetDouble( $dataPt, 0x40 );
        $lon = GetDouble( $dataPt, 0x50 );
        $spd = GetDouble( $dataPt, 0x60 ) * $knotsToKph;
        $trk = GetDouble( $dataPt, 0x68 );

    }
    elsif ( $$dataPt =~ /^.{16}A([NS])([EW])\0/s ) {

        $debug and $et->FoundTag( GPSType => 13 );
        while ( $$dataPt =~ /(A[NS][EW]\0.{28})/sg ) {
            my $dat = $1;
            $lat = abs( GetFloat( \$dat, 4 ) );
            $lon = abs( GetFloat( \$dat, 8 ) );
            $spd = GetFloat( \$dat, 12 ) * $knotsToKph;
            $trk = GetFloat( \$dat, 16 );
            @acc = map SignedInt32, unpack( 'x20V3', $dat );
            ConvertLatLon( $lat, $lon );
            $$et{DOC_NUM} = ++$$et{DOC_COUNT};
            $et->HandleTag( $tagTbl,
                GPSLatitude => $lat * ( substr( $dat, 1, 1 ) eq 'S' ? -1 : 1 )
            );
            $et->HandleTag( $tagTbl,
                GPSLongitude => $lon * ( substr( $dat, 2, 1 ) eq 'W' ? -1 : 1 )
            );
            $et->HandleTag( $tagTbl, GPSSpeed      => $spd );
            $et->HandleTag( $tagTbl, GPSTrack      => $trk );
            $et->HandleTag( $tagTbl, Accelerometer => "@acc" );
        }
        $done = 1;

    }
    elsif ( $$dataPt =~ /^.{20}[\0-\x18][\0-\x3b]{2}[\0-\x09]A([NS])([EW])/s ) {

        $debug and $et->FoundTag( GPSType => 14 );
        while ( $$dataPt =~ /(.{7}[\0-\x09]A[NS][EW].{25})/sg ) {
            my $dat = $1;
            (
                $yr, $mon,    $day,    $hr,  $min, $sec,
                $ss, $latRef, $lonRef, $lat, $lon, $spd
            ) = unpack( 'xC7xCCx5VVx4v', $dat );
            $yr  += 2000;
            $lat /= 1e4;
            $lon /= 1e4;
            ConvertLatLon( $lat, $lon );
            $$et{DOC_NUM} = ++$$et{DOC_COUNT};
            my $time = sprintf( '%.4d:%.2d:%.2d %.2d:%.2d:%.2d.%d',
                $yr, $mon, $day, $hr, $min, $sec, $ss );
            $et->HandleTag( $tagTbl, GPSDateTime => $time );
            $et->HandleTag( $tagTbl,
                GPSLatitude => $lat * ( $latRef eq 'S' ? -1 : 1 ) );
            $et->HandleTag( $tagTbl,
                GPSLongitude => $lon * ( $lonRef eq 'W' ? -1 : 1 ) );
            $et->HandleTag( $tagTbl, GPSSpeed => $spd );
        }
        $done = 1;

    }
    elsif ( $$dataPt =~ /^.{28}A.{11}([NS]).{15}([EW])/s ) {

        $debug and $et->FoundTag( GPSType => 15 );
        ( $latRef, $lonRef ) = ( $1, $2 );
        ( $hr, $min, $sec, $yr, $mon, $day, @acc ) =
          unpack( 'x16V3x52V3V3', $$dataPt );
        $lat = abs( GetDouble( $dataPt, 32 ) );
        $lon = abs( GetDouble( $dataPt, 48 ) );
        $spd = GetDouble( $dataPt, 64 ) * $knotsToKph;
        $trk = GetDouble( $dataPt, 72 );
        @acc = map { SignedInt32 / 1000 } @acc;

    }
    elsif ( $$dataPt =~ /^.{72}A[NS][EW]\0/s ) {

        ( $hr, $min, $sec, $yr, $mon, $day, $stat, $latRef, $lonRef ) =
          unpack( 'x48V6a1a1a1x1V4', $$dataPt );
        if ( substr( $$dataPt, 16, 3 ) eq 'IQS' ) {
            $debug and $et->FoundTag( GPSType => 16 );
            $ddd = 1;
            $lat = abs Get32s( $dataPt, 0x4c ) / 1e7;
            $lon = abs Get32s( $dataPt, 0x50 ) / 1e7;
            $spd = Get32s( $dataPt, 0x54 ) / 100 * $mpsToKph;
            $alt = GetFloat( $dataPt, 0x58 ) / 1000;
        }
        else {
            $lat = GetFloat( $dataPt, 0x4c );
            $lon = GetFloat( $dataPt, 0x50 );
            $spd = GetFloat( $dataPt, 0x54 ) * $knotsToKph;
            $trk = GetFloat( $dataPt, 0x58 );

            if ( $$et{KodakVersion} and $$et{KodakVersion} eq '3.01.054' ) {
                $debug and $et->FoundTag( GPSType => '17b' );
                $lat = ( $lat - 187.982162849635 ) / 3;
                $lon = ( $lon - 2199.19873715495 ) / 2;
                $ddd = 1;
            }
            elsif ( Get32u( $dataPt, 0 ) == 0x400000
                and abs($lat) <= 90
                and abs($lon) <= 180 )
            {
                $debug and $et->FoundTag( GPSType => '17c' );
                $ddd = 1;
                $spd /= $knotsToKph;
            }
            else {
                $debug and $et->FoundTag( GPSType => 17 );
            }
        }
        if ( $dirLen >= 0xb0 ) {
            my ( $lat2, $lon2 ) =
              ( GetDouble( $dataPt, 0x70 ), GetDouble( $dataPt, 0x80 ) );
            if ( abs( $lat2 - $lat ) < 0.001 and abs( $lon2 - $lon ) < 0.001 ) {
                $lat = $lat2;
                $lon = $lon2;
                $alt = GetDouble( $dataPt, 0xa0 );
            }
        }

    }
    elsif ( $$dataPt =~
        m<^.{23}(\d{4})/(\d{2})/(\d{2}) (\d{2}):(\d{2}):(\d{2}) [N|S]>s )
    {

        $debug and $et->FoundTag( GPSType => 18 );
        ( $yr, $mon, $day, $hr, $min, $sec ) = ( $1, $2, $3, $4, $5, $6 );
        $$dataPt =~ s/\0+$//;
        my @a = split ' ', substr( $$dataPt, 43 );
        $ddd = 1;
        foreach (@a) {
            unless (/^([A-Z]):([-+]?\d+(\.\d+)?)$/i) {
                defined $lon
                  and not defined $spd
                  and /^\d+\.\d+$/
                  and $spd = $_ * $knotsToKph;
                next;
            }
            ( $1 eq 'N' or $1 eq 'S' ) and $lat = $2, $latRef = $1, next;
            ( $1 eq 'E' or $1 eq 'W' ) and $lon = $2, $lonRef = $1, next;
            ( $1 eq 'x' or $1 eq 'y' or $1 eq 'z' ) and push( @acc, $2 ), next;
            $1 eq 'A' and $trk = $2, next;

            $$tagTbl{$1}
              or AddTagToTable( $tagTbl, $1,
                { Name => "Unknown_$1", Unknown => 1 } );
            push( @xtra, $1 => $2 ), next;
        }

    }
    elsif ( $$dataPt =~ m/^.{30}A.{20}VV/ ) {

        $debug and $et->FoundTag( GPSType => 19 );
        SetByteOrder('II');
        SetGPSDateTime( $et, $tagTbl, $$dirInfo{SampleTime} );
        $lat = Get32s( $dataPt, 31 ) / 1e5;
        $lon = Get32s( $dataPt, 35 ) / 1e5;
        $spd = Get32s( $dataPt, 43 );

    }
    else {

        $debug and $et->FoundTag( GPSType => 20 );
        my $pos;
        for ( $pos = 0x32 ; ; ) {
            ( $spd, $trk, $yr, $mon, $day, $hr, $min, $sec, $lat, $lon ) =
              unpack "x${pos}nnnCCCCnx1NN", $$dataPt;
            last
              if $yr < 2000
              or $yr > 2200
              or $mon < 1
              or $mon > 12
              or $day < 1
              or $day > 31
              or $hr > 59
              or $min > 59
              or $sec > 600;
            ( $lat, $lon ) = map { SignedInt32 / 1e7 } $lat, $lon;
            $trk -= 0x10000 if $trk >= 0x8000;
            $trk /= 100;
            $trk += 360 if $trk < 0;
            my $time = sprintf( "%.4d:%.2d:%.2d %.2d:%.2d:%04.1fZ",
                $yr, $mon, $day, $hr, $min, $sec / 10 );
            $$et{DOC_NUM} = ++$$et{DOC_COUNT};
            $et->HandleTag( $tagTbl, GPSDateTime  => $time );
            $et->HandleTag( $tagTbl, GPSLatitude  => $lat );
            $et->HandleTag( $tagTbl, GPSLongitude => $lon );
            $et->HandleTag( $tagTbl, GPSSpeed     => $spd / 100 * $mpsToKph );
            $et->HandleTag( $tagTbl, GPSTrack     => $trk );
            last if $pos += 0x20 > length($$dataPt) - 0x1e;
        }
        $done = 1;
    }
    SetByteOrder($oldOrder);
    return $$et{DOC_NUM} ? 1 : 0 if $done;
    return 0                     if defined $yr and ( $mon < 1 or $mon > 12 );
    FoundSomething( $et, $tagTbl, $$dirInfo{SampleTime},
        $$dirInfo{SampleDuration} );
    $sec = '0' . $sec if defined $sec and $sec !~ /^\d{2}/;
    if ( defined $yr ) {
        $yr += 2000 if $yr < 2000;
        my $time = sprintf( '%.4d:%.2d:%.2d %.2d:%.2d:%sZ',
            $yr, $mon, $day, $hr, $min, $sec );
        $et->HandleTag( $tagTbl, GPSDateTime => $time );
    }
    elsif ( defined $hr ) {
        my $time = sprintf( '%.2d:%.2d:%sZ', $hr, $min, $sec );
        $et->HandleTag( $tagTbl, GPSTimeStamp => $time );
    }
    if ( defined $lat and defined $lon ) {
        ConvertLatLon( $lat, $lon ) unless $ddd;
        $et->HandleTag( $tagTbl,
            GPSLatitude => $lat * ( ( $latRef and $latRef eq 'S' ) ? -1 : 1 ) );
        $et->HandleTag( $tagTbl,
            GPSLongitude => $lon * ( ( $lonRef and $lonRef eq 'W' ) ? -1 : 1 )
        );
    }
    $et->HandleTag( $tagTbl, GPSAltitude => $alt ) if defined $alt;
    $et->HandleTag( $tagTbl, GPSSpeed    => $spd ) if defined $spd;
    $et->HandleTag( $tagTbl, GPSTrack    => $trk ) if defined $trk;
    while (@xtra) {
        my $tag = shift @xtra;
        $et->HandleTag( $tagTbl, $tag => shift @xtra );
    }
    $et->HandleTag( $tagTbl, Accelerometer => "@acc" ) if @acc;
    return 1;
}

sub ParseTag($$$) {
    local $_;
    my ( $et, $tag, $dataPt ) = @_;
    my $dataLen = length $$dataPt;

    if ( $tag eq 'stsz' or $tag eq 'stz2' and $dataLen > 12 ) {
        my ( $sz, $num ) = unpack( 'x4N2', $$dataPt );
        my $size = $$et{ee}{size} = [];
        if ( $tag eq 'stsz' ) {
            if ( $sz == 0 ) {
                @$size =
                  ReadValue( $dataPt, 12, 'int32u', $num, $dataLen - 12 );
            }
            else {
                @$size = ($sz) x $num;
            }
        }
        else {
            $sz &= 0xff;
            if ( $sz == 4 ) {
                my @tmp = ReadValue(
                    $dataPt, 12, 'int8u',
                    int( ( $num + 1 ) / 2 ),
                    $dataLen - 12
                );
                foreach (@tmp) {
                    push @$size, $_ >> 4;
                    push @$size, $_ & 0xff;
                }
            }
            elsif ( $sz == 8 || $sz == 16 ) {
                @$size =
                  ReadValue( $dataPt, 12, "int${sz}u", $num, $dataLen - 12 );
            }
        }
    }
    elsif ( $tag eq 'stco' or $tag eq 'co64' and $dataLen > 8 ) {
        my $num  = unpack( 'x4N', $$dataPt );
        my $stco = $$et{ee}{stco} = [];
        @$stco = ReadValue( $dataPt, 8, $tag eq 'stco' ? 'int32u' : 'int64u',
            $num, $dataLen - 8 );
    }
    elsif ( $tag eq 'stsc' and $dataLen > 8 ) {
        my $num = unpack( 'x4N', $$dataPt );
        if ( $dataLen >= 8 + $num * 12 ) {
            my ( $i, @stsc );
            for ( $i = 0 ; $i < $num ; ++$i ) {
                push @stsc,
                  [ unpack( 'x' . ( 8 + $i * 12 ) . 'N3', $$dataPt ) ];
            }
            $$et{ee}{stsc} = \@stsc;
        }
    }
    elsif ( $tag eq 'stts' and $dataLen > 8 ) {
        my $num = unpack( 'x4N', $$dataPt );
        if ( $dataLen >= 8 + $num * 8 ) {
            $$et{ee}{stts} = [ unpack( 'x8N' . ( $num * 2 ), $$dataPt ) ];
        }
    }
    elsif ( $tag eq 'avcC' ) {
        $$et{ee}{avcC} = $$dataPt if $dataLen >= 7;
    }
    elsif ( $tag eq 'JPEG' ) {
        $$et{ee}{JPEG} = $$dataPt;
    }
    elsif ( $tag eq 'gps ' and $dataLen > 8 ) {
        my $num = Get32u( $dataPt, 4 );
        $num = int( ( $dataLen - 8 ) / 8 ) if $num * 8 + 8 > $dataLen;
        my $start = $$et{ee}{start} = [];
        my $size  = $$et{ee}{size}  = [];
        my $i;
        for ( $i = 0 ; $i < $num ; ++$i ) {
            push @$start, Get32u( $dataPt, 8 + $i * 8 );
            push @$size,  Get32u( $dataPt, 12 + $i * 8 );
        }
        $$et{HandlerType} = $tag;
        ProcessSamples($et);
    }
    elsif ( $tag eq 'GPS ' ) {
        my $pos    = 0;
        my $tagTbl = GetTagTable('Image::ExifTool::QuickTime::Stream');
        SetByteOrder('II');
        while ( $pos + 36 < $dataLen ) {
            my $dat = substr( $$dataPt, $pos, 36 );
            last if $dat eq "\x0" x 36;
            my @a = unpack 'VVVVaVaV', $dat;
            $$et{DOC_NUM} = ++$$et{DOC_COUNT};
            SetGPSDateTime( $et, $tagTbl, $a[2] );
            my $lat = $a[5] / 1e3;
            my $lon = $a[7] / 1e3;
            ConvertLatLon( $lat, $lon );
            $lat = -abs($lat) if $a[4] eq 'S';
            $lon = -abs($lon) if $a[6] eq 'W';
            $et->HandleTag( $tagTbl, GPSLatitude  => $lat );
            $et->HandleTag( $tagTbl, GPSLongitude => $lon );
            $et->HandleTag( $tagTbl, GPSSpeed     => $a[3] / 1e3 );
            $pos += 36;
        }
        SetByteOrder('MM');
        delete $$et{DOC_NUM};
    }
}

sub Process_tx3g($$$) {
    my ( $et, $dirInfo, $tagTablePtr ) = @_;
    my $dataPt = $$dirInfo{DataPt};
    return 0 if length $$dataPt < 2;
    $et->VerboseDir( 'tx3g', undef, length($$dataPt) - 2 );
    my $text = substr( $$dataPt, 2 );
    $et->HandleTag( $tagTablePtr, 'Text', $text );
    if ( $text =~ /^HOME\(/ ) {
        my $line;
        foreach $line ( split /\x0a/, $text ) {
            if ( $line =~
                /^HOME\(([EW]):\s*(\d+\.\d+),\s*([NS]):\s*(\d+\.\d+)\)\s*(.*)/ )
            {
                my ( $lon, $lat, $time ) = ( $2, $4, $5 );
                $lon = -$lon if $1 eq 'W';
                $lat = -$lat if $3 eq 'S';
                $time =~ tr/-/:/;
                $et->HandleTag( $tagTablePtr, GPSDateTime => $time );
                $et->HandleTag( $tagTablePtr, HomeLat     => $lat );
                $et->HandleTag( $tagTablePtr, HomeLon     => $lon );
            }
            elsif ( $line =~
                /^GPS\(([EW]):\s*(\d+\.\d+),\s*([NS]):\s*(\d+\.\d+),\s*(.*)m/ )
            {
                my ( $lon, $lat, $alt ) = ( $2, $4, $5 );
                $lon = -$lon if $1 eq 'W';
                $lat = -$lat if $3 eq 'S';
                $et->HandleTag( $tagTablePtr, Lat => $lat );
                $et->HandleTag( $tagTablePtr, Lon => $lon );
                $et->HandleTag( $tagTablePtr, Alt => $alt );
            }
            elsif ( $line =~
/^F\.PRY\s*\((-?[\d.]+)\xc2\xb0,\s*(-?[\d.]+)\xc2\xb0,\s*(-?[\d.]+)\xc2\xb0/
              )
            {
                $et->HandleTag( $tagTablePtr, Yaw   => $1 );
                $et->HandleTag( $tagTablePtr, Pitch => $2 );
                $et->HandleTag( $tagTablePtr, Roll  => $3 );
                if ( $line =~
/G\.PRY\s*\((-?[\d.]+)\xc2\xb0,\s*(-?[\d.]+)\xc2\xb0,\s*(-?[\d.]+)\xc2\xb0/
                  )
                {
                    $et->HandleTag( $tagTablePtr, GimYaw   => $1 );
                    $et->HandleTag( $tagTablePtr, GimPitch => $2 );
                    $et->HandleTag( $tagTablePtr, GimRoll  => $3 );
                }
            }
            else {
                $et->HandleTag( $tagTablePtr, $1, $2 )
                  while $line =~ /([-\w]+):([^:]*[^:\s])(\s|$)/sg;
            }
        }
    }
    elsif ( $text =~
/^\w{3} (\d{4})-(\d{2})-(\d{2}) (\d{2}:\d{2}:\d{2}) ?([-+])(\d{2}):?(\d{2})$/s
      )
    {
        $et->HandleTag( $tagTablePtr, 'DateTime', "$1:$2:$3 $4$5$6:$7" );
    }
    else {
        $et->HandleTag( $tagTablePtr, $1, $2 )
          while $text =~ /(\w+):([^:]*[^:\s])(\s|$)/sg;
    }
    return 1;
}

sub Process_mebx($$$) {
    my ( $et, $dirInfo, $tagTbl ) = @_;
    my $ee = $$et{ee} or return 0;
    return 0 unless $$ee{'keys'};
    my $dataPt = $$dirInfo{DataPt};

    $et->VerboseDir( 'mebx', undef, length $$dataPt );
    my ( $pos, $len );
    for ( $pos = 0 ; $pos + 8 < length($$dataPt) ; $pos += $len ) {
        $len = Get32u( $dataPt, $pos );
        last if $len < 8 or $pos + $len > length $$dataPt;
        my $id   = substr( $$dataPt, $pos + 4, 4 );
        my $info = $$ee{'keys'}{$id};
        if ($info) {
            my $tag = $$info{TagID};
            unless ( $$tagTbl{$tag} ) {
                next unless $tag =~ /^[-\w.]+$/;
                my $name = $tag;
                $name =~ s/[-.](.)/\U$1/g;
                AddTagToTable( $tagTbl, $tag, { Name => ucfirst($name) } );
            }
            my $val =
              ReadValue( $dataPt, $pos + 8, $$info{Format}, undef, $len - 8 );
            $et->HandleTag(
                $tagTbl, $tag, $val,
                DataPt => $dataPt,
                Base   => $$dirInfo{Base},
                Start  => $pos + 8,
                Size   => $len - 8,
            );
        }
        else {
            $et->Warn(
                'No key information for mebx ID ' . PrintableTagID( $id, 1 ) );
        }
    }
    return 1;
}

sub Process_3gf($$$) {
    my ( $et, $dirInfo, $tagTbl ) = @_;
    my $dataPt = $$dirInfo{DataPt};
    my $dirLen = $$dirInfo{DirLen};
    my $recLen = 10;
    $et->VerboseDir( '3gf', undef, $dirLen );
    if ( $dirLen > $recLen and not $et->Options('ExtractEmbedded') ) {
        $dirLen = $recLen;
        EEWarn($et);
    }
    my $pos;
    for ( $pos = 0 ; $pos + $recLen <= $dirLen ; $pos += $recLen ) {
        $$et{DOC_NUM} = ++$$et{DOC_COUNT};
        my $tc = Get32u( $dataPt, $pos );
        last if $tc == 0xffffffff;
        my ( $x, $y, $z ) = (
            Get16s( $dataPt, $pos + 4 ) / 10,
            Get16s( $dataPt, $pos + 6 ) / 10,
            Get16s( $dataPt, $pos + 8 ) / 10
        );
        $et->HandleTag( $tagTbl, TimeCode      => $tc / 1000 );
        $et->HandleTag( $tagTbl, Accelerometer => "$x $y $z" );
    }
    delete $$et{DOC_NUM};
    return 1;
}

sub Process_gps0($$$) {
    my ( $et, $dirInfo, $tagTbl ) = @_;
    my $dataPt = $$dirInfo{DataPt};
    my $dirLen = $$dirInfo{DirLen};
    my ( $pos, $recLen );
    $et->VerboseDir( 'gps0', undef, $dirLen );
    if ( $$dataPt =~ /^.{2}\xf2\xe1\xf0\xeeTT\x98/s ) {
        $recLen = 311;
        for ( $pos = 0 ; $pos + $recLen <= $dirLen ; $pos += $recLen ) {
            my $dat = substr( $$dataPt, $pos, $recLen );
            last unless $dat =~ /^.{2}\xf2\xe1\xf0\xeeTT\x98/s;
            $$et{DOC_NUM} = ++$$et{DOC_COUNT};
            Process_text( $et, \$dat, $tagTbl );
            $pos += $recLen;
        }
        delete $$et{DOC_NUM};
        return 1;
    }
    $recLen = 32;
    SetByteOrder('II');
    if ( $dirLen > $recLen and not $et->Options('ExtractEmbedded') ) {
        $dirLen = $recLen;
        EEWarn($et);
    }
    for ( $pos = 0 ; $pos + $recLen <= $dirLen ; $pos += $recLen ) {
        $$et{DOC_NUM} = ++$$et{DOC_COUNT};
        my $lat = GetDouble( $dataPt, $pos );
        my $lon = GetDouble( $dataPt, $pos + 8 );
        next if abs($lat) > 9000 or abs($lon) > 18000;
        ConvertLatLon( $lat, $lon );
        my @a = unpack( 'C*', substr( $$dataPt, $pos + 22, 6 ) );
        $a[0] += 2000;
        $et->HandleTag( $tagTbl,
            GPSDateTime => sprintf( "%.4d:%.2d:%.2d %.2d:%.2d:%.2dZ", @a ) );
        $et->HandleTag( $tagTbl, GPSLatitude  => $lat );
        $et->HandleTag( $tagTbl, GPSLongitude => $lon );
        $et->HandleTag( $tagTbl, GPSSpeed => Get16u( $dataPt, $pos + 0x14 ) );
        $et->HandleTag( $tagTbl,
            GPSTrack => Get8u( $dataPt, $pos + 0x1c ) * 2 );
        $et->HandleTag( $tagTbl,
            GPSAltitude => Get32s( $dataPt, $pos + 0x10 ) );
    }
    delete $$et{DOC_NUM};
    SetByteOrder('MM');
    return 1;
}

sub Process_gsen($$$) {
    my ( $et, $dirInfo, $tagTbl ) = @_;
    my $dataPt = $$dirInfo{DataPt};
    my $dirLen = $$dirInfo{DirLen};
    my $recLen = 3;
    $et->VerboseDir( 'gsen', undef, $dirLen );
    if ( $dirLen > $recLen and not $et->Options('ExtractEmbedded') ) {
        $dirLen = $recLen;
        EEWarn($et);
    }
    my $pos;
    for ( $pos = 0 ; $pos + $recLen <= $dirLen ; $pos += $recLen ) {
        $$et{DOC_NUM} = ++$$et{DOC_COUNT};
        my @acc = map { $_ /= 16 } unpack "x${pos}c3", $$dataPt;
        $et->HandleTag( $tagTbl, Accelerometer => "@acc" );
    }
    delete $$et{DOC_NUM};
    return 1;
}

sub Process_gdat($$$) {
    my ( $et, $dirInfo, $tagTbl ) = @_;
    unless ( $$et{OPTIONS}{ExtractEmbedded} ) {
        $et->Warn( 'Use the ExtractEmbedded option to extract timed GPSData',
            3 );
        return 0;
    }
    my $dataPt = $$dirInfo{DataPt};
    require Image::ExifTool::XMP;
    $dataPt = Image::ExifTool::XMP::DecodeBase64($$dataPt);
    my ( %dbase, $fix );
    require Image::ExifTool::Import;
    Image::ExifTool::Import::ReadJSON( $dataPt, \%dbase );
    my $info = $dbase{'*'} or return 0;
    $et->HandleTag( $tagTbl, CameraModel => $$info{cameraModel} )
      if $$info{cameraModel};
    my $gps = $$info{gpsData} or return 0;
    return 0 unless ref $gps eq 'ARRAY';

    foreach $fix (@$gps) {
        next
          unless ref $fix eq 'HASH'
          and $$fix{gpsStatus}
          and $$fix{gpsStatus} eq 'A';
        $$et{DOC_NUM} = ++$$et{DOC_COUNT};
        if ( $$fix{datetime} ) {
            $$fix{datetime} =~ tr/-T/: /;
            $et->HandleTag( $tagTbl, GPSDateTime => $$fix{datetime} );
        }
        $et->HandleTag( $tagTbl, GPSLatitude => $$fix{lat} )
          if defined $$fix{lat};
        $et->HandleTag( $tagTbl, GPSLongitude => $$fix{lon} )
          if defined $$fix{lon};
        $et->HandleTag( $tagTbl, GPSSpeed => $$fix{speed} * $mphToKph )
          if defined $$fix{speed};
        $et->HandleTag( $tagTbl, GPSTrack => $$fix{bearing} )
          if defined $$fix{bearing};
        if (    defined $$fix{xAcc}
            and defined $$fix{yAcc}
            and defined $$fix{zAcc} )
        {
            $et->HandleTag( $tagTbl,
                Accelerometer => "$$fix{xAcc} $$fix{yAcc} $$fix{zAcc}" );
        }
    }
    delete $$et{DOC_NUM};
    return 1;
}

sub Process_nbmt($$$) {
    my ( $et, $dataPt, $tagTbl ) = @_;

    if ( $$et{OPTIONS}{ExtractEmbedded} ) {
        $$et{DOC_NUM} = $$et{DOC_COUNT} + 1;
        delete $$et{UnknownTextCount};
        delete $$et{NoMoreTextDecoding};
        $$et{SET_GROUP1} = 'Nextbase';
        Process_text( $et, $dataPt, $tagTbl, 1 );
        delete $$et{SET_GROUP1};
        delete $$et{UnknownTextCount};
        delete $$et{NoMoreTextDecoding};
        delete $$et{DOC_NUM};
    }
    else {
        $et->Warn( 'Use the ExtractEmbedded option to extract timed GPSData',
            3 );
    }
    return 1;
}

sub ProcessKenwood($$$) {
    my ( $et, $dirInfo, $tagTbl ) = @_;
    my $dataPt = $$dirInfo{DataPt};
    my $dirLen = $$dirInfo{DirLen};
    while ( $$dataPt =~ /\xfe\xfe([^\xfe]+)/g ) {
        my $dat = $1;
        next unless $dat =~ /^(\d{4})(\d{2})(\d{2})(\d{2})(\d{2})(\d{2})./gs;
        my $time = "$1:$2:$3 $4:$5:$6";

        next unless $dat =~ /\G(\d{4})(\d{2})(\d{2})(\d{2})(\d{2})(\d{2})./gs;
        next unless $dat =~ /\G([NS])(\d+)([EW])(\d+)/g;
        my ( $lat, $lon ) = ( $2 / 1e4, $4 / 1e4 );
        ConvertLatLon( $lat, $lon );
        $$et{DOC_NUM} = ++$$et{DOC_COUNT};
        $et->HandleTag( $tagTbl, GPSDateTime => $time );
        $et->HandleTag( $tagTbl, GPSLatitude => $lat * ( $1 eq 'S' ? -1 : 1 ) );
        $et->HandleTag( $tagTbl,
            GPSLongitude => $lon * ( $3 eq 'W' ? -1 : 1 ) );
        next unless $dat =~ /\G([-+]\d{4})(\d+)/g;
        $et->HandleTag( $tagTbl, GPSAltitude => $1 + 0 );
        $et->HandleTag( $tagTbl, GPSSpeed    => $2 );
        my @acc;

        while ( $dat =~ /\G([-+]\d+)([-+]\d+)([-+]\d+)/g ) {
            push @acc, $1 / 1000, $2 / 1000, $3 / 1000;
        }
        $et->HandleTag( $tagTbl, Accelerometer => "@acc" ) if @acc;
        unless ( $et->Options('ExtractEmbedded') ) {
            $et->Warn(
                'Use the ExtractEmbedded option to extract all timed GPS', 3 );
            last;
        }
    }
    delete $$et{DOC_NUM};
    return 1;
}

sub ProcessRIFFTrailer($$$) {
    my ( $et, $dirInfo, $tagTbl ) = @_;
    my $raf     = $$dirInfo{RAF};
    my $verbose = $et->Options('Verbose');
    my ( $buff, $pos );
    SetByteOrder('II');
    for ( ; ; ) {
        last unless $raf->Read( $buff, 8 ) == 8;
        my ( $tag, $len ) = unpack( 'a4V', $buff );
        last if $tag eq "\0\0\0\0";
        unless ( $tag =~ /^[\w ]{4}/ and $len < 0x2000000 ) {
            $et->Warn('Bad RIFF trailer');
            last;
        }
        $raf->Read( $buff, $len ) == $len
          or $et->Warn("Truncated $tag record in RIFF trailer"), last;
        if ($verbose) {
            $et->VPrint( 0, "  - RIFF trailer '${tag}' ($len bytes)\n" );
            $et->VerboseDump( \$buff, Addr => $raf->Tell() - $len )
              if $verbose > 2;
            $$et{INDENT} .= '| ';
            $et->VerboseDir( $tag, undef, $len ) if $tag =~ /^(gps0|gsen)$/;
        }
        if ( $tag eq 'gps0' ) {
            my $recLen = 0x28;
            for ( $pos = 0 ; $pos + $recLen < $len ; $pos += $recLen ) {
                substr( $buff, $pos, 4 ) eq 'AITG'
                  or $et->Warn('Unrecognized gps0 record'), last;
                $$et{DOC_NUM} = ++$$et{DOC_COUNT};
                my $lat = GetDouble( \$buff, $pos + 4 );
                my $lon = GetDouble( \$buff, $pos + 12 );
                $et->Warn('Bad gps0 record') and last
                  if abs($lat) > 9000
                  or abs($lon) > 18000;
                ConvertLatLon( $lat, $lon );
                $lat = -$lat if Get8u( \$buff, $pos + 0x21 ) == 2;
                $lon = -$lon if Get8u( \$buff, $pos + 0x22 ) == 2;
                my @a = unpack( 'C*', substr( $buff, $pos + 26, 6 ) );
                $a[0] += 1900;
                $et->HandleTag( $tagTbl,
                    SampleTime => Get32u( \$buff, $pos + 0x24 ) / 1000 );
                $et->HandleTag( $tagTbl,
                    GPSDateTime =>
                      sprintf( "%.4d:%.2d:%.2d %.2d:%.2d:%.2dZ", @a ) );
                $et->HandleTag( $tagTbl, GPSLatitude  => $lat );
                $et->HandleTag( $tagTbl, GPSLongitude => $lon );
                $et->HandleTag( $tagTbl,
                    GPSSpeed => Get16u( \$buff, $pos + 0x18 ) * $knotsToKph );
                $et->HandleTag( $tagTbl,
                    GPSTrack => Get8u( \$buff, $pos + 0x20 ) * 2 );
            }
        }
        elsif ( $tag eq 'gsen' ) {
            my $recLen = 0x0c;
            for ( $pos = 0 ; $pos + $recLen < $len ; $pos += $recLen ) {
                substr( $buff, $pos, 4 ) eq 'AITS'
                  or $et->Warn('Unrecognized gsen record'), last;
                $$et{DOC_NUM} = ++$$et{DOC_COUNT};
                my @acc =
                  map { $_ /= 24 } unpack( 'x' . ( $pos + 4 ) . 'c3', $buff );
                $et->HandleTag( $tagTbl,
                    SampleTime => Get32u( \$buff, $pos + 8 ) / 1000 );
                $et->HandleTag( $tagTbl, Accelerometer => "@acc" );
            }
        }
        $$et{INDENT} = substr( $$et{INDENT}, 0, -2 ) if $verbose;
    }
    delete $$et{DOC_NUM};
    SetByteOrder('MM');
    return 1;
}

sub ProcessKenwoodTrailer($$$) {
    my ( $et, $dirInfo, $tagTbl ) = @_;
    my $raf = $$dirInfo{RAF};
    my $buff;
    $raf->Read( $buff, 14 ) and $buff eq 'CCCCCCCCCCCCCC' or return 0;
    $et->VerboseDir( 'Kenwood trailer', undef, undef );
    unless ( $$et{OPTIONS}{ExtractEmbedded} ) {
        $et->Warn(
'Use the ExtractEmbedded option to extract timed GPSData from Kenwood trailer',
            3
        );
        return 1;
    }
    while ( $raf->Read( $buff, 121 )
        and $buff =~ /^GPSDATA--(\d{4})(\d{2})(\d{2})(\d{2})(\d{2})(\d{2})/ )
    {
        FoundSomething( $et, $tagTbl );
        $et->HandleTag( $tagTbl, GPSDateTime => "$1:$2:$3 $4:$5:$6" );
        my $i = 9 + 14;
        my ( $val, @acc, $tag );
        foreach $tag (qw(GPSLatitude GPSLongitude GPSSpeed unk acc acc acc)) {
            $val = substr( $buff, $i, 14 );
            $i += 14;
            next if $tag eq 'unk';
            my $hemi;
            $hemi = $1 if $val =~ s/^([NSEW])//;
            $val =~ /^[-+]?\d+\.\d+$/ or next;
            $tag eq 'acc' and push( @acc, $val ), next;
            $val = -$val if $hemi and ( $hemi eq 'S' or $hemi eq 'W' );
            $et->HandleTag( $tagTbl, $tag => $val );
        }
        $et->HandleTag( $tagTbl, Accelerometer => "@acc" ) if @acc == 3;
    }
    delete $$et{DOC_NUM};
    return 1;
}

sub ProcessNMEA($$$) {
    my ( $et, $dirInfo, $tagTbl ) = @_;
    my $dataPt = $$dirInfo{DataPt};
    my ( $rtnVal, %fix );
    for ( ; ; ) {
        my ( $tc, $type, $tim );
        if ( $$dataPt =~
            /(?:\[(\d+)\])?\$[A-Z]{2}(RMC|GGA),(\d{2}\d{2}\d+(\.\d*)?),/g )
        {
            ( $tc, $type, $tim ) = ( $1, $2, $3 );
        }
        if ( $fix{tim} and ( not $tim or $fix{tim} != $tim ) ) {
            if ( $fix{dat} and defined $fix{lat} and defined $fix{lon} ) {
                my $sampleTime;
                $sampleTime = ( $fix{tc} - $$et{StartTime} ) / 1000
                  if $fix{tc} and $$et{StartTime};
                FoundSomething( $et, $tagTbl, $sampleTime );
                $et->HandleTag( $tagTbl, GPSDateTime  => $fix{dat} );
                $et->HandleTag( $tagTbl, GPSLatitude  => $fix{lat} );
                $et->HandleTag( $tagTbl, GPSLongitude => $fix{lon} );
                $et->HandleTag( $tagTbl, GPSSpeed => $fix{spd} * $knotsToKph )
                  if defined $fix{spd};
                $et->HandleTag( $tagTbl, GPSTrack => $fix{trk} )
                  if defined $fix{trk};
                $et->HandleTag( $tagTbl, GPSAltitude => $fix{alt} )
                  if defined $fix{alt};
                $et->HandleTag( $tagTbl, GPSSatellites => $fix{nsats} + 0 )
                  if defined $fix{nsats};
                $et->HandleTag( $tagTbl, GPSDOP => $fix{hdop} )
                  if defined $fix{hdop};
            }
            undef %fix;
        }
        $fix{tim} = $tim or last;
        my $pos = pos($$dataPt);
        pos($$dataPt) = $pos - length($tim) - 1;

        if (    $type eq 'RMC'
            and $$dataPt =~
/\G(\d{2})(\d{2})(\d+(\.\d*)?),A?,(\d*?)(\d{1,2}\.\d+),([NS]),(\d*?)(\d{1,2}\.\d+),([EW]),(\d*\.?\d*),(\d*\.?\d*),(\d{2})(\d{2})(\d+)/g
          )
        {
            my $year = $15 + ( $15 >= 70 ? 1900 : 2000 );
            $fix{tc}  = $tc;
            $fix{dat} = sprintf( '%.4d:%.2d:%.2d %.2d:%.2d:%sZ',
                $year, $14, $13, $1, $2, $3 );
            $fix{lat} = ( ( $5 || 0 ) + $6 / 60 ) * ( $7 eq 'N'  ? 1 : -1 );
            $fix{lon} = ( ( $8 || 0 ) + $9 / 60 ) * ( $10 eq 'E' ? 1 : -1 );
            $fix{spd} = $11 if length $11;
            $fix{trk} = $12 if length $12;
        }
        elsif ( $type eq 'GGA'
            and $$dataPt =~
/\G(\d{2})(\d{2})(\d+(\.\d*)?),(\d*?)(\d{1,2}\.\d+),([NS]),(\d*?)(\d{1,2}\.\d+),([EW]),[1-6]?,(\d+)?,(\.\d+|\d+\.?\d*)?,(-?\d+\.?\d*)?,M?/g
          )
        {
            $fix{lat} = ( ( $5 || 0 ) + $6 / 60 ) * ( $7 eq 'N'  ? 1 : -1 );
            $fix{lon} = ( ( $8 || 0 ) + $9 / 60 ) * ( $10 eq 'E' ? 1 : -1 );
            @fix{qw(nsats hdop alt)} = ( $11, $12, $13 );
        }
        else {
            pos($$dataPt) = $pos;
        }
    }
    delete $$et{DOC_NUM};
    return $rtnVal;
}

sub ProcessGPSLog($$$) {
    my ( $et, $dirInfo, $tagTbl ) = @_;
    my $dataPt = $$dirInfo{DataPt};
    my ( $rtnVal, @a );

    return 1 if ProcessNMEA( $et, $dirInfo, $tagTbl );

    while ( $$dataPt =~
/\b(\d{2})(\d{2})(\d{2})(\d{2})(\d{2})(\d{2})\[1\]\[([NS])\]\[(\d{8})\]\[([EW])\]\[(\d{9})\]\[([-+]?\d*)\]\[(\d*)\]\[(\d*)\]\[C?(\d*)\](([-+]\d{3})+)/g
      )
    {
        my $lat = substr( $8,  0, 2 ) + substr( $8,  2 ) / 600000;
        my $lon = substr( $10, 0, 3 ) + substr( $10, 3 ) / 600000;
        $$et{DOC_NUM} = ++$$et{DOC_COUNT};
        $et->HandleTag( $tagTbl, GPSDateTime => "20$1:$2:$3 $4:$5:$6Z" );
        $et->HandleTag( $tagTbl, GPSLatitude => $lat * ( $7 eq 'S' ? -1 : 1 ) );
        $et->HandleTag( $tagTbl,
            GPSLongitude => $lon * ( $9 eq 'W' ? -1 : 1 ) );
        $et->HandleTag( $tagTbl, GPSAltitude   => $11 / 10 ) if length $11;
        $et->HandleTag( $tagTbl, GPSSpeed      => $12 + 0 )  if length $12;
        $et->HandleTag( $tagTbl, GPSTrack      => $13 + 0 )  if length $13;
        $et->HandleTag( $tagTbl, KiloCalories  => $14 / 10 ) if length $14;
        $et->HandleTag( $tagTbl, Accelerometer => $15 )      if length $15;
        $rtnVal = 1;
    }
    delete $$et{DOC_NUM};
    return $rtnVal;
}

my %ttLen = (
    0 => 12,
    1 => 4,
    2 => 12,
    3 => 12,

    5    => 92,
    0xff => 4,
);

sub ProcessTTAD($$$) {
    my ( $et, $dirInfo, $tagTbl ) = @_;
    my $dataPt = $$dirInfo{DataPt};
    my $dirLen = $$dirInfo{DirLen};
    my $pos    = 76;

    return 0 if $dirLen < $pos;

    $et->VerboseDir( 'TTAD', undef, $dirLen );
    SetByteOrder('II');

    my $eeOpt      = $et->Options('ExtractEmbedded');
    my $unknown    = $et->Options('Unknown');
    my $found      = 0;
    my $sampleTime = 0;
    my $resync     = 1;
    my $skipped    = 0;
    my $warned;

    while ( $pos < $dirLen ) {
        my $type = Get8u( $dataPt, $pos++ );
        if ( $resync and $type != 0xff ) {
            ++$skipped > 0x100
              and $et->Warn( 'Unrecognized or bad TTAD data', 1 ), last;
            next;
        }
        unless ( $ttLen{$type} ) {
            $et->Warn( "Unknown TTAD record type $type", 1 ) unless $warned;
            $resync = $warned = 1;
            ++$skipped;
            next;
        }
        last if $pos + $ttLen{$type} > $dirLen;
        if ( $type == 0xff ) {
            my $tm = Get32u( $dataPt, $pos );
            if ($resync) {
                if ( $tm < $sampleTime or $tm > $sampleTime + 250 ) {
                    ++$skipped;
                    next;
                }
                undef $resync;
                $skipped = 0;
            }
            $pos += $ttLen{$type};
            $sampleTime = $tm;
            next;
        }
        unless ($eeOpt) {
            $found & ( 1 << $type ) and $pos += $ttLen{$type}, next;
            $found |= ( 1 << $type );
        }
        if ( $type == 0 or $type == 3 ) {
            FoundSomething( $et, $tagTbl, $sampleTime / 1000 );
            my @a = map { Get32s( $dataPt, $pos + 4 * $_ ) / 1000 } 0 .. 2;
            $et->HandleTag( $tagTbl,
                ( $type ? 'Accelerometer' : 'AngularVelocity' ) => "@a" );
        }
        elsif ( $type == 5 ) {
            FoundSomething( $et, $tagTbl, $sampleTime / 1000 );
            my $t = GetDouble( $dataPt, $pos );
            $et->HandleTag( $tagTbl,
                GPSDateTime => Image::ExifTool::ConvertUnixTime( $t, undef, 3 )
                  . 'Z' );
            $et->HandleTag( $tagTbl,
                GPSLatitude => GetDouble( $dataPt, $pos + 0x1c ) );
            $et->HandleTag( $tagTbl,
                GPSLongitude => GetDouble( $dataPt, $pos + 0x24 ) );
            $et->HandleTag( $tagTbl,
                GPSAltitude => GetDouble( $dataPt, $pos + 0x14 ) );
            $et->HandleTag( $tagTbl,
                GPSSpeed => GetDouble( $dataPt, $pos + 0x0c ) * $mpsToKph );
            $et->HandleTag( $tagTbl,
                GPSTrack => GetDouble( $dataPt, $pos + 0x30 ) );

            if ($unknown) {
                my @a =
                  map { GetDouble( $dataPt, $pos + 0x38 + 8 * $_ ) } 0 .. 2;
                $et->HandleTag( $tagTbl, Unknown03 => "@a" );
            }
        }
        elsif ( $type < 3 ) {
            if ($unknown) {
                FoundSomething( $et, $tagTbl, $sampleTime / 1000 );
                my $n = $type == 1 ? 0 : 2;
                my @a = map { Get32s( $dataPt, $pos + 4 * $_ ) } 0 .. $n;
                $et->HandleTag( $tagTbl, "Unknown0$type" => "@a" );
            }
        }
        else {
            $et->Warn( "Unknown TTAD record type $type", 1 );
        }
        $eeOpt or ( $found & 0x29 ) != 0x29 or EEWarn($et), last;
        $pos += $ttLen{$type};
    }
    SetByteOrder('MM');
    delete $$et{DOC_NUM};
    return 1;
}

sub ProcessInsta360($;$) {
    local $_;
    my ( $et, $dirInfo ) = @_;
    my $raf    = $$et{RAF};
    my $offset = $dirInfo ? $$dirInfo{Offset} || 0 : 0;
    my ( $buff, $dirTable, $dirTablePos );

    if ( $dirInfo and $$dirInfo{DirEnd} ) {
        $raf->Seek( 0, 2 );
        $offset = $raf->Tell() - $$dirInfo{DirEnd};
    }
    return 0
      unless $raf->Seek( -78 - $offset, 2 )
      and $raf->Read( $buff, 78 ) == 78
      and substr( $buff, -32 ) eq "8db42d694ccc418790edff439fe026bf";

    my $verbose    = $et->Options('Verbose');
    my $tagTbl     = GetTagTable('Image::ExifTool::QuickTime::Stream');
    my $trailEnd   = $raf->Tell();
    my $trailerLen = unpack( 'x38V', $buff );
    $trailerLen > $trailEnd
      and $et->Warn('Bad Insta360 trailer size'), return 0;
    if ($dirInfo) {
        $$dirInfo{DirLen}  = $trailerLen;
        $$dirInfo{DataPos} = $trailEnd - $trailerLen;
        if ( $$dirInfo{OutFile} ) {
            if ( $$et{DEL_GROUP}{Insta360} ) {
                ++$$et{CHANGED};
                return 1;
            }
            elsif ( $trailerLen > $trailEnd
                or not $raf->Seek( $$dirInfo{DataPos}, 0 )
                or $raf->Read( ${ $$dirInfo{OutFile} }, $trailerLen ) !=
                $trailerLen )
            {
                return 0;
            }
            else {
                return 1;
            }
        }
        $et->DumpTrailer($dirInfo) if $verbose or $$et{HTML_DUMP};
    }
    unless ( $et->Options('ExtractEmbedded') ) {
        $et->Warn(
'Use ExtractEmbedded option to extract timed metadata from Insta360 trailer',
            3
        );
        return 1;
    }

    my $unknown = $et->Options('Unknown');
    my $epos = -78;
    my ( $i, $p );
    $$et{SET_GROUP0} = 'Trailer';
    $$et{SET_GROUP1} = 'Insta360';
    SetByteOrder('II');
    for ( ; ; ) {
        my ( $id, $len ) = unpack( 'vV', $buff );
        ( $epos -= $len ) + $trailerLen < 0 and last;
        $raf->Seek( $epos - $offset, 2 ) or last;
        if ($verbose) {
            $et->VPrint(
                0,
                sprintf(
                    "Insta360 Record 0x%x (offset 0x%x, %d bytes):\n",
                    $id, $trailEnd + $epos, $len
                )
            );
        }
        my $dlen = $insvDataLen{$id};
        if ( defined $dlen and not $dlen ) {
            if ( $id == 0x300 ) {
                if ( $len % 20 and not $len % 56 ) {
                    $dlen = 56;
                }
                elsif ( $len % 56 and not $len % 20 ) {
                    $dlen = 20;
                }
                else {
                    if ( $raf->Read( $buff, 20 ) == 20 ) {
                        if ( substr( $buff, 16, 3 ) eq "\0\0\0" ) {
                            $dlen = 56;
                        }
                        else {
                            $dlen = 20;
                        }
                    }
                    $raf->Seek( $epos - $offset, 2 ) or last;
                }
            }
            elsif ( $id == 0x200 ) {
                $dlen = $len;
            }
        }
        if (
                $dlen
            and $insvLimit{$id}
            and $len > $insvLimit{$id}[1] * $dlen
            and $et->Warn(
"Insta360 $insvLimit{$id}[0] data is huge. Processing only the first $insvLimit{$id}[1] records",
                2
            )
          )
        {
            $len = $insvLimit{$id}[1] * $dlen;
        }
        $raf->Read( $buff, $len ) == $len or last;
        $et->VerboseDump( \$buff ) if $verbose > 2;
        if ($dlen) {
            if ( $len % $dlen and $id != 0x700 ) {
                $et->Warn(
                    sprintf( 'Unexpected Insta360 record 0x%x length', $id ) );
            }
            elsif ( $id == 0x200 ) {
                if ( $buff =~ /^\xff\xd8\xff/ ) {
                    $et->FoundTag( PreviewImage => $buff );
                }
                elsif ( $buff =~ /^\x01\0\0\0(.{4})\x01/s
                    and unpack( 'V', $1 ) == $dlen )
                {
                    my ( $w, $h ) = unpack( 'x16V2', $buff );
                    my $hdr = Image::ExifTool::MakeTiffHeader( $w, $h, 1, 8 );
                    $et->FoundTag( PreviewTIFF => $hdr . substr( $buff, 40 ) );
                }
            }
            elsif ( $id == 0x300 ) {
                for ( $p = 0 ; $p < $len ; $p += $dlen ) {
                    $$et{DOC_NUM} = ++$$et{DOC_COUNT};
                    my @a;
                    if ( $dlen == 56 ) {
                        @a = map { GetDouble( \$buff, $p + 8 * $_ ) } 1 .. 6;
                    }
                    else {
                        @a = unpack( "x${p}x8v6", $buff );
                        map { $_ = ( $_ - 0x8000 ) / 1000 } @a;
                    }
                    $et->HandleTag( $tagTbl,
                        TimeCode =>
                          sprintf( '%.3f', Get64u( \$buff, $p ) / 1000 ) );
                    $et->HandleTag( $tagTbl, Accelerometer   => "@a[0..2]" );
                    $et->HandleTag( $tagTbl, AngularVelocity => "@a[3..5]" );
                }
            }
            elsif ( $id == 0x400 ) {
                for ( $p = 0 ; $p < $len ; $p += $dlen ) {
                    $$et{DOC_NUM} = ++$$et{DOC_COUNT};
                    $et->HandleTag( $tagTbl,
                        TimeCode =>
                          sprintf( '%.3f', Get64u( \$buff, $p ) / 1000 ) );
                    $et->HandleTag( $tagTbl,
                        ExposureTime => GetDouble( \$buff, $p + 8 ) );
                }
            }
            elsif ( $id == 0x600 ) {
                for ( $p = 0 ; $p < $len ; $p += $dlen ) {
                    $$et{DOC_NUM} = ++$$et{DOC_COUNT};
                    $et->HandleTag( $tagTbl,
                        VideoTimeStamp =>
                          sprintf( '%.3f', Get64u( \$buff, $p ) / 1000 ) );
                }
            }
            elsif ( $id == 0x700 ) {
                for ( $p = 0 ; $p + $dlen <= $len ; $p += $dlen ) {
                    my $tmp = substr( $buff, $p, $dlen );
                    my @a   = unpack( 'VVvaa8aa8aa8a8a8', $tmp );
                    unless (
                        ( $a[5] eq 'N' or $a[5] eq 'S' )
                        and (
                               $a[7] eq 'E'
                            or $a[7] eq 'W'
                            or
                            $a[7] eq 'O'
                        )
                      )
                    {
                        next if $a[3] eq 'V';
                        $et->Warn('Unrecognized INSV GPS format');
                        last;
                    }
                    next unless $a[3] eq 'A';
                    $$et{DOC_NUM} = ++$$et{DOC_COUNT};
                    $a[$_] = GetDouble( \$a[$_], 0 ) foreach 4, 6, 8, 9, 10;
                    $a[4]  = -abs( $a[4] ) if $a[5] eq 'S';
                    $a[6]  = -abs( $a[6] ) if $a[7] ne 'E';
                    my $ms = '';
                    $a[2] and ( $ms = sprintf( '.%.3d', $a[2] ) ) =~ s/0+$//;
                    $et->HandleTag( $tagTbl,
                        GPSDateTime => Image::ExifTool::ConvertUnixTime( $a[0] )
                          . $ms
                          . 'Z' );
                    $et->HandleTag( $tagTbl, GPSLatitude  => $a[4] );
                    $et->HandleTag( $tagTbl, GPSLongitude => $a[6] );
                    $et->HandleTag( $tagTbl, GPSSpeed    => $a[8] * $mpsToKph );
                    $et->HandleTag( $tagTbl, GPSTrack    => $a[9] );
                    $et->HandleTag( $tagTbl, GPSAltitude => $a[10] );
                    $et->HandleTag( $tagTbl, Unknown02   => $a[1] ) if $unknown;
                }
            }
        }
        elsif ( $id == 0x101 ) {
            my $tagTablePtr =
              GetTagTable('Image::ExifTool::QuickTime::INSV_MakerNotes');
            for ( $i = 0, $p = 0 ; $i < 4 ; ++$i ) {
                last if $p + 2 > $len;
                my ( $t, $n ) = unpack( "x${p}CC", $buff );
                last if $p + 2 + $n > $len;
                my $val = substr( $buff, $p + 2, $n );
                $et->HandleTag( $tagTablePtr, $t, $val );
                $p += 2 + $n;
            }
        }
        elsif ( $id == 0x0 ) {
            last if not $len;
            unless ($dirTable) {
                $dirTable    = $buff;
                $dirTablePos = 0;
            }
        }
        if ($dirTable) {
            undef $epos;
            for ( ; ; ) {
                last if $dirTablePos + 10 > length($dirTable);
                my ( $id, $siz, $off ) =
                  unpack( "x${dirTablePos}vVV", $dirTable );
                $dirTablePos += 10;
                if ( $id and $siz and $off + $siz < $trailerLen ) {
                    $epos = $off + $siz - $trailerLen;
                    last;
                }
            }
            last unless defined $epos;
        }
        else {
            ( $epos -= 6 ) + $trailerLen < 0 and last;
        }
        $raf->Seek( $epos - $offset, 2 ) or last;
        $raf->Read( $buff, 6 ) == 6      or last;
    }
    delete $$et{DOC_NUM};
    SetByteOrder('MM');
    delete $$et{SET_GROUP0};
    delete $$et{SET_GROUP1};
    return 1;
}

sub ProcessCAMM($$$) {
    my ( $et, $dirInfo, $tagTbl ) = @_;
    my $dataPt = $$dirInfo{DataPt};
    my $pos    = $$dirInfo{DirStart} || 0;
    my $end    = $pos + ( $$dirInfo{DirLen} || length($$dataPt) - $pos );
    my %size =
      ( 1 => 12, 2 => 16, 3 => 16, 4 => 16, 5 => 28, 6 => 60, 7 => 16 );
    my $rtnVal = 0;
    while ( $pos + 4 < $end ) {
        my $type = Get16u( $dataPt, $pos + 2 );
        my $size = $size{$type}
          or $et->Warn("Unknown camm record type $type"), last;
        $pos + $size > $end and $et->Warn("Truncated camm record $type"), last;
        my $tagTbl = GetTagTable("Image::ExifTool::QuickTime::camm$type");
        $$dirInfo{DirStart} = $pos;
        $$dirInfo{DirLen}   = $size;
        $et->ProcessBinaryData( $dirInfo, $tagTbl ) and $rtnVal = 1;
        $pos += $size;
    }
    return $rtnVal;
}

sub ProcessGarminGPS($$$) {
    my ( $et, $dirInfo, $tagTbl ) = @_;
    my $dataPt  = $$dirInfo{DataPt};
    my $dataLen = length $$dataPt;
    my $pos     = 33;
    my $epoch   = ( 66 * 365 + 17 ) * 24 * 3600;
    my $scl     = 180 / ( 32768 * 65536 );
    $et->VerboseDir('GarminGPS');
    $$et{SET_GROUP1} = 'Garmin';

    while ( $pos + 20 <= $dataLen ) {
        $$et{DOC_NUM} = ++$$et{DOC_COUNT};
        my $time =
          Image::ExifTool::ConvertUnixTime( Get32u( $dataPt, $pos ) - $epoch )
          . 'Z';
        my $lat = Get32s( $dataPt, $pos + 12 );
        my $lon = Get32s( $dataPt, $pos + 16 );
        my $spd = Get16u( $dataPt, $pos + 4 );
        $et->HandleTag( $tagTbl, 'GPSDateTime', $time );
        if ( $lat != -2147483648 or $lon != -2147483648 ) {
            $et->HandleTag( $tagTbl, 'GPSLatitude',  $lat * $scl );
            $et->HandleTag( $tagTbl, 'GPSLongitude', $lon * $scl );
            $et->HandleTag( $tagTbl, 'GPSSpeed',     $spd );
            $et->HandleTag( $tagTbl, 'GPSSpeedRef',  'M' );
        }
        $pos += 20;
    }
    delete $$et{DOC_NUM};
    delete $$et{SET_GROUP1};
    return 1;
}

sub Process360Fly($$$) {
    my ( $et, $dirInfo, $tagTbl ) = @_;
    my $dataPt    = $$dirInfo{DataPt};
    my $dataLen   = length $$dataPt;
    my $pos       = 16;
    my $lastTime  = -1;
    my $streamTbl = GetTagTable('Image::ExifTool::QuickTime::Stream');
    while ( $pos + 32 <= $dataLen ) {
        my $type = ord substr $$dataPt, $pos, 1;
        my $time = Get64u( $dataPt, $pos + 2 );
        if ( $$tagTbl{$type} ) {
            if ( $time != $lastTime ) {
                $$et{DOC_NUM} = ++$$et{DOC_COUNT};
                $lastTime = $time;
            }
        }
        $et->HandleTag(
            $tagTbl, $type, undef,
            DataPt => $dataPt,
            Start  => $pos,
            Size   => 32
        );
        SetGPSDateTime( $et, $streamTbl, $time / 1e6 ) if $type == 5;
        $pos += 32;
    }
    delete $$et{DOC_NUM};
    return 1;
}

sub ProcessFMAS($$$) {
    my ( $et, $dirInfo, $tagTbl ) = @_;
    my $dataPt = $$dirInfo{DataPt};
    return 0
      unless $$dataPt =~ /^FMAS\0\0\0\0.{72}SAMM.{36}A/s
      and length($$dataPt) >= 160;
    $et->VerboseDir( 'FMAS', undef, length($$dataPt) );
    my @a = unpack( 'x96vCCCCCCx16AAACCCvCCvvv', $$dataPt );
    SetByteOrder('II');
    my $acc = ReadValue( $dataPt, 0x6c, 'float', 3 );
    my $lon = $a[10] + ( $a[11] + $a[13] / 6000 ) / 60;
    my $lat = $a[14] + ( $a[15] + $a[16] / 6000 ) / 60;
    $et->HandleTag( $tagTbl,
        GPSDateTime => sprintf( '%.4d:%.2d:%.2d %.2d:%.2d:%.2d', @a[ 0 .. 5 ] )
    );
    $et->HandleTag( $tagTbl, GPSLatitude  => $lat * ( $a[9] eq 'S' ? -1 : 1 ) );
    $et->HandleTag( $tagTbl, GPSLongitude => $lon * ( $a[8] eq 'W' ? -1 : 1 ) );
    $et->HandleTag( $tagTbl, GPSSpeed      => $a[17] * $mphToKph );
    $et->HandleTag( $tagTbl, GPSTrack      => $a[18] );
    $et->HandleTag( $tagTbl, Accelerometer => $acc );
    SetByteOrder('MM');
    return 1;
}

sub ProcessWolfbox($$$) {
    my ( $et, $dirInfo, $tagTbl ) = @_;
    my $dataPt = $$dirInfo{DataPt};
    return 0 if length($$dataPt) < 0xf8;
    $et->VerboseDir( 'Wolfbox', undef, length($$dataPt) );
    SetByteOrder('II');
    my ( $d, $mo, $yr, $h, $m, $s ) = unpack( 'x104V3x44V3', $$dataPt );
    my $time = sprintf '%.4d:%.2d:%.2d %.2d:%.2d:%.2dZ', $yr, $mo, $d, $h, $m,
      $s;
    my ( $pos, @a );

    foreach $pos ( 0x48, 0x58, 0xb0, 0xc0, 0xe8 ) {
        my $val = Get64s( $dataPt, $pos );
        my $scl = Get64s( $dataPt, $pos + 8 );
        push @a, $val / ( $scl || 1 );
    }
    ConvertLatLon( $a[2], $a[3] );
    $et->HandleTag( $tagTbl, GPSDateTime  => $time );
    $et->HandleTag( $tagTbl, GPSLatitude  => $a[2] );
    $et->HandleTag( $tagTbl, GPSLongitude => $a[3] );
    $et->HandleTag( $tagTbl, GPSSpeed     => $a[0] * $knotsToKph );
    $et->HandleTag( $tagTbl, GPSTrack     => $a[1] );
    $et->HandleTag( $tagTbl, GPSAltitude  => $a[4] );
    return 1;
}

sub ScanMediaData($) {
    my $et  = shift;
    my $raf = $$et{RAF} or return;
    my ( $tagTbl, $verbose, $buff, $dataLen, $found );

    my $dataPos = $$et{MediaDataOffset};
    return if $$et{FoundEmbedded} or not $dataPos;

    my ( $pos, $buf2 ) = ( 0, '' );
    my $ee = $et->Options('ExtractEmbedded');
    if ( $ee > 2 ) {
        $raf->Seek( 0, 2 );
        $dataLen = $raf->Tell() - $$et{MediaDataOffset};
    }
    else {
        $dataLen = $$et{MediaDataSize};
    }
    return unless $dataLen and $raf->Seek($dataPos);

    while ($dataLen) {
        my $n = $gpsBlockSize;
        $n = $dataLen - $pos if $n + $pos > $dataLen;
        last
          unless $n > length($buf2)
          and $raf->Read( $buff, $n - length($buf2) );
        $buff = $buf2 . $buff if length $buf2;
        if ( $buff !~ /(\0..\0freeGPS |GP\x06\0\0)/sg ) {
            $buf2 = substr( $buff, -12 );
            $pos += length($buff) - 12;
            next if $found or $pos < 20e6 or $ee > 1;
            last;
        }
        elsif ( $1 eq "GP\x06\0\0" ) {

            my $buffPos = pos($buff);
            my $filePos = $raf->Tell();
            my $start   = $filePos - length($buff) + $buffPos - length($1);
            $raf->Seek($start) or last;
            unless ( defined $found ) {
                $et->VPrint( 0, "---- Extract Embedded ----\n" );
                $$et{INDENT} .= '| ';
                $found = 0;
            }
            my $maxLen = $dataLen - ( $start - $$et{MediaDataOffset} );
            require Image::ExifTool::GoPro;
            $et->VPrint( 0,
                sprintf( "Unreferenced GoPro record at 0x%x\n", $filePos ) );
            my $size = Image::ExifTool::GoPro::ProcessGP6( $et,
                { RAF => $raf, DirLen => $maxLen } );
            if ($size) {
                unless ($found) {
                    $raf->Seek( 0, 2 )
                      and $dataLen = $raf->Tell() - $$et{MediaDataOffset};
                    $found = 2;
                }
                $raf->Seek( $start + $size ) or last;
                $pos  = $start + $size - $$et{MediaDataOffset};
                $buf2 = '';
            }
            else {
                $raf->Seek($filePos) or last;
                $buf2 = substr( $buff, $buffPos );
                $pos += $buffPos;
            }
            next;
        }
        last if length $buff < $gpsBlockSize;
        if ( not $tagTbl ) {
            $tagTbl  = GetTagTable('Image::ExifTool::QuickTime::Stream');
            $verbose = $$et{OPTIONS}{Verbose};
            $et->VPrint( 0, "---- Extract Embedded ----\n" );
            $$et{INDENT} .= '| ';
            $found = 1;
        }
        if ( pos($buff) > 12 ) {
            $pos += pos($buff) - 12;
            $buff = substr( $buff, pos($buff) - 12 );
        }
        my $len = unpack( 'N', $buff );
        if ( $len < 12 ) {
            $len = 12;
        }
        else {
            my $more = $len - length($buff);
            if ( $more > 0 ) {
                last unless $raf->Read( $buf2, $more ) == $more;
                $buff .= $buf2;
            }
            if ($verbose) {
                $et->VerboseDir( 'GPS', undef, $len );
                $et->VerboseDump( \$buff, DataPos => $pos + $dataPos );
            }
            my $dirInfo =
              { DataPt => \$buff, DataPos => $pos + $dataPos, DirLen => $len };
            ProcessFreeGPS( $et, $dirInfo, $tagTbl );
            $$et{FoundGPSByScan} = 1;
        }
        $pos += $len;
        $buf2 = substr( $buff, $len );
    }
    if ($found) {
        delete $$et{DOC_NUM};
        $et->VPrint( 0, "--------------------------\n" );
        $$et{INDENT} = substr $$et{INDENT}, 0, -2;
    }
}

1;

__END__


