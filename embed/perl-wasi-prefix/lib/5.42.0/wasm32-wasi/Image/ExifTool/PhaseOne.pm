
package Image::ExifTool::PhaseOne;

use strict;
use vars            qw($VERSION);
use Image::ExifTool qw(:DataAccess :Utils);
use Image::ExifTool::Exif;

$VERSION = '1.12';

sub WritePhaseOne($$$);
sub ProcessPhaseOne($$$);

my @formatName = ( undef, 'string', 'int16s', undef, 'int32s' );

%Image::ExifTool::PhaseOne::Main = (
    PROCESS_PROC => \&ProcessPhaseOne,
    WRITE_PROC   => \&WritePhaseOne,
    CHECK_PROC   => \&Image::ExifTool::Exif::CheckExif,
    WRITABLE     => '1',
    FORMAT       => 'int32s',
    GROUPS       => { 0          => 'MakerNotes', 2 => 'Camera' },
    VARS         => { ENTRY_SIZE => 16 },
    NOTES        =>
      'These tags are extracted from the maker notes of Phase One images.',
    0x0100 => {
        Name      => 'CameraOrientation',
        ValueConv => '$val & 0x03',
        PrintConv => {
            0 => 'Horizontal (normal)',
            1 => 'Rotate 90 CW',
            2 => 'Rotate 270 CW',
            3 => 'Rotate 180',
        },
    },
    0x0102 => { Name => 'SerialNumber', Format => 'string' },
    0x0105 => 'ISO',
    0x0106 => {
        Name      => 'ColorMatrix1',
        Format    => 'float',
        Count     => 9,
        PrintConv => q{
            my @a = map { sprintf('%.3f', $_) } split ' ', $val;
            return "@a";
        },
        PrintConvInv => '$val',
    },
    0x0107 => { Name => 'WB_RGBLevels', Format => 'float', Count => 3 },
    0x0108 => 'SensorWidth',
    0x0109 => 'SensorHeight',
    0x010a => 'SensorLeftMargin',
    0x010b => 'SensorTopMargin',
    0x010c => 'ImageWidth',
    0x010d => 'ImageHeight',
    0x010e => {
        Name => 'RawFormat',
        PrintConv => {
            0 => 'Uncompressed',
            1 => 'RAW 1',
            2 => 'RAW 2',
            3 => 'IIQ L',

            5 => 'IIQ S',
            6 => 'IIQ Sv2',
            8 => 'IIQ L16',
        },
    },
    0x010f => {
        Name        => 'RawData',
        Format      => 'undef',
        Binary      => 1,
        IsImageData => 1,
        PutFirst    => 1,
        Writable    => 0,
        Drop        => 1,
    },
    0x0110 => {
        Name         => 'SensorCalibration',
        SubDirectory =>
          { TagTable => 'Image::ExifTool::PhaseOne::SensorCalibration' },
    },
    0x0112 => {
        Name         => 'DateTimeOriginal',
        Description  => 'Date/Time Original',
        Format       => 'int32u',
        Writable     => 0,
        Priority     => 0,
        Shift        => 'Time',
        Groups       => { 2 => 'Time' },
        Notes        => 'may be used as a key to encrypt the raw data',
        ValueConv    => 'ConvertUnixTime($val)',
        ValueConvInv => 'GetUnixTime($val)',
        PrintConv    => '$self->ConvertDateTime($val)',
        PrintConvInv => '$self->InverseDateTime($val)',
    },
    0x0113 => 'ImageNumber',
    0x0203 => { Name => 'Software', Format => 'string' },
    0x0204 => { Name => 'System',   Format => 'string' },
    0x0210 => {
        Name         => 'SensorTemperature',
        Format       => 'float',
        PrintConv    => 'sprintf("%.2f C",$val)',
        PrintConvInv => '$val=~s/ ?C//; $val',
    },
    0x0211 => {
        Name         => 'SensorTemperature2',
        Format       => 'float',
        PrintConv    => 'sprintf("%.2f C",$val)',
        PrintConvInv => '$val=~s/ ?C//; $val',
    },
    0x0212 => {
        Name   => 'UnknownDate',
        Format => 'int32u',
        Groups => { 2 => 'Time' },
        Unknown      => 1,
        Shift        => 'Time',
        ValueConv    => 'ConvertUnixTime($val)',
        ValueConvInv => 'GetUnixTime($val)',
        PrintConv    => '$self->ConvertDateTime($val)',
        PrintConvInv => '$self->InverseDateTime($val)',
    },
    0x021c => { Name => 'StripOffsets', Binary => 1, Writable => 0 },
    0x021d => 'BlackLevel',

    0x0222 => 'SplitColumn',
    0x0223 => {
        Name   => 'BlackLevelData',
        Format => 'int16u',
        Count  => -1,
        Binary => 1
    },

    0x0225 => {
        Name      => 'PhaseOne_0x0225',
        Format    => 'int16s',
        Count     => -1,
        Flags     => [ 'Unknown', 'Hidden' ],
        PrintConv => \&Image::ExifTool::LimitLongValues,
    },
    0x0226 => {
        Name      => 'ColorMatrix2',
        Format    => 'float',
        Count     => 9,
        PrintConv => q{
            my @a = map { sprintf('%.3f', $_) } split ' ', $val;
            return "@a";
        },
        PrintConvInv => '$val',
    },
    0x0267 => {
        Name   => 'AFAdjustment',
        Format => 'float',
    },
    0x022b => {
        Name   => 'PhaseOne_0x022b',
        Format => 'float',
        Flags  => [ 'Unknown', 'Hidden' ],
    },
    0x0258 => {
        Name      => 'PhaseOne_0x0258',
        Format    => 'int16s',
        Flags     => [ 'Unknown', 'Hidden' ],
        PrintConv => \&Image::ExifTool::LimitLongValues,
    },
    0x025a => {
        Name      => 'PhaseOne_0x025a',
        Format    => 'int16s',
        Flags     => [ 'Unknown', 'Hidden' ],
        PrintConv => \&Image::ExifTool::LimitLongValues,
    },
    0x0262 => { Name => 'SequenceID', Format => 'string' },
    0x0263 => {
        Name      => 'SequenceKind',
        PrintConv => {
            0 => 'Bracketing: Shutter Speed',
            1 => 'Bracketing: Aperture',
            2 => 'Bracketing: ISO',
            3 => 'Hyperfocal',
            4 => 'Time Lapse',
            5 => 'HDR',
            6 => 'Focus Stacking',
        },
        PrintConvInv => '$val',
    },
    0x0264 => 'SequenceFrameNumber',
    0x0265 => 'SequenceFrameCount',
    0x0301 => { Name => 'FirmwareVersions', Format => 'string' },
    0x0400 => {
        Name         => 'ShutterSpeedValue',
        Format       => 'float',
        ValueConv    => 'abs($val)<100 ? 2**(-$val) : 0',
        ValueConvInv => '$val>0 ? -log($val)/log(2) : -100',
        PrintConv    => 'Image::ExifTool::Exif::PrintExposureTime($val)',
        PrintConvInv => 'Image::ExifTool::Exif::ConvertFraction($val)',
    },
    0x0401 => {
        Name         => 'ApertureValue',
        Format       => 'float',
        ValueConv    => '2 ** ($val / 2)',
        ValueConvInv => '$val>0 ? 2*log($val)/log(2) : 0',
        PrintConv    => 'sprintf("%.1f",$val)',
        PrintConvInv => '$val',
    },
    0x0402 => {
        Name         => 'ExposureCompensation',
        Format       => 'float',
        PrintConv    => 'sprintf("%.3f",$val)',
        PrintConvInv => '$val',
    },
    0x0403 => {
        Name         => 'FocalLength',
        Format       => 'float',
        PrintConv    => 'sprintf("%.1f mm",$val)',
        PrintConvInv => '$val=~s/\s*mm$//;$val',
    },
    0x0410 => { Name => 'CameraModel', Format => 'string' },
    0x0412 => { Name => 'LensModel', Format => 'string' },
    0x0414 => {
        Name         => 'MaxApertureValue',
        Format       => 'float',
        ValueConv    => '2 ** ($val / 2)',
        ValueConvInv => '$val>0 ? 2*log($val)/log(2) : 0',
        PrintConv    => 'sprintf("%.1f",$val)',
        PrintConvInv => '$val',
    },
    0x0415 => {
        Name         => 'MinApertureValue',
        Format       => 'float',
        ValueConv    => '2 ** ($val / 2)',
        ValueConvInv => '$val>0 ? 2*log($val)/log(2) : 0',
        PrintConv    => 'sprintf("%.1f",$val)',
        PrintConvInv => '$val',
    },
    0x0455 => {
        Name   => 'Viewfinder',
        Format => 'string',
    },
);

%Image::ExifTool::PhaseOne::SensorCalibration = (
    PROCESS_PROC => \&ProcessPhaseOne,
    WRITE_PROC   => \&WritePhaseOne,
    CHECK_PROC   => \&Image::ExifTool::Exif::CheckExif,
    GROUPS       => { 0 => 'MakerNotes', 2 => 'Camera' },
    TAG_PREFIX   => 'SensorCalibration',
    WRITE_GROUP  => 'PhaseOne',
    VARS         => { ENTRY_SIZE => 12 },
    0x0400       => {
        Name => 'SensorDefects',
        Format => 'undef',
        Binary => 1,
    },
    0x0401 => {
        Name   => 'AllColorFlatField1',
        Format => 'undef',
        Flags  => [ 'Unknown', 'Binary' ],
    },
    0x0404 => {
        Name   => 'SensorCalibration_0x0404',
        Format => 'string',
        Flags  => [ 'Unknown', 'Hidden' ],
    },
    0x0405 => {
        Name   => 'SensorCalibration_0x0405',
        Format => 'string',
        Flags  => [ 'Unknown', 'Hidden' ],
    },
    0x0406 => {
        Name   => 'SensorCalibration_0x0406',
        Format => 'string',
        Flags  => [ 'Unknown', 'Hidden' ],
    },
    0x0407 => {
        Name     => 'SerialNumber',
        Format   => 'string',
        Writable => 1,
    },
    0x0408 => {
        Name   => 'SensorCalibration_0x0408',
        Format => 'float',
        Flags  => [ 'Unknown', 'Hidden' ],
    },
    0x040b => {
        Name   => 'RedBlueFlatField',
        Format => 'undef',
        Flags  => [ 'Unknown', 'Binary' ],
    },
    0x040f => {
        Name   => 'SensorCalibration_0x040f',
        Format => 'undef',
        Flags  => [ 'Unknown', 'Hidden' ],
    },
    0x0410 => {
        Name   => 'AllColorFlatField2',
        Format => 'undef',
        Flags  => [ 'Unknown', 'Binary' ],
    },
    0x0413 => {
        Name   => 'SensorCalibration_0x0413',
        Format => 'double',
        Flags  => [ 'Unknown', 'Hidden' ],
    },
    0x0414 => {
        Name      => 'SensorCalibration_0x0414',
        Format    => 'undef',
        Flags     => [ 'Unknown', 'Hidden' ],
        ValueConv => q{
            my $order = GetByteOrder();
            if (length $val >= 8 and SetByteOrder(substr($val,0,2))) {
                $val = ReadValue(\$val, 2, 'int16u', 1, length($val)-2) . ' ' .
                       ReadValue(\$val, 4, 'float', undef, length($val)-4);
                SetByteOrder($order);
            }
            return $val;
        },
    },
    0x0416 => {
        Name   => 'AllColorFlatField3',
        Format => 'undef',
        Flags  => [ 'Unknown', 'Binary' ],
    },
    0x0418 => {
        Name   => 'SensorCalibration_0x0418',
        Format => 'undef',
        Flags  => [ 'Unknown', 'Hidden' ],
    },
    0x0419 => {
        Name      => 'LinearizationCoefficients1',
        Format    => 'float',
        PrintConv =>
          'my @a=split " ",$val;join " ", map { sprintf("%.5g",$_) } @a',
    },
    0x041a => {
        Name      => 'LinearizationCoefficients2',
        Format    => 'float',
        PrintConv =>
          'my @a=split " ",$val;join " ", map { sprintf("%.5g",$_) } @a',
    },
    0x041c => {
        Name   => 'SensorCalibration_0x041c',
        Format => 'float',
        Flags  => [ 'Unknown', 'Hidden' ],
    },
    0x041e => {
        Name      => 'SensorCalibration_0x041e',
        Format    => 'undef',
        Flags     => [ 'Unknown', 'Hidden' ],
        ValueConv => q{
            my $order = GetByteOrder();
            if (length $val >= 8 and SetByteOrder(substr($val,0,2))) {
                $val = ReadValue(\$val, 2, 'int16u', 1, length($val)-2) . ' ' .
                       ReadValue(\$val, 4, 'float', undef, length($val)-4);
                SetByteOrder($order);
            }
            return $val;
        },
    },
);

sub HtmlDump($$$$$$%) {
    my ( $et, $tagTablePtr, $tagID, $value, $entry, $entryLen, %parms ) = @_;
    my ( $dirName, $index, $formatStr, $dataPos, $base, $size, $valuePtr ) =
      @parms{qw(DirName Index Format DataPos Base Size Start)};
    my $tagInfo = $et->GetTagInfo( $tagTablePtr, $tagID );
    my ( $tagName, $colName, $subdir );
    my $count = $parms{Count} || $size;
    $base = 0 unless defined $base;
    if ($tagInfo) {
        $tagName = $$tagInfo{Name};
        $subdir  = $$tagInfo{SubDirectory};
        if ( $$tagInfo{Format} ) {
            $formatStr = $$tagInfo{Format};
            $count     = $size / Image::ExifTool::FormatSize($formatStr);
        }
    }
    else {
        $tagName = sprintf( "Tag 0x%.4x", $tagID );
    }
    my $dname = sprintf( "${dirName}-%.2d", $index );
    my $fstr = "$formatStr\[$count]";
    my $tip  = sprintf( "Tag ID: 0x%.4x\n", $tagID )
      . "Format: $fstr\nSize: $size bytes\n";
    if ( $size > 4 ) {
        $tip .= sprintf( "Value offset: 0x%.4x\n",  $valuePtr - $base );
        $tip .= sprintf( "Actual offset: 0x%.4x\n", $valuePtr + $dataPos );
        $tip .= sprintf( "Offset base: 0x%.4x\n",   $dataPos + $base );
        $colName = "<span class=F>$tagName</span>";
    }
    else {
        $colName = $tagName;
    }
    unless ( ref $value ) {
        my $tval =
          length($value) > 32 ? substr( $value, 0, 28 ) . '[...]' : $value;
        $tval =~ tr/\x00-\x1f\x7f-\xff/./;
        $tip .= "Value: $tval";
    }
    $et->HDump( $entry + $dataPos, $entryLen, "$dname $colName", $tip, 1 );
    if ( $size > 4 ) {
        my $dumpPos = $valuePtr + $dataPos;
        $et->HDump( $dumpPos, $size, "$tagName value",
            'SAME', $subdir ? 0x04 : 0 );
    }
}

sub WritePhaseOne($$$) {
    my ( $et, $dirInfo, $tagTablePtr ) = @_;
    $et or return 1;

    my $newTags = $et->GetNewTagInfoHash($tagTablePtr);
    return undef
      unless %$newTags
      or $$et{DropTags}
      or $$et{EDIT_DIRS}{PhaseOne};

    my $dataPt   = $$dirInfo{DataPt};
    my $dataPos  = $$dirInfo{DataPos}  || 0;
    my $dirStart = $$dirInfo{DirStart} || 0;
    my $dirLen   = $$dirInfo{DirLen}   || $$dirInfo{DataLen} - $dirStart;
    my $dirName  = $$dirInfo{DirName};
    my $verbose  = $et->Options('Verbose');

    return undef if $dirLen < 12;
    unless ( $$tagTablePtr{VARS} and $$tagTablePtr{VARS}{ENTRY_SIZE} ) {
        $et->Warn("No ENTRY_SIZE for $$tagTablePtr{TABLE_NAME}");
        return undef;
    }
    my $entrySize = $$tagTablePtr{VARS}{ENTRY_SIZE};
    my $ifdType   = $$tagTablePtr{TAG_PREFIX} || 'PhaseOne';
    my $hdr       = substr( $$dataPt, $dirStart, 12 );
    if ( $entrySize == 16 ) {
        return undef unless $hdr =~ /^(IIII.waR|MMMMRaw.)/s;
    }
    elsif ( $hdr !~ /^(IIII\x01\0\0\0|MMMM\0\0\0\x01)/s ) {
        $et->Warn("Unrecognized $ifdType directory version");
        return undef;
    }
    SetByteOrder( substr( $hdr, 0, 2 ) );
    my $ifdStart = Get32u( \$hdr, 8 );
    return undef if $ifdStart + 8 > $dirLen;
    my $dirBuff = substr( $$dataPt, $dirStart + $ifdStart, 8 );
    my $numEntries = Get32u( \$dirBuff, 0 );
    my $ifdEnd     = $ifdStart + 8 + $entrySize * $numEntries;
    return undef if $numEntries < 2 or $numEntries > 300 or $ifdEnd > $dirLen;
    my $hdrBuff = $hdr;
    my $valBuff = '';
    my $fixup   = Image::ExifTool::Fixup->new;
    my $index;

    for ( $index = 0 ; $index < $numEntries ; ++$index ) {
        my $entry = $dirStart + $ifdStart + 8 + $entrySize * $index;
        my $tagID = Get32u( $dataPt, $entry );
        my $size  = Get32u( $dataPt, $entry + $entrySize - 8 );
        my ( $formatSize, $formatStr );
        if ( $entrySize == 16 ) {
            $formatSize = Get32u( $dataPt, $entry + 4 );
            $formatStr  = $formatName[$formatSize];
            unless ($formatStr) {
                $et->Warn( "Possibly invalid $ifdType IFD entry $index", 1 );
                delete $$newTags{$tagID};
            }
        }
        else {
            $formatSize = 1;
            $formatStr  = 'undef';
        }
        my $valuePtr = $entry + $entrySize - 4;
        if ( $size > 4 ) {
            if ( $size > 0x7fffffff ) {
                $et->Error( "Invalid size for $ifdType IFD entry $index", 1 );
                return undef;
            }
            $valuePtr = Get32u( $dataPt, $valuePtr );
            if ( $valuePtr + $size > $dirLen ) {
                $et->Error(
                    sprintf(
                        "Invalid offset 0x%.4x for $ifdType IFD entry $index",
                        $valuePtr ),
                    1
                );
                return undef;
            }
            $valuePtr += $dirStart;
        }
        my $value   = substr( $$dataPt, $valuePtr, $size );
        my $tagInfo = $$newTags{$tagID} || $$tagTablePtr{$tagID};
        $tagInfo = $et->GetTagInfo( $tagTablePtr, $tagID )
          if $tagInfo and ref($tagInfo) ne 'HASH';
        if ( $$newTags{$tagID} ) {
            $formatStr = $$tagInfo{Format} if $$tagInfo{Format};
            my $count  = int( $size / Image::ExifTool::FormatSize($formatStr) );
            my $val    = ReadValue( \$value, 0, $formatStr, $count, $size );
            my $nvHash = $et->GetNewValueHash($tagInfo);
            if ( $et->IsOverwriting( $nvHash, $val ) ) {
                my $newVal = $et->GetNewValue($nvHash);
                undef $count if $formatStr eq 'string' or $formatStr eq 'undef';
                my $newValue = WriteValue( $newVal, $formatStr, $count );
                if ( defined $newValue ) {
                    $value = $newValue;
                    $size  = length $newValue;
                    $et->VerboseValue( "- $dirName:$$tagInfo{Name}", $val );
                    $et->VerboseValue( "+ $dirName:$$tagInfo{Name}", $newVal );
                    ++$$et{CHANGED};
                }
            }
        }
        elsif ( $tagInfo and $$tagInfo{SubDirectory} ) {
            my $subTable   = GetTagTable( $$tagInfo{SubDirectory}{TagTable} );
            my %subdirInfo = (
                DirName => $$tagInfo{Name},
                DataPt  => \$value,
                DataLen => length $value,
            );
            my $newValue = $et->WriteDirectory( \%subdirInfo, $subTable );
            if ( defined $newValue and length($newValue) ) {
                $value = $newValue;
                $size  = length $newValue;
            }
        }
        elsif ( $$et{DropTags}
            and ( ( $tagInfo and $$tagInfo{Drop} ) or $size > 8192 ) )
        {
            Set32u( Get32u( \$dirBuff, 0 ) - 1, \$dirBuff, 0 );
            next;
        }
        $dirBuff .= substr( $$dataPt, $entry, $entrySize - 8 ) . Set32u($size);

        $value .= ( "\0" x ( 4 - ( $size & 0x03 ) ) )
          if $size & 0x03 or not $size;
        if ( $size <= 4 ) {
            $dirBuff .= $value;
        }
        elsif ( $tagInfo and $$tagInfo{PutFirst} ) {
            $dirBuff .= Set32u( length $hdrBuff );
            $hdrBuff .= $value;
        }
        else {
            $fixup->AddFixup( length $dirBuff );
            $dirBuff .= Set32u( length $valBuff );
            $valBuff .= $value;
        }
    }
    $$fixup{Shift} = length $hdrBuff;
    $fixup->ApplyFixup( \$dirBuff );
    Set32u( length($hdrBuff) + length($valBuff), \$hdrBuff, 8 );
    return $hdrBuff . $valBuff . $dirBuff;
}

sub ProcessPhaseOne($$$) {
    my ( $et, $dirInfo, $tagTablePtr ) = @_;
    my $dataPt   = $$dirInfo{DataPt};
    my $dataPos  = ( $$dirInfo{DataPos} || 0 ) + ( $$dirInfo{Base} || 0 );
    my $dirStart = $$dirInfo{DirStart} || 0;
    my $dirLen   = $$dirInfo{DirLen}   || $$dirInfo{DataLen} - $dirStart;
    my $binary   = $et->Options('Binary');
    my $verbose  = $et->Options('Verbose');
    my $hash     = $$et{ImageDataHash};
    my $htmlDump = $$et{HTML_DUMP};

    return 0 if $dirLen < 12;
    unless ( $$tagTablePtr{VARS} and $$tagTablePtr{VARS}{ENTRY_SIZE} ) {
        $et->Warn("No ENTRY_SIZE for $$tagTablePtr{TABLE_NAME}");
        return undef;
    }
    my $entrySize = $$tagTablePtr{VARS}{ENTRY_SIZE};
    my $ifdType   = $$tagTablePtr{TAG_PREFIX} || 'PhaseOne';

    my $hdr = substr( $$dataPt, $dirStart, 12 );
    if ( $entrySize == 16 ) {
        return 0 unless $hdr =~ /^(IIII.waR|MMMMRaw.)/s;
    }
    elsif ( $hdr !~ /^(IIII\x01\0\0\0|MMMM\0\0\0\x01)/s ) {
        $et->Warn("Unrecognized $ifdType directory version");
        return 0;
    }
    SetByteOrder( substr( $hdr, 0, 2 ) );
    my $ifdStart = Get32u( \$hdr, 8 );
    return 0 if $ifdStart + 8 > $dirLen;
    my $numEntries = Get32u( $dataPt, $dirStart + $ifdStart );
    my $ifdEnd     = $ifdStart + 8 + $entrySize * $numEntries;
    return 0 if $numEntries < 2 or $numEntries > 300 or $ifdEnd > $dirLen;
    $et->VerboseDir( $ifdType, $numEntries );

    if ($htmlDump) {
        $et->HDump( $dirStart + $dataPos,     8, "$ifdType header" );
        $et->HDump( $dirStart + $dataPos + 8, 4, "$ifdType IFD offset" );
        $et->HDump(
            $dirStart + $dataPos + $ifdStart,
            4,
            "$ifdType entries",
            "Entry count: $numEntries"
        );
        $et->HDump( $dirStart + $dataPos + $ifdStart + 4, 4, '[unused]' );
    }
    my $index;
    for ( $index = 0 ; $index < $numEntries ; ++$index ) {
        my $entry    = $dirStart + $ifdStart + 8 + $entrySize * $index;
        my $tagID    = Get32u( $dataPt, $entry );
        my $size     = Get32u( $dataPt, $entry + $entrySize - 8 );
        my $valuePtr = $entry + $entrySize - 4;
        my ( $formatSize, $formatStr, $value );
        if ( $entrySize == 16 ) {
            $formatSize = Get32u( $dataPt, $entry + 4 );
            $formatStr  = $formatName[$formatSize];
            unless ($formatStr) {
                $et->Warn( "Unrecognized $ifdType format size $formatSize", 1 );
                $formatSize = 1;
                $formatStr  = 'undef';
            }
        }
        elsif ( $size % 4 ) {
            $formatSize = 1;
            $formatStr  = 'undef';
        }
        else {
            $formatSize = 4;
            $formatStr  = 'int32s';
        }
        if ( $size > 4 ) {
            if ( $size > 0x7fffffff ) {
                $et->Warn("Invalid size for $ifdType IFD entry $index");
                return 0;
            }
            $valuePtr = Get32u( $dataPt, $valuePtr );
            if ( $valuePtr + $size > $dirLen ) {
                $et->Warn(
                    sprintf(
                        "Invalid offset 0x%.4x for $ifdType IFD entry $index",
                        $valuePtr )
                );
                return 0;
            }
            $valuePtr += $dirStart;
        }
        my $tagInfo = $et->GetTagInfo( $tagTablePtr, $tagID );
        if ($tagInfo) {
            $formatStr = $$tagInfo{Format} if $$tagInfo{Format};
        }
        else {
            next unless $verbose or $htmlDump;
        }
        my $count = int( $size / Image::ExifTool::FormatSize($formatStr) );
        if ( $count > 100000 and not $binary ) {
            $value = \ "Binary data $size bytes";
        }
        else {
            $value = ReadValue( $dataPt, $valuePtr, $formatStr, $count, $size );
            if ( $formatStr eq 'int32s' ) {
                my ($val) = split ' ', $value;
                if ( defined $val ) {
                    my $exp = ( $val & 0x7f800000 ) >> 23;
                    if ( $exp > 120 and $exp < 140 ) {
                        $formatStr = 'float';
                        $value =
                          ReadValue( $dataPt, $valuePtr, $formatStr, $count,
                            $size );
                    }
                }
            }
        }
        if ( $hash and $tagInfo and $$tagInfo{IsImageData} ) {
            my ( $pos, $len ) = ( $valuePtr, $size );
            while ($len) {
                my $n   = $len > 65536 ? 65536 : $len;
                my $tmp = substr( $$dataPt, $pos, $n );
                $hash->add($tmp);
                $len -= $n;
                $pos += $n;
            }
            $et->VPrint( 0,
"$$et{INDENT}(ImageDataHash: $size bytes of PhaseOne:$$tagInfo{Name})\n"
            );
        }
        my %parms = (
            DirName => $ifdType,
            Index   => $index,
            DataPt  => $dataPt,
            DataPos => $dataPos,
            Size    => $size,
            Start   => $valuePtr,
            Format  => $formatStr,
            Count   => $count
        );
        $htmlDump
          and HtmlDump( $et, $tagTablePtr, $tagID, $value, $entry, $entrySize,
            %parms, Base => $dirStart );
        $et->HandleTag( $tagTablePtr, $tagID, $value, %parms );
    }
    return 1;
}

1;

__END__

