
package Image::ExifTool::ITC;

use strict;
use vars            qw($VERSION);
use Image::ExifTool qw(:DataAccess :Utils);

$VERSION = '1.02';

sub ProcessITC($$);

%Image::ExifTool::ITC::Main = (
    NOTES => 'This information is found in iTunes Cover Flow data files.',
    itch  => { SubDirectory => { TagTable => 'Image::ExifTool::ITC::Header' } },
    item  => { SubDirectory => { TagTable => 'Image::ExifTool::ITC::Item' } },
    data  => {
        Name  => 'ImageData',
        Notes => 'embedded JPEG or PNG image, depending on ImageType',
    },
);

%Image::ExifTool::ITC::Header = (
    PROCESS_PROC => \&Image::ExifTool::ProcessBinaryData,
    GROUPS       => { 2 => 'Image' },
    0x10         => {
        Name      => 'DataType',
        Format    => 'undef[4]',
        PrintConv => { artw => 'Artwork' },
    },
);

%Image::ExifTool::ITC::Item = (
    PROCESS_PROC => \&Image::ExifTool::ProcessBinaryData,
    GROUPS       => { 2 => 'Image' },
    FORMAT       => 'int32u',
    FIRST_ENTRY  => 0,
    0            => {
        Name      => 'LibraryID',
        Format    => 'undef[8]',
        ValueConv => 'uc unpack "H*", $val',
    },
    2 => {
        Name      => 'TrackID',
        Format    => 'undef[8]',
        ValueConv => 'uc unpack "H*", $val',
    },
    4 => {
        Name      => 'DataLocation',
        Format    => 'undef[4]',
        PrintConv => {
            down => 'Downloaded Separately',
            locl => 'Local Music File',
        },
    },
    5 => {
        Name      => 'ImageType',
        Format    => 'undef[4]',
        ValueConv => {
            'PNGf'       => 'PNG',
            "\0\0\0\x0d" => 'JPEG',
        },
    },
    7 => 'ImageWidth',
    8 => 'ImageHeight',
);

sub ProcessITC($$) {
    my ( $et, $dirInfo ) = @_;
    my $raf    = $$dirInfo{RAF};
    my $rtnVal = 0;
    my ( $buff, $err, $pos, $tagTablePtr, %dirInfo );

    for ( ; ; ) {
        my $n = $raf->Read( $buff, 8 );
        unless ( $n == 8 ) {
            undef $err unless $n;
            last;
        }
        my ( $size, $tag ) = unpack( 'Na4', $buff );
        if ($rtnVal) {
            last unless $size >= 8 and $size < 0x80000000;
        }
        else {
            last unless $tag eq 'itch';
            last unless $size >= 0x1c and $size < 0x10000;
            $et->SetFileType();
            SetByteOrder('MM');
            $rtnVal = 1;
            $err    = 1;
        }
        if ( $tag eq 'itch' ) {
            $pos = $raf->Tell();
            $size -= 8;
            $raf->Read( $buff, $size ) == $size or last;
            %dirInfo = (
                DirName => 'ITC Header',
                DataPt  => \$buff,
                DataPos => $pos,
            );
            my $tagTablePtr = GetTagTable('Image::ExifTool::ITC::Header');
            $et->ProcessDirectory( \%dirInfo, $tagTablePtr );
        }
        elsif ( $tag eq 'item' ) {
            $size > 12                  or last;
            $raf->Read( $buff, 4 ) == 4 or last;
            my $len = unpack( 'N', $buff );
            $len >= 0xd0 and $len <= $size or last;
            $size -= $len;
            $len  -= 12;

            while ( $len >= 4 ) {
                $raf->Read( $buff, 4 ) == 4 or last;
                $len -= 4;
                last if $buff eq "\0\0\0\0";
            }
            last if $len < 4;
            $pos = $raf->Tell();
            $raf->Read( $buff, $len ) == $len or last;
            unless ( $len >= 0xb4 and substr( $buff, 0xb0, 4 ) eq 'data' ) {
                $et->Warn(
                    'Parsing error. Please submit this ITC file for testing');
                last;
            }
            %dirInfo = (
                DirName => 'ITC Item',
                DataPt  => \$buff,
                DataPos => $pos,
            );
            $tagTablePtr = GetTagTable('Image::ExifTool::ITC::Item');
            $et->ProcessDirectory( \%dirInfo, $tagTablePtr );
            $pos += $len;
            if ( $size > 0 ) {
                $tagTablePtr = GetTagTable('Image::ExifTool::ITC::Main');
                my $tagInfo = $et->GetTagInfo( $tagTablePtr, 'data' );
                my $image = $et->ExtractBinary( $pos, $size, $$tagInfo{Name} );
                $et->FoundTag( $tagInfo, \$image );
                $raf->Seek( $pos + $size, 0 ) or last;
            }
            elsif ( $size < 0 ) {
                last;
            }
        }
        else {
            $et->VPrint( 0, "Unknown $tag block ($size bytes)\n" );
            $raf->Seek( $size - 8, 1 ) or last;
        }
    }
    $err and $et->Warn('ITC file format error');
    return $rtnVal;
}

1;

__END__


