package Image::ExifTool::QuickTime;

use strict;

my %movMap = (
    QuickTime         => 'ItemList',
    ItemList          => 'Meta',
    Keys              => 'Movie',
    AudioKeys         => 'Track',
    VideoKeys         => 'Track',
    Meta              => 'UserData',
    XMP               => 'UserData',
    Microsoft         => 'UserData',
    UserData          => 'Movie',
    Movie             => 'MOV',
    GSpherical        => 'SphericalVideoXML',
    SphericalVideoXML => 'Track',
    Track             => 'Movie',
);
my %mp4Map = (
    QuickTime         => 'ItemList',
    ItemList          => 'Meta',
    Keys              => 'Movie',
    AudioKeys         => 'Track',
    VideoKeys         => 'Track',
    Meta              => 'UserData',
    UserData          => 'Movie',
    Microsoft         => 'UserData',
    Movie             => 'MOV',
    XMP               => 'MOV',
    GSpherical        => 'SphericalVideoXML',
    SphericalVideoXML => 'Track',
    Track             => 'Movie',
);
my %heicMap = (
    Meta                  => 'MOV',
    ItemInformation       => 'Meta',
    ItemPropertyContainer => 'Meta',
    XMP                   => 'ItemInformation',
    EXIF                  => 'ItemInformation',
    ICC_Profile           => 'ItemPropertyContainer',
    IFD0                  => 'EXIF',
    IFD1                  => 'IFD0',
    ExifIFD               => 'IFD0',
    GPS                   => 'IFD0',
    SubIFD                => 'IFD0',
    GlobParamIFD          => 'IFD0',
    PrintIM               => 'IFD0',
    InteropIFD            => 'ExifIFD',
    MakerNotes            => 'ExifIFD',
);
my %cr3Map = (
    Movie        => 'MOV',
    XMP          => 'MOV',
    'UUID-Canon' => 'Movie',
    ExifIFD      => 'UUID-Canon',
    IFD0         => 'UUID-Canon',
    GPS          => 'UUID-Canon',
    'UUID-Canon2' => 'MOV',
    CanonVRD      => 'UUID-Canon2',
);
my %dirMap = (
    MOV  => \%movMap,
    MP4  => \%mp4Map,
    CR3  => \%cr3Map,
    HEIC => \%heicMap,
);

my %qtFormat = (
    'undef' => 0x00,
    string  => 0x01,
    int8s   => 0x15,
    int16s  => 0x15,
    int32s  => 0x15,
    int64s  => 0x15,
    int8u   => 0x16,
    int16u  => 0x16,
    int32u  => 0x16,
    int64u  => 0x16,
    float   => 0x17,
    double  => 0x18,
);
my $undLang = 0x55c4;

my $maxReadLen = 100000000;

my %emptyMeta = (
    hdlr   => 'Handler',
    'keys' => 'Keys',
    lang   => 'Language',
    ctry   => 'Country',
    free   => 'Free',
);

my %fullKeysID = (
    com     => 1,
    xiaomi  => 1,
    samsung => 1,
);

my %ctboID = (
    "\xbe\x7a\xcf\xcb\x97\xa9\x42\xe8\x9c\x71\x99\x94\x91\xe3\xaf\xac" => 1,
    "\xea\xf4\x2b\x5e\x1c\x98\x4b\x88\xb9\xfb\xb7\xdc\x40\x6e\x4d\x16" => 2,

    "\x57\x66\xb8\x29\xbb\x6a\x47\xc5\xbc\xfb\x8b\x9f\x22\x60\xd0\x6d" => 5,
);

{
    my $itemList = \%Image::ExifTool::QuickTime::ItemList;
    my $userData = \%Image::ExifTool::QuickTime::UserData;
    my ( %pref, $tag );
    foreach $tag ( TagTableKeys($itemList) ) {
        my $tagInfo = $$itemList{$tag};
        if ( ref $tagInfo ne 'HASH' ) {
            next if ref $tagInfo;
            $tagInfo = $$itemList{$tag} = { Name => $tagInfo };
        }
        else {
            $$tagInfo{Writable} = 0
              if $$tagInfo{RawConv} and not $$tagInfo{RawConvInv};
            $$tagInfo{Avoid} and $$tagInfo{Preferred} = 0, next;
            next if defined $$tagInfo{Preferred} and not $$tagInfo{Preferred};
        }
        $pref{ $$tagInfo{Name} } = 1;
    }
    foreach $tag ( TagTableKeys($userData) ) {
        my $tagInfo = $$userData{$tag};
        if ( ref $tagInfo ne 'HASH' ) {
            next if ref $tagInfo;
            $tagInfo = $$userData{$tag} = { Name => $tagInfo };
        }
        else {
            $$tagInfo{Writable} = 0
              if $$tagInfo{RawConv} and not $$tagInfo{RawConvInv};
            $$tagInfo{Avoid} and $$tagInfo{Preferred} = 0, next;
            next if defined $$tagInfo{Preferred} or $pref{ $$tagInfo{Name} };
        }
        $$tagInfo{Preferred} = 1;
    }
}

sub PrintInvGPSCoordinates($) {
    my ( $val, $et ) = @_;
    my @v = split /, */, $val;
    if ( @v == 2 or @v == 3 ) {
        my $below = ( $v[2] and $v[2] =~ /below/i );
        $v[0] = Image::ExifTool::GPS::ToDegrees( $v[0], 1 );
        $v[1] = Image::ExifTool::GPS::ToDegrees( $v[1], 1 );
        $v[2] = Image::ExifTool::ToFloat( $v[2] ) * ( $below ? -1 : 1 )
          if @v == 3;
        return "@v";
    }
    return $val if $val =~ /^([-+]?\d+(\.\d*)?)\s+([-+]?\d+(\.\d*)?)$/;
    return $val if $val =~ /^([-+]\d+(\.\d*)?){2,3}(CRS.*)?\/?$/;
    return undef;
}

sub ConvInvISO6709($) {
    local $_;
    my $val = shift;
    my @a   = split ' ', $val;
    if ( @a == 2 or @a == 3 ) {
        my @fmt   = ( '%s%02d.%s%s', '%s%03d.%s%s', '%s%d.%s%s' );
        my @limit = ( 90, 180 );
        foreach (@a) {
            return undef unless Image::ExifTool::IsFloat($_);
            my $lim = shift @limit;
            warn( ( @limit ? 'Lat' : 'Long' ) . "itude out of range\n" )
              if $lim and abs($_) > $lim;
            $_ =~
s/^([-+]?)(\d+)\.?(\d*)/sprintf(shift(@fmt),$1||'+',$2,$3,length($3)<3 ? '0'x(3-length($3)) : '')/e;
        }
        return join '', @a, '/';
    }
    return $val if $val =~ /^([-+]\d+(\.\d*)?){2,3}(CRS.*)?\/?$/;
    return undef;
}

sub Handle_iloc($$$$) {
    my ( $et, $dirInfo, $dataPt, $outfile ) = @_;
    my ( $i, $j, $num, $pos, $id );

    my $off = $$dirInfo{ChunkOffset};
    my $len = length $$dataPt;
    return 0 if $len < 8;
    my $ver  = Get8u( $dataPt, 0 );
    my $siz  = Get16u( $dataPt, 4 );
    my $noff = ( $siz >> 12 );
    my $nlen = ( $siz >> 8 ) & 0x0f;
    my $nbas = ( $siz >> 4 ) & 0x0f;
    my $nind = $siz & 0x0f;
    my %ok   = ( 0 => 1, 4 => 1, 8 => 8 );
    return 0 unless $ok{$noff} and $ok{$nlen} and $ok{$nbas} and $ok{$nind};
    my $tag = $noff == 4 ? 'stco_iloc' : 'co64_iloc';

    if ( $ver < 2 ) {
        $num = Get16u( $dataPt, 6 );
        $pos = 8;
    }
    else {
        return 0 if $len < 10;
        $num = Get32u( $dataPt, 6 );
        $pos = 10;
    }
    for ( $i = 0 ; $i < $num ; ++$i ) {
        if ( $ver < 2 ) {
            return 0 if $pos + 2 > $len;
            $id = Get16u( $dataPt, $pos );
            $pos += 2;
        }
        else {
            return 0 if $pos + 4 > $len;
            $id = Get32u( $dataPt, $pos );
            $pos += 4;
        }
        my ( $constOff, @offBase, @offItem, $minOffset );
        if ( $ver == 1 or $ver == 2 ) {
            return 0 if $pos + 2 > $len;
            $constOff = Get16u( $dataPt, $pos ) & 0x0f;
            $pos += 2;
        }
        return 0 if $pos + 2 > $len;
        my $drefIdx = Get16u( $dataPt, $pos );
        if ($drefIdx) {
            if ( $$et{QtDataRef} and $$et{QtDataRef}[ $drefIdx - 1 ] ) {
                my $dref = $$et{QtDataRef}[ $drefIdx - 1 ];
                $constOff = 1 unless $$dref[1] == 1 and $$dref[0] ne 'rsrc';
            }
            else {
                $et->Error("No data reference for iloc entry $i");
                return 0;
            }
        }
        $pos += 2;
        my $base_offset = GetVarInt( $dataPt, $pos, $nbas );
        if ( $base_offset and not $constOff ) {
            my $tg = ( $nbas == 4 ? 'stco' : 'co64' ) . '_iloc';
            push @offBase,
              [ $tg, length($$outfile) + 8 + $pos - $nbas, $nbas, 0, $id ];
        }
        return 0 if $pos + 2 > $len;
        my $ext_num = Get16u( $dataPt, $pos );
        $pos += 2;
        my $listStartPos = $pos;
        for ( $j = 0 ; $j < $ext_num ; ++$j ) {
            $pos += $nind if $ver == 1 or $ver == 2;
            my $extent_offset = GetVarInt( $dataPt, $pos, $noff );
            return 0 unless defined $extent_offset;
            unless ($constOff) {
                push @offItem,
                  [ $tag, length($$outfile) + 8 + $pos - $noff, $noff, 0, $id ]
                  if $noff;
                $minOffset = $extent_offset
                  if not defined $minOffset or $minOffset > $extent_offset;
            }
            return 0 if $pos + $nlen > length $$dataPt;
            $pos += $nlen;
        }
        if ( defined $minOffset and $minOffset > $base_offset ) {
            $$_[3] = $base_offset foreach @offItem;
            push @$off, @offItem;
        }
        else {
            $$_[3] = $minOffset foreach @offBase;
            push @$off, @offBase;
        }
    }
    return 1;
}

sub GetLangInfo($$) {
    my ( $tagInfo, $langCode ) = @_;
    return undef unless $langCode;
    my $writable = $$tagInfo{Writable};
    $writable = $$tagInfo{Table}{WRITABLE} unless defined $writable;
    return undef unless $writable;
    $langCode =~ tr/_/-/;
    my $langInfo = Image::ExifTool::GetLangInfo( $tagInfo, $langCode );
    return $langInfo;
}

sub CheckQTValue($$$) {
    my ( $et, $tagInfo, $valPtr ) = @_;
    my $format =
      $$tagInfo{Format} || $$tagInfo{Writable} || $$tagInfo{Table}{FORMAT};
    return undef unless $format;
    return Image::ExifTool::CheckValue( $valPtr, $format, $$tagInfo{Count} );
}

sub FormatQTValue($$;$$) {
    my ( $et, $valPt, $tagInfo, $format ) = @_;
    my $writable = $$tagInfo{Writable};
    my $count    = $$tagInfo{Count};
    my $flags;
    $format or $format = $$tagInfo{Format};
    if ( $format and $format ne 'string'
        or not $format and $writable and $writable ne 'string' )
    {
        $$valPt = WriteValue( $$valPt, $format || $writable, $count );
        if ( $writable and $qtFormat{$writable} ) {
            $flags = $qtFormat{$writable};
        }
        else {
            $flags = $qtFormat{ $format || 0 } || 0;
        }
    }
    elsif ( $$valPt =~ /^\xff\xd8\xff/ ) {
        $flags = 0x0d;
    }
    elsif ( $$valPt =~ /^(\x89P|\x8aM|\x8bJ)NG\r\n\x1a\n/ ) {
        $flags = 0x0e;
    }
    elsif ( $$valPt =~ /^BM.{15}\0/s ) {
        $flags = 0x1b;
    }
    else {
        $flags  = 0x01;
        $$valPt = $et->Encode( $$valPt, 'UTF8' );
    }
    defined $$valPt or $et->Warn("Error converting value for $$tagInfo{Name}");
    return $flags;
}

sub SetVarInt($$) {
    my ( $val, $n ) = @_;
    if ( $n == 4 ) {
        return Set32u($val);
    }
    elsif ( $n == 8 ) {
        return Set64u($val);
    }
    return '';
}

sub WriteNextbase($$$) {
    my ( $et, $dirInfo, $tagTablePtr ) = @_;
    $et or return 1;
    $$et{DEL_GROUP}{Nextbase} and ++$$et{CHANGED}, return '';
    return ${ $$dirInfo{DataPt} };
}

sub WriteKeys($$$) {
    my ( $et, $dirInfo, $tagTablePtr ) = @_;
    $et or return 1;
    my $dataPt  = $$dirInfo{DataPt};
    my $dirLen  = length $$dataPt;
    my $outfile = $$dirInfo{OutFile};
    my ( $tag, %done, %remap, %info, %add, $i );

    my $keysGrp =
      $avType{ $$et{MediaType} } ? "$avType{$$et{MediaType}}Keys" : 'Keys';
    $dirLen < 8
      and $et->Warn('Short Keys box'), $dirLen = 8, $$dataPt = "\0" x 8;
    if ( $$et{DEL_GROUP}{$keysGrp} ) {
        $dirLen = 8;

        my $n = Get32u( $dataPt, 4 );
        for ( $i = 1 ; $i <= $n ; ++$i ) { $remap{$i} = 0; }
        $et->VPrint( 0,
                "  [deleting $n $keysGrp entr"
              . ( $n == 1 ? 'y' : 'ies' )
              . "]\n" );
        ++$$et{CHANGED};
    }
    my $pos     = 8;
    my $newTags = $et->GetNewTagInfoHash($tagTablePtr);
    my $newData = substr( $$dataPt, 0, $pos );

    my $newIndex = 1;
    my $index    = 1;
    while ( $pos < $dirLen - 4 ) {
        my $len = unpack( "x${pos}N", $$dataPt );
        last if $len < 8 or $pos + $len > $dirLen;
        my $ns = substr( $$dataPt, $pos + 4, 4 );
        $tag = substr( $$dataPt, $pos + 8, $len - 8 );
        $tag =~ s/\0.*//s;
        $tag =~ s/^com\.apple\.quicktime\.// if $ns eq 'mdta';
        $tag = "Tag_$ns" unless $tag;
        $done{$tag} = 1;
        my $tagInfo = $et->GetTagInfo( $tagTablePtr, $tag );

        if ($tagInfo) {
            $info{$index} = $tagInfo;
            if ( $$newTags{$tag} ) {
                my $nvHash = $et->GetNewValueHash($tagInfo);
                if (    $nvHash
                    and $et->IsOverwriting($nvHash) > 0
                    and not defined $et->GetNewValue($nvHash) )
                {
                    my ( $t, $dontDelete );
                    foreach $t ( keys %$newTags ) {
                        next
                          unless $$newTags{$t}{SrcTagInfo}
                          and $$newTags{$t}{SrcTagInfo} eq $tagInfo;
                        my $nv = $et->GetNewValueHash( $$newTags{$t} );
                        next
                          unless $et->IsOverwriting($nv)
                          and defined $et->GetNewValue($nv);
                        $dontDelete = 1;
                        last;
                    }
                    unless ($dontDelete) {
                        $et->VPrint( 1,
"$$et{INDENT}\[deleting $keysGrp entry $index '${tag}']\n"
                        );
                        $pos += $len;
                        $remap{ $index++ } = 0;
                        ++$$et{CHANGED};
                        next;
                    }
                }
            }
        }
        $newData .= substr( $$dataPt, $pos, $len );
        $remap{ $index++ } = $newIndex++;
        $pos += $len;
    }
    foreach $tag ( sort keys %$newTags ) {
        my $tagInfo = $$newTags{$tag};
        my $id;
        if ( $$tagInfo{LangCode} and $$tagInfo{SrcTagInfo} ) {
            $id = $$tagInfo{SrcTagInfo}{TagID};
        }
        else {
            $id = $tag;
        }
        next if $done{$id};
        my $nvHash = $et->GetNewValueHash($tagInfo);
        next
          unless $$nvHash{IsCreating}
          and $et->IsOverwriting($nvHash)
          and defined $et->GetNewValue($nvHash);
        my $val = $id;
        unless ( $val =~ /^(.*?)\./ and $fullKeysID{$1} ) {
            $val = "com.apple.quicktime.$val";
        }
        $newData .= Set32u( 8 + length($val) ) . 'mdta' . $val;
        $et->VPrint( 1,
            "$$et{INDENT}\[adding $keysGrp entry $newIndex '${id}']\n" );
        $add{ $newIndex++ } = $tagInfo;
        ++$$et{CHANGED};
    }
    my $num = $newIndex - 1;
    if ($num) {
        Set32u( $num, \$newData, 4 );
    }
    else {
        $newData = '';
    }
    $$et{$keysGrp} =
      { Remap => \%remap, Info => \%info, Add => \%add, Num => $num };

    return $newData;
}

sub WriteItemInfo($$$) {
    my ( $et, $dirInfo, $outfile ) = @_;
    my $boxPos = $$dirInfo{BoxPos};
    my $raf    = $$et{RAF};
    my $items  = $$et{ItemInfo};
    my ( %did, @mdatEdit, $name, $tmap );

    return () unless $items and $raf;

    my $primary = $$et{PrimaryItem};
    my $curPos  = $raf->Tell();
    my $lastID  = 0;
    my $id;
    foreach $id ( sort { $a <=> $b } keys %$items ) {
        $lastID  = $id;
        $primary = $id unless defined $primary;
        my $item = $$items{$id};
        next unless $$item{RefersTo} and $$item{RefersTo}{$primary};
        my $type = $$item{ContentType} || $$item{Type} || next;
        $tmap = $id if $type eq 'tmap';

        $name = { Exif => 'EXIF', 'application/rdf+xml' => 'XMP' }->{$type};
        next unless $name;
        next unless $$et{EDIT_DIRS}{$name};
        $did{$name} = 1;
        my ( $warn, $extent, $buff, @edit );
        $warn = 'Missing iloc box' unless $$boxPos{iloc};
        $warn = "No Extents for $type item"
          unless $$item{Extents} and @{ $$item{Extents} };

        if ( $$item{ContentEncoding} ) {
            if ( $$item{ContentEncoding} ne 'deflate' ) {
                $warn =
"Can't currently decode $$item{ContentEncoding} encoded $type metadata";
            }
            elsif ( not eval { require Compress::Zlib } ) {
                $warn =
                  "Install Compress::Zlib to decode deflated $type metadata";
            }
        }
        $warn = "Can't currently decode protected $type metadata"
          if $$item{ProtectionIndex};
        $warn =
"Can't currently extract $type with construction method $$item{ConstructionMethod}"
          if $$item{ConstructionMethod};
        $warn = "$type metadata is not in this file"
          if $$item{DataReferenceIndex};
        $warn and $et->Warn($warn), next;
        my $base = $$item{BaseOffset} || 0;
        my $val  = '';
        foreach $extent ( @{ $$item{Extents} } ) {
            $val .= $buff if defined $buff;
            my $pos = $$extent[1] + $base;
            if ( $$extent[2] ) {
                $raf->Seek( $pos, 0 )            or last;
                $raf->Read( $buff, $$extent[2] ) or last;
            }
            else {
                $buff = '';
            }
            push @edit, [ $pos, $pos + $$extent[2] ];
        }
        next unless defined $buff;
        $buff = $val . $buff if length $val;
        my $comp = $et->Options('Compress');
        if ( defined $comp and ( $comp xor $$item{ContentEncoding} ) ) {
            $et->Warn(
"Can't currently change compression when rewriting $name in HEIC",
                1
            );
        }
        my $wasDeflated;
        if ( $$item{ContentEncoding} ) {
            my ( $v2, $stat );
            my $inflate = Compress::Zlib::inflateInit();
            $inflate and ( $v2, $stat ) = $inflate->inflate($buff);
            $et->VPrint( 0, "  (Inflating stored $name metadata)\n" );
            if ( $inflate and $stat == Compress::Zlib::Z_STREAM_END() ) {
                $buff        = $v2;
                $wasDeflated = 1;
            }
            else {
                $et->Warn("Error inflating $name metadata");
                next;
            }
        }
        my ( $hdr, $subTable, $proc );
        my $strt = 0;
        if ( $name eq 'EXIF' ) {
            if ( not length $buff ) {
                $hdr = "\0\0\0\x06Exif\0\0";
            }
            elsif ( $buff =~ /^(MM\0\x2a|II\x2a\0)/ ) {
                $et->Warn('Missing Exif header');
                $hdr = '';
            }
            elsif ( length($buff) >= 4
                and length($buff) >= 4 + unpack( 'N', $buff ) )
            {
                $hdr  = substr( $buff, 0, 4 + unpack( 'N', $buff ) );
                $strt = length $hdr;
            }
            else {
                $et->Warn('Invalid Exif header');
                next;
            }
            $subTable = GetTagTable('Image::ExifTool::Exif::Main');
            $proc     = \&Image::ExifTool::WriteTIFF;
        }
        else {
            $hdr      = '';
            $subTable = GetTagTable('Image::ExifTool::XMP::Main');
        }
        my %dirInfo = (
            DataPt   => \$buff,
            DataLen  => length $buff,
            DirStart => $strt,
            DirLen   => length($buff) - $strt,
        );
        my $changed = $$et{CHANGED};
        my $newVal  = $et->WriteDirectory( \%dirInfo, $subTable, $proc );
        if (
                defined $newVal
            and $changed ne $$et{CHANGED}
            and
            ( $dirInfo{DirLen} or length $newVal )
          )
        {
            $newVal = $hdr . $newVal if length $hdr and length $newVal;
            if ($wasDeflated) {
                my $deflate = Compress::Zlib::deflateInit();
                if ($deflate) {
                    $et->VPrint( 0, "  (Re-deflating new $name metadata)\n" );
                    $buff = $deflate->deflate($newVal);
                    if ( defined $buff ) {
                        $buff .= $deflate->flush();
                        $newVal = $buff;
                    }
                }
            }
            $edit[0][2] = \$newVal;
            $edit[0][3] = $id;
            push @mdatEdit, @edit;
            my $n = length $newVal;
            foreach $extent ( @{ $$item{Extents} } ) {
                my ( $nlen, $lenPt ) = @$extent[ 3, 4 ];
                if ( $nlen == 8 ) {
                    Set64u( $n, $outfile, $$boxPos{iloc}[0] + 8 + $lenPt );
                }
                elsif ( $n <= 0xffffffff ) {
                    Set32u( $n, $outfile, $$boxPos{iloc}[0] + 8 + $lenPt );
                }
                else {
                    $et->Error("Can't yet promote iloc length to 64 bits");
                    return ();
                }
                $n = 0;
            }
            if ( @{ $$item{Extents} } != 1 ) {
                $et->Error(
"Can't yet handle $name in multiple parts. Please submit sample for testing"
                );
            }
        }
        $$et{CHANGED} = $changed;
    }
    $raf->Seek( $curPos, 0 );

    my ( $countNew, %add, %usedID );
    foreach $name ( 'EXIF', 'XMP' ) {
        next if $did{$name} or not $$et{ADD_DIRS}{$name};
        my @missing;
        $$boxPos{$_} or push @missing, $_ foreach qw(iinf iloc);
        if (@missing) {
            my $str =
              @missing > 1
              ? join( ' and ', @missing ) . ' boxes'
              : "@missing box";
            $et->Warn("Can't create $name. Missing expected $str");
            last;
        }
        unless ( defined $$et{PrimaryItem} ) {
            unless ( defined $primary ) {
                $et->Warn("Can't create $name. No items to reference");
                last;
            }
            if ( $primary < 0x10000 ) {
                $add{hdlr} = pack( 'Na4Nn', 14, 'pitm', 0, $primary );
            }
            else {
                $add{hdlr} =
                  pack( 'Na4CCCCN', 16, 'pitm', 1, 0, 0, 0, $primary );
            }
            $et->Warn( "Added missing PrimaryItemReference (for item $primary)",
                1 );
        }
        my $buff = '';
        my ( $hdr, $subTable, $proc );
        if ( $name eq 'EXIF' ) {
            $hdr      = "\0\0\0\x06Exif\0\0";
            $subTable = GetTagTable('Image::ExifTool::Exif::Main');
            $proc     = \&Image::ExifTool::WriteTIFF;
        }
        else {
            $hdr      = '';
            $subTable = GetTagTable('Image::ExifTool::XMP::Main');
        }
        my %dirInfo = (
            DataPt   => \$buff,
            DataLen  => 0,
            DirStart => 0,
            DirLen   => 0,
        );
        my $changed = $$et{CHANGED};
        my $newVal  = $et->WriteDirectory( \%dirInfo, $subTable, $proc );
        if ( defined $newVal and $changed ne $$et{CHANGED} ) {
            my $irefVer;
            if ( $$boxPos{iref} ) {
                $irefVer = Get8u( $outfile, $$boxPos{iref}[0] + 8 );
            }
            else {
                $irefVer = ( $primary < 0x10000 ? 0 : 1 );
                $$boxPos{iref} =
                  [ $$boxPos{iinf}[0] + $$boxPos{iinf}[1], 0, $irefVer ];
            }
            $newVal = $hdr . $newVal if length $hdr;
            $add{iinf} = $add{iref} = $add{iloc} = '' unless defined $add{iinf};
            my ( $type, $mime );
            my $enc = '';
            if ( $name eq 'XMP' ) {
                $type = "mime\0";
                $mime = "application/rdf+xml\0";
                if ( $et->Options('Compress') and length $newVal ) {
                    if ( not eval { require Compress::Zlib } ) {
                        $et->Warn(
'Install Compress::Zlib to write compressed metadata'
                        );
                    }
                    else {
                        my $deflate = Compress::Zlib::deflateInit();
                        if ($deflate) {
                            $et->VPrint( 0,
                                "  (Deflating new $name metadata)\n" );
                            my $buff = $deflate->deflate($newVal);
                            if ( defined $buff ) {
                                $newVal = $buff . $deflate->flush();
                                $enc    = "deflate\0";
                            }
                        }
                    }
                }
            }
            else {
                $type = "Exif\0";
                $mime = '';
            }
            my $id = ++$lastID;

            my $n = length($type) + length($mime) + length($enc) + 16;
            if ( $id < 0x10000 ) {
                $add{iinf} .=
                    pack( 'Na4CCCCnn', $n, 'infe', 2, 0, 0, 1, $id, 0 )
                  . $type
                  . $mime
                  . $enc;
            }
            else {
                $n += 2;
                $add{iinf} .=
                    pack( 'Na4CCCCNn', $n, 'infe', 3, 0, 0, 1, $id, 0 )
                  . $type
                  . $mime
                  . $enc;
            }
            if ($irefVer) {
                my ( $fmt, $siz, $num ) =
                  defined $tmap ? ( 'N', 22, 2 ) : ( '', 18, 1 );
                $add{iref} .= pack( 'Na4NnN' . $fmt,
                    $siz, 'cdsc', $id, $num, $primary, $tmap );
            }
            else {
                my ( $fmt, $siz, $num ) =
                  defined $tmap ? ( 'n', 16, 2 ) : ( '', 14, 1 );
                $add{iref} .= pack( 'Na4nnn' . $fmt,
                    $siz, 'cdsc', $id, $num, $primary, $tmap );
            }
            my $ilocVer = Get8u( $outfile, $$boxPos{iloc}[0] + 8 );
            my $siz     = Get16u( $outfile, $$boxPos{iloc}[0] + 12 );
            my $noff    = ( $siz >> 12 );
            my $nlen    = ( $siz >> 8 ) & 0x0f;
            my $nbas    = ( $siz >> 4 ) & 0x0f;
            my $nind    = $siz & 0x0f;
            my ( $pbas, $poff );

            if ( $ilocVer == 0 ) {
                $pbas = length( $add{iloc} ) + 4;
                $poff = $pbas + $nbas + 2;
                $add{iloc} .=
                    pack( 'nn', $id, 0 )
                  . SetVarInt( 0, $nbas )
                  . Set16u(1)
                  . SetVarInt( 0,               $noff )
                  . SetVarInt( length($newVal), $nlen );
            }
            elsif ( $ilocVer == 1 ) {
                $pbas = length( $add{iloc} ) + 6;
                $poff = $pbas + $nbas + 2 + $nind;
                $add{iloc} .=
                    pack( 'nnn', $id, 0, 0 )
                  . SetVarInt( 0, $nbas )
                  . Set16u(1)
                  . SetVarInt( 0,               $nind )
                  . SetVarInt( 0,               $noff )
                  . SetVarInt( length($newVal), $nlen );
            }
            elsif ( $ilocVer == 2 ) {
                $pbas = length( $add{iloc} ) + 8;
                $poff = $pbas + $nbas + 2 + $nind;
                $add{iloc} .=
                    pack( 'Nnn', $id, 0, 0 )
                  . SetVarInt( 0, $nbas )
                  . Set16u(1)
                  . SetVarInt( 0,               $nind )
                  . SetVarInt( 0,               $noff )
                  . SetVarInt( length($newVal), $nlen );
            }
            else {
                $et->Warn(
                    "Can't create $name. Unsupported iloc version $ilocVer");
                last;
            }
            my $off = $$dirInfo{ChunkOffset}
              or $et->Warn('Internal error. Missing ChunkOffset'), last;
            my $newOff;
            if ( $noff == 4 ) {
                $newOff = [
                    'stco_iloc', $$boxPos{iloc}[0] + $$boxPos{iloc}[1] + $poff,
                    $noff, 0, $id
                ];
            }
            elsif ( $noff == 8 ) {
                $newOff = [
                    'co64_iloc', $$boxPos{iloc}[0] + $$boxPos{iloc}[1] + $poff,
                    $noff, 0, $id
                ];
            }
            elsif ( $noff == 0 ) {
                if ( $nbas == 4 ) {
                    $newOff = [
                        'stco_iloc',
                        $$boxPos{iloc}[0] + $$boxPos{iloc}[1] + $pbas,
                        $nbas, 0, $id
                    ];
                }
                elsif ( $nbas == 8 ) {
                    $newOff = [
                        'co64_iloc',
                        $$boxPos{iloc}[0] + $$boxPos{iloc}[1] + $pbas,
                        $nbas, 0, $id
                    ];
                }
                else {
                    $et->Warn(
                        "Can't create $name. Invalid iloc offset+base size");
                    last;
                }
            }
            else {
                $et->Warn("Can't create $name. Invalid iloc offset size");
                last;
            }
            push @$off,     $newOff;
            push @mdatEdit, [ 0, 0, \$newVal, $id ];
            $usedID{$id}  = 1;
            $countNew     = ( $countNew || 0 ) + 1;
            $$et{CHANGED} = $changed;
        }
    }
    if ($countNew) {
        my $added = 0;
        my $tag;
        foreach $tag ( sort { $$boxPos{$a}[0] <=> $$boxPos{$b}[0] }
            keys %$boxPos )
        {
            $$boxPos{$tag}[0] += $added;
            next unless $add{$tag};
            my $pos = $$boxPos{$tag}[0];
            unless ( $$boxPos{$tag}[1] ) {
                $tag eq 'iref'
                  or $et->Error('Internal error adding iref box'), last;
                $add{$tag} =
                    Set32u( 12 + length $add{$tag} )
                  . $tag
                  . Set8u( $$boxPos{$tag}[2] )
                  . "\0\0\0"
                  . $add{$tag};
            }
            elsif ( $tag ne 'hdlr' ) {
                my $n = Get32u( $outfile, $pos ) + length( $add{$tag} );
                Set32u( $n, $outfile, $pos );
            }
            if ( $tag eq 'iinf' ) {
                my $iinfVer = Get8u( $outfile, $pos + 8 );
                if ( $iinfVer == 0 ) {
                    my $n = Get16u( $outfile, $pos + 12 ) + $countNew;
                    if ( $n > 0xffff ) {
                        $et->Error(
                            "Can't currently handle rollover to long item count"
                        );
                        return undef;
                    }
                    Set16u( $n, $outfile, $pos + 12 );
                }
                else {
                    my $n = Get32u( $outfile, $pos + 12 ) + $countNew;
                    Set32u( $n, $outfile, $pos + 12 );
                }
            }
            elsif ( $tag eq 'iref' ) {
            }
            elsif ( $tag eq 'iloc' ) {
                my $ilocVer = Get8u( $outfile, $pos + 8 );
                if ( $ilocVer < 2 ) {
                    my $n = Get16u( $outfile, $pos + 14 ) + $countNew;
                    Set16u( $n, $outfile, $pos + 14 );
                    if ( $n > 0xffff ) {
                        $et->Error(
                            "Can't currently handle rollover to long item count"
                        );
                        return undef;
                    }
                }
                else {
                    my $n = Get32u( $outfile, $pos + 14 ) + $countNew;
                    Set32u( $n, $outfile, $pos + 14 );
                }
                if ($added) {
                    $$_[1] += $added foreach @{ $$dirInfo{ChunkOffset} };
                }
            }
            elsif ( $tag ne 'hdlr' ) {
                next;
            }
            substr( $$outfile, $pos + $$boxPos{$tag}[1], 0 ) = $add{$tag};
            $$boxPos{$tag}[1] += length $add{$tag};
            $added += length $add{$tag};
        }
    }
    delete $$et{ItemInfo};
    return @mdatEdit ? \@mdatEdit : undef;
}

sub WriteQuickTime($$$) {
    local $_;
    my ( $et, $dirInfo, $tagTablePtr ) = @_;
    $et or return 1;
    my (
        $mdat,    @mdat, @mdatEdit, $edit, $track,
        $outBuff, $co,   $term,     $delCount
    );
    my (
        %langTags, $canCreate, $delGrp, %boxPos,
        %didDir,   $writeLast, $err,    $atomCount
    );
    my (
        $tag,  $lastTag, $lastPos,  $errStr, $trailer,
        $buf2, $keysGrp, $keysPath, $itemIndex
    );
    my $outfile    = $$dirInfo{OutFile} || return 0;
    my $raf        = $$dirInfo{RAF};
    my $dataPt     = $$dirInfo{DataPt};
    my $dirName    = $$dirInfo{DirName};
    my $dirStart   = $$dirInfo{DirStart} || 0;
    my $parent     = $$dirInfo{Parent};
    my $addDirs    = $$et{ADD_DIRS};
    my $didTag     = $$et{DidTag};
    my $newTags    = {};
    my $createKeys = 0;
    my ( $rtnVal, $rtnErr ) = $dataPt ? ( undef, undef ) : ( 1, 0 );

    if ($raf) {
        $trailer = IdentifyTrailers($raf);
        $trailer and not ref $trailer and $et->Error($trailer), return 1;
    }
    if ($dataPt) {
        $raf = File::RandomAccess->new($dataPt);
    }
    else {
        return 0 unless $raf;
    }
    $outBuff = '';
    $outfile = \$outBuff;

    $raf->Seek( $dirStart, 1 ) if $dirStart;

    if ( $avType{ $$et{MediaType} } ) {
        ( $keysGrp, $keysPath ) =
          ( "$avType{$$et{MediaType}}Keys", 'MOV-Movie-Track' );
    }
    else {
        ( $keysGrp, $keysPath ) = ( 'Keys', 'MOV-Movie' );
    }
    my $curPath = join '-', @{ $$et{PATH} };
    my ( $dir, $writePath ) = ( $dirName, $dirName );
    $writePath = "$dir-$writePath" while defined( $dir = $$et{DirMap}{$dir} );
    if ( ( $$addDirs{Keys} and $curPath =~ /^MOV-Movie(-Meta)?$/ ) ) {
        $createKeys = 1;
    }
    elsif ( ( $$addDirs{AudioKeys} or $$addDirs{VideoKeys} )
        and $curPath =~ /^MOV-Movie-Track(-Meta)?$/ )
    {
        $createKeys = -1;
    }
    elsif (
        ( $curPath eq 'MOV-Movie-Meta-ItemList' )
        or (    $curPath eq 'MOV-Movie-Track-Meta-ItemList'
            and $avType{ $$et{MediaType} } )
      )
    {
        $createKeys = 2;
        my $keys = $$et{$keysGrp};
        if ($keys) {
            my ( $index, %keysInfo );
            foreach $index ( keys %{ $$keys{Info} } ) {
                $keysInfo{ $$keys{Info}{$index} } = $index
                  if $$keys{Remap}{$index};
            }
            my $keysTable = GetTagTable("Image::ExifTool::QuickTime::$keysGrp");
            my $newKeysTags = $et->GetNewTagInfoHash($keysTable);
            foreach ( keys %$newKeysTags ) {
                my $tagInfo = $$newKeysTags{$_};
                $index =
                  $keysInfo{$tagInfo}
                  || (  $$tagInfo{SrcTagInfo}
                    and $keysInfo{ $$tagInfo{SrcTagInfo} } );
                next unless $index;
                my $id = Set32u($index);
                if ( $$tagInfo{LangCode} ) {
                    $langTags{$id} = {} unless $langTags{$id};
                    $langTags{$id}{$_} = $tagInfo;
                    $id .= '-' . $$tagInfo{LangCode};
                }
                $$newTags{$id} = $tagInfo;
            }
        }
    }
    else {
        $newTags = $et->GetNewTagInfoHash($tagTablePtr);
        foreach ( keys %$newTags ) {
            next unless $$newTags{$_}{LangCode} and $$newTags{$_}{SrcTagInfo};
            my $id = $$newTags{$_}{SrcTagInfo}{TagID};
            $langTags{$id} = {} unless $langTags{$id};
            $langTags{$id}{$_} = $$newTags{$_};
        }
    }
    if ( $curPath eq $writePath or $createKeys ) {
        $canCreate = 1;
        $delGrp = $$et{DEL_GROUP}{ $createKeys ? $keysGrp : $dirName };
    }
    $atomCount = $$tagTablePtr{VARS}{ATOM_COUNT} if $$tagTablePtr{VARS};

    $tag       = $lastTag = '';
    $itemIndex = 0 if $dirName eq 'ItemPropertyContainer';

    if ( $dirName eq 'ItemProperties' ) {
        my $pos = $raf->Tell();
        for ( ; ; ) {
            $raf->Read( $buf2, 8 ) == 8 or last;
            my $size = Get32u( \$buf2, 0 ) - 8;
            $tag = substr( $buf2, 4, 4 );
            last if $size < 0;
            $tag eq 'ipma' or $raf->Seek( $size, 1 ), next;
            ParseItemPropAssoc( $buf2, $et )
              if $raf->Read( $buf2, $size ) == $size;
            last;
        }
        $raf->Seek($pos);
    }
    for ( ; ; ) {
        ++$itemIndex if defined $itemIndex;
        $lastPos = $raf->Tell();
        if ( $trailer and $lastPos >= $$trailer[1] ) {
            $errStr = "Corrupted $$trailer[0] trailer"
              if $lastPos != $$trailer[1];
            last;
        }
        $lastTag = $tag if $$tagTablePtr{$tag};
        if ( defined $atomCount and --$atomCount < 0 and $dataPt ) {
            Write( $outfile, substr( $$dataPt, $raf->Tell() ) )
              or $rtnVal = $rtnErr, $err = 1;
            last;
        }
        my ( $hdr, $buff, $keysIndex );
        my $n = $raf->Read( $hdr, 8 );
        unless ( $n == 8 ) {
            if ( $n == 4 and $hdr eq "\0\0\0\0" ) {
                $term = $hdr;
            }
            elsif ( $n != 0 ) {
                $et->Error( "Unknown $n bytes at end of file", 1 )
                  if $n > 3
                  or $hdr ne "\0" x $n;
            }
            last;
        }
        my $size = Get32u( \$hdr, 0 ) - 8;
        $tag = substr( $hdr, 4, 4 );
        if ( $size == -7 ) {
            $raf->Read( $buff, 8 ) == 8
              or $errStr = 'Truncated extended atom', last;
            $hdr .= $buff;
            my ( $hi, $lo ) = unpack( 'NN', $buff );
            if ( $hi or $lo > 0x7fffffff ) {
                if ( $hi > 0x7fffffff ) {
                    $errStr = 'Invalid atom size';
                    last;
                }
                elsif ( not $et->Options('LargeFileSupport') ) {
                    $et->Error(
'End of processing at large atom (LargeFileSupport not enabled)'
                    );
                    last;
                }
                elsif ( $et->Options('LargeFileSupport') eq '2' ) {
                    $et->Warn('Processing large atom (LargeFileSupport is 2)');
                }
            }
            $size = $hi * 4294967296 + $lo - 16;
            $size < 0 and $errStr = 'Invalid extended atom size', last;
        }
        elsif ( $size == -8 ) {
            if ($dataPt) {
                last if $$dirInfo{DirName} eq 'CanonCNTH';
                my $pos = $raf->Tell() - 4;
                $raf->Seek( 0, 2 );
                my $str =
                    $$dirInfo{DirName}
                  . ' with '
                  . ( $raf->Tell() - $pos )
                  . ' bytes';
                $et->Error( "Terminator found in $str remaining", 1 );
            }
            else {
                push @mdat, [ $raf->Tell(), 0, $hdr ];
            }
            last;
        }
        elsif ( $size < 0 ) {
            $errStr = 'Invalid atom size';
            last;
        }

        if ( $tag eq 'mdat' ) {
            if ($dataPt) {
                $et->Error("'mdat' not at top level");
                last;
            }
            push @mdat, [ $raf->Tell(), $raf->Tell() + $size, $hdr ];
            $raf->Seek( $size, 1 )
              or $et->Error("Seek error in mdat atom"), return $rtnVal;
            next;
        }
        elsif ( $tag eq 'cmov' ) {
            $et->Error("Can't yet write compressed movie metadata");
            return $rtnVal;
        }
        elsif ( $tag eq 'wide' ) {
            if ($size) {
                $et->Warn("Incorrect size for 'wide' atom ($size bytes)");
                $raf->Seek( $size, 1 ) or $et->Error('Truncated wide atom');
            }
            next;
        }

        my $got;
        if ( not $size ) {
            $buff = '';
            $got  = 0;
        }
        else {
            $got = $raf->Read( $buff, $size > $maxReadLen ? 0x10000 : $size );
        }
        if ( $got != $size ) {
            my $type;
            if (   $got <= 256 and $size >= 1024 and $tag ne 'mdat'
                or $got < 3000
                and pack( 'N', $size ) =~ /^<b[r>]/
                and $type = 'extraneous HTML' )
            {
                my $bytes = $got + length $hdr;
                $type or $type = 'garbage';
                if ( $$et{OPTIONS}{IgnoreMinorErrors} ) {
                    $et->Warn("Deleted $type at end of file ($bytes bytes)");
                    $buff = $hdr = '';
                }
                else {
                    $et->Error( "Possible $type at end of file ($bytes bytes)",
                        1 );
                    return $rtnVal;
                }
            }
            else {
                $tag = PrintableTagID( $tag, 3 );
                if ( $size > $maxReadLen and $got == 0x10000 ) {
                    my $mb = int( $size / 0x100000 + 0.5 );
                    $errStr =
                      "'${tag}' atom is too large for rewriting ($mb MB)";
                }
                else {
                    $errStr = "Truncated '${tag}' atom";
                }
                last;
            }
        }
        if (    $tag eq 'hdlr'
            and length $buff >= 12
            and @{ $$et{PATH} }
            and $$et{PATH}[-1] eq 'Media' )
        {
            $$et{MediaType} = substr( $buff, 8, 4 );
        }
        if ( $tag =~ /^(stco|co64|iloc|mfra|moof|sidx|saio|gps |CTBO|uuid)$/ ) {
            my $flg = $$et{QtDataFlg};
            if ( $tag eq 'mfra' or $tag eq 'moof' ) {
                $et->Error("Can't yet handle movie fragments when writing");
                return $rtnVal;
            }
            elsif ( $tag eq 'sidx' or $tag eq 'saio' ) {
                $et->Error("Can't yet handle $tag box when writing");
                return $rtnVal;
            }
            elsif ( $tag eq 'iloc' ) {
                Handle_iloc( $et, $dirInfo, \$buff, $outfile )
                  or $et->Error('Error parsing iloc atom');
            }
            elsif ( $tag eq 'gps ' ) {
                if (    $$dirInfo{DirID}
                    and $$dirInfo{DirID} eq 'moov'
                    and length $buff > 8 )
                {
                    my $off = $$dirInfo{ChunkOffset};
                    my $num = Get32u( \$buff, 4 );
                    $num = int( ( length($buff) - 8 ) / 8 )
                      if $num * 8 + 8 > length($buff);
                    my $i;
                    for ( $i = 0 ; $i < $num ; ++$i ) {
                        push @$off,
                          [
                            'stco_gps ',
                            length($$outfile) + length($hdr) + 8 + $i * 8, 4
                          ];
                    }
                }
            }
            elsif ( $tag eq 'CTBO' or $tag eq 'uuid' ) {
                push @{ $$dirInfo{ChunkOffset} },
                  [ $tag, length($$outfile), length($hdr) + $size ];
            }
            elsif ( not $flg or $flg == 1 ) {
                $flg or $$et{AssumedDataRef} = 1;
                push @{ $$dirInfo{ChunkOffset} },
                  [ $tag, length($$outfile) + length($hdr), $size ];
            }
            elsif ( $flg == 3 ) {
                $et->Error(
                    "Can't write files with mixed internal/external media data"
                );
                return $rtnVal;
            }
        }

        my $tagInfo = $et->GetTagInfo( $tagTablePtr, $tag, \$buff );

        &{ $$tagInfo{WriteHook} }( $buff, $et )
          if $tagInfo and $$tagInfo{WriteHook};

        if ( not $tagInfo and $dirName eq 'ItemList' and $$et{$keysGrp} ) {
            $keysIndex = unpack( 'N', $tag );
            my $newIndex = $$et{$keysGrp}{Remap}{$keysIndex};
            if ( defined $newIndex ) {
                $tagInfo = $$et{$keysGrp}{Info}{$keysIndex};
                unless ($newIndex) {
                    if ($tagInfo) {
                        $et->VPrint( 1, "    - Keys:$$tagInfo{Name}" );
                    }
                    else {
                        $delCount = ( $delCount || 0 ) + 1;
                    }
                    ++$$et{CHANGED};
                    next;
                }
                unless ( $keysIndex == $newIndex ) {
                    $tag = Set32u($newIndex);
                    substr( $hdr, 4, 4 ) = $tag;
                }
            }
            else {
                undef $keysIndex;
            }
        }
        if ($delGrp) {
            if ( $dirName eq 'ItemList' ) {
                $delCount = ( $delCount || 0 ) + 1;
                ++$$et{CHANGED};
                next;
            }
            elsif ( $dirName eq 'UserData'
                and ( not $tagInfo or not $$tagInfo{SubDirectory} ) )
            {
                $delCount = ( $delCount || 0 ) + 1;
                ++$$et{CHANGED};
                next;
            }
        }
        undef $tagInfo if $tagInfo and $$tagInfo{AddedUnknown};

        if ( defined $itemIndex and $$et{ItemInfo} ) {
            my $items = $$et{ItemInfo};
            my ( $id, $prop, $isPrimary );
            my $primary = $$et{PrimaryItem};
            unless ( defined $primary ) {
                ($primary) = sort { $a <=> $b } keys %{ $$et{ItemInfo} }
                  if $$et{ItemInfo};
                $primary = 0 unless defined $primary;
            }
            my $pitem = $$items{$primary} || {};
            $$pitem{RefersTo} or $$pitem{RefersTo} = {};
          ItemID2: foreach $id ( reverse sort { $a <=> $b } keys %$items ) {
                next unless $$items{$id}{Association};
                my $item = $$items{$id};
                foreach $prop ( @{ $$item{Association} } ) {
                    next unless $prop == $itemIndex;
                    my $dont = $dontInherit{$tag} || 0;
                    last
                      unless $id == $primary
                      or ( not $dont
                        and ( $$item{RefersTo} and $$item{RefersTo}{$primary} )
                      )
                      or
                      ( $dont != 1 and $$pitem{RefersTo}{$id} );
                    $isPrimary = 1;
                    last ItemID2;
                }
            }
            undef $tagInfo unless $isPrimary;
        }

        if ( $tagInfo
            and ( not defined $$tagInfo{Writable} or $$tagInfo{Writable} ) )
        {
            my $subdir = $$tagInfo{SubDirectory};
            my ( $newData, @chunkOffset );

            if ($subdir) {

                if ( $tag eq 'trak' ) {
                    $$et{MediaType} = '';
                    delete $$et{AssumedDataRef};
                }
                my $subName = $$subdir{DirName} || $$tagInfo{Name};
                my $start   = $$subdir{Start}   || 0;
                my $base    = ( $$dirInfo{Base} || 0 ) + $raf->Tell() - $size;
                my $dPos    = 0;
                my $hdrLen  = $start;
                $trailer = $$trailer[3]
                  if $$tagInfo{DontRead}
                  and $trailer
                  and $base == $$trailer[1];
                if ( $$subdir{Base} ) {
                    my $localBase = eval $$subdir{Base};
                    $dPos -= $localBase;
                    $base -= $dPos;
                    $hdrLen -= $localBase if $localBase <= $hdrLen;
                }
                my %subdirInfo = (
                    Parent     => $dirName,
                    DirName    => $subName,
                    Name       => $$tagInfo{Name},
                    TagInfo    => $tagInfo,
                    DirID      => $tag,
                    DataPt     => \$buff,
                    DataLen    => $size,
                    DataPos    => $dPos,
                    DirStart   => $start,
                    DirLen     => $size - $start,
                    Base       => $base,
                    HasData    => $$subdir{HasData},
                    Multi      => $$subdir{Multi},
                    OutFile    => $outfile,
                    NoRefTest  => 1,
                    WriteGroup => $$tagInfo{WriteGroup},
                    Permanent  => $$tagInfo{Permanent},
                    ChunkOffset => \@chunkOffset,
                );
                $subdirInfo{InPlace} = 2 if $et->Options('QuickTimePad');
                if ( $hdrLen and $hdrLen < $size ) {
                    my $header = substr( $buff, 0, $hdrLen );
                    $subdirInfo{HeaderPtr} = \$header;
                }
                SetByteOrder('II')
                  if $$subdir{ByteOrder} and $$subdir{ByteOrder} =~ /^Little/;
                my $oldWriteGroup = $$et{CUR_WRITE_GROUP};
                if ( $subName eq 'Track' ) {
                    $track or $track = 0;
                    $$et{CUR_WRITE_GROUP} = 'Track' . ( ++$track );
                }
                my $subTable = GetTagTable( $$subdir{TagTable} );
                $$et{DemoteErrors} = 1
                  unless $$subTable{GROUPS}{0} eq 'QuickTime';
                my $oldChanged = $$et{CHANGED};
                $newData = $et->WriteDirectory( \%subdirInfo, $subTable,
                    $$subdir{WriteProc} );
                if ( $$et{DemoteErrors} ) {
                    $$et{CHANGED} = $oldChanged if $$et{DemoteErrors} > 1;
                    delete $$et{DemoteErrors};
                }
                if (
                        defined $newData
                    and not length $newData
                    and (
                        $$tagInfo{Permanent}
                        or ( $$tagTablePtr{PERMANENT}
                            and not defined $$tagInfo{Permanent} )
                    )
                  )
                {
                    $$et{CHANGED} = $oldChanged;
                    undef $newData;
                }
                if ( $tag eq 'trak' ) {
                    $$et{MediaType} = '';
                    if ( $$et{AssumedDataRef} ) {
                        my $grp = $$et{CUR_WRITE_GROUP} || $dirName;
                        $et->Error(
"Can't locate data reference to update offsets for $grp"
                        );
                        delete $$et{AssumedDataRef};
                    }
                }
                $$et{CUR_WRITE_GROUP} = $oldWriteGroup;
                SetByteOrder('MM');
                if (
                        $start
                    and defined $newData
                    and (
                        length $newData
                        or ( defined $$tagInfo{Permanent}
                            and not $$tagInfo{Permanent} )
                    )
                  )
                {
                    $newData = substr( $buff, 0, $start ) . $newData;
                    $$_[1] += $start foreach @chunkOffset;
                }
                if (    $curPath eq $writePath
                    and $$addDirs{$subName}
                    and $$addDirs{$subName} eq $dirName )
                {
                    delete $$addDirs{$subName};
                }
                $didDir{$tag} = 1;

            }
            else {

                my $nvHash = $et->GetNewValueHash($tagInfo);
                if ( $nvHash or $langTags{$tag} or $delGrp ) {
                    my $nvHashNoLang = $nvHash;
                    my ( $val, $len, $lang, $type, $flags, $ctry,
                        $charsetQuickTime );
                    my $format = $$tagInfo{Format};
                    my $hasData =
                      ( $$dirInfo{HasData} and $buff =~ /\0...data\0/s );
                    my $langInfo = $tagInfo;
                    if ($hasData) {
                        my $pos = 0;
                        for ( ; ; $pos += $len ) {
                            if ( $pos + 16 > $size ) {
                                if ( $langTags{$tag} ) {
                                    my $tg;
                                    foreach $tg ( '',
                                        sort keys %{ $langTags{$tag} } )
                                    {
                                        my $ti =
                                            $tg
                                          ? $langTags{$tag}{$tg}
                                          : $nvHashNoLang;
                                        $nvHash = $et->GetNewValueHash($ti);
                                        next
                                          unless $nvHash
                                          and not $$didTag{$nvHash};
                                        $$didTag{$nvHash} = 1;
                                        next
                                          unless $$nvHash{IsCreating}
                                          and $et->IsOverwriting($nvHash);
                                        my $newVal = $et->GetNewValue($nvHash);
                                        next unless defined $newVal;
                                        my $prVal = $newVal;
                                        my $flags = FormatQTValue(
                                            $et,      \$newVal,
                                            $tagInfo, $format
                                        );
                                        next unless defined $newVal;
                                        my ( $ctry, $lang ) = ( 0, 0 );

                                        if ( $$ti{LangCode} ) {
                                            unless ( $$ti{LangCode} =~
                                                /^([A-Z]{3})?[-_]?([A-Z]{2})?$/i
                                              )
                                            {
                                                $et->Warn(
"Invalid language code for $$ti{Name}"
                                                );
                                                next;
                                            }
                                            if ( $1 and $1 ne 'und' ) {
                                                $lang =
                                                  ( $lang << 5 ) | ( $_ - 0x60 )
                                                  foreach unpack 'C*', lc($1);
                                            }
                                            $ctry =
                                              unpack( 'n',
                                                pack( 'a2', uc($2) ) )
                                              if $2 and $2 ne 'ZZ';
                                        }
                                        $newData = substr( $buff, 0, $pos )
                                          unless defined $newData;
                                        $newData .= pack( 'Na4Nnn',
                                            16 + length($newVal),
                                            'data', $flags, $ctry, $lang )
                                          . $newVal;
                                        my $grp = $et->GetGroup( $ti, 1 );
                                        $et->VerboseValue( "+ $grp:$$ti{Name}",
                                            $prVal );
                                        ++$$et{CHANGED};
                                    }
                                }
                                last;
                            }
                            ( $len, $type, $flags, $ctry, $lang ) =
                              unpack( "x${pos}Na4Nnn", $buff );
                            $lang or $lang = $undLang;
                            $langInfo = $tagInfo;
                            my $delTag = $delGrp;
                            my $newVal;
                            my $langCode = GetLangCode( $lang, $ctry, 1 );
                            for ( ; ; ) {
                                $langInfo = GetLangInfo( $tagInfo, $langCode );
                                $nvHash   = $et->GetNewValueHash($langInfo);
                                last
                                  if $nvHash
                                  or not $ctry
                                  or $lang ne $undLang
                                  or length($langCode) == 2;
                                $langCode =
                                  lc unpack( 'a2', pack( 'n', $ctry ) );
                            }
                            if ( not $nvHash and $nvHashNoLang ) {
                                if (    $lang eq $undLang
                                    and not $ctry
                                    and not $$didTag{$nvHashNoLang} )
                                {
                                    $nvHash = $nvHashNoLang;
                                }
                                else {
                                    $delTag = 1;
                                }
                            }
                            last if $pos + $len > $size;
                            if ( $type eq 'data' and $len >= 16 ) {
                                $pos += 16;
                                $len -= 16;
                                $val = substr( $buff, $pos, $len );
                                if ( $stringEncoding{$flags} ) {
                                    $val = $et->Decode( $val,
                                        $stringEncoding{$flags} );
                                    $val =~ s/\0$// unless $$tagInfo{Binary};
                                    $flags = 0x01;
                                }
                                else {
                                    if ($format) {
                                        if (    $$tagInfo{Writable}
                                            and
                                            $qtFormat{ $$tagInfo{Writable} } )
                                        {
                                            $flags =
                                              $qtFormat{ $$tagInfo{Writable} };
                                        }
                                        elsif ( $qtFormat{$format} ) {
                                            $flags = $qtFormat{$format};
                                        }
                                    }
                                    else {
                                        $format =
                                          QuickTimeFormat( $flags, $len );
                                    }
                                    $val =
                                      ReadValue( \$val, 0, $format,
                                        $$tagInfo{Count}, $len )
                                      if $format;
                                }
                                if (
                                    (
                                            $nvHash
                                        and $et->IsOverwriting( $nvHash, $val )
                                    )
                                    or $delTag
                                  )
                                {
                                    $newVal = $et->GetNewValue($nvHash)
                                      if defined $nvHash;
                                    if (   $delTag
                                        or not defined $newVal
                                        or $$didTag{$nvHash} )
                                    {
                                        my $grp = $et->GetGroup( $langInfo, 1 );
                                        $et->VerboseValue(
                                            "- $grp:$$langInfo{Name}", $val );
                                        $newData = substr( $buff, 0, $pos - 16 )
                                          unless defined $newData;
                                        ++$$et{CHANGED};
                                        next;
                                    }
                                    my $prVal = $newVal;
                                    $flags =
                                      FormatQTValue( $et, \$newVal, $tagInfo,
                                        $format );
                                    next unless defined $newVal;
                                    my $grp = $et->GetGroup( $langInfo, 1 );
                                    $et->VerboseValue(
                                        "- $grp:$$langInfo{Name}", $val );
                                    $et->VerboseValue(
                                        "+ $grp:$$langInfo{Name}", $prVal );
                                    $newData = substr( $buff, 0, $pos - 16 )
                                      unless defined $newData;
                                    my $wLang = $lang eq $undLang ? 0 : $lang;
                                    $newData .= pack( 'Na4Nnn',
                                        length($newVal) + 16,
                                        $type, $flags, $ctry, $wLang );
                                    $newData .= $newVal;
                                    ++$$et{CHANGED};
                                }
                                elsif ( defined $newData ) {
                                    $newData .=
                                      substr( $buff, $pos - 16, $len + 16 );
                                }
                            }
                            elsif ( defined $newData ) {
                                $newData .= substr( $buff, $pos, $len );
                            }
                            $$didTag{$nvHash} = 1 if $nvHash;
                        }
                        $newData .= substr( $buff, $pos )
                          if defined $newData and $pos < $size;
                        undef $val;
                    }
                    elsif ($format) {
                        $val = ReadValue( \$buff, 0, $format, undef, $size );
                    }
                    elsif ( ( $tag =~ /^\xa9/ or $$tagInfo{IText} )
                        and $size >= ( $$tagInfo{IText} || 4 ) )
                    {
                        my $hdr;
                        if ( $$tagInfo{IText} and $$tagInfo{IText} >= 6 ) {
                            my $iText = $$tagInfo{IText};
                            my $pos   = $iText - 2;
                            $lang = unpack( "x${pos}n", $buff );
                            $hdr  = substr( $buff, 4, $iText - 6 );
                            $len  = $size - $iText;
                            $val  = substr( $buff, $iText, $len );
                        }
                        else {
                            ( $len, $lang ) = unpack( 'nn', $buff );
                            $len -= 4 if 4 + $len > $size;
                            $len = $size - 4 if $len > $size - 4 or $len < 0;
                            $val = substr( $buff, 4, $len );
                        }
                        $lang or $lang = $undLang;
                        my $enc;
                        if ( $lang < 0x400 and $val !~ /^\xfe\xff/ ) {
                            $charsetQuickTime =
                              $et->Options('CharsetQuickTime');
                            $enc = $charsetQuickTime;
                        }
                        else {
                            $enc = $val =~ s/^\xfe\xff// ? 'UTF16' : 'UTF8';
                        }
                        unless ( $$tagInfo{NoDecode} ) {
                            $val = $et->Decode( $val, $enc );
                            $val =~ s/\0+$//;
                        }
                        $val = $hdr . $val if defined $hdr;
                        my $langCode = UnpackLang( $lang, 1 );
                        $langInfo = GetLangInfo( $tagInfo, $langCode );
                        $nvHash   = $et->GetNewValueHash($langInfo);
                        if ( not $nvHash and $nvHashNoLang ) {
                            if ( $lang eq $undLang
                                and not $$didTag{$nvHashNoLang} )
                            {
                                $nvHash = $nvHashNoLang;
                            }
                            elsif ($canCreate) {
                                my $grp = $et->GetGroup( $langInfo, 1 );
                                $et->VerboseValue( "- $grp:$$langInfo{Name}",
                                    $val );
                                ++$$et{CHANGED};
                                next;
                            }
                        }
                    }
                    else {
                        $val = $buff;
                        if ( $tag =~ /^\xa9/ or $$tagInfo{IText} ) {
                            $et->Warn("Corrupted $$tagInfo{Name} value");
                        }
                    }
                    if ( $nvHash and defined $val ) {
                        if ( $et->IsOverwriting( $nvHash, $val ) ) {
                            $newData = $et->GetNewValue($nvHash);
                            $newData = '' unless defined $newData or $canCreate;
                            ++$$et{CHANGED};
                            my $grp = $et->GetGroup( $langInfo, 1 );
                            $et->VerboseValue( "- $grp:$$langInfo{Name}",
                                $val );
                            unless ( defined $newData
                                and not $$didTag{$nvHash} )
                            {
                                next unless defined $itemIndex;
                            }
                            $et->VerboseValue( "+ $grp:$$langInfo{Name}",
                                $newData );
                            if ( defined $lang ) {
                                my $iText = $$tagInfo{IText} || 0;
                                my $hdr;
                                if ( $iText > 6 ) {
                                    $newData .= ' ' x ( $iText - 6 )
                                      if length($newData) < $iText - 6;
                                    $hdr = substr( $newData, 0, $iText - 6 );
                                    $newData = substr( $newData, $iText - 6 );
                                }
                                unless ( $$tagInfo{NoDecode} ) {
                                    $newData = $et->Encode( $newData,
                                          $lang < 0x400
                                        ? $charsetQuickTime
                                        : 'UTF8' );
                                }
                                my $wLang = $lang eq $undLang ? 0 : $lang;
                                if ( $iText < 6 ) {
                                    $newData =
                                      pack( 'nn', length($newData), $wLang )
                                      . $newData;
                                }
                                elsif ( $iText == 6 ) {
                                    $newData =
                                      pack( 'Nn', 0, $wLang ) . $newData . "\0";
                                }
                                else {
                                    $newData =
                                        "\0\0\0\0"
                                      . $hdr
                                      . pack( 'n', $wLang )
                                      . $newData . "\0";
                                }
                            }
                            elsif ( not $format
                                or $format =~ /^string/
                                and not $$tagInfo{Binary}
                                and not $$tagInfo{ValueConv} )
                            {
                                $newData = $et->Encode( $newData, 'UTF8' );
                            }
                            elsif ( $format and not $$tagInfo{Binary} ) {
                                $newData = WriteValue( $newData, $format,
                                    $$tagInfo{Count} );
                            }
                        }
                        $$didTag{$nvHash} = 1;
                    }
                }
            }
            if ( defined $newData ) {
                my $sizeDiff = length($buff) - length($newData);
                if (    $sizeDiff > 0
                    and $$tagInfo{PreservePadding}
                    and $et->Options('QuickTimePad') )
                {
                    $newData .= "\0" x $sizeDiff;
                    $et->VPrint( 1,
                        "    ($$tagInfo{Name} padded to original size)" );
                }
                elsif ($sizeDiff) {
                    $et->VPrint( 1, "    ($$tagInfo{Name} changed size)" );
                }
                my $len = length($newData) + 8;
                $len > 0x7fffffff
                  and $et->Error("$$tagInfo{Name} to large to write"), last;
                $$dirInfo{ChunkOffset}[-1][2] = $len if $tag eq 'uuid';
                next unless $len > 8;

                if (@chunkOffset) {
                    $$_[1] += 8 + length $$outfile foreach @chunkOffset;
                    push @{ $$dirInfo{ChunkOffset} }, @chunkOffset;
                }
                if ( $$tagInfo{WriteLast} ) {
                    $writeLast =
                      ( $writeLast || '' ) . Set32u($len) . $tag . $newData;
                }
                else {
                    $boxPos{$tag} = [ length($$outfile), length($newData) + 8 ];
                    Write( $outfile, Set32u($len), $tag, $newData )
                      or $rtnVal = $rtnErr, $err = 1, last;
                }
                next;
            }
        }
        if ( $tag eq 'dinf' ) {
            $$et{QtDataRef} = [];
        }
        elsif ( $parent eq 'DataInfo' and length($buff) >= 4 ) {
            push @{ $$et{QtDataRef} }, [ $tag, Get32u( \$buff, 0 ) ];
        }
        elsif ( $tag eq 'stsd' and length($buff) >= 8 ) {
            my $n = Get32u( \$buff, 4 );
            my ( $pos, $flg ) = ( 8, 0 );
            my ( $i, $msg );
            for ( $i = 0 ; $i < $n ; ++$i ) {
                $pos + 16 <= length($buff)
                  or $msg = 'Truncated sample table', last;
                my $siz = Get32u( \$buff, $pos );
                $pos + $siz <= length($buff)
                  or $msg = 'Truncated sample table', last;
                my $drefIdx = Get16u( \$buff, $pos + 14 );
                my $drefTbl = $$et{QtDataRef};
                if ( not $drefIdx ) {
                    $flg |= 0x01;
                }
                elsif ( $drefTbl and $$drefTbl[ $drefIdx - 1 ] ) {
                    my $dref = $$drefTbl[ $drefIdx - 1 ];
                    $flg |=
                      ( $$dref[1] == 1 and $$dref[0] ne 'rsrc' ) ? 0x01 : 0x02;
                }
                else {
                    $msg = "No data reference for sample description $i";
                    last;
                }
                $pos += $siz;
            }
            if ($msg) {
                if ( $avType{ $$et{MediaType} } ) {
                    my $grp = $$et{CUR_WRITE_GROUP} || $parent;
                    $et->Error("$msg for $grp");
                    return $rtnErr;
                }
                $flg = 1;
            }
            $$et{QtDataFlg} = $flg;
            if ( $$et{AssumedDataRef} ) {
                if ( $flg != $$et{AssumedDataRef} ) {
                    my $grp = $$et{CUR_WRITE_GROUP} || $parent;
                    $et->Error(
                        "Assumed incorrect data reference for $grp (was $flg)");
                }
                delete $$et{AssumedDataRef};
            }
        }
        if ( $tagInfo and $$tagInfo{WriteLast} ) {
            $writeLast = ( $writeLast || '' ) . $hdr . $buff;
        }
        else {
            $boxPos{$tag} = [ length($$outfile), length($hdr) + length($buff) ];
            Write( $outfile, $hdr, $buff ) or $rtnVal = $rtnErr, $err = 1, last;
        }
    }
    if ($errStr) {
        if (
                ( $lastTag eq 'mdat' or $lastTag eq 'moov' )
            and not $dataPt
            and ( not $$tagTablePtr{$tag}
                or ref $$tagTablePtr{$tag} eq 'HASH'
                and $$tagTablePtr{$tag}{Unknown} )
          )
        {
            $buf2 = '';
            $raf->Seek( $lastPos, 0 ) and $raf->Read( $buf2, 8 );
            my ( $type, $len );
            if ( $buf2 eq 'CCCCCCCC' ) {
                $type = 'Kenwood';
            }
            elsif ( $buf2 =~ /^(gpsa|gps0|gsen|gsea)...\0/s ) {
                $type = 'RIFF';
            }
            else {
                $type = 'Unknown';
            }
            if ($trailer) {
                $len = $$trailer[1] - $lastPos;
            }
            else {
                $raf->Seek( 0, 2 )
                  or $et->Error('Seek error'), return $dataPt ? undef : 1;
                $len = $raf->Tell() - $lastPos;
            }
            $trailer = [ $type, $lastPos, $len, $trailer ];
        }
        else {
            $et->Error($errStr);
            return $dataPt ? undef : 1;
        }
    }
    $et->VPrint( 0,
            "  [deleting $delCount $dirName tag"
          . ( $delCount == 1 ? '' : 's' )
          . "]\n" )
      if $delCount;

    if ( $createKeys < 0 ) {
        if ( $avType{ $$et{MediaType} } ) {
            $createKeys = 1;
            ( $keysGrp, $keysPath ) =
              ( "$avType{$$et{MediaType}}Keys", 'MOV-Movie-Track' );
        }
        else {
            $canCreate = 0;
        }
    }
    $createKeys &= ~0x01 unless $$addDirs{$keysGrp};

    if ( $canCreate and ( exists $$et{EDIT_DIRS}{$dirName} or $createKeys ) ) {
        my $dirs = $et->GetAddDirHash( $tagTablePtr, $dirName );
        my @addTags = sort( keys(%$dirs), keys %$newTags );
        my ( $tag, $index );
        if ($createKeys) {
            if ( $curPath eq $keysPath ) {
                unless ( $didDir{meta} ) {
                    $$dirs{meta} = $Image::ExifTool::QuickTime::Movie{meta};
                    push @addTags, 'meta';
                }
            }
            elsif ( $curPath eq "$keysPath-Meta" ) {
                undef @addTags;
                $dirs = {};
                foreach ( 'keys', 'ilst' ) {
                    next if $didDir{$_};
                    $$dirs{$_} = $Image::ExifTool::QuickTime::Meta{$_};
                    push @addTags, $_;
                }
            }
            elsif ( $curPath eq "$keysPath-Meta-ItemList" and $$et{$keysGrp} ) {
                foreach $index ( sort { $a <=> $b }
                    keys %{ $$et{$keysGrp}{Add} } )
                {
                    my $id = Set32u($index);
                    $$newTags{$id} = $$et{$keysGrp}{Add}{$index};
                    push @addTags, $id;
                }
            }
            else {
                $dirs = $et->GetAddDirHash( $tagTablePtr, $dirName );
                push @addTags, sort keys %$dirs;
            }
        }
        foreach $tag (@addTags) {
            my $tagInfo = $$dirs{$tag} || $$newTags{$tag};
            unless ( ref $tagInfo eq 'HASH' ) {

                next unless ref $tagInfo eq 'ARRAY';
                $tagInfo = $et->GetTagInfo( $tagTablePtr, $tag ) or next;
            }
            next if defined $$tagInfo{CanCreate} and not $$tagInfo{CanCreate};
            next
              if defined $$tagInfo{MediaType}
              and $$et{MediaType} ne $$tagInfo{MediaType};
            my $subdir = $$tagInfo{SubDirectory};
            unless ($subdir) {
                my $nvHash = $et->GetNewValueHash($tagInfo);
                next unless $nvHash and not $$didTag{$nvHash};
                next
                  unless $$nvHash{IsCreating} and $et->IsOverwriting($nvHash);
                my $newVal = $et->GetNewValue($nvHash);
                next unless defined $newVal;
                my $prVal = $newVal;
                my $flags = FormatQTValue( $et, \$newVal, $tagInfo );
                next unless defined $newVal;
                my ( $ctry, $lang ) = ( 0, 0 );

                if ( $$tagInfo{LangCode} ) {
                    $tag = substr( $tag, 0, 4 );
                    unless ( $$tagInfo{LangCode} =~
                        /^([A-Z]{3})?[-_]?([A-Z]{2})?$/i )
                    {
                        $et->Warn("Invalid language code for $$tagInfo{Name}");
                        next;
                    }
                    if ( $1 and $1 ne 'und' ) {
                        $lang = ( $lang << 5 ) | ( $_ - 0x60 )
                          foreach unpack 'C*', lc($1);
                    }
                    $ctry = unpack( 'n', pack( 'a2', uc($2) ) )
                      if $2 and $2 ne 'ZZ';
                }
                if ( $$dirInfo{HasData} ) {
                    $newVal = pack( 'Na4Nnn',
                        16 + length($newVal),
                        'data', $flags, $ctry, $lang )
                      . $newVal;
                }
                elsif ( $tag =~ /^\xa9/ or $$tagInfo{IText} ) {
                    if ($ctry) {
                        my $grp = $et->GetGroup( $tagInfo, 1 );
                        $et->Warn(
                            "Can't use country code for $grp:$$tagInfo{Name}");
                        next;
                    }
                    elsif ( $$tagInfo{IText} and $$tagInfo{IText} >= 6 ) {
                        my $n = $$tagInfo{IText} - 6;
                        $newVal .= ' ' x $n if length($newVal) < $n;
                        $newVal =
                            "\0\0\0\0"
                          . substr( $newVal, 0, $n )
                          . pack( 'n', 0, $lang )
                          . substr( $newVal, $n ) . "\0";
                    }
                    else {
                        $newVal =
                          pack( 'nn', length($newVal), $lang ) . $newVal;
                    }
                }
                elsif ( $ctry or $lang ) {
                    my $grp = $et->GetGroup( $tagInfo, 1 );
                    $et->Warn(
                        "Can't use language code for $grp:$$tagInfo{Name}");
                    next;
                }
                if ( $$tagInfo{WriteLast} ) {
                    $writeLast =
                        ( $writeLast || '' )
                      . Set32u( 8 + length($newVal) )
                      . $tag
                      . $newVal;
                }
                else {
                    $boxPos{$tag} = [ length($$outfile), 8 + length($newVal) ];
                    Write( $outfile, Set32u( 8 + length($newVal) ),
                        $tag, $newVal )
                      or $rtnVal = $rtnErr, $err = 1;
                }
                my $grp = $et->GetGroup( $tagInfo, 1 );
                $et->VerboseValue( "+ $grp:$$tagInfo{Name}", $prVal );
                $$didTag{$nvHash} = 1;
                ++$$et{CHANGED};
                next;
            }
            my $subName = $$subdir{DirName} || $$tagInfo{Name};
            if ( $createKeys and $curPath eq $keysPath and $subName eq 'Meta' )
            {
                $et->VPrint( 0,
                    "  Creating Meta with mdta Handler and Keys\n" );
                $buf2 =
                    "\0\0\0\x20hdlr\0\0\0\0\0\0\0\0mdta\0\0\0\0\0\0\0\0\0\0\0\0"
                  . "\0\0\0\x10keys\0\0\0\0\0\0\0\0"
                  . "\0\0\0\x08ilst";
            }
            elsif ( $createKeys and $curPath eq "$keysPath-Meta" ) {
                $buf2 = ( $subName eq 'Keys' ? "\0\0\0\0\0\0\0\0" : '' );
            }
            elsif ( $subName eq 'Meta' and $$et{OPTIONS}{QuickTimeHandler} ) {
                $et->VPrint( 0, "  Creating Meta with mdir Handler\n" );
                $buf2 =
                  "\0\0\0\x20hdlr\0\0\0\0\0\0\0\0mdir\0\0\0\0\0\0\0\0\0\0\0\0";
            }
            else {
                next
                  unless $curPath eq $writePath
                  and $$addDirs{$subName}
                  and $$addDirs{$subName} eq $dirName;
                $buf2 = '';
            }
            my %subdirInfo = (
                Parent      => $dirName,
                DirName     => $subName,
                DataPt      => \$buf2,
                DirStart    => 0,
                HasData     => $$subdir{HasData},
                OutFile     => $outfile,
                ChunkOffset => [],
                WriteGroup  => $$tagInfo{WriteGroup},
            );
            my $subTable = GetTagTable( $$subdir{TagTable} );
            my $newData  = $et->WriteDirectory( \%subdirInfo, $subTable,
                $$subdir{WriteProc} );
            if ( $newData and length($newData) <= 0x7ffffff7 ) {
                my $prefix = '';
                if ( $$subdir{Start} ) {
                    if ( $$subdir{Start} == 4 ) {
                        $prefix = "\0\0\0\0";
                    }
                    else {
                        my $cond = $$tagInfo{Condition};
                        $prefix = eval qq("$1")
                          if $cond and $cond =~ m{=~\s*\/\^(.*)/};
                        length($prefix) == $$subdir{Start}
                          or $et->Error('Internal UUID error');
                    }
                }
                my $newHdr =
                    Set32u( 8 + length($newData) + length($prefix) )
                  . $tag
                  . $prefix;
                if ( $$tagInfo{WriteLast} ) {
                    $writeLast = ( $writeLast || '' ) . $newHdr . $newData;
                }
                else {
                    if ( $tag eq 'uuid' ) {
                        my $off = $$dirInfo{ChunkOffset};
                        push @$off,
                          [
                            $tag, length($$outfile),
                            length($newHdr) + length($newData)
                          ];
                    }
                    $boxPos{$tag} =
                      [ length($$outfile), length($newHdr) + length($newData) ];
                    Write( $outfile, $newHdr, $newData )
                      or $rtnVal = $rtnErr, $err = 1;
                }
            }
            delete $$addDirs{$subName} unless $createKeys;
        }
    }
    if ( $curPath eq 'MOV-Meta' and $$et{EDIT_DIRS}{ItemInformation} ) {
        $$dirInfo{BoxPos} = \%boxPos;
        my $mdatEdit = WriteItemInfo( $et, $dirInfo, $outfile );
        if ($mdatEdit) {
            $et->Error('Multiple top-level Meta containers') if $$et{mdatEdit};
            $$et{mdatEdit} = $mdatEdit;
        }
    }
    Write( $outfile, $term )
      or $rtnVal = $rtnErr, $err = 1
      if $term and length $$outfile;

    if ( $dirName eq 'Meta' ) {
        my $isEmpty = 1;
        $emptyMeta{$_} or $isEmpty = 0, last foreach keys %boxPos;
        if ($isEmpty) {
            $et->VPrint( 0,
                '  Deleting '
                  . join( '+', sort map { $emptyMeta{$_} } keys %boxPos ) )
              if %boxPos;
            $$outfile = '';
        }
        if ( $curPath eq "$keysPath-Meta" ) {
            delete $$addDirs{$keysGrp};
            delete $$et{$keysGrp};
        }
    }

    if ($dataPt) {
        $et->Error("Internal error: WriteLast not on top-level atom!\n")
          if $writeLast;
        return $err ? undef : $$outfile;
    }

    my $off = $$dirInfo{ChunkOffset};
    if ( not @mdat ) {
        foreach $co (@$off) {
            next if $$co[0] eq 'uuid';
            $et->Error('Media data referenced but not found');
            return $rtnVal;
        }
        $et->Warn( 'No media data', 1 );
    }

    if ( $$et{mdatEdit} ) {
        @mdatEdit = @{ $$et{mdatEdit} };
        delete $$et{mdatEdit};
    }
    foreach $edit (@mdatEdit) {
        my ( @thisMdat, @newMdat, $changed );
        foreach $mdat (@mdat) {
            if ( length $$mdat[2] ) {
                push @newMdat, @thisMdat;
                undef @thisMdat;
            }
            push @thisMdat, $mdat;
            next if defined $$mdat[5] or $changed;
            if (
                not $$edit[0]
                or

                (
                    (
                        ( $$edit[0] < $$mdat[1] or not $$mdat[1] )
                        and $$edit[1] > $$mdat[0]
                    )
                    or
                    (
                        $$edit[0] == $$edit[1]
                        and ( $$edit[0] == $$mdat[0] or $$edit[0] == $$mdat[1] )
                    )
                )
              )
            {
                if ( not $$edit[0] ) {
                    $$edit[0] = $$edit[1] = $$mdat[0];
                }
                elsif ( $$edit[0] < $$mdat[0]
                    or ( $$edit[1] > $$mdat[1] and $$mdat[1] ) )
                {
                    $et->Error('ItemInfo runs across mdat boundary');
                    return $rtnVal;
                }
                my $hdrChunk = $thisMdat[0];
                $hdrChunk
                  or $et->Error('Internal error finding mdat header'),
                  return $rtnVal;
                my $diff = ( $$edit[2] ? length( ${ $$edit[2] } ) : 0 ) -
                  ( $$edit[1] - $$edit[0] );
                if ($diff) {
                    if ( length( $$hdrChunk[2] ) == 8 ) {
                        my $size = Get32u( \$$hdrChunk[2], 0 );
                        if ($size) {
                            $size += $diff;
                            $size > 0xffffffff
                              and $et->Error(
                                "Can't yet grow mdat across 4GB boundary"),
                              return $rtnVal;
                            Set32u( $size, \$$hdrChunk[2], 0 );
                        }
                    }
                    elsif ( length( $$hdrChunk[2] ) == 16 ) {
                        my $size = Get64u( \$$hdrChunk[2], 8 );
                        if ($size) {
                            $size += $diff;
                            Set64u( $size, \$$hdrChunk[2], 8 );
                        }
                    }
                    else {
                        $et->Error('Internal error. Invalid mdat header');
                        return $rtnVal;
                    }
                }
                $changed = 1;
                if ( $$edit[0] > $$mdat[0] ) {
                    push @thisMdat,
                      [ $$edit[0], $$edit[1], '', 0, $$edit[2], $$edit[3] ]
                      if $$edit[2];
                    push @thisMdat, [ $$edit[1], $$mdat[1], '' ];
                    $$mdat[1] = $$edit[0];
                }
                else {
                    if ( $$edit[2] ) {
                        splice @thisMdat, -1, 0,
                          [
                            $$edit[0], $$edit[1], $$mdat[2],
                            0,         $$edit[2], $$edit[3]
                          ];
                        $$mdat[2] = '';

                        if ( $$edit[3] ) {
                            my $n = 0;
                            foreach $co (@$off) {
                                next
                                  unless defined $$co[4]
                                  and $$co[4] == $$edit[3];
                                ++$n;
                                if ( $$co[0] eq 'stco_iloc' ) {
                                    Set32u( $$mdat[0], $outfile, $$co[1] );
                                }
                                else {
                                    Set64u( $$mdat[0], $outfile, $$co[1] );
                                }
                            }
                            $n == 1
                              or $et->Error(
                                'Internal error updating chunk offsets');
                        }
                    }
                    $$mdat[0] = $$edit[1];
                }
            }
        }
        if ($changed) {
            @mdat = ( @newMdat, @thisMdat );
            ++$$et{CHANGED};
        }
        else {
            $et->Error('Internal error modifying mdat');
        }
    }

    my $pos = length $$outfile;
    foreach $mdat (@mdat) {
        $pos += length $$mdat[2];
        $$mdat[3] = $pos;
        $pos += $$mdat[4] ? length( ${ $$mdat[4] } ) : $$mdat[1] - $$mdat[0];
    }

    foreach $co (@$off) {
        my ( $type, $ptr, $len, $base, $id ) = @$co;
        $base = 0 unless $base;
        unless ( $type =~ /^(stco|co64)_?(.*)$/ ) {
            next if $type eq 'uuid';
            $type eq 'CTBO'
              or $et->Error('Internal error fixing offsets'), last;
            $$co[2] > 12 or $et->Error('Invalid CTBO atom'),      last;
            @mdat        or $et->Error('Missing CR3 image data'), last;
            my $n = Get32u( $outfile, $$co[1] + 8 );
            $$co[2] < $n * 20 + 12 and $et->Error('Truncated CTBO atom'), last;
            my ( %ctboOff, $i );
            foreach (@$off) {
                next unless $$_[0] eq 'uuid' and $$_[2] >= 24;
                my $pos = $$_[1];
                next if $pos + 24 > length $$outfile;
                my $siz = Get32u( $outfile, $pos );
                if ( $siz == 1 ) {
                    next unless $$_[2] >= 32;
                    $pos += 8;
                }
                my $id = $ctboID{ substr( $$outfile, $pos + 8, 16 ) };
                $ctboOff{$id} = $_ if defined $id;
            }
            $ctboOff{3} = [ 'mdat', $mdat[0][3] - length $mdat[0][2], -1 ];
            for ( $i = 0 ; $i < $n ; ++$i ) {
                my $pos = $$co[1] + 12 + $i * 20;
                my $id  = Get32u( $outfile, $pos );
                next
                  unless Get64u( $outfile, $pos + 12 )
                  or $id == 1
                  or $id == 2;
                if ( not defined $ctboOff{$id} ) {
                    $id == 1
                      or $id == 2
                      or $et->Error("Can't handle CR3 CTBO ID number $id"),
                      last;
                    $ctboOff{$id} = [ 'uuid', 0, 0 ];
                }
                Set64u( $ctboOff{$id}[1], $outfile, $pos + 4 );
                Set64u( $ctboOff{$id}[2], $outfile, $pos + 12 )
                  unless $ctboOff{$id}[2] < 0;
            }
            next;
        }
        my $siz = $1 eq 'co64' ? 8 : 4;
        my ( $n, $tag );
        if ($2) {
            $n    = 1;
            $type = $1;
            $tag  = $2;
        }
        else {
            next if $len < 8;
            $n = Get32u( $outfile, $ptr + 4 );
            $ptr += 8;
            $len -= 8;
            $tag = $1;
        }
        my $end = $ptr + $n * $siz;
        $end > $ptr + $len and $et->Error("Invalid $tag table"), return $rtnVal;
        for ( ; $ptr < $end ; $ptr += $siz ) {
            my ( $ok, $i );
            my $val =
              $type eq 'co64'
              ? Get64u( $outfile, $ptr )
              : Get32u( $outfile, $ptr );
            for ( $i = 0 ; $i < @mdat ; ++$i ) {
                $mdat = $mdat[$i];
                my $pos = $val + $base;
                if ( defined $$mdat[5] ) {

                    unless ( defined $id and $id == $$mdat[5] ) {
                        next
                          unless $pos == $$mdat[0] and $$mdat[0] != $$mdat[1];
                    }
                }
                else {
                    next
                      unless $pos >= $$mdat[0]
                      and ( $pos <= $$mdat[1] or not $$mdat[1] );
                    next
                      if $pos == $$mdat[1]
                      and $i + 1 < @mdat
                      and $pos == $mdat[ $i + 1 ][0];
                }
                $val += $$mdat[3] - $$mdat[0];
                if ( $val < 0 ) {
                    $et->Error("Error fixing up $tag offset");
                    return $rtnVal;
                }
                if ( $type eq 'co64' ) {
                    Set64u( $val, $outfile, $ptr );
                }
                elsif ( $val <= 0xffffffff ) {
                    Set32u( $val, $outfile, $ptr );
                }
                else {
                    $et->Error("Can't yet promote $tag offset to 64 bits");
                    return $rtnVal;
                }
                $ok = 1;
                last;
            }
            unless ($ok) {
                $et->Error("Chunk offset in $tag atom is outside media data");
                return $rtnVal;
            }
        }
    }

    $outfile = $$dirInfo{OutFile};

    Write( $outfile, $outBuff ) or $rtnVal = 0;

    foreach $mdat (@mdat) {
        Write( $outfile, $$mdat[2] ) or $rtnVal = 0;
        if ( $$mdat[4] ) {
            Write( $outfile, ${ $$mdat[4] } ) or $rtnVal = 0;
        }
        else {
            $raf->Seek( $$mdat[0], 0 ) or $et->Error('Seek error'), last;
            if ( $$mdat[1] ) {
                my $result = Image::ExifTool::CopyBlock( $raf, $outfile,
                    $$mdat[1] - $$mdat[0] );
                defined $result or $rtnVal = 0, last;
                $result or $et->Error("Truncated mdat atom"), last;
            }
            else {
                while ( $raf->Read( $buf2, 65536 ) ) {
                    Write( $outfile, $buf2 ) or $rtnVal = 0, last;
                }
            }
        }
    }

    Write( $outfile, $writeLast ) or $rtnVal = 0 if $writeLast;

    while ( $rtnVal and $trailer ) {
        my $nvTrail = $et->GetNewValueHash( $Image::ExifTool::Extra{Trailer} );
        if (   $$et{DEL_GROUP}{Trailer}
            or $$et{DEL_GROUP}{ $$trailer[0] }
            or ( $nvTrail and not( $$nvTrail{Value} and $$nvTrail{Value}[0] ) )
          )
        {
            $et->Warn( "Deleted $$trailer[0] trailer", 1 );
            ++$$et{CHANGED};
            $trailer = $$trailer[3];
            next;
        }
        $raf->Seek( $$trailer[1], 0 ) or $rtnVal = 0, last;
        if ( $$trailer[0] eq 'MIE' ) {
            require Image::ExifTool::MIE;
            my %dirInfo = ( RAF => $raf, OutFile => $outfile );
            my $result  = Image::ExifTool::MIE::ProcessMIE( $et, \%dirInfo );
            $result > 0
              or $et->Error('Error writing MIE trailer'), $rtnVal = 0, last;
        }
        else {
            $et->Warn(
                sprintf( 'Copying %s trailer from offset 0x%x (%d bytes)',
                    @$trailer[ 0 .. 2 ] ),
                1
            );
            my $len = $$trailer[2];
            while ($len) {
                my $n = $len > 65536 ? 65536 : $len;
                $raf->Read( $buf2, $n ) == $n and Write( $outfile, $buf2 )
                  or $rtnVal = 0, last;
                $len -= $n;
            }
            $rtnVal or $et->Error("Error copying $$trailer[0] trailer"), last;
        }
        $trailer = $$trailer[3];
    }
    return $rtnVal;
}

sub WriteMOV($$) {
    my ( $et, $dirInfo ) = @_;
    $et                      or return 1;
    my $raf = $$dirInfo{RAF} or return 0;
    my ( $buff, $ftype );

    return 0 unless $raf->Read( $buff, 8 ) == 8;
    my ( $size, $tag ) = unpack( 'Na4', $buff );
    return 0 if $size < 8 and $size != 1;

    my $tagTablePtr = GetTagTable('Image::ExifTool::QuickTime::Main');
    return 0 unless $$tagTablePtr{$tag};

    if (    $tag eq 'ftyp'
        and $size >= 12
        and $size < 100000
        and $raf->Read( $buff, $size - 8 ) == $size - 8
        and $buff !~ /^(....)+(qt  )/s )
    {
        if ( $buff =~ /^crx / ) {
            $ftype = 'CR3',;
        }
        elsif ( $buff =~ /^(heic|mif1|msf1|heix|hevc|hevx|avif)/ ) {
            $ftype = 'HEIC';
        }
        else {
            $ftype = 'MP4';
        }
    }
    else {
        $ftype = 'MOV';
    }
    $et->SetFileType($ftype);
    if ( $ftype eq 'HEIC' ) {
        $et->InitWriteDirs( $dirMap{$ftype}, 'EXIF', 'QuickTime' );
    }
    else {
        $et->InitWriteDirs( $dirMap{$ftype}, 'XMP', 'QuickTime' );
    }
    $$et{DirMap} = $dirMap{$ftype};

    $$et{DidTag} = {};
    SetByteOrder('MM');
    $raf->Seek( 0, 0 );

    $$et{MediaType}        = '';
    $$dirInfo{Parent}      = '';
    $$dirInfo{DirName}     = 'MOV';
    $$dirInfo{ChunkOffset} = [];
    return WriteQuickTime( $et, $dirInfo, $tagTablePtr ) ? 1 : -1;
}

1;

__END__

