
package Image::ExifTool::H264;

use strict;
use vars            qw($VERSION %convMake);
use Image::ExifTool qw(:DataAccess :Utils);
use Image::ExifTool::Exif;
use Image::ExifTool::GPS;

$VERSION = '1.17';

sub ProcessSEI($$);

my $parsePictureTiming;

%convMake = (
    0x0103 => 'Panasonic',
    0x0108 => 'Sony',
    0x1011 => 'Canon',
    0x1104 => 'JVC',
);

%Image::ExifTool::H264::Main = (
    GROUPS => { 2      => 'Video' },
    VARS   => { ID_FMT => 'none' },
    NOTES  => q{
        Tags extracted from H.264 video streams.  The metadata for AVCHD videos is
        stored in this stream.
    },
    ImageWidth  => {},
    ImageHeight => {},
    MDPM => { SubDirectory => { TagTable => 'Image::ExifTool::H264::MDPM' } },
);

%Image::ExifTool::H264::MDPM = (
    GROUPS       => { 2 => 'Camera' },
    PROCESS_PROC => \&ProcessSEI,
    TAG_PREFIX   => 'MDPM',
    NOTES        => q{
        The following tags are decoded from the Modified Digital Video Pack Metadata
        (MDPM) of the unregistered user data with UUID
        17ee8c60f84d11d98cd60800200c9a66 in the H.264 Supplemental Enhancement
        Information (SEI).  I<[Yes, this description is confusing, but nothing
        compared to the challenge of actually decoding the data!]>  This information
        may exist at regular intervals through the entire video, but only the first
        occurrence is extracted unless the L<ExtractEmbedded|../ExifTool.html#ExtractEmbedded> (-ee) option is used (in
        which case subsequent occurrences are extracted as sub-documents).
    },
    0x13 => {
        Name      => 'TimeCode',
        Notes     => 'hours:minutes:seconds:frames',
        ValueConv => 'sprintf("%.2x:%.2x:%.2x:%.2x",reverse unpack("C*",$val))',
    },
    0x18 => {
        Name        => 'DateTimeOriginal',
        Description => 'Date/Time Original',
        Groups      => { 2 => 'Time' },
        Notes       => 'combined with tag 0x19',
        Combine     => 1,

        ValueConv => q{
            my ($tz, @a) = unpack('C*',$val);
            return sprintf('%.2x%.2x:%.2x:%.2x %.2x:%.2x:%.2x%s%.2d:%s%s', @a,
                           $tz & 0x20 ? '-' : '+', ($tz >> 1) & 0x0f,
                           $tz & 0x01 ? '30' : '00',
                           $tz & 0x40 ? ' DST' : '');
        },
        PrintConv => '$self->ConvertDateTime($val)',
    },
    0x70 => {
        Name         => 'Camera1',
        SubDirectory => { TagTable => 'Image::ExifTool::H264::Camera1' },
    },
    0x71 => {
        Name         => 'Camera2',
        SubDirectory => { TagTable => 'Image::ExifTool::H264::Camera2' },
    },
    0x7f => {
        Name         => 'Shutter',
        SubDirectory => {
            TagTable  => 'Image::ExifTool::H264::Shutter',
            ByteOrder => 'LittleEndian',
        },
    },
    0xa0 => {
        Name      => 'ExposureTime',
        Format    => 'rational32u',
        Groups    => { 2 => 'Image' },
        PrintConv => 'Image::ExifTool::Exif::PrintExposureTime($val)',
    },
    0xa1 => {
        Name   => 'FNumber',
        Format => 'rational32u',
        Groups => { 2 => 'Image' },
    },
    0xa2 => {
        Name      => 'ExposureProgram',
        Format    => 'int32u',
        PrintConv => {
            0 => 'Not Defined',
            1 => 'Manual',
            2 => 'Program AE',
            3 => 'Aperture-priority AE',
            4 => 'Shutter speed priority AE',
            5 => 'Creative (Slow speed)',
            6 => 'Action (High speed)',
            7 => 'Portrait',
            8 => 'Landscape',
        },
    },
    0xa3 => {
        Name   => 'BrightnessValue',
        Format => 'rational32s',
        Groups => { 2 => 'Image' },
    },
    0xa4 => {
        Name      => 'ExposureCompensation',
        Format    => 'rational32s',
        Groups    => { 2 => 'Image' },
        PrintConv => 'Image::ExifTool::Exif::PrintFraction($val)',
    },
    0xa5 => {
        Name      => 'MaxApertureValue',
        Format    => 'rational32u',
        ValueConv => '2 ** ($val / 2)',
        PrintConv => 'sprintf("%.1f",$val)',
    },
    0xa6 => {
        Name          => 'Flash',
        Format        => 'int32u',
        Flags         => 'PrintHex',
        SeparateTable => 'EXIF Flash',
        PrintConv     => \%Image::ExifTool::Exif::flash,
    },
    0xa7 => {
        Name      => 'CustomRendered',
        Format    => 'int32u',
        Groups    => { 2 => 'Image' },
        PrintConv => {
            0 => 'Normal',
            1 => 'Custom',
        },
    },
    0xa8 => {
        Name      => 'WhiteBalance',
        Format    => 'int32u',
        Priority  => 0,
        PrintConv => {
            0 => 'Auto',
            1 => 'Manual',
        },
    },
    0xa9 => {
        Name      => 'FocalLengthIn35mmFormat',
        Format    => 'rational32u',
        PrintConv => '"$val mm"',
    },
    0xaa => {
        Name      => 'SceneCaptureType',
        Format    => 'int32u',
        PrintConv => {
            0 => 'Standard',
            1 => 'Landscape',
            2 => 'Portrait',
            3 => 'Night',
        },
    },
    0xb0 => {
        Name      => 'GPSVersionID',
        Format    => 'int8u',
        Count     => 4,
        Groups    => { 1 => 'GPS', 2 => 'Location' },
        PrintConv => '$val =~ tr/ /./; $val',
    },
    0xb1 => {
        Name      => 'GPSLatitudeRef',
        Format    => 'string',
        Groups    => { 1 => 'GPS', 2 => 'Location' },
        PrintConv => {
            N => 'North',
            S => 'South',
        },
    },
    0xb2 => {
        Name      => 'GPSLatitude',
        Format    => 'rational32u',
        Groups    => { 1 => 'GPS', 2 => 'Location' },
        Notes     => 'combined with tags 0xb3 and 0xb4',
        Combine   => 2,
        ValueConv => 'Image::ExifTool::GPS::ToDegrees($val)',
        PrintConv => 'Image::ExifTool::GPS::ToDMS($self, $val, 1)',
    },
    0xb5 => {
        Name      => 'GPSLongitudeRef',
        Format    => 'string',
        Groups    => { 1 => 'GPS', 2 => 'Location' },
        PrintConv => {
            E => 'East',
            W => 'West',
        },
    },
    0xb6 => {
        Name      => 'GPSLongitude',
        Format    => 'rational32u',
        Groups    => { 1 => 'GPS', 2 => 'Location' },
        Combine   => 2,
        Notes     => 'combined with tags 0xb7 and 0xb8',
        ValueConv => 'Image::ExifTool::GPS::ToDegrees($val)',
        PrintConv => 'Image::ExifTool::GPS::ToDMS($self, $val, 1)',
    },
    0xb9 => {
        Name      => 'GPSAltitudeRef',
        Format    => 'int32u',
        Groups    => { 1 => 'GPS', 2 => 'Location' },
        ValueConv => '$val ? 1 : 0',
        PrintConv => {
            0 => 'Above Sea Level',
            1 => 'Below Sea Level',
        },
    },
    0xba => {
        Name   => 'GPSAltitude',
        Format => 'rational32u',
        Groups => { 1 => 'GPS', 2 => 'Location' },
    },
    0xbb => {
        Name      => 'GPSTimeStamp',
        Format    => 'rational32u',
        Groups    => { 1 => 'GPS', 2 => 'Time' },
        Combine   => 2,
        Notes     => 'combined with tags 0xbc and 0xbd',
        ValueConv => 'Image::ExifTool::GPS::ConvertTimeStamp($val)',
        PrintConv => 'Image::ExifTool::GPS::PrintTimeStamp($val)',
    },
    0xbe => {
        Name      => 'GPSStatus',
        Format    => 'string',
        Groups    => { 1 => 'GPS', 2 => 'Location' },
        PrintConv => {
            A => 'Measurement Active',
            V => 'Measurement Void',
        },
    },
    0xbf => {
        Name      => 'GPSMeasureMode',
        Format    => 'string',
        Groups    => { 1 => 'GPS', 2 => 'Location' },
        PrintConv => {
            2 => '2-Dimensional Measurement',
            3 => '3-Dimensional Measurement',
        },
    },
    0xc0 => {
        Name        => 'GPSDOP',
        Description => 'GPS Dilution Of Precision',
        Format      => 'rational32u',
        Groups      => { 1 => 'GPS', 2 => 'Location' },
    },
    0xc1 => {
        Name      => 'GPSSpeedRef',
        Format    => 'string',
        Groups    => { 1 => 'GPS', 2 => 'Location' },
        PrintConv => {
            K => 'km/h',
            M => 'mph',
            N => 'knots',
        },
    },
    0xc2 => {
        Name   => 'GPSSpeed',
        Format => 'rational32u',
        Groups => { 1 => 'GPS', 2 => 'Location' },
    },
    0xc3 => {
        Name      => 'GPSTrackRef',
        Format    => 'string',
        Groups    => { 1 => 'GPS', 2 => 'Location' },
        PrintConv => {
            M => 'Magnetic North',
            T => 'True North',
        },
    },
    0xc4 => {
        Name   => 'GPSTrack',
        Format => 'rational32u',
        Groups => { 1 => 'GPS', 2 => 'Location' },
    },
    0xc5 => {
        Name      => 'GPSImgDirectionRef',
        Format    => 'string',
        Groups    => { 1 => 'GPS', 2 => 'Location' },
        PrintConv => {
            M => 'Magnetic North',
            T => 'True North',
        },
    },
    0xc6 => {
        Name   => 'GPSImgDirection',
        Format => 'rational32u',
        Groups => { 1 => 'GPS', 2 => 'Location' },
    },
    0xc7 => {
        Name    => 'GPSMapDatum',
        Format  => 'string',
        Groups  => { 1 => 'GPS', 2 => 'Location' },
        Combine => 1,
        Notes   => 'combined with tag 0xc8',
    },
    0xca => {
        Name      => 'GPSDateStamp',
        Format    => 'string',
        Groups    => { 1 => 'GPS', 2 => 'Time' },
        Combine   => 2,
        Notes     => 'combined with tags 0xcb and 0xcc',
        ValueConv => 'Image::ExifTool::Exif::ExifDate($val)',
    },
    0xe0 => {
        Name         => 'MakeModel',
        SubDirectory => { TagTable => 'Image::ExifTool::H264::MakeModel' },
    },
    0xe1 => {
        Name         => 'RecInfo',
        Condition    => '$$self{Make} eq "Canon"',
        Notes        => 'Canon only',
        SubDirectory => { TagTable => 'Image::ExifTool::H264::RecInfo' },
    },
    0xe4 => {
        Name        => 'Model',
        Condition   => '$$self{Make} eq "Sony"',
        Description => 'Camera Model Name',
        Notes       => 'Sony only, combined with tags 0xe5 and 0xe6',
        Format      => 'string',
        Combine     => 2,
        RawConv     => '$val eq "" ? undef : $val',
    },
    0xee => {
        Name         => 'FrameInfo',
        Condition    => '$$self{Make} eq "Canon"',
        Notes        => 'Canon only',
        SubDirectory => { TagTable => 'Image::ExifTool::H264::FrameInfo' },
    },
);

%Image::ExifTool::H264::Camera1 = (
    PROCESS_PROC => \&Image::ExifTool::ProcessBinaryData,
    GROUPS       => { 2 => 'Camera' },
    TAG_PREFIX   => 'Camera1',
    PRINT_CONV   => 'sprintf("0x%.2x",$val)',
    FIRST_ENTRY  => 0,
    0            => {
        Name      => 'ApertureSetting',
        PrintHex  => 1,
        PrintConv => {
            0xff  => 'Auto',
            0xfe  => 'Closed',
            OTHER => sub { sprintf( '%.1f', 2**( ( $_[0] & 0x3f ) / 8 ) ) },
        },
    },
    1 => {
        Name => 'Gain',
        Mask => 0x0f,
        ValueConv => '($val - 1) * 3',
        PrintConv => '$val==42 ? "Out of range" : "$val dB"',
    },
    1.1 => {
        Name      => 'ExposureProgram',
        Mask      => 0xf0,
        ValueConv => '$val == 15 ? undef : $val',
        PrintConv => {
            0 => 'Program AE',
            1 => 'Gain',
            2 => 'Shutter speed priority AE',
            3 => 'Aperture-priority AE',
            4 => 'Manual',
        },
    },
    2.1 => {
        Name      => 'WhiteBalance',
        Mask      => 0xe0,
        ValueConv => '$val == 7 ? undef : $val',
        PrintConv => {
            0 => 'Auto',
            1 => 'Hold',
            2 => '1-Push',
            3 => 'Daylight',
        },
    },
    3 => {
        Name      => 'Focus',
        ValueConv => '$val == 0xff ? undef : $val',
        PrintConv => q{
            my $foc = ($val & 0x7e) / (($val & 0x01) ? 40 : 400);
            return(($val & 0x80 ? 'Manual' : 'Auto') . " ($foc)");
        },
    },
);

%Image::ExifTool::H264::Camera2 = (
    PROCESS_PROC => \&Image::ExifTool::ProcessBinaryData,
    GROUPS       => { 2 => 'Camera' },
    TAG_PREFIX   => 'Camera2',
    PRINT_CONV   => 'sprintf("0x%.2x",$val)',
    FIRST_ENTRY  => 0,
    1            => {
        Name      => 'ImageStabilization',
        PrintHex  => 1,
        PrintConv => {
            0     => 'Off',
            0x3f  => 'On (0x3f)',
            0xbf  => 'Off (0xbf)',
            0xff  => 'n/a',
            OTHER => sub {
                my $val = shift;
                sprintf( "%s (0x%.2x)", $val & 0x10 ? "On" : "Off", $val );
            },
        },
    },
);

%Image::ExifTool::H264::Shutter = (
    PROCESS_PROC => \&Image::ExifTool::ProcessBinaryData,
    GROUPS       => { 2 => 'Image' },
    TAG_PREFIX   => 'Shutter',
    PRINT_CONV   => 'sprintf("0x%.2x",$val)',
    FIRST_ENTRY  => 0,
    FORMAT       => 'int16u',
    1.1          => {
        Name      => 'ExposureTime',
        Mask      => 0x7fff,
        RawConv   => '$val == 0x7fff ? undef : $val',
        ValueConv => '$val / 28125',
        PrintConv => 'Image::ExifTool::Exif::PrintExposureTime($val)',
    },
);

%Image::ExifTool::H264::MakeModel = (
    PROCESS_PROC => \&Image::ExifTool::ProcessBinaryData,
    GROUPS       => { 2 => 'Camera' },
    FORMAT       => 'int16u',
    FIRST_ENTRY  => 0,
    0            => {
        Name     => 'Make',
        PrintHex => 1,
        RawConv  =>
'$$self{Make} = ($Image::ExifTool::H264::convMake{$val} || "Unknown"); $val',
        PrintConv => \%convMake,
    },
);

%Image::ExifTool::H264::RecInfo = (
    PROCESS_PROC => \&Image::ExifTool::ProcessBinaryData,
    GROUPS       => { 2 => 'Camera' },
    FORMAT       => 'int8u',
    NOTES        => 'Recording information stored by some Canon video cameras.',
    FIRST_ENTRY  => 0,
    0            => {
        Name      => 'RecordingMode',
        PrintConv => {
            0x02 => 'XP+',
            0x04 => 'SP',
            0x05 => 'LP',
            0x06 => 'FXP',
            0x07 => 'MXP',
        },
    },
);

%Image::ExifTool::H264::FrameInfo = (
    PROCESS_PROC => \&Image::ExifTool::ProcessBinaryData,
    GROUPS       => { 2 => 'Video' },
    FORMAT       => 'int8u',
    NOTES       => 'Frame rate information stored by some Canon video cameras.',
    FIRST_ENTRY => 0,
    0           => 'CaptureFrameRate',
    1           => 'VideoFrameRate',
);

sub ReadNextWord($) {
    my $bstr = shift;
    my $pos  = $$bstr{Pos};
    if ( $pos + 4 <= $$bstr{Len} ) {
        $$bstr{Word} = unpack( "x$pos N", ${ $$bstr{DataPt} } );
        $$bstr{Mask} = 0x80000000;
        $$bstr{Pos} += 4;
    }
    elsif ( $pos < $$bstr{Len} ) {
        my @bytes = unpack( "x$pos C*", ${ $$bstr{DataPt} } );
        my ( $word, $mask ) = ( shift(@bytes), 0x80 );
        while (@bytes) {
            $word = ( $word << 8 ) | shift(@bytes);
            $mask <<= 8;
        }
        $$bstr{Word} = $word;
        $$bstr{Mask} = $mask;
        $$bstr{Pos}  = $$bstr{Len};
    }
    else {
        return 0;
    }
    return 1;
}

sub NewBitStream($) {
    my $dataPt = shift;
    my $bstr   = {
        DataPt => $dataPt,
        Len    => length($$dataPt),
        Pos    => 0,
        Mask   => 0,
    };
    ReadNextWord($bstr) or undef $bstr;
    return $bstr;
}

sub BitsLeft($) {
    my $bstr = shift;
    my $bits = 0;
    my $mask = $$bstr{Mask};
    while ($mask) {
        ++$bits;
        $mask >>= 1;
    }
    return $bits + 8 * ( $$bstr{Len} - $$bstr{Pos} );
}

sub GetIntN($$) {
    my ( $bstr, $bits ) = @_;
    my $val = 0;
    while ( $bits-- ) {
        $val <<= 1;
        ++$val if $$bstr{Mask} & $$bstr{Word};
        $$bstr{Mask} >>= 1 and next;
        ReadNextWord($bstr) or last;
    }
    return $val;
}

sub GetGolomb($) {
    my $bstr = shift;
    my $count = 0;
    until ( $$bstr{Mask} & $$bstr{Word} ) {
        ++$count;
        $$bstr{Mask} >>= 1 and next;
        ReadNextWord($bstr) or last;
    }
    return GetIntN( $bstr, $count + 1 ) - 1;
}

sub GetGolombS($) {
    my $bstr = shift;
    my $val  = GetGolomb($bstr) + 1;
    return ( $val & 1 ) ? -( $val >> 1 ) : ( $val >> 1 );
}

sub DecodeScalingMatrices($) {
    my $bstr = shift;
    if ( GetIntN( $bstr, 1 ) ) {
        my ( $i, $j );
        for ( $i = 0 ; $i < 8 ; ++$i ) {
            my $size = $i < 6 ? 16 : 64;
            next unless GetIntN( $bstr, 1 );
            my ( $last, $next ) = ( 8, 8 );
            for ( $j = 0 ; $j < $size ; ++$j ) {
                $next = ( $last + GetGolombS($bstr) ) & 0xff if $next;
                last unless $j or $next;
            }
        }
    }
}

sub ParseSeqParamSet($$$) {
    my ( $et, $tagTablePtr, $dataPt ) = @_;
    my $bstr = NewBitStream($dataPt) or return;
    my ( $t, $i, $j, $n );
    $t = GetIntN( $bstr, 8 );
    GetIntN( $bstr, 16 );
    GetGolomb($bstr);
    if ( $t >= 100 ) {
        $t = GetGolomb($bstr);
        if ( $t == 3 ) {
            GetIntN( $bstr, 1 );
            $n = 12;
        }
        else {
            $n = 8;
        }
        GetGolomb($bstr);
        GetGolomb($bstr);
        GetIntN( $bstr, 1 );
        DecodeScalingMatrices($bstr);
    }
    GetGolomb($bstr);
    $t = GetGolomb($bstr);
    if ( $t == 0 ) {
        GetGolomb($bstr);
    }
    elsif ( $t == 1 ) {
        GetIntN( $bstr, 1 );
        GetGolomb($bstr);
        GetGolomb($bstr);
        $n = GetGolomb($bstr);
        for ( $i = 0 ; $i < $n ; ++$i ) {
            GetGolomb($bstr);
        }
    }
    GetGolomb($bstr);
    GetIntN( $bstr, 1 );
    my $w = GetGolomb($bstr);
    my $h = GetGolomb($bstr);
    my $f = GetIntN( $bstr, 1 );
    $f or GetIntN( $bstr, 1 );
    GetIntN( $bstr, 1 );

    $w = ( $w + 1 ) * 16;
    $h = ( 2 - $f ) * ( $h + 1 ) * 16;
    $t = GetIntN( $bstr, 1 );
    if ($t) {
        my $m = 4 - $f * 2;
        $w -= 4 * GetGolomb($bstr);
        $w -= 4 * GetGolomb($bstr);
        $h -= $m * GetGolomb($bstr);
        $h -= $m * GetGolomb($bstr);
    }
    return unless $$bstr{Mask};
    if ( $w >= 160 and $w <= 4096 and $h >= 120 and $h <= 3072 ) {
        $et->HandleTag( $tagTablePtr, ImageWidth  => $w );
        $et->HandleTag( $tagTablePtr, ImageHeight => $h );
    }
    return unless $parsePictureTiming;

    GetIntN( $bstr, 1 ) or return;
    $t = GetIntN( $bstr, 1 );
    if ($t) {
        $t = GetIntN( $bstr, 8 );
        if ( $t == 255 ) {
            GetIntN( $bstr, 32 );
        }
    }
    $t = GetIntN( $bstr, 1 );
    GetIntN( $bstr, 1 ) if $t;
    $t = GetIntN( $bstr, 1 );
    if ($t) {
        GetIntN( $bstr, 4 );
        $t = GetIntN( $bstr, 1 );
        GetIntN( $bstr, 24 ) if $t;
    }
    $t = GetIntN( $bstr, 1 );
    if ($t) {
        GetGolomb($bstr);
        GetGolomb($bstr);
    }
    $t = GetIntN( $bstr, 1 );
    if ($t) {
        return if BitsLeft($bstr) < 65;
        $$et{VUI_units} = GetIntN( $bstr, 32 );
        $$et{VUI_scale} = GetIntN( $bstr, 32 );
        GetIntN( $bstr, 1 );
    }
    my $hard;
    for ( $j = 0 ; $j < 2 ; ++$j ) {
        $t = GetIntN( $bstr, 1 );
        if ($t) {
            $$et{VUI_hard} = 1;
            $hard          = 1;
            $n             = GetGolomb($bstr);
            GetIntN( $bstr, 8 );
            for ( $i = 0 ; $i <= $n ; ++$i ) {
                GetGolomb($bstr);
                GetGolomb($bstr);
                GetIntN( $bstr, 1 );
            }
            GetIntN( $bstr, 5 );
            $$et{VUI_clen} = GetIntN( $bstr, 5 );
            $$et{VUI_dlen} = GetIntN( $bstr, 5 );
            $$et{VUI_toff} = GetIntN( $bstr, 5 );
        }
    }
    GetIntN( $bstr, 1 ) if $hard;
    $$et{VUI_pic} = GetIntN( $bstr, 1 );

}

sub ParsePictureTiming($$) {
    my ( $et, $dataPt ) = @_;
    my $bstr = NewBitStream($dataPt) or return;
    my ( $i, $t, $n );
    if ( $$et{VUI_hard} ) {
        GetIntN( $bstr, $$et{VUI_clen} + 1 );
        GetIntN( $bstr, $$et{VUI_dlen} + 1 );
    }
    if ( $$et{VUI_pic} ) {
        $t = GetIntN( $bstr, 4 );

        $n = {
            0 => 1,
            1 => 1,
            2 => 1,
            3 => 2,
            4 => 2,
            5 => 3,
            6 => 3,
            7 => 2,
            8 => 3
        }->{$t};
        $n or return;
        for ( $i = 0 ; $i < $n ; ++$i ) {
            $t = GetIntN( $bstr, 1 );
            next unless $t;
            my ( $nu, $s, $m, $h, $o );
            GetIntN( $bstr, 2 );
            $nu = GetIntN( $bstr, 1 );
            GetIntN( $bstr, 5 );
            $t = GetIntN( $bstr, 1 );
            GetIntN( $bstr, 1 );
            GetIntN( $bstr, 1 );
            GetIntN( $bstr, 8 );

            if ($t) {
                $s = GetIntN( $bstr, 6 );
                $m = GetIntN( $bstr, 6 );
                $h = GetIntN( $bstr, 5 );
            }
            else {
                $t = GetIntN( $bstr, 1 );
                if ($t) {
                    $s = GetIntN( $bstr, 6 );
                    $t = GetIntN( $bstr, 1 );
                    if ($t) {
                        $m = GetIntN( $bstr, 6 );
                        $t = GetIntN( $bstr, 1 );
                        $h = GetIntN( $bstr, 5 ) if $t;
                    }
                }
            }
            if ( $$et{VUI_toff} ) {
                $o = GetIntN( $bstr, $$et{VUI_toff} );
            }
            last;
        }
    }
}

sub ProcessSEI($$) {
    my ( $et, $dirInfo ) = @_;
    my $dataPt = $$dirInfo{DataPt};
    my $end    = length($$dataPt);
    my $pos    = 0;
    my ( $type, $size, $index, $t );

    for ( ; ; ) {
        $type = 0;
        for ( ; ; ) {
            return 0 if $pos >= $end;
            $t = Get8u( $dataPt, $pos++ );
            $type += $t;
            last unless $t == 255;
        }
        return 0 if $type == 0x80;
        $size = 0;
        for ( ; ; ) {
            return 0 if $pos >= $end;
            $t = Get8u( $dataPt, $pos++ );
            $size += $t;
            last unless $t == 255;
        }
        return 0 if $pos + $size > $end;
        $et->VPrint( 1, "    (SEI type $type)\n" );
        if ( $type == 1 ) {
            if ($parsePictureTiming) {
                my $buff = substr( $$dataPt, $pos, $size );
                ParsePictureTiming( $et, $dataPt );
            }
        }
        elsif ( $type == 5 ) {
            last;
        }
        $pos += $size;
    }

    return 0
      unless $size > 20
      and substr( $$dataPt, $pos, 20 ) eq
      "\x17\xee\x8c\x60\xf8\x4d\x11\xd9\x8c\xd6\x08\0\x20\x0c\x9a\x66MDPM";
    my $tagTablePtr = GetTagTable('Image::ExifTool::H264::MDPM');
    my $oldIndent   = $$et{INDENT};
    $$et{INDENT} .= '| ';
    $end = $pos + $size;
    $pos += 20;
    my $num     = Get8u( $dataPt, $pos++ );
    my $lastTag = 0;
    $et->VerboseDir( 'MDPM', $num ) if $et->Options('Verbose');

    for ( $index = 0 ; $index < $num and $pos < $end ; ++$index ) {
        my $tag = Get8u( $dataPt, $pos );
        if ( $tag <= $lastTag ) {
            $et->Warn('Entries in MDPM directory are out of sequence');
            last;
        }
        $lastTag = $tag;
        my $buff = substr( $$dataPt, $pos + 1, 4 );
        my $from;
        my $tagInfo = $et->GetTagInfo( $tagTablePtr, $tag );
        if ($tagInfo) {
            if ( $$tagInfo{Unknown} and not $$tagInfo{SetPrintConv} ) {
                $$tagInfo{PrintConv} = 'sprintf("0x%.8x", unpack("N", $val))';
                $$tagInfo{SetPrintConv} = 1;
            }
            my $combine = $$tagTablePtr{$tag}{Combine};
            while ($combine) {
                last if $pos + 5 >= $end;
                my $t = Get8u( $dataPt, $pos + 5 );
                last if $t != $lastTag + 1;
                $pos += 5;
                $buff .= substr( $$dataPt, $pos + 1, 4 );
                $from = $index unless defined $from;
                ++$index;
                ++$lastTag;
                --$combine;
            }
            $et->HandleTag(
                $tagTablePtr, $tag, undef,
                TagInfo => $tagInfo,
                DataPt  => \$buff,
                Size    => length($buff),
                Index   => defined $from ? "$from-$index" : $index,
            );
        }
        $pos += 5;
    }
    $$et{INDENT} = $oldIndent;
    return 1;
}

sub ParseH264Video($$) {
    my ( $et, $dataPt ) = @_;
    my $verbose      = $et->Options('Verbose');
    my $out          = $et->Options('TextOut');
    my $tagTablePtr  = GetTagTable('Image::ExifTool::H264::Main');
    my %parseNalUnit = ( 0x06 => 1, 0x07 => 1 );
    my $foundUserData;
    my $len = length $$dataPt;
    my $pos = 0;

    while ( $pos < $len ) {
        my ( $nextPos, $end );
        if ( $$dataPt =~ /(\0{2,3}\x01)/g ) {
            $nextPos     = pos $$dataPt;
            $end         = $nextPos - length $1;
            $pos or $pos = $nextPos, next;
        }
        else {
            last unless $pos;
            $nextPos = $end = $len;
        }
        last if $pos >= $len;
        my $nal_unit_type = Get8u( $dataPt, $pos );
        ++$pos;
        $nal_unit_type & 0x80 and $et->Warn('H264 forbidden bit error'), last;
        $nal_unit_type &= 0x1f;
        $parseNalUnit{$nal_unit_type} or $verbose or $pos = $nextPos, next;
        my $buff = '';
        pos($$dataPt) = $pos + 1;

        while ( $$dataPt =~ /\0\0\x03/g ) {
            last if pos $$dataPt > $end;
            $buff .= substr( $$dataPt, $pos, pos($$dataPt) - 1 - $pos );
            $pos = pos $$dataPt;
        }
        $buff .= substr( $$dataPt, $pos, $end - $pos );
        if ( $verbose > 1 ) {
            printf $out "  NAL Unit Type: 0x%x (%d bytes)\n", $nal_unit_type,
              length $buff;
            $et->VerboseDump( \$buff );
        }
        pos($$dataPt) = $pos = $nextPos;

        if ( $nal_unit_type == 0x06 ) {

            if ( $$et{GotNAL06} ) {
                next unless $et->Options('ExtractEmbedded');
                $$et{DOC_NUM} = $$et{GotNAL06};
            }
            $foundUserData = ProcessSEI( $et, { DataPt => \$buff } );
            delete $$et{DOC_NUM};
            next unless $foundUserData;
            $$et{GotNAL06} = ( $$et{GotNAL06} || 0 ) + 1;

        }
        elsif ( $nal_unit_type == 0x07 ) {

            next if $$et{GotNAL07};
            $$et{GotNAL07} = 1;
            ParseSeqParamSet( $et, $tagTablePtr, \$buff );
        }
        delete $parseNalUnit{$nal_unit_type};
    }
    return 0 if $foundUserData or $$et{ParsedH264};
    $$et{ParsedH264} = 1;
    return 1;
}

1;

__END__


