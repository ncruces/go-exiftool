
package Image::ExifTool::FLAC;

use strict;
use vars            qw($VERSION);
use Image::ExifTool qw(:DataAccess :Utils);

$VERSION = '1.09';

sub ProcessBitStream($$$);

%Image::ExifTool::FLAC::Main = (
    NOTES => q{
        Free Lossless Audio Codec (FLAC) meta information.  ExifTool also extracts
        ID3 information from these files.
    },
    0 => {
        Name         => 'StreamInfo',
        SubDirectory => { TagTable => 'Image::ExifTool::FLAC::StreamInfo' },
    },
    1 => { Name => 'Padding', Binary => 1, Unknown => 1 },
    2 => [
        {
            Name         => 'Application_riff',
            Condition    => '$$valPt =~ /^riff(?!RIFF)/',
            SubDirectory => {
                TagTable  => 'Image::ExifTool::RIFF::Main',
                ByteOrder => 'LittleEndian',
                Start     => 4,
            },
        },
        {
            Name    => 'ApplicationUnknown',
            Binary  => 1,
            Unknown => 1,
        }
    ],
    3 => { Name => 'SeekTable', Binary => 1, Unknown => 1 },
    4 => {
        Name         => 'VorbisComment',
        SubDirectory => { TagTable => 'Image::ExifTool::Vorbis::Comments' },
    },
    5 => { Name => 'CueSheet', Binary => 1, Unknown => 1 },
    6 => {
        Name         => 'Picture',
        SubDirectory => { TagTable => 'Image::ExifTool::FLAC::Picture' },
    },
);

%Image::ExifTool::FLAC::StreamInfo = (
    PROCESS_PROC => \&ProcessBitStream,
    NOTES        =>
      'FLAC is big-endian, so bit 0 is the high-order bit in this table.',
    GROUPS       => { 2 => 'Audio' },
    'Bit000-015' => 'BlockSizeMin',
    'Bit016-031' => 'BlockSizeMax',
    'Bit032-055' => 'FrameSizeMin',
    'Bit056-079' => 'FrameSizeMax',
    'Bit080-099' => 'SampleRate',
    'Bit100-102' => {
        Name      => 'Channels',
        ValueConv => '$val + 1',
    },
    'Bit103-107' => {
        Name      => 'BitsPerSample',
        ValueConv => '$val + 1',
    },
    'Bit108-143' => 'TotalSamples',
    'Bit144-271' => {
        Name      => 'MD5Signature',
        Format    => 'undef',
        ValueConv => 'unpack("H*",$val)',
    },
);

%Image::ExifTool::FLAC::Picture = (
    PROCESS_PROC => \&Image::ExifTool::ProcessBinaryData,
    GROUPS       => { 2 => 'Image' },
    FORMAT       => 'int32u',
    0            => {
        Name      => 'PictureType',
        PrintConv => {
            0  => 'Other',
            1  => '32x32 PNG Icon',
            2  => 'Other Icon',
            3  => 'Front Cover',
            4  => 'Back Cover',
            5  => 'Leaflet',
            6  => 'Media',
            7  => 'Lead Artist',
            8  => 'Artist',
            9  => 'Conductor',
            10 => 'Band',
            11 => 'Composer',
            12 => 'Lyricist',
            13 => 'Recording Studio or Location',
            14 => 'Recording Session',
            15 => 'Performance',
            16 => 'Capture from Movie or Video',
            17 => 'Bright(ly) Colored Fish',
            18 => 'Illustration',
            19 => 'Band Logo',
            20 => 'Publisher Logo',
        },
    },
    1 => {
        Name   => 'PictureMIMEType',
        Format => 'var_pstr32',
    },
    2 => {
        Name      => 'PictureDescription',
        Format    => 'var_pstr32',
        ValueConv => '$self->Decode($val, "UTF8")',
    },
    3 => 'PictureWidth',
    4 => 'PictureHeight',
    5 => 'PictureBitsPerPixel',
    6 => 'PictureIndexedColors',
    7 => 'PictureLength',
    8 => {
        Name   => 'Picture',
        Groups => { 2 => 'Preview' },
        Format => 'undef[$val{7}]',
        Binary => 1,
    },
);

%Image::ExifTool::FLAC::Composite = (
    Duration => {
        Require => {
            0 => 'FLAC:SampleRate',
            1 => 'FLAC:TotalSamples',
        },
        ValueConv => '($val[0] and $val[1]) ? $val[1] / $val[0] : undef',
        PrintConv => 'ConvertDuration($val)',
    },
);

Image::ExifTool::AddCompositeTags('Image::ExifTool::FLAC');

sub ProcessBitStream($$$) {
    my ( $et, $dirInfo, $tagTablePtr ) = @_;
    my $dataPt    = $$dirInfo{DataPt};
    my $dataPos   = $$dirInfo{DataPos};
    my $dirStart  = $$dirInfo{DirStart} || 0;
    my $dirLen    = $$dirInfo{DirLen}   || ( length($$dataPt) - $dirStart );
    my $verbose   = $et->Options('Verbose');
    my $byteOrder = GetByteOrder();
    my $tag;

    if ($verbose) {
        $et->VPrint( 0,
            "  + [BitStream directory, $dirLen bytes, '${byteOrder}' order]\n"
        );
    }
    foreach $tag ( sort keys %$tagTablePtr ) {
        next unless $tag =~ /^Bit(\d+)-?(\d+)?/;
        my ( $b1, $b2 ) = ( $1, $2 || $1 );
        my ( $i1, $i2 ) = ( int( $b1 / 8 ), int( $b2 / 8 ) );
        my ( $f1, $f2 ) = ( $b1 % 8, $b2 % 8 );
        last if $i2 >= $dirLen;
        my ( $val, $extra );
        if ( ref $$tagTablePtr{$tag} ne 'HASH'
            or not $$tagTablePtr{$tag}{Format} )
        {
            my ( $i, $mask );
            $val   = 0;
            $extra = ', Mask=0x' if $verbose and ( $f1 != 0 or $f2 != 7 );
            if ( $byteOrder eq 'MM' ) {
                for ( $i = $i1 ; $i <= $i2 ; ++$i ) {
                    $mask = 0xff;
                    if ( $i == $i1 and $f1 ) {
                        foreach ( ( 8 - $f1 ) .. 7 ) { $mask ^= ( 1 << $_ ) }
                    }
                    if ( $i == $i2 and $f2 < 7 ) {
                        foreach ( 0 .. ( 6 - $f2 ) ) { $mask ^= ( 1 << $_ ) }
                    }
                    $val =
                      $val * 256 + ( $mask & Get8u( $dataPt, $i + $dirStart ) );
                    $extra .= sprintf( '%.2x', $mask ) if $extra;
                }
            }
            else {
                for ( $i = $i2 ; $i >= $i1 ; --$i ) {
                    $mask = 0xff;
                    if ( $i == $i1 and $f1 ) {
                        foreach ( 0 .. ( $f1 - 1 ) ) { $mask ^= ( 1 << $_ ) }
                    }
                    if ( $i == $i2 and $f2 < 7 ) {
                        foreach ( ( $f2 + 1 ) .. 7 ) { $mask ^= ( 1 << $_ ) }
                    }
                    $val =
                      $val * 256 + ( $mask & Get8u( $dataPt, $i + $dirStart ) );
                    $extra .= sprintf( '%.2x', $mask ) if $extra;
                }
            }
            until ( $mask & 0x01 ) {
                $val /= 2;
                $mask >>= 1;
            }
        }
        $et->HandleTag(
            $tagTablePtr, $tag, $val,
            DataPt  => $dataPt,
            DataPos => $dataPos,
            Start   => $dirStart + $i1,
            Size    => $i2 - $i1 + 1,
            Extra   => $extra,
        );
    }
    return 1;
}

sub ProcessFLAC($$) {
    my ( $et, $dirInfo ) = @_;

    unless ( $$et{DoneID3} ) {
        require Image::ExifTool::ID3;
        Image::ExifTool::ID3::ProcessID3( $et, $dirInfo ) and return 1;
    }
    my $raf     = $$dirInfo{RAF};
    my $verbose = $et->Options('Verbose');
    my $out     = $et->Options('TextOut');
    my ( $buff, $err );

    $raf->Read( $buff, 4 ) == 4 and $buff eq 'fLaC' or return 0;
    $et->SetFileType();
    SetByteOrder('MM');
    my $tagTablePtr = GetTagTable('Image::ExifTool::FLAC::Main');
    for ( ; ; ) {
        $raf->Read( $buff, 4 ) == 4 or last;
        my $flag = unpack( 'C', $buff );
        my $size = unpack( 'N', $buff ) & 0x00ffffff;
        $raf->Read( $buff, $size ) == $size or $err = 1, last;
        my $last = $flag & 0x80;
        my $tag  = $flag & 0x7f;
        if ($verbose) {
            print $out "FLAC metadata block, type $tag:\n";
            $et->VerboseDump( \$buff, DataPos => $raf->Tell() - $size );
        }
        $et->HandleTag(
            $tagTablePtr, $tag, $buff,
            DataPt  => \$buff,
            DataPos => $raf->Tell() - $size,
            Start   => 0,
            Size    => $size,
        );
        last if $last;
    }
    $err and $et->Warn('Format error in FLAC file');
    return 1;
}

1;

__END__


