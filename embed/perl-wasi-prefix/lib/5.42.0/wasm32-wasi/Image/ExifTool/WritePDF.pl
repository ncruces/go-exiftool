package Image::ExifTool::PDF;

use strict;
use vars qw($lastFetched);

sub WriteObject($$);
sub EncodeString($);
sub CryptObject($);

my $beginComment = '%BeginExifToolUpdate';
my $endComment   = '%EndExifToolUpdate ';

my $keyExt;
my $pdfVer;

my %myDictTags = (
    _tags       => 1,
    _stream     => 1,
    _decrypted  => 1,
    _needCrypt  => 1,
    _filtered   => 1,
    _entry_size => 1,
    _table      => 1,
);

my %pdfMap = ( XMP => 'PDF', );

sub CheckPDF($$$) {
    my ( $et, $tagInfo, $valPtr ) = @_;
    my $format = $$tagInfo{Writable} || $tagInfo->{Table}->{WRITABLE};
    if ( not $format ) {
        return 'No writable format';
    }
    elsif ( $format eq 'string' ) {
    }
    elsif ( $format eq 'date' ) {
        return 'Bad date format' unless $$valPtr =~ /^\d{4}/;
    }
    elsif ( $format eq 'integer' ) {
        return 'Not an integer' unless Image::ExifTool::IsInt($$valPtr);
    }
    elsif ( $format eq 'real' ) {
        return 'Not a real number'
          unless $$valPtr =~ /^[+-]?(?=\d|\.\d)\d*(\.\d*)?$/;
    }
    elsif ( $format eq 'boolean' ) {
        $$valPtr = ( $$valPtr and $$valPtr !~ /^f/i ) ? 'true' : 'false';
    }
    elsif ( $format eq 'name' ) {
        return 'Invalid PDF name' if $$valPtr =~ /\0/;
    }
    else {
        return "Invalid PDF format '${format}'";
    }
    return undef;
}

sub WritePDFValue($$$) {
    my ( $et, $val, $format ) = @_;
    if ( not $format ) {
        return undef;
    }
    elsif ( $format eq 'string' ) {
        $val = "\xfe\xff" . $et->Encode( $val, 'UCS2', 'MM' )
          if $val =~ /[\x80-\xff]/;
        EncodeString( \$val );
    }
    elsif ( $format eq 'date' ) {
        $val =~ s/(:\d{2})\.\d*/$1/;
        $val =~ s/([-+]\d{2}):(\d{2})/${1}'${2}'/;
        $val =~ tr/ ://d;
        $val = "D:$val";
        EncodeString( \$val );
    }
    elsif ( $format =~ /^(integer|real|boolean)$/ ) {
    }
    elsif ( $format eq 'name' ) {
        return undef if $val =~ /\0/;
        if ( $pdfVer >= 1.2 ) {
            $val =~ s/([\t\n\f\r ()<>[\]{}\/%#])/sprintf('#%.2x',ord $1)/sge;
        }
        else {
            return undef if $val =~ /[\t\n\f\r ()<>[\]{}\/%]/;
        }
        $val = "/$val";
    }
    else {
        return undef;
    }
    return $val;
}

sub EncodeString($) {
    my $strPt = shift;
    if ( ref $$strPt eq 'ARRAY' ) {
        my $str;
        foreach $str ( @{$$strPt} ) {
            EncodeString( \$str );
        }
        return;
    }
    Crypt( $strPt, $keyExt, 1 );

    if ( $$strPt =~ /[\x00-\x08\x0a-\x1f\x7f\xff]/ ) {
        my $str = '';
        my $len = length $$strPt;
        my $i   = 0;
        for ( ; ; ) {
            my $n = $len - $i or last;
            $n = 40 if $n > 40;
            $str .= $/ if $i;
            $str .= unpack( 'H*', substr( $$strPt, $i, $n ) );
            $i += $n;
        }
        $$strPt = "<$str>";
    }
    else {
        $$strPt =~ s/([()\\])/\\$1/g;
        $$strPt = "($$strPt)";
    }
}

sub CryptObject($) {
    my $obj = $_[0];
    if ( not ref $obj ) {
        if ( $obj =~ /^[(<]/ ) {
            undef $lastFetched;
            my $val = ReadPDFValue($obj);
            EncodeString( \$val );
            $_[0] = $val;
        }
    }
    elsif ( ref $obj eq 'HASH' ) {
        my $tag;
        my $needCrypt = $$obj{_needCrypt};
        foreach $tag ( keys %$obj ) {
            next if $myDictTags{$tag};
            if ($needCrypt) {
                next
                  unless defined $$needCrypt{$tag}
                  ? $$needCrypt{$tag}
                  : $$needCrypt{'*'};
            }
            CryptObject( $$obj{$tag} );
        }
        delete $$obj{_needCrypt};
    }
    elsif ( ref $obj eq 'ARRAY' ) {
        my $val;
        foreach $val (@$obj) {
            CryptObject($val);
        }
    }
}

sub GetFreeEntries($) {
    my $dict = shift;
    my %xrefFree;
    my $w = $$dict{W};
    if ( ref $w eq 'ARRAY' ) {
        my $bytes = "@$w";
        my $fmt;
        if ( $bytes eq '1 4 2' ) {
            $fmt = 'CNn';
        }
        elsif ( $bytes eq '1 8 2' ) {
            $fmt = 'CNNn';
        }
        else {
            return \%xrefFree;
        }
        my $size  = $$dict{_entry_size};
        my $index = $$dict{Index};
        my $len   = length $$dict{_stream};
        my $num = scalar(@$index) / 2;
        my $pos = 0;
        my ( $i, $j );
        for ( $i = 0 ; $i < $num ; ++$i ) {
            my $start = $$index[ $i * 2 ];
            my $count = $$index[ $i * 2 + 1 ];
            for ( $j = 0 ; $j < $count ; ++$j ) {
                last if $pos + $size > $len;
                my @t = unpack( "x$pos $fmt", $$dict{_stream} );
                if ( @t == 4 ) {
                    $t[1] = $t[1] * 4294967296 + $t[2];
                    $t[2] = $t[3];
                    @t    = 3;
                }
                $xrefFree{ $start + $j } = [ $t[1], $t[2], 'f' ] if $t[0] == 0;
                $pos += $size;
            }
        }
    }
    return \%xrefFree;
}

sub WriteObject($$) {
    my ( $outfile, $obj ) = @_;
    if ( ref $obj eq 'SCALAR' ) {
        Write( $outfile, ' ', $$obj ) or return 0;
    }
    elsif ( ref $obj eq 'ARRAY' ) {
        Write( $outfile, @$obj > 10 ? $/ : ' ', '[' ) or return 0;
        my $item;
        foreach $item (@$obj) {
            WriteObject( $outfile, $item ) or return 0;
        }
        Write( $outfile, ' ]' ) or return 0;
    }
    elsif ( ref $obj eq 'HASH' ) {
        my $tag;
        Write( $outfile, $/, '<<' ) or return 0;
        if ( $$obj{_stream} ) {
            CryptStream( $obj, $keyExt ) if $$obj{_decrypted};
            $$obj{Length} = length $$obj{_stream};
            push @{ $$obj{_tags} }, 'Length';
            delete $$obj{Filter};
            delete $$obj{DecodeParms};
            delete $$obj{DL};
        }
        my %wrote = %myDictTags;
        foreach $tag ( @{ $$obj{_tags} }, sort keys %$obj ) {
            next if $wrote{$tag}                 or not defined $$obj{$tag};
            Write( $outfile, $/, "/$tag" )       or return 0;
            WriteObject( $outfile, $$obj{$tag} ) or return 0;
            $wrote{$tag} = 1;
        }
        Write( $outfile, $/, '>>' ) or return 0;
        if ( $$obj{_stream} ) {
            Write( $outfile, $/, "stream\x0d\x0a" ) or return 0;
            Write( $outfile, $$obj{_stream}, $/, 'endstream' ) or return 0;
        }
    }
    else {
        Write( $outfile, ' ', $obj );
    }
    return 1;
}

sub WritePDF($$) {
    my ( $et, $dirInfo ) = @_;
    my $raf     = $$dirInfo{RAF};
    my $outfile = $$dirInfo{OutFile};
    my ( $buff, %capture, %newXRef, %newObj, $objRef );
    my ( $out, $id, $gen, $obj );

    my $pos = $raf->Tell();
    $raf->Read( $buff, 1024 ) >= 8   or return 0;
    $buff =~ /^(\s*)%PDF-(\d+\.\d+)/ or return 0;
    $$et{PDFBase} = length $1;
    $raf->Seek( $pos, 0 );

    my $newTool = Image::ExifTool->new;
    $newTool->Options( List      => 1 );
    $newTool->Options( Password  => $et->Options('Password') );
    $newTool->Options( NoPDFList => $et->Options('NoPDFList') );
    $$newTool{PDF_CAPTURE} = \%capture;
    my $info = $newTool->ImageInfo( $raf, 'XMP', 'PDF:*', 'Error', 'Warning' );
    $pdfVer = $$newTool{PDFVersion};
    $pdfVer or $et->Error('Missing PDF:PDFVersion'), return 0;

    if ( $pdfVer > 2.0 ) {
        $et->Error( "Writing PDF $pdfVer is untested", 1 ) and return 0;
    }
    if ( $capture{Error} or $$info{Error} ) {
        $et->Error( $capture{Error} || $$info{Error} );
        return 1;
    }
    foreach $obj (qw(Main Root xref)) {
        next if $capture{$obj};
        $et->Error( $$info{Warning} ) if $$info{Warning};
        $et->Error("Can't find $obj object");
        return 1;
    }
    $et->InitWriteDirs( \%pdfMap, 'XMP' );

    $raf->Seek( -64, 2 ) and $raf->Read( $buff, 64 ) and $raf->Seek( 0, 0 )
      or return -1;
    my $rtn = 1;
    my $prevUpdate;
    if ( $buff =~ /$endComment(\d+)\s+(startxref\s+\d+\s+%%EOF\s+)?$/s ) {
        $prevUpdate = $1;
        Image::ExifTool::CopyBlock( $raf, $outfile,
            $prevUpdate + $$et{PDFBase} )
          or $rtn = -1;
        unless ($raf->Read( $buff, length $beginComment )
            and $buff eq $beginComment )
        {
            $et->Error('Previous ExifTool update is corrupted');
            return $rtn;
        }
        $raf->Seek( $prevUpdate + $$et{PDFBase}, 0 ) or $rtn = -1;
        if ( $$et{DEL_GROUP}{'PDF-update'} ) {
            $et->VPrint( 0, "  Reverted previous ExifTool updates\n" );
            ++$$et{CHANGED};
            return $rtn;
        }
    }
    elsif ( $$et{DEL_GROUP}{'PDF-update'} ) {
        $et->Error('File contains no previous ExifTool update');
        return $rtn;
    }
    else {
        while ( $raf->Read( $buff, 65536 ) ) {
            Write( $outfile, $buff ) or $rtn = -1;
        }
    }
    $out = $et->Options('TextOut') if $et->Options('Verbose');
    my $xref     = $capture{xref};
    my $mainDict = $capture{Main};
    my $metaRef  = $capture{Root}->{Metadata};
    my $nextObject;

    my $prevInfoRef;
    if ($prevUpdate) {
        unless ( $capture{Prev} ) {
            $et->Error("Can't locate trailer dictionary prior to last edit");
            return $rtn;
        }
        $prevInfoRef = $capture{Prev}->{Info};
        $nextObject = $capture{Prev}->{Size};
        undef $metaRef
          if $metaRef
          and $$metaRef =~ /^(\d+)/
          and $1 >= $nextObject;
    }
    else {
        $prevInfoRef = $$mainDict{Info};
        $nextObject  = $$mainDict{Size};
    }

    my $infoChanged = 0;
    if ( $$et{DEL_GROUP}{PDF} and $capture{Info} ) {
        delete $capture{Info};
        $info = { XMP => $$info{XMP} };
        print $out "  Deleting PDF Info dictionary\n" if $out;
        ++$infoChanged;
    }

    $capture{Info} = { _tags => [] } unless $capture{Info};
    my $infoDict = $capture{Info};

    my $infoRef = $prevInfoRef || \ "$nextObject 0 R";
    unless ( ref $infoRef eq 'SCALAR' ) {
        $et->Error("Info dictionary is not an indirect object");
        return $rtn;
    }
    $keyExt = $$infoRef;

    CryptObject($infoDict) if $$infoDict{_needCrypt};

    local $/ = $capture{newline};

    my $newTags = $et->GetNewTagInfoHash( \%Image::ExifTool::PDF::Info );
    my $tagID;
    foreach $tagID ( sort keys %$newTags ) {
        my $tagInfo = $$newTags{$tagID};
        if ( $pdfVer >= 2.0 and not $$tagInfo{PDF2} ) {
            next
              if $et->Warn(
"Writing PDF:$$tagInfo{Name} is deprecated for PDF 2.0 documents",
                2
              );
        }
        my $nvHash = $et->GetNewValueHash($tagInfo);
        my ( @vals, $deleted );
        my $tag    = $$tagInfo{Name};
        my $val    = $$info{$tag};
        my $tagKey = $tag;
        unless ( defined $val ) {
            ($tagKey) = grep /^$tag/, keys %$info;
            $val = $$info{$tagKey} if $tagKey;
        }
        if ( defined $val ) {
            my @oldVals;
            if ( ref $val eq 'ARRAY' ) {
                @oldVals = @$val;
                $val     = shift @oldVals;
            }
            for ( ; ; ) {
                if ( $et->IsOverwriting( $nvHash, $val ) > 0 ) {
                    $deleted = 1;
                    $et->VerboseValue( "- PDF:$tag", $val );
                    ++$infoChanged;
                }
                else {
                    push @vals, $val;
                }
                last unless @oldVals;
                $val = shift @oldVals;
            }
            delete $$infoDict{$tagID} unless @vals;
        }
        elsif ( $$nvHash{EditOnly} ) {
            next;
        }
        next
          unless $deleted
          or $$tagInfo{List}
          or not exists $$infoDict{$tagID};

        my @newVals = $et->GetNewValue($nvHash);
        if (@newVals) {
            push @vals, @newVals;
            ++$infoChanged;
            if ($out) {
                foreach $val (@newVals) {
                    $et->VerboseValue( "+ PDF:$tag", $val );
                }
            }
        }
        unless (@vals) {
            delete $$infoDict{$tagID};
            next;
        }
        my $writable =
          $$tagInfo{Writable} || $Image::ExifTool::PDF::Info{WRITABLE};
        if ( not $$tagInfo{List} ) {
            $val = WritePDFValue( $et, shift(@vals), $writable );
        }
        elsif ( $$tagInfo{List} eq 'array' ) {
            foreach $val (@vals) {
                $val = WritePDFValue( $et, $val, $writable );
                defined $val or undef(@vals), last;
            }
            $val = @vals ? \@vals : undef;
        }
        else {
            $val = WritePDFValue( $et, join( $et->Options('ListSep'), @vals ),
                $writable );
        }
        if ( defined $val ) {
            $$infoDict{$tagID} = $val;
            ++$infoChanged;
        }
        else {
            $et->Warn("Error converting $$tagInfo{Name} value");
        }
    }
    if ($infoChanged) {
        $$et{CHANGED} += $infoChanged;
    }
    elsif ($prevUpdate) {
        my $oldPos = LocateObject( $xref, $$infoRef );
        $infoChanged = 1 if $oldPos and $oldPos > $prevUpdate;
    }

    if ($infoChanged) {
        if ( scalar( keys %{ $capture{Info} } ) > 1 ) {
            $newObj{$$infoRef} = $capture{Info};
            $$mainDict{Info} = $infoRef;
            ++$nextObject unless $prevInfoRef;
        }
        else {
            delete $$mainDict{Info};
            $newObj{$$infoRef} = '' if $prevInfoRef;
        }
    }

    my %xmpInfo = (
        DataPt => $$info{XMP},
        Parent => 'PDF',
    );
    my $xmpTable   = Image::ExifTool::GetTagTable('Image::ExifTool::XMP::Main');
    my $oldChanged = $$et{CHANGED};
    my $newXMP     = $et->WriteDirectory( \%xmpInfo, $xmpTable );
    $newXMP = $$info{XMP} ? ${ $$info{XMP} } : '' unless defined $newXMP;

    unless ( $newXMP or $$info{XMP} ) {
        $$et{CHANGED} = $oldChanged;
        $et->VPrint( 0, "  (XMP not changed -- still empty)\n" );
    }
    my ( $metaChanged, $rootChanged );

    if ( $$et{CHANGED} != $oldChanged and defined $newXMP ) {
        $metaChanged = 1;
    }
    elsif ( $prevUpdate and $capture{Root}->{Metadata} ) {
        my $oldPos = LocateObject( $xref, ${ $capture{Root}->{Metadata} } );
        $metaChanged = 1 if $oldPos and $oldPos > $prevUpdate;
    }
    if ($metaChanged) {
        if ($newXMP) {
            unless ( ref $metaRef ) {
                $metaRef = \ "$nextObject 0 R";
                ++$nextObject;
                $capture{Root}->{Metadata} = $metaRef;
                $rootChanged = 1;
            }
            $newObj{$$metaRef} = {
                Type    => '/Metadata',
                Subtype => '/XML',
                _tags      => [qw(Type Subtype)],
                _stream    => $newXMP,
                _decrypted => 1,
            };
        }
        elsif ( $capture{Root}->{Metadata} ) {
            $newObj{ ${ $capture{Root}->{Metadata} } } = '';
            delete $capture{Root}->{Metadata};
            $rootChanged = 1;
        }
    }
    my $rootRef = $$mainDict{Root};
    unless ($rootRef) {
        $et->Error("Can't find Root dictionary");
        return $rtn;
    }
    if ( not $rootChanged and $prevUpdate ) {
        my $oldPos = LocateObject( $xref, $$rootRef );
        $rootChanged = 1 if $oldPos and $oldPos > $prevUpdate;
    }
    $newObj{$$rootRef} = $capture{Root} if $rootChanged;
    if ( $$et{CHANGED} ) {
        my $oldEOF = Tell($outfile) - $$et{PDFBase};
        Write( $outfile, $beginComment ) or $rtn = -1;

        foreach $objRef ( sort keys %newObj ) {
            $objRef =~ /^(\d+) (\d+)/ or $rtn = -1, last;
            ( $id, $gen ) = ( $1, $2 );
            if ( not $newObj{$objRef} ) {
                ++$gen if $gen < 65535;
                $newXRef{$id} = [ 0, $gen, 'f' ];
                next;
            }
            $newXRef{$id} =
              [ Tell($outfile) - $$et{PDFBase} + length($/), $gen, 'n' ];
            $keyExt = "$id $gen obj";
            Write( $outfile, $/, $keyExt )            or $rtn = -1;
            WriteObject( $outfile, $newObj{$objRef} ) or $rtn = -1;
            Write( $outfile, $/, 'endobj' )           or $rtn = -1;
        }

        $$mainDict{Prev} = $capture{startxref} unless $prevUpdate;

        $newXRef{0} = [ 0, 65535, 'f' ];

        if ($prevUpdate) {
            my $mainFree;
            if ( $$mainDict{Type} and $$mainDict{Type} eq '/XRef' ) {
                $mainFree = GetFreeEntries( $xref->{dicts}->[0] );
            }
            else {
                $mainFree = $capture{mainFree};
            }
            foreach $id ( sort { $a <=> $b } keys %$mainFree ) {
                $newXRef{$id} = $$mainFree{$id} unless $newXRef{$id};
            }
        }

        my $prevFree = 0;
        foreach $id ( sort { $b <=> $a } keys %newXRef ) {
            next unless $newXRef{$id}->[2] eq 'f';

            if ( $id >= $nextObject ) {
                delete $newXRef{$id};
                next;
            }
            $newXRef{$id}->[0] = $prevFree;
            $prevFree = $id;
        }

        $$mainDict{Size} = $nextObject;

        if ( ref $$mainDict{ID} eq 'ARRAY' and @{ $$mainDict{ID} } > 1 ) {
            $id = $mainDict->{ID}->[1];
            if ( $id =~ /^<([0-9a-f]{2})/i ) {
                my $byte = unpack( 'H2', chr( ( hex($1) + 1 ) & 0xff ) );
                substr( $id, 1, 2 ) = $byte;
            }
            elsif ( $id =~ /^\((.)/s
                and $1 ne '\\'
                and $1 ne ')'
                and $1 ne '(' )
            {
                my $ch = chr( ( ord($1) + 1 ) & 0xff );
                $ch = 'a' if $ch =~ /[()\\\x00-\x08\x0a-\x1f\x7f\xff]/;
                substr( $id, 1, 1 ) = $ch;
            }
            $mainDict->{ID}->[1] = $id;
        }

        my $startxref = Tell($outfile) - $$et{PDFBase} + length($/);

        if ( $$mainDict{Type} and $$mainDict{Type} eq '/XRef' ) {

            $newXRef{ $nextObject++ } =
              [ Tell($outfile) - $$et{PDFBase} + length($/), 0, 'n' ];
            $$mainDict{Size} = $nextObject;
            my $bits = 4;
          Restart: for ( ; ; ) {
                $$mainDict{W}       = [ 1, $bits, 2 ];
                $$mainDict{Index}   = [];
                $$mainDict{_stream} = '';
                my @ids = sort { $a <=> $b } keys %newXRef;
                while (@ids) {
                    my $startID = $ids[0];
                    for ( ; ; ) {
                        $id = shift @ids;
                        my ( $pos, $gen, $type ) = @{ $newXRef{$id} };
                        if ( $pos > 0xffffffff ) {
                            if ( $bits == 4 ) {
                                $bits = 8;
                                next Restart;
                            }
                        }
                        if ( $bits == 4 ) {
                            $$mainDict{_stream} .=
                              pack( 'CNn', $type eq 'f' ? 0 : 1, $pos, $gen );
                        }
                        else {
                            my $hi = int( $pos / 4294967296 );
                            my $lo = $pos - $hi * 4294967296;
                            $$mainDict{_stream} .= pack( 'CNNn',
                                $type eq 'f' ? 0 : 1,
                                $hi, $lo, $gen );
                        }
                        last if not @ids or $ids[0] != $id + 1;
                    }
                    push @{ $$mainDict{Index} }, $startID, $id - $startID + 1;
                }
                last;
            }
            $keyExt = "$id 0 obj";
            Write( $outfile, $/, $keyExt )     or $rtn = -1;
            WriteObject( $outfile, $mainDict ) or $rtn = -1;
            Write( $outfile, $/, 'endobj' )    or $rtn = -1;

        }
        else {

            Write( $outfile, $/, 'xref', $/ ) or $rtn = -1;
            my $endl = ( length($/) == 1 ? ' ' : '' ) . $/;
            my @ids  = sort { $a <=> $b } keys %newXRef;
            while (@ids) {
                my $startID = $ids[0];
                $buff = '';
                for ( ; ; ) {
                    $id = shift @ids;
                    $buff .=
                      sprintf( "%.10d %.5d %s%s", @{ $newXRef{$id} }, $endl );
                    last if not @ids or $ids[0] != $id + 1;
                }
                Write( $outfile, $startID, ' ', $id - $startID + 1, $/, $buff )
                  or $rtn = -1;
            }

            Write( $outfile, 'trailer' ) or $rtn = -1;
            WriteObject( $outfile, $mainDict ) or $rtn = -1;
        }
        Write( $outfile, $/, $endComment, $oldEOF, $/ ) or $rtn = -1;

        Write( $outfile, 'startxref', $/, $startxref, $/, '%%EOF', $/ )
          or $rtn = -1;

    }
    elsif ($prevUpdate) {

        $raf->Seek( $prevUpdate + $$et{PDFBase}, 0 ) or $rtn = -1;
        while ( $raf->Read( $buff, 65536 ) ) {
            Write( $outfile, $buff ) or $rtn = -1;
        }
    }
    if (    $rtn > 0
        and $$et{CHANGED}
        and ( $$et{DEL_GROUP}{PDF} or $$et{DEL_GROUP}{XMP} ) )
    {
        $et->Warn(
            'ExifTool PDF edits are reversible. Deleted tags may be recovered!',
            1
        );
    }
    undef $newTool;
    undef %capture;
    return $rtn;
}

1;

__END__

