
package Image::ExifTool::RIFF;

use strict;

my %webpMap = (
    'XMP '       => 'RIFF',
    EXIF         => 'RIFF',
    ICCP         => 'RIFF',
    C2PA         => 'RIFF',
    JUMBF        => 'C2PA',
    XMP          => 'XMP ',
    IFD0         => 'EXIF',
    IFD1         => 'IFD0',
    ICC_Profile  => 'ICCP',
    ExifIFD      => 'IFD0',
    GPS          => 'IFD0',
    SubIFD       => 'IFD0',
    GlobParamIFD => 'IFD0',
    PrintIM      => 'IFD0',
    InteropIFD   => 'ExifIFD',
    MakerNotes   => 'ExifIFD',
);

my %deletableGroup = (
    "XMP\0" => 'XMP',
    SEAL    => 'SEAL',
);

sub WriteRIFF($$) {
    my ( $et, $dirInfo ) = @_;
    $et or return 1;
    my $outfile = $$dirInfo{OutFile};
    my $outsize = 0;
    my $raf     = $$dirInfo{RAF};
    my $verbose = $et->Options('Verbose');
    my ( $buff, $err, $pass, %has, %dirDat, $imageWidth, $imageHeight );

    for ( $pass = 0 ; ; ++$pass ) {
        my %doneDir;
        return 0 unless $raf->Read( $buff, 12 ) == 12;
        return 0 unless $buff =~ /^(RIFF|RF64)....(.{4})/s;

        unless ( $1 eq 'RIFF' and $2 eq 'WEBP' ) {
            my $type = $2;
            $type =~ tr/-_a-zA-Z//dc;
            $et->Error("Can't currently write $1 $type files");
            return 1;
        }
        SetByteOrder('II');

        $et->Options( Verbose => 0 ) if $pass;
        $et->InitWriteDirs( \%webpMap );
        my $addDirs  = $$et{ADD_DIRS};
        my $editDirs = $$et{EDIT_DIRS};
        $$addDirs{IFD0} = 'EXIF' if $$addDirs{EXIF};
        my ( $createVP8X, $deleteVP8X );

        if ($pass) {
            $et->Options( Verbose => $verbose );
            my $needsVP8X = (
                     $has{ANIM}
                  or $has{'XMP '}
                  or $has{EXIF}
                  or $has{ALPH}
                  or $has{ICCP}
            );
            if ( $has{VP8X} and not $needsVP8X and $$et{CHANGED} ) {
                $deleteVP8X = 1;
                $outsize -= 18;
            }
            elsif ( $needsVP8X and not $has{VP8X} ) {
                if ( defined $imageWidth ) {
                    ++$$et{CHANGED};
                    $createVP8X = 1;
                    $outsize += 18;
                }
                else {
                    $et->Warn(
                        'Error getting image size for required VP8X chunk');
                }
            }
            Set32u( $outsize - 8, \$buff, 4 );
            Write( $outfile, $buff ) or $err = 1;
            if ($createVP8X) {
                $et->VPrint( 0,
                    "  Adding required VP8X chunk (Extended WEBP)\n" );
                my $flags = 0;
                $flags |= 0x02 if $has{ANIM};
                $flags |= 0x04 if $has{'XMP '};
                $flags |= 0x08 if $has{EXIF};
                $flags |= 0x10 if $has{ALPH};
                $flags |= 0x20 if $has{ICCP};
                Write(
                    $outfile, 'VP8X',
                    pack( 'V3v',
                        10,
                        $flags,
                        ( $imageWidth - 1 ) |
                          ( ( ( $imageHeight - 1 ) & 0xff ) << 24 ),
                        ( $imageHeight - 1 ) >> 8 )
                );
                Write( $outfile, $dirDat{ICCP} ) or $err = 1 if $dirDat{ICCP};
            }
        }
        else {
            $outsize += length $buff;
        }
        my $pos = 12;
        for ( ; ; ) {
            my ( $tag, $len );
            my $num = $raf->Read( $buff, 8 );
            if ( $num < 8 ) {
                $num and $et->Error('RIFF format error'), return 1;
                last
                  unless $$addDirs{EXIF}
                  or $$addDirs{'XMP '}
                  or $$addDirs{ICCP};
                $num  = $len = 0;
                $buff = $tag = '';
            }
            else {
                $pos += 8;
                ( $tag, $len ) = unpack( 'a4V', $buff );
                if ( $len <= 0 ) {
                    if ( $len < 0 ) {
                        $et->Error('Invalid chunk length');
                        return 1;
                    }
                    elsif ( $tag eq "\0\0\0\0" ) {
                        $et->Error(
                            'Encountered empty null chunk. Processing aborted');
                        return 1;
                    }
                    else {
                        if ($pass) {
                            Write( $outfile, $buff ) or $err = 1;
                        }
                        else {
                            $outsize += length $buff;
                        }
                        next;
                    }
                }
            }
            my $len2 = $len + ( $len & 0x01 );
            if ( $deletableGroup{$tag} ) {
                if ( $$et{DEL_GROUP}{ $deletableGroup{$tag} } ) {
                    $raf->Seek( $len2, 1 ) or $et->Error('Seek error'), last;
                    $et->VPrint( 0, "  Deleting $deletableGroup{$tag}\n" )
                      if $pass;
                    ++$$et{CHANGED};
                    next;
                }
                elsif ( $tag eq "XMP\0" ) {
                    $et->Warn( 'Incorrect XMP tag ID', 1 ) if $pass;
                }
            }
            if (   $$editDirs{$tag}
                or $tag eq ''
                or ( $tag eq 'XMP ' and $$addDirs{EXIF} ) )
            {
                my $handledTag;
                if ($len2) {
                    $et->Warn("Duplicate '${tag}' chunk")
                      if $doneDir{$tag} and not $pass;
                    $doneDir{$tag} = 1;
                    $raf->Read( $buff, $len2 ) == $len2
                      or $et->Error("Truncated '${tag}' chunk"), last;
                    $pos += $len2;
                }
                else {
                    $buff = '';
                }
                my %dirName = (
                    EXIF   => 'IFD0',
                    'XMP ' => 'XMP',
                    ICCP   => 'ICC_Profile',
                    C2PA   => 'JUMBF'
                );
                my %tblName = (
                    EXIF   => 'Exif',
                    'XMP ' => 'XMP',
                    ICCP   => 'ICC_Profile',
                    C2PA   => 'Jpeg2000'
                );
                my $dir;
                foreach $dir ( 'EXIF', 'XMP ', 'ICCP', 'C2PA' ) {
                    next
                      unless $tag eq $dir
                      or (
                        $$addDirs{$dir}
                        and
                        ( $tag eq '' or ( $tag eq 'XMP ' and $dir eq 'EXIF' ) )
                      );
                    my $start;
                    unless ($pass) {
                        my $dataPt = \$buff;
                        if ( $tag eq 'EXIF' ) {
                            if ( $buff =~ /^Exif\0\0/ ) {
                                if ( $$et{DEL_GROUP}{EXIF} ) {
                                    $buff = substr( $buff, 6 );
                                    $len  -= 6;
                                    $len2 -= 6;
                                }
                                else {
                                    $et->Warn( 'Improper EXIF header', 1 )
                                      unless $pass;
                                    $start = 6;
                                }
                            }
                            else {
                                $start = 0;
                            }
                        }
                        elsif ( $dir ne $tag ) {
                            my $buf2 = '';
                            $dataPt = \$buf2;
                        }
                        my %dirInfo = (
                            DataPt   => $dataPt,
                            DataPos  => 0,
                            DirStart => $start,
                            Base     => $pos - $len2,
                            Parent   => $dir,
                            DirName  => $dirName{$dir},
                        );
                        if ( ref $Image::ExifTool::RIFF::Main{$dir} eq 'HASH' )
                        {
                            $dirInfo{TagInfo} =
                              $Image::ExifTool::RIFF::Main{$dir};
                        }
                        my $tagTablePtr =
                          GetTagTable("Image::ExifTool::$tblName{$dir}::Main");
                        my $writeProc =
                          $dir eq 'EXIF' ? \&Image::ExifTool::WriteTIFF : undef;
                        $dirDat{$dir} =
                          $et->WriteDirectory( \%dirInfo, $tagTablePtr,
                            $writeProc );
                    }
                    delete $$addDirs{$dir};
                    if ( defined $dirDat{$dir} ) {
                        if ( $dir eq $tag ) {
                            $handledTag = 1;

                            ++$$et{CHANGED} unless length $dirDat{$dir};
                        }
                        if ( length $dirDat{$dir} ) {
                            if ($pass) {
                                Write( $outfile, $dirDat{$dir} )
                                  or $err = 1
                                  unless $dir eq 'ICCP';
                            }
                            else {
                                my $hdr =
                                  $start ? substr( $buff, 0, $start ) : '';
                                my $dirLen =
                                  length( $dirDat{$dir} ) + length($hdr);
                                my $pad = $dirLen & 0x01 ? "\0" : '';
                                $dirDat{$dir} =
                                    $dir
                                  . Set32u($dirLen)
                                  . $hdr
                                  . $dirDat{$dir}
                                  . $pad;
                                $outsize += length( $dirDat{$dir} );
                                $has{$dir} = 1;
                            }
                        }
                    }
                }
                if ( not $handledTag and length $buff ) {
                    if ($pass) {
                        Write( $outfile, $tag, Set32u($len), $buff )
                          or $err = 1;
                    }
                    else {
                        $outsize += 8 + length($buff);
                        $has{$tag} = 1;
                    }
                }
                next;
            }
            $pos += $len2;
            if ( $tag eq 'VP8X' ) {
                my $buf2;
                if ( $len2 < 10 or $raf->Read( $buf2, $len2 ) != $len2 ) {
                    $et->Error('Truncated VP8X chunk');
                    return 1;
                }
                if ($pass) {
                    if ($deleteVP8X) {
                        $et->VPrint( 0,
"  Deleting unnecessary VP8X chunk (Standard WEBP)\n"
                        );
                        next;
                    }
                    my $flags = Get32u( \$buf2, 0 );
                    $flags &= ~0x2c;
                    $flags |= 0x04 if $has{'XMP '};
                    $flags |= 0x08 if $has{EXIF};
                    $flags |= 0x20 if $has{ICCP};
                    Set32u( $flags, \$buf2, 0 );
                    Write( $outfile, $buff, $buf2 ) or $err = 1;
                }
                else {
                    $imageWidth  = ( Get32u( \$buf2, 4 ) & 0xffffff ) + 1;
                    $imageHeight = ( Get32u( \$buf2, 6 ) >> 8 ) + 1;
                    $outsize += 8 + $len2;
                    $has{$tag} = 1;
                }
                Write( $outfile, $dirDat{ICCP} ) or $err = 1 if $dirDat{ICCP};
                next;
            }
            if ($pass) {
                Write( $outfile, $buff ) or $err = 1;
            }
            else {
                $outsize += length $buff;
                $has{$tag} = 1;
            }
            unless ( $pass or defined $imageWidth ) {
                if ( $tag eq 'VP8 ' and $len2 >= 16 ) {
                    $raf->Read( $buff, 16 ) == 16
                      or $et->Error('Truncated VP8 chunk'), return 1;
                    $outsize += 16;
                    if ( $buff =~ /^...\x9d\x01\x2a/s ) {
                        $imageWidth  = Get16u( \$buff, 6 ) & 0x3fff;
                        $imageHeight = Get16u( \$buff, 8 ) & 0x3fff;
                    }
                    $len2 -= 16;
                }
                elsif ( $tag eq 'VP8L' and $len2 >= 6 ) {
                    $raf->Read( $buff, 6 ) == 6
                      or $et->Error('Truncated VP8L chunk'), return 1;
                    $outsize += 6;
                    if ( $buff =~ /^\x2f/s ) {
                        my $word = Get32u( \$buff, 2 );
                        $imageWidth  = ( Get16u( \$buff, 1 ) & 0x3fff ) + 1;
                        $imageHeight = ( ( $word >> 6 ) & 0x3fff ) + 1;
                        $has{ALPH}   = 1 if $word & 0x100000;
                    }
                    $len2 -= 6;
                }
            }
            if ($pass) {
                while ($len2) {
                    my $num = $len2;
                    $num = 65536 if $num > 65536;
                    $raf->Read( $buff, $num ) == $num
                      or $et->Error('Truncated RIFF chunk'), last;
                    Write( $outfile, $buff ) or $err = 1, last;
                    $len2 -= $num;
                }
            }
            else {
                $raf->Seek( $len2, 1 ) or $et->Error('Seek error'), last;
                $outsize += $len2;
            }
        }
        last if $pass;
        $raf->Seek( 0, 0 ) or $et->Error('Seek error'), last;
    }
    return $err ? -1 : 1;
}

1;

__END__

