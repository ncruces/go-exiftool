
package Image::ExifTool::Ogg;

use strict;
use vars            qw($VERSION);
use Image::ExifTool qw(:DataAccess :Utils);

$VERSION = '1.04';

my $MAX_PACKETS = 2;

%Image::ExifTool::Ogg::Main = (
    NOTES => q{
        ExifTool extracts the following types of information from Ogg files.  See
        L<http://www.xiph.org/vorbis/doc/> for the Ogg specification.
    },
    vorbis =>
      { SubDirectory => { TagTable => 'Image::ExifTool::Vorbis::Main' } },
    theora =>
      { SubDirectory => { TagTable => 'Image::ExifTool::Theora::Main' } },
    Opus => { SubDirectory => { TagTable => 'Image::ExifTool::Opus::Main' } },
    FLAC => { SubDirectory => { TagTable => 'Image::ExifTool::FLAC::Main' } },
    ID3  => { SubDirectory => { TagTable => 'Image::ExifTool::ID3::Main' } },
);

sub ProcessPacket($$) {
    my ( $et, $dataPt ) = @_;
    my $rtnVal = 0;
    if (   $$dataPt =~ /^(.)(vorbis|theora)/s
        or $$dataPt =~ /^(OpusHead|OpusTags)/ )
    {
        my ( $tag, $type, $pos ) =
          $2 ? ( ord($1), ucfirst($2), 7 ) : ( $1, 'Opus', 8 );
        $et->OverrideFileType('OGV')
          if $type eq 'Theora' and $$et{FILE_TYPE} eq 'OGG';
        $et->OverrideFileType('OPUS')
          if $type eq 'Opus' and $$et{FILE_TYPE} eq 'OGG';
        my $tagTablePtr = GetTagTable("Image::ExifTool::${type}::Main");
        my $tagInfo     = $et->GetTagInfo( $tagTablePtr, $tag );
        return 0 unless $tagInfo and $$tagInfo{SubDirectory};
        my $subdir  = $$tagInfo{SubDirectory};
        my %dirInfo = (
            DataPt   => $dataPt,
            DirName  => $$tagInfo{Name},
            DirStart => $pos,
        );
        my $table = GetTagTable( $$subdir{TagTable} );
        $$et{SET_GROUP1} = $type            if $type eq 'Theora';
        SetByteOrder( $$subdir{ByteOrder} ) if $$subdir{ByteOrder};
        $rtnVal = $et->ProcessDirectory( \%dirInfo, $table );
        SetByteOrder('II');
        delete $$et{SET_GROUP1};
    }
    return $rtnVal;
}

sub ProcessOGG($$) {
    my ( $et, $dirInfo ) = @_;

    unless ( $$et{DoneID3} ) {
        require Image::ExifTool::ID3;
        Image::ExifTool::ID3::ProcessID3( $et, $dirInfo ) and return 1;
    }
    my $raf     = $$dirInfo{RAF};
    my $verbose = $et->Options('Verbose');
    my $out     = $et->Options('TextOut');
    my ( $success, $page, $packets, $streams, $stream ) = ( 0, 0, 0, 0, '' );
    my ( $buff, $flag, %val, $numFlac, %streamPage );

    for ( ; ; ) {
        if ( $raf and $raf->Read( $buff, 28 ) == 28 ) {
            unless ( $buff =~ /^OggS/ ) {
                $success and $et->Warn('Lost synchronization');
                last;
            }
            unless ($success) {
                $success = 1;
                $et->SetFileType();
                SetByteOrder('II');
            }
            $flag   = Get8u( \$buff, 5 );
            $stream = Get32u( \$buff, 14 );
            if ( $flag & 0x02 ) {
                ++$streams;
                $streamPage{$stream} = $page = 0;
            }
            else {
                $page = $streamPage{$stream};
            }
            ++$packets unless $flag & 0x01;
        }
        else {
            last unless %val;
            ($stream) = sort keys %val;
            $flag = 0;
            undef $raf;
        }

        if ( defined $numFlac ) {
            last unless $raf;
            --$numFlac;
        }
        else {
            if ( defined $val{$stream} and not $flag & 0x01 ) {
                ProcessPacket( $et, \$val{$stream} );
                delete $val{$stream};
                if ( $packets > $MAX_PACKETS * $streams or not defined $raf ) {
                    last unless %val;
                }
            }
            last if $packets > $MAX_PACKETS * $streams and not %val;
        }

        my $pageNum = Get32u( \$buff, 18 );
        my $nseg    = Get8u( \$buff, 26 );

        my $dataLen = Get8u( \$buff, 27 );
        if ($nseg) {
            last unless $raf;
            $raf->Read( $buff, $nseg - 1 ) == $nseg - 1 or last;
            my @segs = unpack( 'C*', $buff );
            foreach (@segs) { $dataLen += $_ }
        }
        if ( defined $page ) {
            if ( $page == $pageNum ) {
                $streamPage{$stream} = ++$page;
            }
            else {
                $et->Warn('Missing page(s) in Ogg file');
                undef $page;
                delete $streamPage{$stream};
            }
        }
        last unless $raf and $raf->Read( $buff, $dataLen ) == $dataLen;
        if ( $verbose > 1 ) {
            printf $out "Page %d, stream 0x%x, flag 0x%x (%d bytes)\n",
              $pageNum, $stream, $flag, $dataLen;
            $et->VerboseDump( \$buff, DataPos => $raf->Tell() - $dataLen );
        }
        if ( defined $val{$stream} ) {
            $val{$stream} .= $buff;
        }
        elsif ( not $flag & 0x01 ) {

            if ( $buff =~ /^(.(vorbis|theora)|Opus(Head|Tags))/s ) {
                $val{$stream} = $buff;
            }
            elsif ( $buff =~ /^\x7fFLAC..(..)/s ) {
                $numFlac = unpack( 'n', $1 );
                $val{$stream} = substr( $buff, 9 );
            }
        }
        if ( defined $numFlac ) {
            last if $numFlac <= 0;
        }
        elsif ( defined $val{$stream} and $flag & 0x04 ) {
            ProcessPacket( $et, \$val{$stream} );
            delete $val{$stream};
        }
    }
    if ( defined $numFlac and defined $val{$stream} ) {
        require Image::ExifTool::FLAC;
        my %dirInfo = ( RAF => File::RandomAccess->new( \$val{$stream} ) );
        Image::ExifTool::FLAC::ProcessFLAC( $et, \%dirInfo );
    }
    return $success;
}

1;

__END__


