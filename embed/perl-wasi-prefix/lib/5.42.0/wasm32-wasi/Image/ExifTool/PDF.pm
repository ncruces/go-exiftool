
package Image::ExifTool::PDF;

use strict;
use vars            qw($VERSION $AUTOLOAD $lastFetched);
use Image::ExifTool qw(:DataAccess :Utils);
require Exporter;

$VERSION = '1.62';

sub FetchObject($$$$);
sub ExtractObject($$;$$);
sub ReadToNested($;$);
sub ProcessDict($$$$;$$);
sub ProcessAcroForm($$$$;$$);
sub ExpandArray($);
sub ReadPDFValue($);
sub CheckPDF($$$);

my $cryptInfo;
my $cryptString;
my $cryptStream;
my $lastOffset;
my %streamObjs;
my %fetched;
my $pdfVer;

my %supportedFilter = (
    '/FlateDecode'    => 1,
    '/Crypt'          => 1,
    '/Identity'       => 1,
    '/DCTDecode'      => 1,
    '/JPXDecode'      => 1,
    '/LZWDecode'      => 1,
    '/ASCIIHexDecode' => 1,
    '/ASCII85Decode'  => 1,
);

%Image::ExifTool::PDF::Main = (
    GROUPS => { 2       => 'Document' },
    VARS   => { CAPTURE => [ 'Main', 'Prev' ] },
    Info   => {
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::Info' },
        IgnoreDuplicates => 1,
    },
    Root => {
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::Root' },
    },
    Encrypt => {
        NoProcess    => 1,
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::Encrypt' },
    },
    _linearized => {
        Name  => 'Linearized',
        Notes =>
'flag set if document is linearized for fast web display; not a real Tag ID',
        PrintConv => { 'true' => 'Yes', 'false' => 'No' },
    },
);

%Image::ExifTool::PDF::Info = (
    GROUPS          => { 2       => 'Document' },
    VARS            => { CAPTURE => ['Info'] },
    EXTRACT_UNKNOWN => 1,
    WRITE_PROC      => \&Image::ExifTool::DummyWriteProc,
    CHECK_PROC      => \&CheckPDF,
    WRITABLE        => 'string',
    PRIORITY => 0,
    NOTES    => q{
        As well as the tags listed below, the PDF specification allows for
        user-defined tags to exist in the Info dictionary.  These tags, which should
        have corresponding XMP-pdfx entries in the XMP of the PDF XML Metadata
        object, are also extracted by ExifTool.

        B<Writable> specifies the value format, and may be C<string>, C<date>,
        C<integer>, C<real>, C<boolean> or C<name> for PDF tags.
    },
    Title    => {},
    Author   => { Groups => { 2 => 'Author' } },
    Subject  => {},
    Keywords => {
        List  => 'string',
        Notes => q{
            stored as a string but treated as a comma- or semicolon-separated list of
            items when reading if the string contains commas or semicolons, whichever is
            more numerous, otherwise it is treated a space-separated list of items.  The
            list behaviour may be defeated by setting the API NoPDFList option.  Written
            as a comma-separated string.  Note that the corresponding XMP-pdf:Keywords
            tag is not treated as a list, so the NoPDFList option should be used when
            copying between these two.
        },
    },
    Creator      => {},
    Producer     => {},
    CreationDate => {
        Name         => 'CreateDate',
        Writable     => 'date',
        PDF2         => 1,
        Groups       => { 2 => 'Time' },
        Shift        => 'Time',
        PrintConv    => '$self->ConvertDateTime($val)',
        PrintConvInv => '$self->InverseDateTime($val)',
    },
    ModDate => {
        Name         => 'ModifyDate',
        Writable     => 'date',
        PDF2         => 1,
        Groups       => { 2 => 'Time' },
        Shift        => 'Time',
        PrintConv    => '$self->ConvertDateTime($val)',
        PrintConvInv => '$self->InverseDateTime($val)',
    },
    SourceModified => {
        Name         => 'SourceModified',
        Writable     => 'date',
        PDF2         => 1,
        Groups       => { 2 => 'Time' },
        Shift        => 'Time',
        PrintConv    => '$self->ConvertDateTime($val)',
        PrintConvInv => '$self->InverseDateTime($val)',
    },
    Trapped => {
        Protected => 1,
        ValueConv    => '$val=~s{^/}{}; $val',
        ValueConvInv => '"/$val"',
    },
    'AAPL:Keywords' => {
        Name  => 'AppleKeywords',
        List  => 'array',
        Notes => q{
            keywords written by Apple utilities, although they seem to use PDF:Keywords
            when reading
        },
    },
);

%Image::ExifTool::PDF::Root = (
    GROUPS => { 2 => 'Document' },
    VARS     => { CAPTURE => ['Root'] },
    NOTES    => 'This is the PDF document catalog.',
    MarkInfo => {
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::MarkInfo' },
    },
    Metadata => {
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::Metadata' },
    },
    Pages => {
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::Pages' },
    },
    Perms => {
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::Perms' },
    },
    AcroForm => {
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::AcroForm' },
    },
    AF => {
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::AF' },
    },
    Lang       => 'Language',
    PageLayout => {},
    PageMode   => {},
    Version    => {
        Name    => 'PDFVersion',
        RawConv =>
          '$$self{PDFVersion} = $val if $$self{PDFVersion} < $val; $val',
    },
);

%Image::ExifTool::PDF::Encrypt = (
    GROUPS => { 2 => 'Document' },
    NOTES  => 'Tags extracted from the document Encrypt dictionary.',
    Filter => {
        Name  => 'Encryption',
        Notes => q{
            extracted value is actually a combination of the Filter, SubFilter, V, R and
            Length information from the Encrypt dictionary
        },
    },
    P => {
        Name             => 'UserAccess',
        ValueConv        => '$val & 0x0f3c',
        PrintConvColumns => 2,
        PrintConv        => {
            BITMASK => {
                2  => 'Print',
                3  => 'Modify',
                4  => 'Copy',
                5  => 'Annotate',
                8  => 'Fill forms',
                9  => 'Extract',
                10 => 'Assemble',
                11 => 'Print high-res',
            }
        },
    },
);

%Image::ExifTool::PDF::Pages = (
    GROUPS => { 2 => 'Document' },
    Count  => 'PageCount',
    Kids   => {
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::Kids' },
    },
    MediaBox => { Name => 'MediaBox', List => 1 },
);

%Image::ExifTool::PDF::Perms = (
    NOTES  => 'Additional document permissions imposed by digital signatures.',
    DocMDP => {
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::Signature' },
    },
    FieldMDP => {
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::Signature' },
    },
    UR3 => {
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::Signature' },
    },
);

%Image::ExifTool::PDF::AcroForm = (
    PROCESS_PROC => \&ProcessAcroForm,
    _has_xfa     => {
        Name  => 'HasXFA',
        Notes => q{
            this tag is defined if a document contains form fields, and is true if it
            uses XML Forms Architecture; not a real Tag ID
        },
        PrintConv => { 'true' => 'Yes', 'false' => 'No' },
    },
);

%Image::ExifTool::PDF::AF = (
    PROCESS_PROC => \&ProcessAF,
    NOTES        =>
'Processed only for C2PA information if AFRelationship is "/C2PA_Manifest".',
    EF => {
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::EF' },
    },
);

%Image::ExifTool::PDF::EF = (
    F => {
        Name         => 'F_',
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::F' },
    },
);

%Image::ExifTool::PDF::F = (
    NOTES   => 'C2PA JUMBF metadata extracted from "/C2PA_Manifest" file.',
    _stream => {
        Name         => 'JUMBF',
        Condition    => '$$self{AFRelationship} eq "/C2PA_Manifest"',
        SubDirectory => {
            TagTable  => 'Image::ExifTool::Jpeg2000::Main',
            DirName   => 'JUMBF',
            ByteOrder => 'BigEndian',
        },
    },
);

%Image::ExifTool::PDF::Kids = (
    Metadata => {
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::Metadata' },
    },
    PieceInfo => {
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::PieceInfo' },
    },
    Resources => {
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::Resources' },
    },
    Kids => {
        Condition    => '$self->Options("ExtractEmbedded")',
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::Kids' },
    },
);

%Image::ExifTool::PDF::Resources = (
    ColorSpace => {
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::ColorSpace' },
    },
    XObject => {
        Condition    => '$self->Options("ExtractEmbedded")',
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::XObject' },
    },
    Properties => {
        Condition    => '$self->Options("ExtractEmbedded")',
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::Properties' },
    },
);

%Image::ExifTool::PDF::ColorSpace = (
    DefaultRGB => {
        SubDirectory  => { TagTable => 'Image::ExifTool::PDF::DefaultRGB' },
        ConvertToDict => 1,
    },
    DefaultCMYK => {
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::DefaultRGB' },
        ConvertToDict => 1,
    },
    Cs1 => {
        SubDirectory  => { TagTable => 'Image::ExifTool::PDF::DefaultRGB' },
        ConvertToDict => 1,
    },
    CS0 => {
        SubDirectory  => { TagTable => 'Image::ExifTool::PDF::DefaultRGB' },
        ConvertToDict => 1,
    },
);

%Image::ExifTool::PDF::DefaultRGB = (
    ICCBased => {
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::ICCBased' },
    },
);

%Image::ExifTool::PDF::ICCBased = (
    _stream => {
        Name         => 'ICC_Profile',
        SubDirectory => { TagTable => 'Image::ExifTool::ICC_Profile::Main' },
    },
);

%Image::ExifTool::PDF::XObject = (
    EXTRACT_UNKNOWN => 0,
    Im              => {
        Notes => q{
            the L<ExtractEmbedded|../ExifTool.html#ExtractEmbedded> option enables information to be extracted from these
            embedded images
        },
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::Im' },
    },
);

%Image::ExifTool::PDF::Im = (
    NOTES => q{
        Information extracted from embedded images with the L<ExtractEmbedded|../ExifTool.html#ExtractEmbedded> option.
        The EmbeddedImage and its metadata are extracted only for JPEG and Jpeg2000
        image formats.
    },
    Width      => 'EmbeddedImageWidth',
    Height     => 'EmbeddedImageHeight',
    Filter     => { Name => 'EmbeddedImageFilter', List => 1 },
    ColorSpace => {
        Name    => 'EmbeddedImageColorSpace',
        List    => 1,
        RawConv => 'ref $val ? undef : $val',
    },
    Image_stream => {
        Name   => 'EmbeddedImage',
        Groups => { 2 => 'Preview' },
        Binary => 1,
    },
);

%Image::ExifTool::PDF::Properties = (
    EXTRACT_UNKNOWN => 0,
    MC              => {
        Notes => q{
            the L<ExtractEmbedded|../ExifTool.html#ExtractEmbedded> option enables information to be extracted from these
            embedded metadata dictionaries
        },
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::MC' },
    }
);

%Image::ExifTool::PDF::MC = (
    Metadata => {
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::Metadata' },
    }
);

%Image::ExifTool::PDF::PieceInfo = (
    AdobePhotoshop => {
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::AdobePhotoshop' },
    },
    Illustrator => {
        Condition => q{
            $self->OverrideFileType("AI") unless $$self{FILE_EXT} and $$self{FILE_EXT} eq 'PDF';
            return 1;
        },
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::Illustrator' },
    },
);

%Image::ExifTool::PDF::AdobePhotoshop = (
    Private => {
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::Private' },
    },
);

%Image::ExifTool::PDF::Illustrator = (
    Private => {
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::AIPrivate' },
    },
);

%Image::ExifTool::PDF::Private = (
    ImageResources => {
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::ImageResources' },
    },
);

%Image::ExifTool::PDF::AIPrivate = (
    GROUPS          => { 2 => 'Document' },
    EXTRACT_UNKNOWN => 0,
    AIMetaData      => {
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::AIMetaData' },
    },
    AIPrivateData => {
        Notes => q{
            the L<ExtractEmbedded|../ExifTool.html#ExtractEmbedded> option enables information to be extracted from embedded
            PostScript documents in the AIPrivateData# and AIPDFPrivateData# streams
        },
        JoinStreams  => 1,
        SubDirectory => { TagTable => 'Image::ExifTool::PostScript::Main' },
    },
    AIPDFPrivateData => {
        JoinStreams  => 1,
        SubDirectory => { TagTable => 'Image::ExifTool::PostScript::Main' },
    },
    RoundTripVersion => {},
    ContainerVersion => {},
    CreatorVersion   => {},
);

%Image::ExifTool::PDF::AIMetaData = (
    _stream => {
        Name         => 'AIStream',
        SubDirectory => { TagTable => 'Image::ExifTool::PostScript::Main' },
    },
);

%Image::ExifTool::PDF::ImageResources = (
    _stream => {
        Name         => 'PhotoshopStream',
        SubDirectory => { TagTable => 'Image::ExifTool::Photoshop::Main' },
    },
);

%Image::ExifTool::PDF::MarkInfo = (
    GROUPS => { 2 => 'Document' },
    Marked => {
        Name      => 'TaggedPDF',
        Notes     => "not a Tagged PDF if this tag is missing",
        PrintConv => { 'true' => 'Yes', 'false' => 'No' },
    },
);

%Image::ExifTool::PDF::Metadata = (
    GROUPS     => { 2 => 'Document' },
    XML_stream => {
        Name         => 'XMP',
        SubDirectory => { TagTable => 'Image::ExifTool::XMP::Main' },
    },
);

%Image::ExifTool::PDF::Signature = (
    GROUPS      => { 2 => 'Document' },
    ContactInfo => 'SignerContactInfo',
    Location    => 'SigningLocation',
    M           => {
        Name      => 'SigningDate',
        Format    => 'date',
        Groups    => { 2 => 'Time' },
        PrintConv => '$self->ConvertDateTime($val)',
    },
    Name      => 'SigningAuthority',
    Reason    => 'SigningReason',
    Reference => {
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::Reference' },
    },
    Prop_AuthTime => {
        Name      => 'AuthenticationTime',
        PrintConv => 'ConvertTimeSpan($val) . " ago"',
    },
    Prop_AuthType => 'AuthenticationType',
);

%Image::ExifTool::PDF::Reference = (
    TransformParams => {
        SubDirectory => { TagTable => 'Image::ExifTool::PDF::TransformParams' },
    },
);

%Image::ExifTool::PDF::TransformParams = (
    GROUPS => { 2 => 'Document' },
    Annots => {
        Name  => 'AnnotationUsageRights',
        Notes => q{
            possible values are Create, Delete, Modify, Copy, Import and Export;
            additional values for UR3 signatures are Online and SummaryView
        },
        List => 1,
    },
    Document => {
        Name  => 'DocumentUsageRights',
        Notes => 'only possible value is FullSave',
        List  => 1,
    },
    Form => {
        Name  => 'FormUsageRights',
        Notes => q{
            possible values are FillIn, Import, Export, SubmitStandalone and
            SpawnTemplate; additional values for UR3 signatures are BarcodePlaintext and
            Online
        },
        List => 1,
    },
    FormEX => {
        Name  => 'FormExtraUsageRights',
        Notes => 'UR signatures only; only possible value is BarcodePlaintext',
        List  => 1,
    },
    Signature => {
        Name  => 'SignatureUsageRights',
        Notes => 'only possible value is Modify',
        List  => 1,
    },
    EF => {
        Name  => 'EmbeddedFileUsageRights',
        Notes => 'possible values are Create, Delete, Modify and Import',
        List  => 1,
    },
    Msg => 'UsageRightsMessage',
    P   => {
        Name  => 'ModificationPermissions',
        Notes => q{
            1-3 for DocMDP signatures, default 2; true/false for UR3 signatures, default
            false
        },
        PrintConv => {
            1 => 'No changes permitted',
            2 => 'Fill forms, Create page templates, Sign',
            3 =>
'Fill forms, Create page templates, Sign, Create/Delete/Edit annotations',
            'true'  => 'Restrict all applications to reader permissions',
            'false' => 'Do not restrict applications to reader permissions',
        },
    },
    Action => {
        Name      => 'FieldPermissions',
        Notes     => 'FieldMDP signatures only',
        PrintConv => {
            'All'     => 'Disallow changes to all form fields',
            'Include' => 'Disallow changes to specified form fields',
            'Exclude' => 'Allow changes to specified form fields',
        },
    },
    Fields => {
        Notes => 'FieldMDP signatures only',
        Name  => 'FormFields',
        List  => 1,
    },
);

%Image::ExifTool::PDF::Unknown = ( GROUPS => { 2 => 'Unknown' }, );

sub AUTOLOAD {
    return Image::ExifTool::DoAutoLoad( $AUTOLOAD, @_ );
}

sub ConvertPDFDate($) {
    my $date = shift;
    $date =~ s/^D://;
    my $default = '00000101000000';
    if ( length $date < length $default ) {
        $date .= substr( $default, length $date );
    }
    $date =~ /^(\d{4})(\d{2})(\d{2})(\d{2})(\d{2})(\d{2})(.*)/ or return $date;
    $date = "$1:$2:$3 $4:$5:$6";
    if ($7) {
        my $tz = $7;
        if ( $tz =~ /^\s*Z/i ) {
            $date .= 'Z';
        }
        elsif ( $tz =~ /^\s*([-+])\s*(\d+)[': ]+(\d*)/ ) {
            $date .= $1 . $2 . ':' . ( $3 || '00' );
        }
    }
    return $date;
}

sub LocateAnyObject($$) {
    my ( $xref, $ref ) = @_;
    return undef unless $xref;
    return $$xref{$ref} if exists $$xref{$ref};
    return undef unless $ref =~ /^(\d+)/;
    my $objNum = $1;
    return 0 if defined $$xref{$objNum};
    return undef unless $$xref{dicts};
    my $dict;

    foreach $dict ( @{ $$xref{dicts} } ) {
        next if $objNum >= $$dict{Size};
        my $index = $$dict{Index};
        next if $objNum < $$index[0];
        my $size = $$dict{_entry_size};
        my $num  = scalar(@$index) / 2;
        my $tot  = 0;
        my $i;
        for ( $i = 0 ; $i < $num ; ++$i ) {
            my $start = $$index[ $i * 2 ];
            my $count = $$index[ $i * 2 + 1 ];
            last if $objNum < $start;
            if ( $objNum < $start + $count ) {
                my $offset = $size * ( $objNum - $start + $tot );
                last if $offset + $size > length $$dict{_stream};
                my @c = unpack( "x$offset C$size", $$dict{_stream} );
                my ( @t, $j, $k );
                my $w = $$dict{W};
                for ( $j = 0 ; $j < 3 ; ++$j ) {
                    $$w[$j] or $t[$j] = ( $j ? 0 : 1 ), next;
                    $t[$j] = shift(@c);
                    for ( $k = 1 ; $k < $$w[$j] ; ++$k ) {
                        $t[$j] = 256 * $t[$j] + shift(@c);
                    }
                }
                my $ref2 = "$objNum $t[2] R";
                if ( $t[0] == 1 ) {
                    $$xref{$ref2} = $t[1];
                }
                elsif ( $t[0] == 2 ) {
                    $ref2 = "$objNum 0 R";
                    $$xref{$ref2} = "I$t[2] $t[1] 0 R";
                }
                elsif ( $t[0] == 0 ) {
                    $$xref{$ref2} = 0;
                }
                else {
                    $$xref{$ref2} = undef;
                }
                $$xref{$objNum} = $t[1];
                return $$xref{$ref} if $ref eq $ref2;
                return 0;
            }
            $tot += $count;
        }
    }
    return undef;
}

sub LocateObject($$) {
    my ( $xref, $ref ) = @_;
    my $offset = LocateAnyObject( $xref, $ref );
    return undef if $offset and $offset =~ /^I/;
    return $offset;
}

sub CheckObject($$$$) {
    my ( $et, $tag, $ref, $offset ) = @_;
    my ( $data, $obj, $dat, $pat );

    my $raf = $$et{RAF};
    $raf->Seek( $offset + $$et{PDFBase}, 0 )
      or $et->Warn("Bad $tag offset"), return undef;
    ( $obj = $ref ) =~ s/R/obj/;
    for ( ; ; ) {
        $raf->ReadLine($data)
          or $et->Warn("Error reading $tag data"), return undef;
        last if $data =~ s/^$obj//;
        next if $data =~ /^\s+$/;

        while ( $data =~ /^\d+(\s+\d+)?\s*$/ ) {
            $raf->ReadLine($dat);
            $data .= $dat;
        }
        ( $pat = $obj ) =~ s/ /\\s+/g;
        unless ( $data =~ s/$pat// ) {
            $tag = ucfirst $tag;
            $et->Warn("$tag object ($obj) not found at offset $offset");
            return undef;
        }
        last;
    }
    for ( ; ; ) {
        last if $data =~ /\S/ and $data !~ /^\s*%/;
        $raf->ReadLine($data)
          or $et->Warn("Error reading $tag data"), return undef;
    }
    return $data;
}

sub FetchObject($$$$) {
    my ( $et, $ref, $xref, $tag ) = @_;
    $lastFetched = $ref;
    my $offset = LocateAnyObject( $xref, $ref );
    $lastOffset = $offset;
    unless ($offset) {
        $et->Warn("Bad $tag reference") unless defined $offset;
        return undef;
    }
    my ( $data, $obj );
    if ( $offset =~ s/^I(\d+) // ) {
        my $index    = $1;
        my ($objNum) = split ' ', $ref;
        $ref = $offset;
        $obj = $streamObjs{$ref};
        unless ($obj) {
            return undef if defined $obj;
            $streamObjs{$ref} = '';
            $obj = FetchObject( $et, $ref, $xref, $tag );
            return undef unless defined $obj and ref($obj) eq 'HASH';
            return undef unless $$obj{First} and $$obj{N};
            return undef unless DecodeStream( $et, $obj );
            my $num   = $$obj{N} * 2;
            my @table = split ' ', $$obj{_stream}, $num;
            return undef unless @table == $num;
            $$obj{_stream} = substr( $$obj{_stream}, $$obj{First} );
            $table[ $num - 1 ] =~ s/^(\d+).*/$1/s;
            $$obj{_table} = \@table;
            $streamObjs{$ref} = $obj;
        }
        my $i     = 2 * $index;
        my $table = $$obj{_table};
        unless ( $index < $$obj{N} and $$table[$i] == $objNum ) {
            $et->Warn("Bad index for stream object $tag");
            return undef;
        }
        $offset = $$table[ $i + 1 ];
        my $len = ( $$table[ $i + 3 ] || length( $$obj{_stream} ) ) - $offset;
        $data = substr( $$obj{_stream}, $offset, $len );
        undef $lastFetched if $cryptStream;
        return ExtractObject( $et, \$data );
    }
    $data = CheckObject( $et, $tag, $ref, $offset );
    return undef unless defined $data;

    return ExtractObject( $et, \$data, $$et{RAF}, $xref );
}

sub ReadPDFValue($) {
    my $str = shift;
    if ( ref $str eq 'ARRAY' ) {
        my ( $val, @vals );
        foreach $val (@$str) {
            push @vals, ReadPDFValue($val);
        }
        return \@vals;
    }
    length $str or return $str;
    my $delim = substr( $str, 0, 1 );
    if ( $delim eq '(' ) {
        $str = $1 if $str =~ /^.*?\((.*)\)/s;

        while ( $str =~ /\\(.)/sg ) {
            my $n = pos($str) - 2;
            my $c = $1;
            my $r;
            if ( $c =~ /[0-7]/ ) {
                $c .= $1 if $str =~ /\G([0-7]{1,2})/g;
                $r = chr( oct($c) & 0xff );
            }
            elsif ( $c eq "\x0d" ) {
                $c .= $1 if $str =~ /\G(\x0a)/g;
                $r = '';
            }
            elsif ( $c eq "\x0a" ) {
                $r = '';
            }
            else {
                ( $r = $c ) =~ tr/nrtbf/\n\r\t\b\f/;
            }
            substr( $str, $n, length($c) + 1 ) = $r;
            pos($str) = $n + length($r);
        }
        Crypt( \$str, $lastFetched ) if $cryptString;
    }
    elsif ( $delim eq '<' ) {

        $str =~ tr/0-9A-Fa-f//dc;
        $str .= '0' if length($str) & 0x01;
        $str = pack( 'H*', $str );
        Crypt( \$str, $lastFetched ) if $cryptString;
    }
    elsif ( $delim eq '/' ) {
        $str = substr( $str, 1 );
        $str =~ s/#([0-9a-f]{2})/chr(hex($1))/sgei if $pdfVer >= 1.2;
    }
    return $str;
}

sub ExtractObject($$;$$) {
    my ( $et, $dataPt, $raf, $xref ) = @_;
    my ( @tags, $data, $objData );
    my $dict = {};
    my $delim;

    for ( ; ; ) {
        if ( $$dataPt =~ /^\s*(<{1,2}|\[|\()/s ) {
            $delim = $1;
            $$dataPt =~ s/^\s+//;
            $objData = ReadToNested( $dataPt, $raf );
            return undef unless defined $objData;
            last;
        }
        elsif ( $$dataPt =~ s{^\s*(\S[^[(/<>\s]*)\s*}{}s ) {
            $objData = $1;
            if ( $objData =~ /^\d+$/ and $$dataPt =~ s/^(\d+)\s+R//s ) {
                $objData .= "$1 R";
                $objData = \$objData;
            }
            return $objData;
        }
        $raf and $raf->ReadLine($data) or return undef;
        $$dataPt .= $data;
    }
    if ( $delim eq '(' or $delim eq '<' ) {
        return $objData;
    }
    elsif ( $delim eq '[' ) {
        $objData =~ /^.*?\[(.*)\]/s or return undef;
        my $data = $1;
        my @list;
        for ( ; ; ) {
            last unless $data =~ m{\s*(\S[^[(/<>\s]*)}sg;
            my $val = $1;
            if ( $val =~ /^(<{1,2}|\[|\()/ ) {
                my $pos = pos($data) - length($val);
                my $buff = substr( $data, $pos );
                $val = ReadToNested( \$buff );
                last unless defined $val;
                pos($data) = $pos + length($val);
                $val = ExtractObject( $et, \$val );
            }
            elsif ( $val =~ /^\d/ ) {
                my $pos = pos($data);
                if ( $data =~ /\G\s+(\d+)\s+R/g ) {
                    $val = \ "$val $1 R";
                }
                else {
                    pos($data) = $pos;
                }
            }
            push @list, $val;
        }
        return \@list;
    }
    while ( $objData =~ m{(\s*)/([^/[\]()<>{}\s]+)\s*(\S[^[(/<>\s]*)}sg ) {
        my $tag = $2;
        my $val = $3;
        if ( $val =~ /^(<{1,2}|\[|\()/ ) {
            $objData = substr( $objData, pos($objData) - length($val) );
            $val     = ReadToNested( \$objData, $raf );
            last unless defined $val;
            $val = ExtractObject( $et, \$val );
            pos($objData) = 0;
        }
        elsif ( $val =~ /^\d/ ) {
            my $pos = pos($objData);
            if ( $objData =~ /\G\s+(\d+)\s+R/sg ) {
                $val = \ "$val $1 R";
            }
            else {
                pos($objData) = $pos;
            }
        }
        if ( $$dict{$tag} ) {
            $et->Warn("Duplicate '${tag}' entry in dictionary (ignored)");
        }
        else {
            push @tags, $tag;
            $$dict{$tag} = $val;
        }
    }
    return undef unless @tags;
    $$dict{_tags} = \@tags;
    return $dict unless $raf;
    my $length = $$dict{Length} or return $dict;
    if ( ref $length ) {
        $length = $$length;
        my $oldpos = $raf->Tell();
        my $offset = LocateObject( $xref, $length ) or return $dict;
        $offset or $et->Warn('Bad stream Length object'), return $dict;
        $data = CheckObject( $et, 'stream Length', $length, $offset );
        defined $data or return $dict;
        $data =~ /^\s*(\d+)/
          or $et->Warn('Stream Length not found'), return $dict;
        $length = $1;
        $raf->Seek( $oldpos, 0 );
    }
    for ( ; ; ) {
        if ( $$dataPt =~ /(\S+)/ ) {
            last unless $1 eq 'stream';
            $$dataPt .= $data if $raf->ReadLine($data);
            $$dataPt =~ s/^\s*stream(\x0a|\x0d\x0a)//s;
            my $more = $length - length($$dataPt);
            if ( $more > 0 ) {
                unless ( $raf->Read( $data, $more ) == $more ) {
                    $et->Warn('Error reading stream data');
                    $$dataPt = '';
                    return $dict;
                }
                $$dict{_stream} = $$dataPt . $data;
                $$dataPt = '';
            }
            elsif ( $more < 0 ) {
                $$dict{_stream} = substr( $$dataPt, 0, $length );
                $$dataPt        = substr( $$dataPt, $length );
            }
            else {
                $$dict{_stream} = $$dataPt;
                $$dataPt = '';
            }
            last;
        }
        $raf->ReadLine($data) or last;
        $$dataPt .= $data;
    }
    return $dict;
}

my %closingDelim = (
    '('  => ')',
    '['  => ']',
    '<'  => '>',
    '<<' => '>>',
);

sub ReadToNested($;$) {
    my ( $dataPt, $raf ) = @_;
    my @delim = ('');
    pos($$dataPt) = 0;
    for ( ; ; ) {
        unless ( $$dataPt =~ /(\\*)(\(|\)|<{1,2}|>{1,2}|\[|\]|%)/g ) {
            my $buff;
            last unless $raf and $raf->ReadLine($buff);
            $$dataPt .= $buff;
            pos($$dataPt) = length($$dataPt) - length($buff);
            next;
        }
        if ( $delim[0] eq ')' ) {
            next if length($1) & 0x01;
            next unless $2 eq '(' or $2 eq ')';
        }
        elsif ( $2 eq '%' ) {
            my $pos = pos($$dataPt) - 1;
            $$dataPt =~ /.*/g;
            my $end = pos($$dataPt);
            $$dataPt = substr( $$dataPt, 0, $pos ) . substr( $$dataPt, $end );
            pos($$dataPt) = $pos;
            next;
        }
        if ( $closingDelim{$2} ) {
            unshift @delim, $closingDelim{$2};
            next;
        }
        unless ( $2 eq $delim[0] ) {
            next unless $2 eq '>>' and $delim[0] eq '>';
            pos($$dataPt) = pos($$dataPt) - 1;
        }
        shift @delim;
        next if $delim[0];
        my $pos  = pos($$dataPt);
        my $buff = substr( $$dataPt, 0, $pos );
        $$dataPt = substr( $$dataPt, $pos );
        return $buff;
    }
    return undef;
}

sub DecodeLZW($) {
    my $dataPt = shift;
    return 0 if length $$dataPt < 4;
    my @lzw  = ( map( chr, 0 .. 255 ), undef, undef );
    my $mask = 0x01ff;
    my @dat  = unpack 'n*', $$dataPt . "\0";
    my $word = ( $dat[0] << 16 ) | $dat[1];
    my ( $bit, $pos, $bits, $out ) = ( 0, 2, 9, '' );
    my $lastVal;

    for ( ; ; ) {
        my $shift = 32 - ( $bit + $bits );
        if ( $shift < 0 ) {
            return 0 if $pos >= @dat;
            $word = ( ( $word & 0xffff ) << 16 ) | $dat[ $pos++ ];
            $bit   -= 16;
            $shift += 16;
        }
        my $code = ( $word >> $shift ) & $mask;
        $bit += $bits;
        my $val = $lzw[$code];
        if ( defined $val ) {
            push @lzw, $lastVal . substr( $val, 0, 1 ) if defined $lastVal;
        }
        elsif ( $code == @lzw ) {
            return 0 unless defined $lastVal;
            $val = $lastVal . substr( $lastVal, 0, 1 );
            push @lzw, $val;
        }
        elsif ( $code == 256 ) {
            splice @lzw, 258;
            $bits = 9;
            $mask = 0x1ff;
            undef $lastVal;
            next;
        }
        elsif ( $code == 257 ) {
            last;
        }
        else {
            return 0;
        }
        $out .= $val;

        @lzw >= $mask and $bits < 12 and ++$bits, $mask |= $mask << 1;
        $lastVal = $val;
    }
    $$dataPt = $out;
    return 1;
}

sub DecodeStream($$) {
    local $_;
    my ( $et, $dict ) = @_;

    return 0 unless $$dict{_stream};

    my ( @filters, @decodeParms, $filter );
    if ( ref $$dict{Filter} eq 'ARRAY' ) {
        @filters = @{ $$dict{Filter} };
    }
    elsif ( defined $$dict{Filter} ) {
        @filters = ( $$dict{Filter} );
    }
    foreach $filter (@filters) {
        next if $supportedFilter{$filter};
        $et->Warn("Unsupported Filter $filter");
        return 0;
    }
    unless ( defined $$dict{_decrypted}
        or ( $filters[0] and $filters[0] eq '/Crypt' ) )
    {
        CryptStream( $dict, $lastFetched );
    }
    return 1 unless $$dict{Filter};
    return 0 if defined $$dict{_filtered};
    $$dict{_filtered} = 1;

    if ( ref $$dict{DecodeParms} eq 'ARRAY' ) {
        @decodeParms = @{ $$dict{DecodeParms} };
    }
    else {
        @decodeParms = ( $$dict{DecodeParms} );
    }

    foreach $filter (@filters) {
        my $decodeParms = shift @decodeParms;

        if ( $filter eq '/FlateDecode' ) {
            my $pre;
            if ( ref $decodeParms eq 'HASH' ) {
                $pre = $$decodeParms{Predictor};
                if ( $pre and $pre ne '1' and $pre ne '12' ) {
                    $et->Warn(
                        "FlateDecode Predictor $pre currently not supported");
                    return 0;
                }
            }
            if ( eval { require Compress::Zlib } ) {
                my $inflate = Compress::Zlib::inflateInit();
                my ( $buff, $stat );
                $inflate
                  and ( $buff, $stat ) = $inflate->inflate( $$dict{_stream} );
                if ( $inflate and $stat == Compress::Zlib::Z_STREAM_END() ) {
                    $$dict{_stream} = $buff;
                }
                else {
                    $et->Warn('Error inflating stream');
                    return 0;
                }
            }
            else {
                $et->Warn('Install Compress::Zlib to process filtered streams');
                return 0;
            }
            next unless $pre and $pre eq '12';

            my $cols = $$decodeParms{Columns};
            unless ($cols) {
                $et->Warn('No Columns for decoding stream');
                return 0;
            }
            my @bytes = unpack( 'C*', $$dict{_stream} );
            my @pre   = (0) x $cols;
            my $buff  = '';
            while ( @bytes > $cols ) {
                unless ( ( $_ = shift @bytes ) == 2 ) {
                    $et->Warn("Unsupported PNG filter $_");
                    return 0;
                }
                foreach (@pre) {
                    $_ = ( $_ + shift(@bytes) ) & 0xff;
                }
                $buff .= pack( 'C*', @pre );
            }
            $$dict{_stream} = $buff;

        }
        elsif ( $filter eq '/Crypt' ) {

            next if defined $$dict{_decrypted};
            next unless ref $decodeParms eq 'HASH';
            my $name = $$decodeParms{Name};
            next unless defined $name or $name eq 'Identity';
            if ( $name ne 'StdCF' ) {
                $et->Warn("Unsupported Crypt Filter $name");
                return 0;
            }
            unless ($cryptInfo) {
                $et->Warn('Missing Encrypt StdCF entry');
                return 0;
            }
            Crypt( \$$dict{_stream}, 'none' );
            $$dict{_decrypted} = ( $cryptStream ? 1 : 0 );

        }
        elsif ( $filter eq '/LZWDecode' ) {

            if ( ref $decodeParms eq 'HASH' ) {
                if ( $$decodeParms{Predictor} ) {
                    $et->Warn(
"LZWDecode Predictor $$decodeParms{Predictor} currently not supported"
                    );
                    return 0;
                }
                elsif ( $$decodeParms{EarlyChange} ) {
                    $et->Warn("LZWDecode EarlyChange currently not supported");
                    return 0;
                }
            }
            unless ( DecodeLZW( \$$dict{_stream} ) ) {
                $et->Warn('LZW decompress error');
                return 0;
            }

        }
        elsif ( $filter eq '/ASCIIHexDecode' ) {

            $$dict{_stream} =~ s/>.*//;
            $$dict{_stream} =~ tr/0-9a-zA-Z//d;
            $$dict{_stream} = pack 'H*', $$dict{_stream};

        }
        elsif ( $filter eq '/ASCII85Decode' ) {

            my ( $err, @out, $i );
            my ( $n, $val ) = ( 0, 0 );
            foreach ( split //, $$dict{_stream} ) {
                if ( $_ ge '!' and $_ le 'u' ) {
                    ;
                    $val = 85 * $val + ord($_) - 33;
                    next unless ++$n == 5;
                }
                elsif ( $_ eq '~' ) {
                    $n == 1 and $err = 1;
                    for ( $i = $n ; $i < 5 ; ++$i ) { $val *= 85; }
                }
                elsif ( $_ eq 'z' ) {
                    $n and $err = 2, last;
                    $n = 5;
                }
                else {
                    next if /^\s$/;
                    $err = 3, last;
                }
                $val = unpack( 'V', pack( 'N', $val ) );
                while ( --$n > 0 ) {
                    push @out, $val & 0xff;
                    $val >>= 8;
                }
                last if $_ eq '~';
            }
            $err and $et->Warn("ASCII85Decode error $err");
            $$dict{_stream} = pack( 'C*', @out );
        }
    }
    return 1;
}

sub RC4Init($) {
    my @key   = unpack( 'C*', shift );
    my @state = ( 0 .. 255 );
    my ( $i, $j ) = ( 0, 0 );
    while ( $i < 256 ) {
        my $st = $state[$i];
        $j             = ( $j + $st + $key[ $i % scalar(@key) ] ) & 0xff;
        $state[ $i++ ] = $state[$j];
        $state[$j]     = $st;
    }
    return { State => \@state, XY => [ 0, 0 ] };
}

sub RC4Crypt($$) {
    my ( $dataPt, $key ) = @_;
    $key = RC4Init($key) unless ref $key eq 'HASH';
    my $state = $$key{State};
    my ( $x, $y ) = @{ $$key{XY} };

    my @data = unpack( 'C*', $$dataPt );
    foreach (@data) {
        $x = ( $x + 1 ) & 0xff;
        my $stx = $$state[$x];
        $y = ( $stx + $y ) & 0xff;
        my $sty = $$state[$x] = $$state[$y];
        $$state[$y] = $stx;
        $_ ^= $$state[ ( $stx + $sty ) & 0xff ];
    }
    $$key{XY} = [ $x, $y ];
    $$dataPt = pack( 'C*', @data );
}

my $cipherMore;

sub CipherUpdate($) {
    my $dat = shift;
    my $pos = 0;
    $dat = $cipherMore . $dat if length $dat;
    while ( $pos + 16 <= length($dat) ) {
        substr( $dat, $pos, 16 ) =
          Image::ExifTool::AES::Cipher( substr( $dat, $pos, 16 ) );
        $pos += 16;
    }
    if ( $pos < length $dat ) {
        $cipherMore = substr( $dat, $pos );
        $dat        = substr( $dat, 0, $pos );
    }
    else {
        $cipherMore = '';
    }
    return $dat;
}

sub GetHash($$$$) {
    my ( $password, $salt, $vector, $rev ) = @_;

    return Digest::SHA::sha256( $password, $salt, $vector ) if $rev == 5;

    my $blockSize = 32;
    my $input =
      Digest::SHA::sha256( $password, $salt, $vector ) . ( "\0" x 32 );
    my $key = substr( $input, 0,  16 );
    my $iv  = substr( $input, 16, 16 );
    my $h;
    my $x = '';
    my $i = 0;

    while ( $i < 64 or $i < ord( substr( $x, -1, 1 ) ) + 32 ) {

        my $block = substr( $input, 0, $blockSize );
        $x = '';
        Image::ExifTool::AES::Crypt( \$x, $key, $iv, 1 );
        $cipherMore = '';

        my ( $j, $digest );
        for ( $j = 0 ; $j < 64 ; ++$j ) {
            $x = '';
            $x .= CipherUpdate($password) if length $password;
            $x .= CipherUpdate($block);
            $x .= CipherUpdate($vector) if length $vector;
            if ( $j == 0 ) {
                my @a   = unpack( 'C16', $x );
                my $sum = 0;
                $sum += $_ foreach @a;
                $blockSize = 32 + ( $sum % 3 ) * 16;
                $digest    = Digest::SHA->new( $blockSize * 8 );
            }
            $digest->add($x);
        }

        $h   = $digest->digest();
        $key = substr( $h, 0, 16 );
        substr( $input, 0, 16 ) = $h;
        $iv = substr( $h, 16, 16 );
        ++$i;
    }
    return substr( $h, 0, 32 );
}

sub DecryptInit($$$) {
    local $_;
    my ( $et, $encrypt, $id ) = @_;

    undef $cryptInfo;
    unless ( $encrypt and ref $encrypt eq 'HASH' ) {
        return 'Error loading Encrypt object';
    }
    my $filt = $$encrypt{Filter};
    unless ( $filt and $filt =~ s/^\/// ) {
        return 'Encrypt dictionary has no Filter!';
    }
    my $ver = $$encrypt{V} || 0;
    my $rev = $$encrypt{R} || 0;
    my $enc = "$filt V$ver";
    $enc .= ".$rev" if $filt eq 'Standard';
    $enc .= " ($1)"
      if $$encrypt{SubFilter} and $$encrypt{SubFilter} =~ /^\/(.*)/;
    $enc .= ' (' . ( $$encrypt{Length} || 40 ) . '-bit)' if $filt eq 'Standard';
    my $tagTablePtr = GetTagTable('Image::ExifTool::PDF::Encrypt');
    $et->HandleTag( $tagTablePtr, 'Filter', $enc );

    if ( $filt ne 'Standard' ) {
        return "Encryption filter $filt currently not supported";
    }
    elsif ( not defined $$encrypt{R} ) {
        return 'Standard security handler missing revision';
    }
    unless ( $$encrypt{O} and $$encrypt{P} and $$encrypt{U} ) {
        return 'Incomplete Encrypt specification';
    }
    if ( "$ver.$rev" >= 5.6 ) {
        $et->Warn( 'Decryption is very slow for encryption V5.6 or higher', 3 );
    }
    $et->HandleTag( $tagTablePtr, 'P', $$encrypt{P} );

    my %parm;

    if ( $ver == 1 or $ver == 2 ) {
        $cryptString = $cryptStream = 1;
    }
    elsif ( $ver == 4 or $ver == 5 ) {
        foreach ( 'StrF', 'StmF' ) {
            my $flagPt = $_ eq 'StrF' ? \$cryptString : \$cryptStream;
            $$flagPt = $$encrypt{$_};
            undef $$flagPt if $$flagPt and $$flagPt eq '/Identity';
            return "Unsupported $_ encryption $$flagPt"
              if $$flagPt and $$flagPt ne '/StdCF';
        }
        if ( $cryptString or $cryptStream ) {
            return 'Missing or invalid Encrypt StdCF entry'
              unless ref $$encrypt{CF} eq 'HASH'
              and ref $$encrypt{CF}{StdCF} eq 'HASH'
              and $$encrypt{CF}{StdCF}{CFM};
            my $cryptMeth = $$encrypt{CF}{StdCF}{CFM};
            unless ( $cryptMeth =~ /^\/(V2|AESV2|AESV3)$/ ) {
                return "Unsupported encryption method $cryptMeth";
            }
            $$encrypt{ '_' . lc($1) } = 1 if $cryptMeth =~ /^\/(AESV2|AESV3)$/;
        }
        if ( $ver == 5 ) {
            foreach ( 'OE', 'UE' ) {
                return "Missing Encrypt $_ entry" unless $$encrypt{$_};
                $parm{$_} = ReadPDFValue( $$encrypt{$_} );
                return "Invalid Encrypt $_ entry" unless length $parm{$_} == 32;
            }
            require Image::ExifTool::AES;
        }
    }
    else {
        return "Encryption version $ver currently not supported";
    }
    $id or return "Can't decrypt (no document ID)";

    if ( $ver < 5 ) {
        unless ( eval { require Digest::MD5 } ) {
            return "Install Digest::MD5 to process encrypted PDF";
        }
    }
    else {
        unless ( eval { require Digest::SHA } ) {
            return "Install Digest::SHA to process AES-256 encrypted PDF";
        }
    }

    my $pad = "\x28\xBF\x4E\x5E\x4E\x75\x8A\x41\x64\x00\x4E\x56\xFF\xFA\x01\x08"
      . "\x2E\x2E\x00\xB6\xD0\x68\x3E\x80\x2F\x0C\xA9\xFE\x64\x53\x69\x7A";
    my $o = ReadPDFValue( $$encrypt{O} );
    my $u = ReadPDFValue( $$encrypt{U} );

    if (   $ver < 4
        or not $$encrypt{EncryptMetadata}
        or $$encrypt{EncryptMetadata} !~ /false/i )
    {
        $$encrypt{_meta} = 1;
    }
    my ( $try, $key );
    for ( $try = 0 ; ; ++$try ) {
        my $password;
        if ( $try == 0 ) {
            $password = '';
        }
        elsif ( $try == 1 ) {
            $password = $et->Options('Password');
            return 'Document is password protected (use Password option)'
              unless defined $password;
            if (
                $] >= 5.006
                and (  $$et{OPTIONS}{EncodeHangs}
                    or eval { require Encode; Encode::is_utf8($password) }
                    or $@ )
              )
            {
                local $SIG{'__WARN__'} = sub { };
                $password = ( $$et{OPTIONS}{EncodeHangs} or $@ )
                  ? pack( 'C*',
                    unpack( $] < 5.010000 ? 'U0C*' : 'C0C*', $password ) )
                  : Encode::encode( 'utf8', $password );
            }
        }
        else {
            return 'Incorrect password';
        }
        if ( $ver < 5 ) {
            if ( length $password ) {
                $password = $et->Encode( $password, 'PDFDoc' );
                if ( length($password) > 32 ) {
                    $password = substr( $password, 0, 32 );
                }
                elsif ( length($password) < 32 ) {
                    $password .= substr( $pad, 0, 32 - length($password) );
                }
            }
            else {
                $password = $pad;
            }
            $key = $password . $o . pack( 'V', $$encrypt{P} ) . $id;
            my $rep = 1;
            if ( $rev == 3 or $rev == 4 ) {
                $key .= "\xff\xff\xff\xff" unless $$encrypt{_meta};
                $rep += 50;
            }
            my ( $len, $i, $dat );
            if ( $ver == 1 ) {
                $len = 5;
            }
            else {
                $len = $$encrypt{Length} || 40;
                $len >= 40 or return 'Bad Encrypt Length';
                $len = int( $len / 8 );
            }
            for ( $i = 0 ; $i < $rep ; ++$i ) {
                $key = substr( Digest::MD5::md5($key), 0, $len );
            }
            if ( $rev >= 3 ) {
                $dat = Digest::MD5::md5( $pad . $id );
                RC4Crypt( \$dat, $key );
                for ( $i = 1 ; $i <= 19 ; ++$i ) {
                    my @key = unpack( 'C*', $key );
                    foreach (@key) { $_ ^= $i; }
                    RC4Crypt( \$dat, pack( 'C*', @key ) );
                }
                $dat .= substr( $u, 16 );
            }
            else {
                $dat = $pad;
                RC4Crypt( \$dat, $key );
            }
            last if $dat eq $u;
        }
        else {
            return 'Invalid O or U Encrypt entries'
              if length($o) < 48
              or length($u) < 48;
            if ( length $password ) {
                $password = $et->Encode( $password, 'UTF8' );
                $password = substr( $password, 0, 127 )
                  if length($password) > 127;
            }
            my $sha = GetHash(
                $password,
                substr( $o, 32, 8 ),
                substr( $u, 0,  48 ), $rev
            );
            if ( $sha eq substr( $o, 0, 32 ) ) {
                $key = GetHash(
                    $password,
                    substr( $o, 40, 8 ),
                    substr( $u, 0,  48 ), $rev
                );
                my $dat = ( "\0" x 16 ) . $parm{OE};
                my $err = Image::ExifTool::AES::Crypt( \$dat, $key, 0, 1 );
                return $err if $err;
                $key = $dat;
                last;
            }
            $sha = GetHash( $password, substr( $u, 32, 8 ), '', $rev );
            if ( $sha eq substr( $u, 0, 32 ) ) {
                $key = GetHash( $password, substr( $u, 40, 8 ), '', $rev );
                my $dat = ( "\0" x 16 ) . $parm{UE};
                my $err = Image::ExifTool::AES::Crypt( \$dat, $key, 0, 1 );
                return $err if $err;
                $key = $dat;
                last;
            }
        }
    }
    $$encrypt{_key} = $key;
    $cryptInfo = $encrypt;
    return undef;
}

sub Crypt($$;$) {
    return unless $cryptInfo;
    my ( $dataPt, $keyExt, $encrypt ) = @_;
    return unless defined $keyExt;
    my $key = $$cryptInfo{_key};
    unless ( $$cryptInfo{_aesv3} ) {
        unless ( $keyExt eq 'none' ) {
            unless ( $keyExt =~ /^(I\d+ )?(\d+) (\d+)/ ) {
                $$cryptInfo{_error} = 'Invalid object reference for encryption';
                return;
            }
            $key .=
              substr( pack( 'V', $2 ), 0, 3 ) . substr( pack( 'V', $3 ), 0, 2 );
        }
        $key .= 'sAlT' if $$cryptInfo{_aesv2};
        my $len = length($key);
        $key = Digest::MD5::md5($key);
        $key = substr( $key, 0, $len ) if $len < 16;
    }
    if ( $$cryptInfo{_aesv2} or $$cryptInfo{_aesv3} ) {
        require Image::ExifTool::AES;
        my $err = Image::ExifTool::AES::Crypt( $dataPt, $key, $encrypt );
        $err and $$cryptInfo{_error} = $err;
    }
    else {
        RC4Crypt( $dataPt, $key );
    }
}

sub CryptStream($$) {
    return unless $cryptStream;
    my ( $dict, $keyExt ) = @_;
    my $type = $$dict{Type} || '';
    if (    $cryptInfo
        and $type ne '/XRef'
        and ( $$cryptInfo{_meta} or $type ne '/Metadata' ) )
    {
        Crypt( \$$dict{_stream}, $keyExt, $$dict{_decrypted} );
        $$dict{_decrypted} = ( $$dict{_decrypted} ? undef : 1 );
    }
    else {
        $$dict{_decrypted} = 0;
    }
}

sub NewPDFTag($$) {
    my ( $tagTablePtr, $tag ) = @_;
    my $name = $tag;
    $name =~ s/#([0-9a-f]{2})/chr(hex($1))/ige;
    $name =~ s/[^-\w]+/_/g;
    $name =~ s/(^|_)([a-z])/\U$2/g;
    my $tagInfo = { Name => $name };
    AddTagToTable( $tagTablePtr, $tag, $tagInfo );
    return $tagInfo;
}

sub ProcessAcroForm($$$$;$$) {
    my ( $et, $tagTablePtr, $dict, $xref, $nesting, $type ) = @_;
    $et->HandleTag( $tagTablePtr, '_has_xfa', $$dict{XFA} ? 'true' : 'false' );
    return 1 unless $et->Options('Verbose');
    return ProcessDict( $et, $tagTablePtr, $dict, $xref, $nesting, $type );
}

sub ProcessAF($$$$;$$) {
    my ( $et, $tagTablePtr, $dict, $xref, $nesting, $type ) = @_;
    $$et{AFRelationship} = $$dict{AFRelationship} || '';
    return 1
      unless $et->Options('Verbose')
      or $$et{AFRelationship} eq '/C2PA_Manifest';
    return ProcessDict( $et, $tagTablePtr, $dict, $xref, $nesting, $type );
}

sub ExpandArray($) {
    my $val  = shift;
    my @list = @$val;
    foreach (@list) {
        ref $_ eq 'SCALAR' and $_ = "ref($$_)",      next;
        ref $_ eq 'ARRAY'  and $_ = ExpandArray($_), next;
        defined $_ or $_ = '<undef>', next;
    }
    return '[' . join( ',', @list ) . ']';
}

sub ProcessDict($$$$;$$) {
    local $_;
    my ( $et, $tagTablePtr, $dict, $xref, $nesting, $type ) = @_;
    my $verbose = $et->Options('Verbose');
    my $unknown = $$tagTablePtr{EXTRACT_UNKNOWN};
    my $embedded =
      ( defined $unknown and not $unknown and $et->Options('ExtractEmbedded') );
    my @tags = @{ $$dict{_tags} };
    my ( $next, %join, $validInfo );
    my $index = 0;

    $nesting = ( $nesting || 0 ) + 1;
    if ( $nesting > 50 ) {
        $et->Warn('Nesting too deep (directory ignored)');
        return;
    }
    if (    $$et{PDF_CAPTURE}
        and $$tagTablePtr{VARS}
        and $tagTablePtr->{VARS}->{CAPTURE} )
    {
        my $name;
        foreach $name ( @{ $tagTablePtr->{VARS}->{CAPTURE} } ) {
            next if $$et{PDF_CAPTURE}{$name};
            next if $type and $type ne $name;
            $$et{PDF_CAPTURE}{$name} = $dict;
            last;
        }
    }
    $validInfo = ( $et->Options('Validate')
          and $tagTablePtr eq \%Image::ExifTool::PDF::Info );
    for ( ; ; ) {
        my ( $tag, $isSubDoc );
        if (@tags) {
            $tag = shift @tags;
        }
        elsif ( defined $next and not $next ) {
            $tag  = 'Next';
            $next = 1;
        }
        else {
            last;
        }
        my $val     = $$dict{$tag};
        my $tagInfo = $et->GetTagInfo( $tagTablePtr, $tag );
        if ($tagInfo) {
            undef $tagInfo if $$tagInfo{NoProcess};
        }
        elsif ( $embedded
            and $tag =~ /^(.*?)(\d+)$/
            and $$tagTablePtr{$1}
            and ( ref $val ne 'SCALAR' or not $fetched{$$val} ) )
        {
            my ( $name, $num ) = ( $1, $2 );
            $tagInfo = $et->GetTagInfo( $tagTablePtr, $name );
            if ( ref $tagInfo eq 'HASH' and $$tagInfo{JoinStreams} ) {
                $fetched{$$val} = 1;
                my $obj = FetchObject( $et, $$val, $xref, $tag );
                $join{$name} = [] unless $join{$name};
                next unless ref $obj eq 'HASH' and $$obj{_stream};
                DecodeStream( $et, $obj );
                $join{$name}->[$num] = $$obj{_stream};
                undef $tagInfo;
            }
            else {
                $isSubDoc = 1;
            }
        }
        if (    $validInfo
            and $$et{PDFVersion} >= 2.0
            and ( not $tagInfo or not $$tagInfo{PDF2} ) )
        {
            my $name = $tagInfo ? ":$$tagInfo{Name}" : " Info tag '${tag}'";
            $et->Warn("PDF$name is deprecated in PDF 2.0");
        }
        if ($verbose) {
            my ( $val2, $extra );
            if ( ref $val eq 'SCALAR' ) {
                $extra = ", indirect object ($$val)";
                if ( $fetched{$$val} ) {
                    $val2 = "ref($$val)";
                }
                elsif ( $tag eq 'Next' and not $next ) {
                    $next = 0;
                    next;
                }
                else {
                    $fetched{$$val} = 1;
                    $val = FetchObject( $et, $$val, $xref, $tag );
                    unless ( defined $val ) {
                        my $str;
                        if ( defined $lastOffset ) {
                            $val2 = '<free>';
                            $str  = 'Object was freed';
                        }
                        else {
                            $val2 = '<err>';
                            $str  = 'Error reading object';
                        }
                        $et->VPrint( 0, "$$et{INDENT}${str}:\n" );
                    }
                }
            }
            elsif ( ref $val eq 'HASH' ) {
                $extra = ', direct dictionary';
            }
            elsif ( ref $val eq 'ARRAY' ) {
                $extra = ', direct array of ' . scalar(@$val) . ' objects';
            }
            else {
                $extra = ', direct object';
            }
            my $isSubdir;
            if ( ref $val eq 'HASH' ) {
                $isSubdir = 1;
            }
            elsif ( ref $val eq 'ARRAY' ) {
                $isSubdir = 1 if @$val;
                foreach (@$val) {
                    next if ref $_ eq 'HASH' or ref $_ eq 'SCALAR';
                    undef $isSubdir;
                    last;
                }
            }
            if ($isSubdir) {
                $tagInfo
                  or $tagInfo = {
                    Name         => $tag,
                    SubDirectory =>
                      { TagTable => 'Image::ExifTool::PDF::Unknown' },
                  };
            }
            else {
                $val2 = ExpandArray($val) if ref $val eq 'ARRAY';
                if ( not $tagInfo and defined $val and $unknown ) {
                    $tagInfo = NewPDFTag( $tagTablePtr, $tag );
                }
            }
            $et->VerboseInfo(
                $tag, $tagInfo,
                Value => $val2 || $val,
                Extra => $extra,
                Index => $index++,
            );
            next unless defined $val;
        }
        unless ($tagInfo) {
            next unless $unknown;
            $tagInfo = NewPDFTag( $tagTablePtr, $tag );
        }
        my ( $oldDocNum, $oldNumTags );
        if ($isSubDoc) {
            $oldDocNum    = $$et{DOC_NUM};
            $oldNumTags   = $$et{NUM_FOUND};
            $$et{DOC_NUM} = ++$$et{DOC_COUNT};
        }
        if ( $$tagInfo{SubDirectory} ) {
            my @subDicts;
            if ( ref $val eq 'ARRAY' ) {
                if (    $$tagInfo{ConvertToDict}
                    and @$val == 2
                    and not ref $$val[0] )
                {
                    my $tg = $$val[0];
                    $tg =~ s(^/)();
                    my %dict = ( _tags => [$tg], $tg => $$val[1] );
                    @subDicts = ( \%dict );
                }
                else {
                    @subDicts = @{$val};
                }
            }
            else {
                @subDicts = ($val);
            }
            for ( ; ; ) {
                my $subDict = shift @subDicts or last;
                my $prevFetched = $lastFetched;
                if ( ref $subDict eq 'SCALAR' ) {
                    next if $fetched{$$subDict};
                    if ( $$tagInfo{IgnoreDuplicates} ) {
                        my $flag = "ProcessedPDF_$tag";
                        if ( $$et{$flag} ) {
                            next
                              if $et->Warn( "Ignored duplicate $tag dictionary",
                                2 );
                        }
                        else {
                            $$et{$flag} = 1;
                        }
                    }
                    $fetched{$$subDict} = 1;
                    my $obj = FetchObject( $et, $$subDict, $xref, $tag );
                    unless ( defined $obj ) {
                        unless ( defined $lastOffset ) {
                            $et->Warn("Error reading $tag object ($$subDict)");
                        }
                        next;
                    }
                    $subDict = $obj;
                }
                if ( ref $subDict eq 'ARRAY' ) {
                    next if @$subDict < 2;
                    my %hash = ( _tags => [] );
                    while ( @$subDict >= 2 ) {
                        my $key = shift @$subDict;
                        $key =~ s/^\///;
                        push @{ $hash{_tags} }, $key;
                        $hash{$key} = shift @$subDict;
                    }
                    $subDict = \%hash;
                }
                else {
                    next unless ref $subDict eq 'HASH';
                }
                $$subDict{_needCrypt}{'*'} = 1 unless $lastFetched;
                my $subTablePtr =
                  GetTagTable( $tagInfo->{SubDirectory}->{TagTable} );
                if ( not $verbose ) {
                    my $proc = $$subTablePtr{PROCESS_PROC} || \&ProcessDict;
                    &$proc( $et, $subTablePtr, $subDict, $xref, $nesting );
                }
                elsif ($next) {
                    undef $next;
                    $index       = 0;
                    $tagTablePtr = $subTablePtr;
                    $dict        = $subDict;
                    @tags        = @{ $$subDict{_tags} };
                    $et->VerboseDir( $tag, scalar(@tags) );
                }
                else {
                    my $oldIndent = $$et{INDENT};
                    my $oldDir    = $$et{DIR_NAME};
                    $$et{INDENT} .= '| ';
                    $$et{DIR_NAME} = $tag;
                    $et->VerboseDir( $tag, scalar( @{ $$subDict{_tags} } ) );
                    my $proc = $$subTablePtr{PROCESS_PROC} || \&ProcessDict;
                    &$proc( $et, $subTablePtr, $subDict, $xref, $nesting );
                    $$et{INDENT}   = $oldIndent;
                    $$et{DIR_NAME} = $oldDir;
                }
                $lastFetched = $prevFetched;
            }
        }
        else {
            if ( ref $val eq 'SCALAR' ) {
                my $prevFetched = $lastFetched;
                $val = FetchObject( $et, $$val, $xref, $tag );
                if ( defined $val ) {
                    $val = ReadPDFValue($val);
                    $$dict{_needCrypt}{$tag} = ( $lastFetched ? 0 : 1 )
                      if $cryptString;
                    $lastFetched = $prevFetched;
                }
            }
            else {
                $val = ReadPDFValue($val);
            }
            if ( ref $val ) {
                if ( ref $val eq 'ARRAY' ) {
                    delete $$et{LIST_TAGS}{$tagInfo} if $$tagInfo{List};
                    my $v;
                    foreach $v (@$val) {
                        $et->FoundTag( $tagInfo, $v );
                    }
                }
            }
            elsif ( defined $val ) {
                my $format =
                  $$tagInfo{Format} || $$tagInfo{Writable} || 'string';
                $val = ConvertPDFDate($val) if $format eq 'date';
                if ( not $$tagInfo{Binary} and $val =~ /[\x18-\x1f\x80-\xff]/ )
                {
                    $val = $et->Decode( $val,
                        ( $val =~ s/^\xfe\xff// ? 'UTF16' : 'PDFDoc' ), 'MM' );
                }
                if ( $$tagInfo{List} and not $$et{OPTIONS}{NoPDFList} ) {
                    my $comma = $val =~ tr/,/,/;
                    my $semi  = $val =~ tr/;/;/;
                    my $split;
                    if ( $comma or $semi ) {
                        $split = $comma > $semi ? ',+\\s*' : ';+\\s*';
                    }
                    else {
                        $split = ' ';
                    }
                    my @values = split $split, $val;
                    $et->FoundTag( $tagInfo, $_ ) foreach @values;
                }
                else {
                    $et->FoundTag( $tagInfo, $val );
                }
            }
        }
        if ($isSubDoc) {
            $$et{DOC_NUM} = $oldDocNum;
            --$$et{DOC_COUNT} if $oldNumTags == $$et{NUM_FOUND};
        }
    }

    if (%join) {
        my ( $tag, $i );
        foreach $tag ( sort keys %join ) {
            my $list = $join{$tag};
            last unless defined $$list[1] and $$list[1] =~ /^%.*?([\x0d\x0a]*)/;
            my $buff = "%!PS-Adobe-3.0$1";
            for ( $i = 1 ; defined $$list[$i] ; ++$i ) {
                $buff .= $$list[$i];
                undef $$list[$i];
            }
            my $oldDocNum  = $$et{DOC_NUM};
            my $oldNumTags = $$et{NUM_FOUND};
            $$et{DOC_NUM} = ++$$et{DOC_COUNT};
            $et->HandleTag( $tagTablePtr, $tag, $buff );
            $$et{DOC_NUM} = $oldDocNum;
            --$$et{DOC_COUNT} if $oldNumTags == $$et{NUM_FOUND};
            delete $$et{DOC_NUM};
        }
    }
    for ( ; ; ) {
        last unless $$dict{_stream};
        my $tag = '_stream';
        ( $tag = $$dict{Subtype} . $tag ) =~ s/^\/// if $$dict{Subtype};
        last unless $$tagTablePtr{$tag};
        my $tagInfo = $et->GetTagInfo( $tagTablePtr, $tag ) or last;
        my $subdir  = $$tagInfo{SubDirectory};
        unless ($subdir) {
            delete $$et{LIST_TAGS}{ $$tagTablePtr{Filter} };
            my $filter = $$dict{Filter} || '';
            $filter = @$filter[-1] if ref $filter eq 'ARRAY';
            my $result;
            if ( $filter eq '/DCTDecode' or $filter eq '/JPXDecode' ) {
                DecodeStream( $et, $dict ) or last;
                $et->FoundTag( $tagInfo, \$$dict{_stream} );
                $result =
                  $et->ExtractInfo( \$$dict{_stream}, { ReEntry => 1 } );
            }
            unless ($result) {
                $et->FoundTag( 'FileType',
                    defined $result ? '(unknown)' : '(unsupported)' );
            }
            last;
        }
        if (
            $cryptInfo
            and (  $$cryptInfo{_aesv2}
                or $$cryptInfo{_aesv3}
                and $$dict{Length}
                and $$dict{Length} > 10000 )
            and not $$dict{_decrypted}
            and not $$et{PDF_CAPTURE}
          )
        {
            my $type = $$dict{Type} || '';
            if ( $type ne '/Metadata' or $$dict{Length} > 100000 ) {
                if ( $$et{OPTIONS}{IgnoreMinorErrors} ) {
                    $et->Warn(
                        "Decrypting large $$tagInfo{Name} (will be slow)");
                }
                else {
                    $et->Warn( "Skipping large AES-encrypted $$tagInfo{Name}",
                        2 );
                    last;
                }
            }
        }
        DecodeStream( $et, $dict ) or last;
        if ( $verbose > 2 ) {
            $et->VPrint( 2, "$$et{INDENT}$$et{DIR_NAME} stream data\n" );
            $et->VerboseDump( \$$dict{_stream} );
        }
        my %dirInfo = (
            DataPt   => \$$dict{_stream},
            DataLen  => length $$dict{_stream},
            DirStart => 0,
            DirLen   => length $$dict{_stream},
            Parent   => 'PDF',
            DirName  => $$subdir{DirName},
        );
        my $subTablePtr = GetTagTable( $$subdir{TagTable} );
        unless ( $et->ProcessDirectory( \%dirInfo, $subTablePtr ) ) {
            $et->Warn("Error processing $$tagInfo{Name} information");
        }
        last;
    }
}

sub ReadPDF($$) {
    my ( $et, $dirInfo ) = @_;
    my $raf     = $$dirInfo{RAF};
    my $verbose = $et->Options('Verbose');
    my ( $buff, $encrypt, $id );
    $raf->Read( $buff, 1024 ) >= 8   or return 0;
    $buff =~ /^(\s*)%PDF-(\d+\.\d+)/ or return 0;
    $$et{PDFBase} = length $1
      and $et->Warn( 'PDF header is not at start of file', 1 );
    $pdfVer = $$et{PDFVersion} = $2;
    $et->SetFileType();

    my $tagTablePtr = GetTagTable('Image::ExifTool::PDF::Root');
    $et->HandleTag( $tagTablePtr, 'Version', $pdfVer );
    $tagTablePtr = GetTagTable('Image::ExifTool::PDF::Main');
    my $capture = $$et{PDF_CAPTURE};
    unless ($capture) {
        my $lin = 'false';
        if ( $buff =~ /<</g ) {
            $buff = substr( $buff, pos($buff) - 2 );
            my $dict = ExtractObject( $et, \$buff );
            if ( ref $dict eq 'HASH' and $$dict{Linearized} and $$dict{L} ) {
                if ( not $$et{VALUE}{FileSize} ) {
                    undef $lin;
                }
                elsif ( $$dict{L} == $$et{VALUE}{FileSize} - $$et{PDFBase} ) {
                    $lin = 'true';
                }
            }
        }
        $et->HandleTag( $tagTablePtr, '_linearized', $lin ) if $lin;
    }
    my @xrefOffsets;
    $raf->Seek( 0, 2 ) or return -2;
    my $len = $raf->Tell();
    $len = 1024 if $len > 1024;
    $raf->Seek( -$len, 2 )            or return -2;
    $raf->Read( $buff, $len ) == $len or return -3;
    $buff =~ /^.*startxref(\s+)(\d+)(\s+)((%[^\x0d\x0a]*\s+)*)%%EOF/s
      or return -4;

    if ($4) {
        my @com = split /[\x0d\x0d]+/, $4;
        foreach (@com) {
            /^(%+\s*)<seal seal=/ or next;
            my $dat = substr $_, length($1);
            my $tbl = GetTagTable('Image::ExifTool::XMP::SEAL');
            $et->ProcessDirectory( { DataPt => \$dat }, $tbl );
        }
    }
    my $ws = $1 . $3;
    my $xr = $2;
    push @xrefOffsets, $xr, 'Main';
    local $/ = $ws =~ /(\x0d\x0a|\x0d|\x0a)/ ? $1 : "\x0a";
    my ( %xref, @mainDicts, %loaded, $mainFree );
    my ( $xrefSize, $mainDictSize ) = ( 0, 0 );
    if ($capture) {
        $capture->{startxref} = $xr;
        $capture->{xref}      = \%xref;
        $capture->{newline}   = $/;
        $capture->{mainFree}  = $mainFree = {};
    }
  XRef:
    while (@xrefOffsets) {
        my $offset = shift @xrefOffsets;
        my $type   = shift @xrefOffsets;
        next if $loaded{$offset};
        unless ( $raf->Seek( $offset + $$et{PDFBase}, 0 ) ) {
            %loaded or return -5;
            $et->Warn('Bad offset for secondary xref table');
            next;
        }
        for ( ; ; ) {
            unless ( $raf->ReadLine($buff) ) {
                %loaded or return -6;
                $et->Warn('Bad offset for secondary xref table');
                next XRef;
            }
            last if $buff =~ /\S/;
        }
        my $loadXRefStream;
        if ( $buff =~ s/^\s*xref\s+//s ) {
            for ( ; ; ) {
                $raf->ReadLine($buff) or return -6 until $buff =~ /\S/;
                last if $buff =~ s/^\s*trailer([\s<[(])/$1/s;
                $buff =~ s/^\s*(\d+)\s+(\d+)\s+//s or return -4;
                my ( $start, $num ) = ( $1, $2 );
                $raf->Seek( -length($buff), 1 ) or return -4;
                my $i;
                for ( $i = 0 ; $i < $num ; ++$i ) {
                    $raf->Read( $buff, 20 ) == 20          or return -6;
                    $buff =~ /^\s*(\d{10}) (\d{5}) (f|n)/s or return -4;
                    my $num = $start + $i;
                    $xrefSize = $num if $num > $xrefSize;
                    LocateAnyObject( \%xref, $num ) if $xref{dicts};
                    unless ( defined $xref{$num} ) {
                        my ( $offset, $gen ) = ( int($1), int($2) );
                        $xref{$num} = $offset;
                        if ( $3 eq 'f' ) {
                            $$mainFree{$num} = [ $offset, $gen, 'f' ]
                              if $mainFree;
                            next;
                        }
                        $xref{"$num $gen R"} = $offset;
                    }
                }
                $buff = '';
            }
            undef $mainFree;
        }
        elsif ( $buff =~ s/^\s*(\d+)\s+(\d+)\s+obj//s ) {
            $loadXRefStream = 1;
        }
        else {
            %loaded or return -4;
            $et->Warn('Invalid secondary xref table');
            next;
        }
        my $mainDict = ExtractObject( $et, \$buff, $raf, \%xref );
        unless ( ref $mainDict eq 'HASH' ) {
            %loaded or return -8;
            $et->Warn('Error loading secondary dictionary');
            next;
        }
        $mainDictSize = $$mainDict{Size}
          if $$mainDict{Size} and $$mainDict{Size} > $mainDictSize;
        if ($loadXRefStream) {
            if (    $$mainDict{Type} eq '/XRef'
                and $$mainDict{W}
                and @{ $$mainDict{W} } > 2
                and $$mainDict{Size}
                and DecodeStream( $et, $mainDict ) )
            {
                $$mainDict{Index}
                  or $$mainDict{Index} = [ 0, $$mainDict{Size} ];
                my $w    = $$mainDict{W};
                my $size = 0;
                foreach (@$w) { $size += $_; }
                $$mainDict{_entry_size} = $size;
                $xref{dicts} = [] unless $xref{dicts};
                push @{ $xref{dicts} }, $mainDict;
            }
            else {
                %loaded or return -9;
                $et->Warn('Invalid xref stream in secondary dictionary');
            }
        }
        $loaded{$offset} = 1;
        push @xrefOffsets, $$mainDict{XRefStm}, 'XRefStm'
          if $$mainDict{XRefStm};
        $encrypt = $$mainDict{Encrypt} if $$mainDict{Encrypt};
        undef $encrypt                 if $encrypt and $encrypt eq 'null';
        if ( $$mainDict{ID} and ref $$mainDict{ID} eq 'ARRAY' ) {
            $id = ReadPDFValue( $mainDict->{ID}->[0] );
        }
        push @mainDicts, $mainDict, $type;
        push @xrefOffsets, $$mainDict{Prev}, 'Prev' if $$mainDict{Prev};
    }
    if ( $xrefSize > $mainDictSize ) {
        my $str =
"Objects in xref table ($xrefSize) exceed trailer dictionary Size ($mainDictSize)";
        $capture ? $et->Error($str) : $et->Warn($str);
    }
    if ($encrypt) {
        if ( ref $encrypt eq 'SCALAR' ) {
            $encrypt = FetchObject( $et, $$encrypt, \%xref, 'Encrypt' );
        }
        my $err = DecryptInit( $et, $encrypt, $id );
        if ($err) {
            $et->Warn($err);
            $$capture{Error} = $err if $capture;
            return -1;
        }
    }
    my $i   = 0;
    my $num = ( scalar @mainDicts ) / 2;
    while (@mainDicts) {
        my $dict = shift @mainDicts;
        my $type = shift @mainDicts;
        if ($verbose) {
            ++$i;
            my $n = scalar( @{ $$dict{_tags} } );
            $et->VPrint( 0, "PDF dictionary ($i of $num) with $n entries:\n" );
        }
        ProcessDict( $et, $tagTablePtr, $dict, \%xref, 0, $type );
    }
    if ($encrypt) {
        my $err = $$encrypt{_error};
        if ($err) {
            $et->Warn($err);
            $$capture{Error} = $err if $capture;
            return -1;
        }
    }
    return 1;
}

my %pdfWarning = (
    -2 => 'Error seeking in file',
    -3 => 'Error reading file',
    -4 => 'Invalid xref table',
    -5 => 'Invalid xref offset',
    -6 => 'Error reading xref table',
    -7 => 'Error reading trailer',
    -8 => 'Error reading main dictionary',
    -9 => 'Invalid xref stream in main dictionary',
);

sub ProcessPDF($$) {
    my ( $et, $dirInfo ) = @_;

    undef $cryptInfo;
    undef $cryptStream;
    undef $cryptString;
    my $result = ReadPDF( $et, $dirInfo );
    if ( $result < 0 ) {
        $et->Warn( $pdfWarning{$result} ) if $pdfWarning{$result};
        $result = 1;
    }
    undef %streamObjs;
    undef %fetched;
    return $result;
}

1;

__END__

