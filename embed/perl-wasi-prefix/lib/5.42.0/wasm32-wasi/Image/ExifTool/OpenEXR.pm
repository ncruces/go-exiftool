
package Image::ExifTool::OpenEXR;

use strict;
use vars            qw($VERSION);
use Image::ExifTool qw(:DataAccess :Utils);
use Image::ExifTool::GPS;

$VERSION = '1.07';

my %formatType = (
    box2f          => 'float[4]',
    box2i          => 'int32s[4]',
    chlist         => 1,
    chromaticities => 'float[8]',
    compression    => 'int8u',
    double         => 'double',
    envmap         => 'int8u',
    float          => 'float',
    'int'          => 'int32s',
    keycode        => 'int32s[7]',
    lineOrder      => 'int8u',
    m33f           => 'float[9]',
    m44f           => 'float[16]',
    rational       => 'rational64s',
    string         => 'string',
    stringvector   => 1,
    tiledesc       => 1,
    timecode       => 'int32u[2]',
    v2f            => 'float[2]',
    v2i            => 'int32s[2]',
    v3f            => 'float[3]',
    v3i            => 'int32s[3]',
);

%Image::ExifTool::OpenEXR::Main = (
    GROUPS => { 2 => 'Image' },
    NOTES  => q{
        Information extracted from EXR images.  Use the ExtractEmbedded option to
        extract information from all frames of a multipart image.  See
        L<http://www.openexr.com/> for the official specification.
    },
    _ver   => { Name => 'EXRVersion', Notes => 'low byte of Flags word' },
    _flags => {
        Name      => 'Flags',
        PrintConv => {
            BITMASK => {
                9  => 'Tiled',
                10 => 'Long names',
                11 => 'Deep data',
                12 => 'Multipart',
            }
        },
    },
    adoptedNeutral => {},
    altitude       => {
        Name      => 'GPSAltitude',
        Groups    => { 2 => 'Location' },
        PrintConv => q{
            $val = int($val * 10) / 10;
            return(($val =~ s/^-// ? "$val m Below" : "$val m Above") . " Sea Level");
        },
    },
    aperture       => { PrintConv => 'sprintf("%.1f",$val)' },
    channels       => {},
    chromaticities => {},
    capDate        => {
        Name        => 'DateTimeOriginal',
        Description => 'Date/Time Original',
        Groups      => { 2 => 'Time' },
        PrintConv   => '$self->ConvertDateTime($val)',
    },
    comments    => {},
    compression => {
        PrintConvColumns => 2,
        PrintConv        => {
            0 => 'None',
            1 => 'RLE',
            2 => 'ZIPS',
            3 => 'ZIP',
            4 => 'PIZ',
            5 => 'PXR24',
            6 => 'B44',
            7 => 'B44A',
            8 => 'DWAA',
            9 => 'DWAB',
        },
    },
    dataWindow    => {},
    displayWindow => {},
    envmap        => {
        Name      => 'EnvironmentMap',
        PrintConv => {
            0 => 'Latitude/Longitude',
            1 => 'Cube',
        },
    },
    expTime => {
        Name      => 'ExposureTime',
        PrintConv => 'Image::ExifTool::Exif::PrintExposureTime($val)',
    },
    focus => {
        Name      => 'FocusDistance',
        PrintConv => '"$val m"',
    },
    framesPerSecond => {},
    keyCode         => {},
    isoSpeed        => { Name => 'ISO' },
    latitude        => {
        Name      => 'GPSLatitude',
        Groups    => { 2 => 'Location' },
        PrintConv => 'Image::ExifTool::GPS::ToDMS($self, $val, 1, "N")',
    },
    lineOrder => {
        PrintConv => {
            0 => 'Increasing Y',
            1 => 'Decreasing Y',
            2 => 'Random Y',
        },
    },
    longitude => {
        Name      => 'GPSLongitude',
        Groups    => { 2 => 'Location' },
        PrintConv => 'Image::ExifTool::GPS::ToDMS($self, $val, 1, "E")',
    },
    lookModTransform   => {},
    multiView          => {},
    owner              => { Groups => { 2 => 'Author' } },
    pixelAspectRatio   => {},
    preview            => { Groups => { 2 => 'Preview' } },
    renderingTransform => {},
    screenWindowCenter => {},
    screenWindowWidth  => {},
    tiles              => {},
    timeCode           => {},
    utcOffset          => {
        Name      => 'TimeZone',
        Groups    => { 2 => 'Time' },
        PrintConv => 'TimeZoneString($val / 60)',
    },
    whiteLuminance => {},
    worldToCamera  => {},
    worldToNDC     => {},
    wrapmodes      => { Name => 'WrapModes' },
    xDensity       => { Name => 'XResolution' },
    name           => {},
    type           => {},
    version        => {},
    chunkCount     => {},
    exif => {
        Name         => 'EXIF',
        SubDirectory => {
            TagTable    => 'Image::ExifTool::Exif::Main',
            ProcessProc => \&Image::ExifTool::ProcessTIFF,
            Start       => 4,
        },
    },
    xmp => {
        Name         => 'XMP',
        SubDirectory => { TagTable => 'Image::ExifTool::XMP::Main' },
    },
);

sub ProcessEXR($$) {
    my ( $et, $dirInfo ) = @_;
    my $raf     = $$dirInfo{RAF};
    my $verbose = $et->Options('Verbose');
    my $binary  = $et->Options('Binary') || $verbose;
    my ( $buff, $dim );

    return 0 unless $raf->Read( $buff, 8 ) == 8;
    return 0 unless $buff =~ /^\x76\x2f\x31\x01/s;
    $et->SetFileType();
    SetByteOrder('II');
    my $tagTablePtr = GetTagTable('Image::ExifTool::OpenEXR::Main');

    my $flags = unpack( 'x4V', $buff );
    $et->HandleTag( $tagTablePtr, '_ver',   $flags & 0xff );
    $et->HandleTag( $tagTablePtr, '_flags', $flags & 0xffffff00 );
    my $maxLen = ( $flags & 0x400 ) ? 255 : 31;
    my $multi  = $flags & 0x1000;

    for ( ; ; ) {
        $raf->Read( $buff, ( $maxLen + 1 ) * 2 + 5 ) or last;
        if ( $buff =~ /^\0/ ) {
            last unless $multi and $et->Options('ExtractEmbedded');
            last if $buff =~ s/^(\0+)// and length($1) > 1;
            $$et{DOC_NUM} = ++$$et{DOC_COUNT};
        }
        unless ( $buff =~ /^([^\0]{1,$maxLen})\0([^\0]{1,$maxLen})\0(.{4})/sg )
        {
            $et->Warn('EXR format error');
            last;
        }
        my ( $tag, $type, $size ) = ( $1, $2, unpack( 'V', $3 ) );
        unless ( $raf->Seek( pos($buff) - length($buff), 1 ) ) {
            $et->Warn('Seek error');
            last;
        }
        my $tagInfo = $et->GetTagInfo( $tagTablePtr, $tag );
        unless ($tagInfo) {
            my $name = ucfirst $tag;
            $name =~ s/([^a-zA-Z])([a-z])/$1\U$2/g;
            $name =~ tr/-_a-zA-Z0-9//dc;
            if ( length $name <= 1 ) {
                if ( length $name ) {
                    $name = "Tag$name";
                }
                else {
                    $name = 'Invalid';
                }
            }
            $tagInfo = { Name => $name };
            AddTagToTable( $tagTablePtr, $tag, $tagInfo );
            $et->VPrint( 0, $$et{INDENT}, "[adding $tag]\n" );
        }
        my ( $val, $success, $buf2 );
        my $format = $formatType{$type};
        my $subdir = $$tagInfo{SubDirectory};
        if ( $format or $binary or $subdir ) {
            $raf->Read( $buf2, $size ) == $size and $success = 1;
            if ($subdir) {
                $et->HandleTag(
                    $tagTablePtr, $tag, undef,
                    DataPt  => \$buf2,
                    DataPos => $raf->Tell() - length($buf2)
                );
                next if $success;
            }
            elsif ( not $format ) {
                $val = \$buf2;
            }
            elsif ( $format ne '1' ) {
                if ( $format =~ /^(\w+)\[?(\d*)/ ) {
                    my ( $fmt, $cnt ) = ( $1, $2 );
                    $cnt = $fmt eq 'string' ? $size : 1 unless $cnt;
                    $val = ReadValue( \$buf2, 0, $fmt, $cnt, $size );
                }
            }
            elsif ( $type eq 'tiledesc' ) {
                if ( $size >= 9 ) {
                    my $x    = Get32u( \$buf2, 0 );
                    my $y    = Get32u( \$buf2, 4 );
                    my $mode = Get8u( \$buf2, 8 );
                    my $lvl  = {
                        0 => 'One Level',
                        1 => 'MIMAP Levels',
                        2 => 'RIPMAP Levels'
                    }->{ $mode & 0x0f };
                    $lvl or $lvl = 'Unknown Levels (' . ( $mode & 0xf ) . ')';
                    my $rnd =
                      { 0 => 'Round Down', 1 => 'Round Up' }->{ $mode >> 4 };
                    $rnd or $rnd = 'Unknown Rounding (' . ( $mode >> 4 ) . ')';
                    $val = "${x}x$y; $lvl; $rnd";
                }
            }
            elsif ( $type eq 'chlist' ) {
                $val = [];
                while ( $buf2 =~ /\G([^\0]{1,31})\0(.{16})/sg ) {
                    my ( $str, $dat ) = ( $1, $2 );
                    my ( $pix, $lin, $x, $y ) = unpack( 'VCx3VV', $dat );
                    $pix = { 0 => 'int8u', 1 => 'half', 2 => 'float' }->{$pix}
                      || "unknown($pix)";
                    push @$val,
                      "$str $pix" . ( $lin ? ' linear' : '' ) . " $x $y";
                }
            }
            elsif ( $type eq 'stringvector' ) {
                $val = [];
                my $pos = 0;
                while ( $pos + 4 <= length($buf2) ) {
                    my $len = Get32u( \$buf2, $pos );
                    last if $pos + 4 + $len > length($buf2);
                    push @$val, substr( $buf2, $pos + 4, $len );
                    $pos += 4 + $len;
                }
            }
            else {
                $val = \$buf2;
            }
        }
        else {
            $val     = \ "Binary data $size bytes";
            $success = $raf->Seek( $size, 1 );
        }
        unless ($success) {
            $et->Warn('Truncated or corrupted EXR file');
            last;
        }
        $val = '<bad>' unless defined $val;

        if (
            (
                $tag eq 'dataWindow'
                or ( not $dim and $tag eq 'displayWindow' )
            )
            and $val =~ /^(-?\d+) (-?\d+) (-?\d+) (-?\d+)$/
            and not $$et{DOC_NUM}
          )
        {
            $dim = [ $3 - $1 + 1, $4 - $2 + 1 ];
        }
        if ($verbose) {
            my $dataPt = ref $val eq 'SCALAR' ? $val : \$buf2;
            $et->VerboseInfo(
                $tag, $tagInfo,
                Table  => $tagTablePtr,
                Value  => $val,
                Size   => $size,
                Format => $type,
                DataPt => $dataPt,
                Addr   => $raf->Tell() - $size,
            );
        }
        $et->FoundTag( $tagInfo, $val );
    }
    delete $$et{DOC_NUM};
    if ($dim) {
        $et->FoundTag( 'ImageWidth',  $$dim[0] );
        $et->FoundTag( 'ImageHeight', $$dim[1] );
    }
    return 1;
}

1;

__END__


