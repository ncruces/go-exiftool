
package Image::ExifTool::FLIF;

use strict;
use vars            qw($VERSION);
use Image::ExifTool qw(:DataAccess :Utils);

$VERSION = '1.02';

my %flifMap = (
    EXIF         => 'FLIF',
    XMP          => 'FLIF',
    ICC_Profile  => 'FLIF',
    IFD0         => 'EXIF',
    IFD1         => 'IFD0',
    ExifIFD      => 'IFD0',
    GPS          => 'IFD0',
    SubIFD       => 'IFD0',
    GlobParamIFD => 'IFD0',
    PrintIM      => 'IFD0',
    InteropIFD   => 'ExifIFD',
    MakerNotes   => 'ExifIFD',
);

%Image::ExifTool::FLIF::Main = (
    GROUPS => { 0      => 'File', 1 => 'File', 2 => 'Image' },
    VARS   => { ID_FMT => 'dec' },
    NOTES  => q{
        Information extracted from Free Lossless Image Format files.  See
        L<http://flif.info/> for more information.
    },
    0 => {
        Name      => 'ImageType',
        PrintConv => {
            '1' => 'Grayscale (non-interlaced)',
            '3' => 'RGB (non-interlaced)',
            '4' => 'RGBA (non-interlaced)',
            'A' => 'Grayscale (interlaced)',
            'C' => 'RGB (interlaced)',
            'D' => 'RGBA (interlaced)',
            'Q' => 'Grayscale Animation (non-interlaced)',
            'S' => 'RGB Animation (non-interlaced)',
            'T' => 'RGBA Animation (non-interlaced)',
            'a' => 'Grayscale Animation (interlaced)',
            'c' => 'RGB Animation (interlaced)',
            'd' => 'RGBA Animation (interlaced)',
        },
    },
    1 => {
        Name      => 'BitDepth',
        PrintConv => {
            '0' => 'Custom',
            '1' => 8,
            '2' => 16,
        },
    },
    2 => 'ImageWidth',
    3 => 'ImageHeight',
    4 => 'AnimationFrames',
    5 => {
        Name      => 'Encoding',
        PrintConv => {
            0 => 'FLIF16',
        },
    },
    iCCP => {
        Name         => 'ICC_Profile',
        SubDirectory => {
            TagTable => 'Image::ExifTool::ICC_Profile::Main',
        },
    },
    eXif => {
        Name         => 'EXIF',
        SubDirectory => {
            TagTable    => 'Image::ExifTool::Exif::Main',
            ProcessProc => \&Image::ExifTool::ProcessTIFF,
            WriteProc   => \&Image::ExifTool::WriteTIFF,
            Start       => 6,
            Header      => "Exif\0\0",
        },
    },
    eXmp => {
        Name         => 'XMP',
        SubDirectory => {
            TagTable => 'Image::ExifTool::XMP::Main',
        },
    },
);

sub GetVarInt($;$) {
    my ( $raf, $add ) = @_;
    my ( $val, $buff );
    for ( $val = 0 ; ; $val <<= 7 ) {
        $raf->Read( $buff, 1 ) or return undef;
        my $byte = ord($buff);
        $val |= ( $byte & 0x7f );
        last unless $byte & 0x80;
    }
    return $val + ( $add || 0 );
}

sub SetVarInt($) {
    my $val  = shift;
    my $buff = '';
    my $high = 0;
    for ( ; ; ) {
        $buff = chr( ( $val & 0x7f ) | $high ) . $buff;
        last unless $val >>= 7;
        $high = 0x80;
    }
    return $buff;
}

sub ReadFLIFHeader($) {
    my $raf = shift;
    my ( $buff, @vals );

    return ()
      unless $raf->Read( $buff, 6 ) == 6 and $buff =~ /^FLIF([0-\x6f])([0-2])/;

    push @vals, $1, $2;
    push @vals, GetVarInt( $raf, +1 ), GetVarInt( $raf, +1 );
    push @vals, GetVarInt( $raf, +2 ) if $vals[0] gt 'H';

    return () unless defined $vals[-1];
    return @vals if wantarray;

    my $hdrLen = $raf->Tell();
    return ()
      unless $raf->Seek( 0, 0 )
      and $raf->Read( $buff, $hdrLen ) == $hdrLen;
    return $buff;
}

sub WriteFLIF($$) {
    my ( $et, $dirInfo ) = @_;
    my $raf = $$dirInfo{RAF};
    my ( $buff, $soi, @addTags, %doneTag );

    $buff = ReadFLIFHeader($raf) or return 0;
    my $outfile = $$dirInfo{OutFile};
    Write( $outfile, $buff ) or return -1;

    $et->InitWriteDirs( \%flifMap );
    my $tagTablePtr = GetTagTable('Image::ExifTool::FLIF::Main');

    for ( ; ; ) {
        my ( $tag, $size, $inflated );
        if ( not defined $soi ) {
            $raf->Read( $buff, 4 ) == 4 or $et->Error('Unexpected EOF'), last;
            if ( $buff lt ' ' ) {
                $soi = $buff;

                foreach $tag ( 'eXif', 'eXmp', 'iCCP' ) {
                    push @addTags, $tag
                      if $$et{ADD_DIRS}{ $$tagTablePtr{$tag}{Name} }
                      and not $doneTag{$tag};
                }
            }
        }
        if ( not defined $soi ) {
            $tag  = $buff;
            $size = GetVarInt($raf);
        }
        elsif (@addTags) {
            $tag = shift @addTags;
            ( $buff, $size ) = ( '', 0 );
        }
        else {
            Write( $outfile, $soi ) or return -1;
            Write( $outfile, $buff )
              or return -1
              while $raf->Read( $buff, 65536 );
            last;
        }
        my $tagInfo = $et->GetTagInfo( $tagTablePtr, $tag );
        if (    $tagInfo
            and $$tagInfo{SubDirectory}
            and $$et{EDIT_DIRS}{ $$tagInfo{Name} } )
        {
            $doneTag{$tag} = 1;
            unless ( defined $soi ) {
                $raf->Read( $buff, $size ) == $size
                  or $et->Error("Truncated FLIF $tag chunk"), last;
            }
            if (    eval { require IO::Uncompress::RawInflate }
                and eval { require IO::Compress::RawDeflate } )
            {
                if ( length $buff == 0 ) {
                    $inflated = $buff;
                }
                elsif (
                    not IO::Uncompress::RawInflate::rawinflate(
                        \$buff => \$inflated
                    )
                  )
                {
                    $et->Error("Error inflating FLIF $tag chunk"), last;
                }
                my $subdir     = $$tagInfo{SubDirectory};
                my %subdirInfo = (
                    DirName  => $$tagInfo{Name},
                    DataPt   => \$inflated,
                    DirStart => length($inflated) ? $$subdir{Start} : undef,
                    ReadOnly => 1,
                );
                my $subTable = GetTagTable( $$subdir{TagTable} );
                $inflated = $et->WriteDirectory( \%subdirInfo, $subTable,
                    $$subdir{WriteProc} );
                if ( defined $inflated ) {
                    next unless length $inflated;
                    $inflated = $$subdir{Header} . $inflated
                      if $$subdir{Header};
                    unless (
                        IO::Compress::RawDeflate::rawdeflate(
                            \$inflated => \$buff
                        )
                      )
                    {
                        $et->Error("Error deflating FLIF $tag chunk"), last;
                    }
                }
            }
            else {
                $et->Warn(
                    'Install IO::Compress::RawDeflate to write FLIF metadata');
            }
            Write( $outfile, $tag, SetVarInt( length $buff ), $buff )
              or return -1;
        }
        elsif ( not defined $soi ) {
            Write( $outfile, $tag, SetVarInt($size) )           or return -1;
            Image::ExifTool::CopyBlock( $raf, $outfile, $size ) or return -1;
        }
    }
    return 1;
}

sub ProcessFLIF($$) {
    my ( $et, $dirInfo ) = @_;
    my $raf = $$dirInfo{RAF};
    my ( $buff, $tag, $inflated );

    my @vals = ReadFLIFHeader($raf) or return 0;

    $et->SetFileType();
    my $tagTablePtr = GetTagTable('Image::ExifTool::FLIF::Main');
    my $verbose     = $et->Options('Verbose');

    $et->VPrint( 0, "FLIF header:\n" ) if $verbose;
    for ( $tag = 0 ; defined $vals[$tag] ; ++$tag ) {
        $et->HandleTag( $tagTablePtr, $tag, $vals[$tag] );
    }

    for ( ; ; ) {
        $raf->Read( $tag, 4 ) == 4 or $et->Warn('Unexpected EOF'), last;
        my $byte = ord substr( $tag, 0, 1 );
        $byte < 32 and $et->HandleTag( $tagTablePtr, 5, $byte ), last;
        my $size = GetVarInt($raf);
        $et->VPrint( 0, "FLIF $tag ($size bytes):\n" ) if $verbose;
        if ( $$tagTablePtr{$tag} ) {
            $raf->Read( $buff, $size ) == $size
              or $et->Warn("Truncated FLIF $tag chunk"), last;
            $et->VerboseDump( \$buff, Addr => $raf->Tell() - $size )
              if $verbose > 2;
            if ( eval { require IO::Uncompress::RawInflate } ) {
                if (
                    IO::Uncompress::RawInflate::rawinflate(
                        \$buff => \$inflated
                    )
                  )
                {
                    $et->HandleTag(
                        $tagTablePtr, $tag, $inflated,
                        DataPt => \$inflated,
                        Size   => length $inflated,
                        Extra  => ' inflated',
                    );
                }
                else {
                    $et->Warn("Error inflating FLIF $tag chunk");
                }
            }
            else {
                $et->Warn(
                    'Install IO::Uncompress::RawInflate to decode FLIF metadata'
                );
            }
        }
        else {
            $raf->Seek( $size, 1 ) or $et->Warn('Seek error'), last;
        }
    }
    return 1;
}

1;

__END__


