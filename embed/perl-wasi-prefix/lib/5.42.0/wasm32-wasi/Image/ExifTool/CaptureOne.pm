
package Image::ExifTool::CaptureOne;

use strict;
use vars            qw($VERSION);
use Image::ExifTool qw(:DataAccess :Utils);
use Image::ExifTool::XMP;
use Image::ExifTool::ZIP;

$VERSION = '1.05';

%Image::ExifTool::CaptureOne::Main = (
    GROUPS           => { 0 => 'XML', 1 => 'XML', 2 => 'Image' },
    PROCESS_PROC     => \&Image::ExifTool::XMP::ProcessXMP,
    VARS             => { ID_FMT    => 'none' },
    ColorCorrections => { ValueConv => '\$val', Hidden => 1 },
);

sub HandleCOSAttrs($$$$) {
    my ( $attrList, $attrs, $prop, $valPt ) = @_;
    my $changed;
    if ( not length $$valPt and defined $$attrs{K} and defined $$attrs{V} ) {
        $$prop  = $$attrs{K};
        $$valPt = $$attrs{V};
        my @attrs = @$attrList;
        @$attrList = ();
        my $a;
        foreach $a (@attrs) {
            if ( $a eq 'K' or $a eq 'V' ) {
                delete $$attrs{$a};
            }
            else {
                push @$attrList, $a;
            }
        }
        $changed = 1;
    }
    return $changed;
}

sub FoundCOS($$$$;$) {
    my ( $et, $tagTablePtr, $props, $val, $attrs ) = @_;

    my $tag = $$props[-1];
    unless ( $$tagTablePtr{$tag} ) {
        $et->VPrint( 0, "  | [adding $tag]\n" );
        my $name = ucfirst $tag;
        $name =~ tr/-_a-zA-Z0-9//dc;
        return 0 unless length $tag;
        my %tagInfo = ( Name => $tag );
        if ( $name =~ /Date(?![a-z])/ ) {
            $tagInfo{Groups} = { 2 => 'Time' };
            $tagInfo{ValueConv} =
              'Image::ExifTool::XMP::ConvertXMPDate($val,1)';
            $tagInfo{PrintConv} = '$self->ConvertDateTime($val)';
        }
        AddTagToTable( $tagTablePtr, $tag, \%tagInfo );
    }
    $val = $et->Decode( $val, "UTF8" );
    $val = Image::ExifTool::XMP::UnescapeXML($val);
    $et->HandleTag( $tagTablePtr, $tag, $val );
    return 0;
}

sub ProcessCOS($$) {
    my ( $et, $dirInfo ) = @_;

    $$dirInfo{XMPParseOpts} = {
        AttrProc  => \&HandleCOSAttrs,
        FoundProc => \&FoundCOS,
    };
    my $tagTablePtr = GetTagTable('Image::ExifTool::CaptureOne::Main');
    my $success     = $et->ProcessDirectory( $dirInfo, $tagTablePtr );
    delete $$dirInfo{XMLParseArgs};
    return $success;
}

sub ProcessEIP($$) {
    my ( $et, $dirInfo ) = @_;
    my $zip = $$dirInfo{ZIP};
    my ( $file, $buff, $status, $member, %parseFile );

    $et->SetFileType('EIP');

    local $SIG{'__WARN__'} = \&Image::ExifTool::ZIP::WarnProc;
    my @members = $zip->membersMatching('^manifest\d*.xml$');
    while (@members) {
        my $m = shift @members;
        my $f = $m->fileName();
        next if $file and $file gt $f;
        $member = $m;
        $file   = $f;
    }
    if ($member) {
        ( $buff, $status ) = $zip->contents($member);
        if ( not $status ) {
            my $foundImage;
            while ( $buff =~ m{<(RawPath|SettingsPath)>(.*?)</\1>}sg ) {
                $file = $2;
                next unless $file =~ /\.(cos|iiq|jpe?g|tiff?)$/i;
                $parseFile{$file} = 1;
                $foundImage = 1 unless $file =~ /\.cos$/i;
            }
            undef %parseFile unless $foundImage;
        }
    }
    my $docNum = 0;
    @members = $zip->members();
    foreach $member (@members) {
        $file = $member->fileName();
        next unless defined $file;
        $et->VPrint( 0, "File: $file\n" );
        $$et{DOC_NUM} = ++$docNum;
        Image::ExifTool::ZIP::HandleMember( $et, $member );
        if (%parseFile) {
            next unless $parseFile{$file};
        }
        else {
            next
              unless $file =~
              m{^([^/]+\.(iiq|jpe?g|tiff?)|CaptureOne/.*\.cos)$}i;
        }
        ( $buff, $status ) = $zip->contents($member);
        $status and $et->Warn("Error extracting $file"), next;
        if ( $file =~ /\.cos$/i ) {
            my %dirInfo = (
                DataPt  => \$buff,
                DirLen  => length $buff,
                DataLen => length $buff,
            );
            ProcessCOS( $et, \%dirInfo );
        }
        else {
            if ( $$et{HTML_DUMP} ) {
                $$et{HTML_DUMP}{Error} =
                  "Sorry, can't dump images embedded in ZIP files";
            }
            $et->ExtractInfo( \$buff, { ReEntry => 1 } );
        }
        undef $buff;
    }
    delete $$et{DOC_NUM};
    return 1;
}

1;

__END__


