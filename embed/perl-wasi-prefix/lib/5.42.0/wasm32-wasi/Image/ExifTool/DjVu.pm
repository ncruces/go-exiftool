
package Image::ExifTool::DjVu;

use strict;
use vars            qw($VERSION);
use Image::ExifTool qw(:DataAccess :Utils);

$VERSION = '1.07';

sub ParseAnt($);
sub ProcessAnt($$$);
sub ProcessMeta($$$);
sub ProcessBZZ($$$);

%Image::ExifTool::DjVu::Main = (
    GROUPS => { 2 => 'Image' },
    NOTES  => q{
        Information is extracted from the following chunks in DjVu images. See
        L<http://www.djvu.org/> for the DjVu specification.
    },
    INFO => {
        SubDirectory => { TagTable => 'Image::ExifTool::DjVu::Info' },
    },
    FORM => {
        TypeOnly     => 1,
        SubDirectory => { TagTable => 'Image::ExifTool::DjVu::Form' },
    },
    ANTa => {
        SubDirectory => { TagTable => 'Image::ExifTool::DjVu::Ant' },
    },
    ANTz => {
        Name         => 'CompressedAnnotation',
        SubDirectory => {
            TagTable    => 'Image::ExifTool::DjVu::Ant',
            ProcessProc => \&ProcessBZZ,
        }
    },
    INCL => 'IncludedFileID',
);

%Image::ExifTool::DjVu::Info = (
    PROCESS_PROC => \&Image::ExifTool::ProcessBinaryData,
    GROUPS       => { 2 => 'Image' },
    FORMAT       => 'int8u',
    PRIORITY     => 0,
    0            => {
        Name   => 'ImageWidth',
        Format => 'int16u',
    },
    2 => {
        Name   => 'ImageHeight',
        Format => 'int16u',
    },
    4 => {
        Name        => 'DjVuVersion',
        Description => 'DjVu Version',
        Format      => 'int8u[2]',
        ValueConv => '$val=~/(\d+) (\d+)/ ? "$2.$1" : "0.$val"',
    },
    6 => {
        Name      => 'SpatialResolution',
        Format    => 'int16u',
        ValueConv => '(($val & 0xff)<<8) + ($val>>8)',
    },
    8 => {
        Name      => 'Gamma',
        ValueConv => '$val / 10',
    },
    9 => {
        Name      => 'Orientation',
        Mask      => 0x07,
        PrintConv => {
            1 => 'Horizontal (normal)',
            2 => 'Rotate 180',
            5 => 'Rotate 90 CW',
            6 => 'Rotate 270 CW',
        },
    },
);

%Image::ExifTool::DjVu::Form = (
    PROCESS_PROC => \&Image::ExifTool::ProcessBinaryData,
    GROUPS       => { 2 => 'Image' },
    0            => {
        Name      => 'SubfileType',
        Format    => 'undef[4]',
        Priority  => 0,
        PrintConv => {
            DJVU => 'Single-page image',
            DJVM => 'Multi-page document',
            PM44 => 'Color IW44',
            BM44 => 'Grayscale IW44',
            DJVI => 'Shared component',
            THUM => 'Thumbnail image',
        },
    },
);

%Image::ExifTool::DjVu::Ant = (
    PROCESS_PROC => \&Image::ExifTool::DjVu::ProcessAnt,
    GROUPS       => { 2 => 'Image' },
    NOTES        => 'Information extracted from annotation chunks.',
    metadata => {
        SubDirectory => { TagTable => 'Image::ExifTool::DjVu::Meta' }
    },
    xmp => {
        Name         => 'XMP',
        SubDirectory => { TagTable => 'Image::ExifTool::XMP::Main' }
    },
);

%Image::ExifTool::DjVu::Meta = (
    PROCESS_PROC => \&Image::ExifTool::DjVu::ProcessMeta,
    GROUPS       => { 1 => 'DjVu-Meta', 2 => 'Image' },
    NOTES        => q{
        This table lists the standard DjVu metadata tags, but ExifTool will extract
        any tags that exist even if they don't appear here.  The DjVu v3
        documentation endorses tags borrowed from two standards: 1) BibTeX
        bibliography system tags (all lowercase Tag ID's in the table below), and 2)
        PDF DocInfo tags (capitalized Tag ID's).
    },
    address      => { Groups => { 2 => 'Location' } },
    annote       => { Name   => 'Annotation' },
    author       => { Groups => { 2 => 'Author' } },
    booktitle    => { Name   => 'BookTitle' },
    chapter      => {},
    crossref     => { Name => 'CrossRef' },
    edition      => {},
    eprint       => { Name => 'EPrint' },
    howpublished => { Name => 'HowPublished' },
    institution  => {},
    journal      => {},
    key          => {},
    month        => { Groups => { 2 => 'Time' } },
    note         => {},
    number       => {},
    organization => {},
    pages        => {},
    publisher    => {},
    school       => {},
    series       => {},
    title        => {},
    type         => {},
    url          => { Name => 'URL' },
    volume       => {},
    year         => { Groups => { 2 => 'Time' } },
    Title        => {},
    Author       => { Groups => { 2 => 'Author' } },
    Subject      => {},
    Keywords     => {},
    Creator      => {},
    Producer     => {},
    CreationDate => {
        Name   => 'CreateDate',
        Groups => { 2 => 'Time' },
        ValueConv =>
'require Image::ExifTool::XMP; Image::ExifTool::XMP::ConvertXMPDate($val)',
        PrintConv => '$self->ConvertDateTime($val)',
    },
    ModDate => {
        Name      => 'ModifyDate',
        Groups    => { 2 => 'Time' },
        ValueConv =>
'require Image::ExifTool::XMP; Image::ExifTool::XMP::ConvertXMPDate($val)',
        PrintConv => '$self->ConvertDateTime($val)',
    },
    Trapped => {
        ValueConv => '$val=~s{^/}{}; $val',
    },
);

sub ParseAnt($) {
    my $dataPt = shift;
    my ( @toks, $tok, $more );
  Tok: for ( ; ; ) {
        last unless $$dataPt =~ /(\S)/sg;
        if ( $1 eq '(' ) {
            $tok = ParseAnt($dataPt);
        }
        elsif ( $1 eq ')' ) {
            $more = 1;
            last;
        }
        elsif ( $1 eq '"' ) {
            $tok = '';
            for ( ; ; ) {
                my $pos = pos($$dataPt);
                last Tok unless $$dataPt =~ /"/sg;
                $tok .= substr( $$dataPt, $pos, pos($$dataPt) - 1 - $pos );
                last unless $tok =~ /(\\+)$/ and length($1) & 0x01;
                $tok .= '"';
            }
            my %esc = (
                a    => "\a",
                b    => "\b",
                f    => "\f",
                n    => "\n",
                r    => "\r",
                t    => "\t",
                '"'  => '"',
                '\\' => '\\'
            );
            $tok =~ s/\\(.)/$esc{$1}||'\\'.$1/egs;
        }
        else {
            pos($$dataPt) = pos($$dataPt) - 1;
            $tok = $$dataPt =~ /([^\s()"]+)/sg ? $1 : undef;
        }
        push @toks, $tok if defined $tok;
    }
    pos($$dataPt) = length $$dataPt unless $more;
    return @toks ? \@toks : undef;
}

sub ProcessAnt($$$) {
    my ( $et, $dirInfo, $tagTablePtr ) = @_;
    my $dataPt = $$dirInfo{DataPt};

    return 1 unless $$dataPt =~ /\(\s*(metadata|xmp)[\s("]/s;

    pos($$dataPt) = 0;
    my $toks = ParseAnt($dataPt) or return 0;

    my $ant;
    foreach $ant (@$toks) {
        next unless ref $ant eq 'ARRAY' and @$ant >= 2;
        my $tag = shift @$ant;
        next if ref $tag or not defined $$tagTablePtr{$tag};
        if ( $tag eq 'metadata' ) {
            $et->HandleTag( $tagTablePtr, $tag, $ant );
        }
        else {
            next if ref $$ant[0];
            $et->HandleTag( $tagTablePtr, $tag, $$ant[0] );
        }
    }
    return 1;
}

sub ProcessMeta($$$) {
    my ( $et, $dirInfo, $tagTablePtr ) = @_;
    my $dataPt = $$dirInfo{DataPt};
    return 0 unless ref $$dataPt eq 'ARRAY';
    $et->VerboseDir( 'Metadata', scalar @$$dataPt );
    my ( $item, $err );
    foreach $item (@$$dataPt) {
        $err = 1, next
          unless ref $item eq 'ARRAY'
          and @$item >= 2
          and not ref $$item[0]
          and not ref $$item[1];
        unless ( $$tagTablePtr{ $$item[0] } ) {
            my $name = $$item[0];
            $name =~ tr/-_a-zA-Z0-9//dc;
            length $name or $err = 1, next;
            AddTagToTable( $tagTablePtr, $$item[0],
                { Name => ucfirst($name) } );
        }
        $et->HandleTag( $tagTablePtr, $$item[0], $$item[1] );
    }
    $err and $et->Warn('Ignored invalid metadata entry(s)');
    return 1;
}

sub ProcessBZZ($$$) {
    my ( $et, $dirInfo, $tagTablePtr ) = @_;
    require Image::ExifTool::BZZ;
    my $buff = Image::ExifTool::BZZ::Decode( $$dirInfo{DataPt} );
    unless ( defined $buff ) {
        $et->Warn("Error decoding $$dirInfo{DirName}");
        return 0;
    }
    my $verbose = $et->Options('Verbose');
    if ( $verbose >= 3 ) {
        $et->VerboseDir( "Decoded $$dirInfo{DirName}", 0, length $buff );
        $et->VerboseDump( \$buff );
    }
    $$dirInfo{DataPt}  = \$buff;
    $$dirInfo{DataLen} = $$dirInfo{DirLen} = length $buff;
    my $processProc = $$tagTablePtr{PROCESS_PROC} or return 0;
    return &$processProc( $et, $dirInfo, $tagTablePtr );
}

1;

__END__


