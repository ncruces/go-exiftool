package Image::ExifTool::CanonRaw;

use strict;
use vars qw($VERSION $AUTOLOAD %crwTagFormat);
use Image::ExifTool::Fixup;

my %crwMap = (
    XMP      => 'CanonVRD',
    CanonVRD => 'Trailer',
);

my %mapRawTag = (
    0x080b => 0x07,
    0x0810 => 0x09,
    0x0815 => 0x06,
    0x1028 => 0x03,
    0x1029 => 0x02,
    0x102a => 0x04,
    0x102d => 0x01,
    0x1033 => 0x0f,
    0x1038 => 0x12,
    0x1039 => 0x13,
    0x1093 => 0x93,
    0x10a8 => 0xa8,
    0x10a9 => 0xa9,
    0x10aa => 0xaa,
    0x10ae => 0xae,
    0x10b4 => 0xb4,
    0x10b5 => 0xb5,
    0x10c0 => 0xc0,
    0x10c1 => 0xc1,
    0x180b => 0x0c,
    0x1817 => 0x08,
    0x1834 => 0x10,
    0x183b => 0x15,
);
my %mapRotation = (
    0   => 1,
    90  => 6,
    180 => 3,
    270 => 8,
);

sub InitMakerNotes($) {
    my $et = shift;
    $$et{MAKER_NOTE_INFO} = {
        Entries   => {},
        ValBuff   => "\0\0\0\0",
        FixupTags => {},
    };
}

sub BuildMakerNotes($$$$$$) {
    my ( $et, $rawTag, $tagInfo, $valuePt, $formName, $count ) = @_;

    my $tagID = $mapRawTag{$rawTag} || return;
    $formName or warn( sprintf "No format for tag 0x%x!\n", $rawTag ), return;
    return if $tagInfo and $$tagInfo{Name} eq 'UserComment';
    my $format = $Image::ExifTool::Exif::formatNumber{$formName};
    my $fsiz   = $Image::ExifTool::Exif::formatSize[$format];
    my $size   = length($$valuePt);
    my $value;
    if ( $count and $size != $count * $fsiz ) {
        if ( $size < $count * $fsiz ) {
            warn sprintf( "Value too short for raw tag 0x%x\n", $rawTag );
            return;
        }
        $size  = $count * $fsiz;
        $value = substr( $$valuePt, 0, $size );
    }
    else {
        $count = $size / $fsiz;
        $value = $$valuePt;
    }
    my $offsetVal;
    my $makerInfo = $$et{MAKER_NOTE_INFO};
    if ( $size > 4 ) {
        my $len = length $makerInfo->{ValBuff};
        $offsetVal = Set32u($len);
        $makerInfo->{ValBuff} .= $value;
        $size & 0x01 and $makerInfo->{ValBuff} .= "\0";
        $makerInfo->{FixupTags}->{$tagID} = 1;
    }
    else {
        $offsetVal = $value;
        $size < 4 and $offsetVal .= "\0" x ( 4 - $size );
    }
    $makerInfo->{Entries}->{$tagID} =
      Set16u($tagID) . Set16u($format) . Set32u($count) . $offsetVal;
}

sub SaveMakerNotes($) {
    my $et = shift;
    my $makerInfo = $$et{MAKER_NOTE_INFO};
    delete $$et{MAKER_NOTE_INFO};
    my $dirEntries = $makerInfo->{Entries};
    my $numEntries = scalar( keys %$dirEntries );
    my $fixup      = Image::ExifTool::Fixup->new;
    return unless $numEntries;
    my $makerNotes = Set16u($numEntries);
    my $tagID;

    foreach $tagID ( sort { $a <=> $b } keys %$dirEntries ) {
        $makerNotes .= $$dirEntries{$tagID};
        next unless $makerInfo->{FixupTags}->{$tagID};
        $fixup->AddFixup( length($makerNotes) - 4 );
    }
    $fixup->{Shift} += length($makerNotes);
    $$et{MAKER_NOTE_BYTE_ORDER} = GetByteOrder();
    $makerNotes .= $makerInfo->{ValBuff};
    my $tagTablePtr =
      Image::ExifTool::GetTagTable('Image::ExifTool::Exif::Main');
    my $tagInfo = $et->GetTagInfo( $tagTablePtr, 0x927c, \$makerNotes );
    my $key = $et->FoundTag( $tagInfo, $makerNotes );
    $$et{TAG_EXTRA}{$key}{Fixup} = $fixup;
    delete $makerInfo->{Entries};
    delete $makerInfo->{ValBuff};
    delete $makerInfo->{FixupTags};
    my $rotation = $et->GetValue( 'Rotation', 'ValueConv' );

    if ( defined $rotation and defined $mapRotation{$rotation} ) {
        $tagInfo = $et->GetTagInfo( $tagTablePtr, 0x112 );
        $et->FoundTag( $tagInfo, $mapRotation{$rotation} );
    }
}

sub CheckCanonRaw($$$) {
    my ( $et, $tagInfo, $valPtr ) = @_;
    my $tagName = $$tagInfo{Name};
    if ( $tagName eq 'JpgFromRaw' or $tagName eq 'ThumbnailImage' ) {
        unless ( $$valPtr =~ /^\xff\xd8/ or $et->Options('IgnoreMinorErrors') )
        {
            return '[Minor] Not a valid image';
        }
    }
    else {
        my $format = $$tagInfo{Format};
        my $count  = $$tagInfo{Count};
        unless ($format) {
            my $tagType = ( $$tagInfo{TagID} >> 8 ) & 0x38;
            $format = $crwTagFormat{$tagType};
        }
        $format
          and return Image::ExifTool::CheckValue( $valPtr, $format, $count );
    }
    return undef;
}

sub WriteCR2($$$) {
    my ( $et, $dirInfo, $tagTablePtr ) = @_;
    my $dataPt  = $$dirInfo{DataPt}  or return 0;
    my $outfile = $$dirInfo{OutFile} or return 0;
    $$dirInfo{RAF} or return 0;

    if ( $$dataPt !~ /^.{8}CR\x02\0/s ) {
        my ( $msg, $minor );
        if ( $$dataPt =~ /^.{8}CR/s ) {
            $msg =
              'Unsupported Canon RAW file. May cause problems if rewritten';
            $minor = 1;
        }
        elsif ( $$dataPt =~ /^.{8}\xba\xb0\xac\xbb/s ) {
            $msg = 'Can not currently write Canon 1D RAW images';
        }
        else {
            $msg = 'Unrecognized Canon RAW file';
        }
        return 0 if $et->Error( $msg, $minor );
    }

    $$dirInfo{NewDataPos} = 16;
    my $newData = $et->WriteDirectory( $dirInfo, $tagTablePtr );
    return 0 unless defined $newData;
    unless ( $$dirInfo{LastIFD} ) {
        $et->Error("CR2 image IFD may not be deleted");
        return 0;
    }

    if ( length($newData) ) {
        my $header = substr( $$dataPt, 0, 16 );
        Set32u( 16, \$header, 4 );
        Set32u( $$dirInfo{LastIFD}, \$header, 12 );
        Write( $outfile, $header, $newData ) or return 0;
        undef $newData;

        if ( ref $$dirInfo{ImageData} ) {
            $et->CopyImageData( $$dirInfo{ImageData}, $outfile ) or return 0;
            delete $$dirInfo{ImageData};
        }
    }
    return 1;
}

sub WriteCanonRaw($$$) {
    my ( $et, $dirInfo, $tagTablePtr ) = @_;
    $et or return 1;
    my $blockStart = $$dirInfo{DirStart};
    my $blockSize  = $$dirInfo{DirLen};
    my $raf        = $$dirInfo{RAF}     or return 0;
    my $outfile    = $$dirInfo{OutFile} or return 0;
    my $outPos     = $$dirInfo{OutPos}  or return 0;
    my $outBase    = $outPos;
    my $verbose    = $et->Options('Verbose');
    my $out        = $et->Options('TextOut');
    my ( $buff, $tagInfo );

    $raf->Seek( $blockStart + $blockSize - 4, 0 ) or return 0;
    $raf->Read( $buff, 4 ) == 4                   or return 0;
    my $dirOffset = Get32u( \$buff, 0 ) + $blockStart;
    $$et{ProcessedCanonRaw} or $$et{ProcessedCanonRaw} = {};
    if ( $$et{ProcessedCanonRaw}{$dirOffset} ) {
        $et->Error("Double-referenced $$dirInfo{DirName} directory");
        return 0;
    }
    $$et{ProcessedCanonRaw}{$dirOffset} = 1;
    $raf->Seek( $dirOffset, 0 ) or return 0;
    $raf->Read( $buff, 2 ) == 2 or return 0;
    my $entries = Get16u( \$buff, 0 );

    $raf->Read( $buff, 10 * $entries ) == 10 * $entries or return 0;
    my $newDir = '';

    my $newTags = $et->GetNewTagInfoHash($tagTablePtr);

    my ( @addTags, %delTag );
    if ( $$dirInfo{Nesting} == 0 ) {
        my $tagID;
        foreach $tagID ( keys %$newTags ) {
            my $permanent = $newTags->{$tagID}->{Permanent};
            push( @addTags, $tagID ) if defined($permanent) and not $permanent;
        }
    }

    my $index;
    for ( $index = 0 ; ; ++$index ) {
        my ( $pt, $tag, $size, $valuePtr, $ptr, $value );
        if ( $index < $entries ) {
            $pt       = 10 * $index;
            $tag      = Get16u( \$buff, $pt );
            $size     = Get32u( \$buff, $pt + 2 );
            $valuePtr = Get32u( \$buff, $pt + 6 );
            $ptr      = $valuePtr + $blockStart;
        }
        if ( @addTags and ( not defined($tag) or $tag >= $addTags[0] ) ) {
            my $addTag = shift @addTags;
            $tagInfo = $$newTags{$addTag};
            my $newVal = $et->GetNewValue($tagInfo);
            if ( defined $newVal ) {
                $newVal .= "\0" if length($newVal) & 0x01;
                $newDir .=
                    Set16u($addTag)
                  . Set32u( length($newVal) )
                  . Set32u( $outPos - $outBase );
                Write( $outfile, $newVal ) or return 0;
                $outPos += length($newVal);
                $verbose > 1 and print $out "    + CanonRaw:$$tagInfo{Name}\n";
                ++$$et{CHANGED};
            }
            $delTag{$addTag} = 1;
        }
        last unless defined $tag;
        return 0 if $tag & 0x8000;
        my $tagID      = $tag & 0x3fff;
        my $tagType    = ( $tag >> 8 ) & 0x38;
        my $valueInDir = ( $tag & 0x4000 );

        my $tagInfo = $et->GetTagInfo( $tagTablePtr, $tagID );
        my $format  = $crwTagFormat{$tagType};
        my ( $count, $subdir );
        if ($tagInfo) {
            $subdir = $$tagInfo{SubDirectory};
            $format = $$tagInfo{Format} if $$tagInfo{Format};
            $count  = $$tagInfo{Count};
        }
        if ($valueInDir) {
            $size  = 8;
            $value = substr( $buff, $pt + 2, $size );
            $count = 1
              if not defined $count
              and $format
              and $format ne 'string'
              and not $subdir;
        }
        else {
            if ( $tagType == 0x28 or $tagType == 0x30 ) {
                my $name;
                $tagInfo and $name = $$tagInfo{Name};
                $name or $name = sprintf( "CanonRaw_0x%.4x", $tagID );
                my %subdirInfo = (
                    DirName  => $name,
                    DataLen  => 0,
                    DirStart => $ptr,
                    DirLen   => $size,
                    Nesting  => $$dirInfo{Nesting} + 1,
                    RAF      => $raf,
                    Parent   => $$dirInfo{DirName},
                    OutFile  => $outfile,
                    OutPos   => $outPos,
                );
                my $result = $et->WriteDirectory( \%subdirInfo, $tagTablePtr );
                return 0 unless $result;
                $size     = $subdirInfo{OutPos} - $outPos;
                $valuePtr = $outPos - $outBase;
                $outPos   = $subdirInfo{OutPos};
            }
            else {
                $valuePtr + $size <= $blockSize or return 0;
                $raf->Seek( $ptr, 0 )                or return 0;
                $raf->Read( $value, $size ) == $size or return 0;
            }
        }
        if ( $format and not $count ) {
            my $fnum = $Image::ExifTool::Exif::formatNumber{$format};
            my $fsiz = $Image::ExifTool::Exif::formatSize[$fnum];
            $count = int( $size / $fsiz );
        }
        if ($tagInfo) {
            if ( $subdir and $$subdir{TagTable} ) {
                my $name = $$tagInfo{Name};
                my $newTagTable =
                  Image::ExifTool::GetTagTable( $$subdir{TagTable} );
                return 0 unless $newTagTable;
                my $subdirStart = 0;
                $subdirStart = eval $$subdir{Start} if $$subdir{Start};
                my $dirData    = \$value;
                my %subdirInfo = (
                    Name     => $name,
                    DataPt   => $dirData,
                    DataLen  => $size,
                    DirStart => $subdirStart,
                    DirLen   => $size - $subdirStart,
                    Nesting  => $$dirInfo{Nesting} + 1,
                    RAF      => $raf,
                    Parent   => $$dirInfo{DirName},
                );
                if ( defined $$subdir{Validate}
                    and not eval $$subdir{Validate} )
                {
                    $et->Warn("Invalid $name data");
                }
                else {
                    $subdir = $et->WriteDirectory( \%subdirInfo, $newTagTable );
                    if ( defined $subdir and length $subdir ) {
                        if ($subdirStart) {
                            $value =
                              substr( $value, 0, $subdirStart ) . $subdir;
                        }
                        else {
                            $value = $subdir;
                        }
                    }
                }
            }
            elsif ( $$newTags{$tagID} ) {
                if ( $delTag{$tagID} ) {
                    $verbose > 1
                      and print $out "    - CanonRaw:$$tagInfo{Name}\n";
                    ++$$et{CHANGED};
                    next;
                }
                my $oldVal;
                if ($format) {
                    $oldVal = ReadValue( \$value, 0, $format, $count, $size );
                }
                else {
                    $oldVal = $value;
                }
                my $nvHash = $et->GetNewValueHash($tagInfo);
                if ( $et->IsOverwriting( $nvHash, $oldVal ) ) {
                    my $newVal = $et->GetNewValue($nvHash);
                    my $verboseVal;
                    $verboseVal = $newVal if $verbose > 1;
                    if ( defined $newVal and $format ) {
                        $newVal = WriteValue( $newVal, $format, $count );
                    }
                    if ( defined $newVal ) {
                        $value = $newVal;
                        ++$$et{CHANGED};
                        $et->VerboseValue( "- CanonRaw:$$tagInfo{Name}",
                            $oldVal );
                        $et->VerboseValue( "+ CanonRaw:$$tagInfo{Name}",
                            $verboseVal );
                    }
                }
            }
        }
        if ($valueInDir) {
            my $len = length $value;
            if ( $len < 8 ) {
                $value .= substr( $buff, $pt + 2 + 8 - $len, 8 - $len );
            }
            elsif ( $len > 8 ) {
                warn "Value too long! -- truncated\n";
                $value = substr( $value, 0, 8 );
            }
            $newDir .= Set16u($tag) . $value;
            next;
        }
        if ( defined $value ) {
            my $writable = $$tagInfo{Writable};
            my $diff     = length($value) - $size;
            if ($diff) {
                if ( $writable and $writable eq 'resize' ) {
                    $size += $diff;
                }
                elsif ( $diff > 0 ) {
                    $value .= ( "\0" x $diff );
                }
                else {
                    $value = substr( $value, 0, $size );
                }
            }
            $value .= "\0" if $size & 0x01;
            $valuePtr = $outPos - $outBase;
            Write( $outfile, $value ) or return 0;
            $outPos += length($value);
        }
        $newDir .= Set16u($tag) . Set32u($size) . Set32u($valuePtr);
    }
    $entries = length($newDir) / 10;
    $newDir  = Set16u($entries) . $newDir . Set32u( $outPos - $outBase );
    Write( $outfile, $newDir ) or return 0;

    $$dirInfo{OutPos} = $outPos + length($newDir);
    $$dirInfo{OutDirStart} = $outPos - $outBase;

    return 1;
}

sub WriteCRW($$) {
    my ( $et, $dirInfo ) = @_;
    my $outfile = $$dirInfo{OutFile};
    my $raf     = $$dirInfo{RAF};
    my $rtnVal  = 0;
    my ( $buff, $err, $sig );

    $raf->Read( $buff, 2 ) == 2 or return 0;
    SetByteOrder($buff)         or return 0;
    $raf->Read( $buff, 4 ) == 4 or return 0;
    $raf->Read( $sig, 8 ) == 8  or return 0;
    $sig =~ /^HEAP(CCDR|JPGM)/  or return 0;
    my $type = $1;
    my $hlen = Get32u( \$buff, 0 );

    if ( $$et{DEL_GROUP}{MakerNotes} ) {
        if ( $type eq 'CCDR' ) {
            $et->Error("Can't delete MakerNotes from CRW");
            return 0;
        }
        else {
            ++$$et{CHANGED};
            return 1;
        }
    }
    if ( $$et{FILE_TYPE} eq 'CRW' ) {
        $et->InitWriteDirs( \%crwMap, 'XMP' );
    }

    $raf->Seek( 0, 0 )                  or return 0;
    $raf->Read( $buff, $hlen ) == $hlen or return 0;
    Write( $outfile, $buff ) or $err = 1;

    $raf->Seek( 0, 2 )          or return 0;
    my $filesize = $raf->Tell() or return 0;

    my %dirInfo = (
        DataLen  => 0,
        DirStart => $hlen,
        DirLen   => $filesize - $hlen,
        Nesting  => 0,
        RAF      => $raf,
        Parent   => 'CRW',
        OutFile  => $outfile,
        OutPos   => $hlen,
    );
    my $tagTablePtr =
      Image::ExifTool::GetTagTable('Image::ExifTool::CanonRaw::Main');
    my $success = $et->WriteDirectory( \%dirInfo, $tagTablePtr );

    my $trailPt;
    while ($success) {
        my $trailInfo = $et->IdentifyTrailer($raf) or last;
        $buff                = '';
        $$trailInfo{OutFile} = \$buff;
        $success             = $et->ProcessTrailers($trailInfo) or last;
        $trailPt             = $$trailInfo{OutFile};
        undef $trailPt if length($$trailPt) < 4;
        last;
    }
    if ($success) {
        $trailPt = $et->AddNewTrailers( $trailPt, 'CanonVRD' );
        if ( not $trailPt and $$et{ADD_DIRS}{CanonVRD} ) {
            my $outbuff   = '';
            my $saveOrder = GetByteOrder();
            require Image::ExifTool::CanonVRD;
            if (
                Image::ExifTool::CanonVRD::ProcessCanonVRD( $et,
                    { OutFile => \$outbuff } ) > 0
              )
            {
                $trailPt = \$outbuff;
            }
            SetByteOrder($saveOrder);
        }
        if ($trailPt) {
            my $newDirStart = Set32u( $dirInfo{OutDirStart} );
            my $len         = length $$trailPt;
            my $pad         = ( $len & 0x01 ) ? ' ' : '';
            Write( $outfile, $pad, substr( $$trailPt, 0, $len - 4 ),
                $newDirStart )
              or $err = 1;
        }
        $rtnVal = $err ? -1 : 1;
    }
    else {
        $et->Error('Error rewriting CRW file');
    }
    return $rtnVal;
}

1;

__END__

