
package Image::ExifTool::GIF;

use strict;
use vars            qw($VERSION);
use Image::ExifTool qw(:DataAccess :Utils);

$VERSION = '1.21';

my %gifMap = (
    XMP         => 'GIF',
    ICC_Profile => 'GIF',
);

my @appExtensions = ( 'XMP Data/XMP', 'ICCRGBG1/012' );

%Image::ExifTool::GIF::Main = (
    GROUPS => { 2      => 'Image' },
    VARS   => { ID_FMT => 'none' },
    NOTES  => q{
        This table lists information extracted from GIF images. See
        L<http://www.w3.org/Graphics/GIF/spec-gif89a.txt> for the official GIF89a
        specification.
    },
    GIFVersion => {},
    FrameCount => { Notes => 'number of animated images' },
    Text       => { Notes => 'text displayed in image' },
    Comment    => {
        Writable => 2,
    },
    Duration => {
        Notes     => 'duration of a single animation iteration',
        PrintConv => 'sprintf("%.2f s",$val)',
    },
    ScreenDescriptor => {
        SubDirectory => { TagTable => 'Image::ExifTool::GIF::Screen' },
    },
    Extensions => {
        SubDirectory => { TagTable => 'Image::ExifTool::GIF::Extensions' },
    },
    TransparentColor => {},
);

%Image::ExifTool::GIF::Extensions = (
    GROUPS         => { 2 => 'Image' },
    NOTES          => 'Tags extracted from GIF89a application extensions.',
    WRITE_PROC     => sub { return 1 },
    'NETSCAPE/2.0' => {
        Name         => 'Animation',
        SubDirectory => { TagTable => 'Image::ExifTool::GIF::Animation' },
    },
    'XMP Data/XMP' => {
        Name => 'XMP',
        IncludeLengthBytes => 2,
        Terminator         => q(<\\?xpacket end=['"][wr]['"]\\?>),
        Writable           => 2,
        SubDirectory       => { TagTable => 'Image::ExifTool::XMP::Main' },
    },
    'ICCRGBG1/012' => {
        Name         => 'ICC_Profile',
        Writable     => 2,
        SubDirectory => { TagTable => 'Image::ExifTool::ICC_Profile::Main' },
    },
    'MIDICTRL/Jon' => {
        Name         => 'MIDIControl',
        SubDirectory => { TagTable => 'Image::ExifTool::GIF::MIDIControl' },
    },
    'MIDISONG/Dm7' => {
        Name   => 'MIDISong',
        Groups => { 2 => 'Audio' },
        Binary => 1,
    },
    'C2PA_GIF/' => {
        Name         => 'JUMBF',
        SubDirectory => { TagTable => 'Image::ExifTool::Jpeg2000::Main' },
    },
);

%Image::ExifTool::GIF::Screen = (
    PROCESS_PROC => \&Image::ExifTool::ProcessBinaryData,
    GROUPS       => { 2 => 'Image' },
    NOTES => 'Information extracted from the GIF logical screen descriptor.',
    0     => {
        Name   => 'ImageWidth',
        Format => 'int16u',
    },
    2 => {
        Name   => 'ImageHeight',
        Format => 'int16u',
    },
    4.1 => {
        Name      => 'HasColorMap',
        Mask      => 0x80,
        PrintConv => { 0 => 'No', 1 => 'Yes' },
    },
    4.2 => {
        Name      => 'ColorResolutionDepth',
        Mask      => 0x70,
        ValueConv => '$val + 1',
    },
    4.3 => {
        Name      => 'BitsPerPixel',
        Mask      => 0x07,
        ValueConv => '$val + 1',
    },
    5 => 'BackgroundColor',
    6 => {
        Name      => 'PixelAspectRatio',
        RawConv   => '$val ? $val : undef',
        ValueConv => '($val + 15) / 64',
    },
);

%Image::ExifTool::GIF::Animation = (
    PROCESS_PROC => \&Image::ExifTool::ProcessBinaryData,
    GROUPS       => { 2 => 'Image' },
    NOTES        =>
      'Information extracted from the "NETSCAPE2.0" animation extension.',
    1 => {
        Name      => 'AnimationIterations',
        Format    => 'int16u',
        PrintConv => '$val ? $val : "Infinite"',
    },
);

%Image::ExifTool::GIF::MIDIControl = (
    PROCESS_PROC => \&Image::ExifTool::ProcessBinaryData,
    GROUPS       => { 2 => 'Audio' },
    NOTES => 'Information extracted from the MIDI control block extension.',
    0     => 'MIDIControlVersion',
    1     => 'SequenceNumber',
    2     => 'MelodicPolyphony',
    3     => 'PercussivePolyphony',
    4     => {
        Name      => 'ChannelUsage',
        Format    => 'int16u',
        PrintConv => 'sprintf("0x%.4x", $val)',
    },
    6 => {
        Name      => 'DelayTime',
        Format    => 'int16u',
        ValueConv => '$val / 100',
        PrintConv => '$val . " s"',
    },
);

sub ProcessGIF($$) {
    my ( $et, $dirInfo ) = @_;
    my $outfile = $$dirInfo{OutFile};
    my $raf     = $$dirInfo{RAF};
    my $verbose = $et->Options('Verbose');
    my $out     = $et->Options('TextOut');
    my ( $a,          $s,          $ch,         $length,    $buff );
    my ( $err,        $newComment, $setComment, $nvComment, $newExt );
    my ( $addDirs,    %doneDir );
    my ( $frameCount, $delayTime ) = ( 0, 0 );

    return 0
      unless $raf->Read( $buff, 6 ) == 6
      and $buff =~ /^GIF(8[79]a)$/
      and $raf->Read( $s, 7 ) == 7;

    my $ver         = $1;
    my $rtnVal      = 0;
    my $tagTablePtr = GetTagTable('Image::ExifTool::GIF::Main');
    my $extTable    = GetTagTable('Image::ExifTool::GIF::Extensions');
    SetByteOrder('II');

    if ($outfile) {
        my $ext;
        foreach $ext ( sort keys %$extTable ) {
            next unless ref $$extTable{$ext} eq 'HASH';
            my $extInfo = $$extTable{$ext};
            next
              unless $$extInfo{SubDirectory}
              and $$extInfo{Writable}
              and not $gifMap{ $$extInfo{Name} };
            $gifMap{ $$extInfo{Name} } = 'GIF';
            push @appExtensions, $ext;
        }
        $et->InitWriteDirs( \%gifMap, 'XMP' );
        $addDirs = $$et{ADD_DIRS};
        my $delGroup = $$et{DEL_GROUP};
        $newComment = $et->GetNewValue( 'Comment', \$nvComment );
        $setComment = 1 if $nvComment or $$delGroup{File};
        $buff = 'GIF89a' if %$addDirs or defined $newComment;
        Write( $outfile, $buff, $s ) or $err = 1;
        $newExt = $et->GetNewTagInfoHash($extTable);
    }
    else {
        $et->SetFileType();
        $et->HandleTag( $tagTablePtr, 'GIFVersion',       $ver );
        $et->HandleTag( $tagTablePtr, 'ScreenDescriptor', $s );
    }
    my $flags = Get8u( \$s, 4 );
    if ( $flags & 0x80 ) {

        $length = 3 * ( 2 << ( $flags & 0x07 ) );
        $raf->Read( $buff, $length ) == $length or return 0;
        Write( $outfile, $buff ) or $err = 1 if $outfile;
    }
  Block:
    for ( ; ; ) {
        last unless $raf->Read( $ch, 1 );
        if ( $outfile and ord($ch) != 0x21 ) {
            if ( defined $newComment and $$nvComment{IsCreating} ) {
                Write( $outfile, "\x21\xfe" ) or $err = 1;
                $verbose and print $out "  + Comment = $newComment\n";
                my $len = length($newComment);
                my $n;
                for ( $n = 0 ; $n < $len ; $n += 255 ) {
                    my $size = $len - $n;
                    $size > 255 and $size = 255;
                    my $str = substr( $newComment, $n, $size );
                    Write( $outfile, pack( 'C', $size ), $str ) or $err = 1;
                }
                Write( $outfile, "\0" ) or $err = 1;
                undef $newComment;
                undef $nvComment;
                ++$$et{CHANGED};
            }
            my $ext;
            my @new = sort keys %$newExt;
            foreach $ext ( @appExtensions, @new ) {
                my $extInfo = $$extTable{$ext};
                my $name    = $$extInfo{Name};
                if ( $$newExt{$ext} ) {
                    delete $$newExt{$ext};
                    $doneDir{$name} = 1;
                    $buff = $et->GetNewValue($extInfo);
                    $et->VerboseValue( "+ GIF:$name", $buff );
                }
                elsif ( exists $$addDirs{$name}
                    and not defined $doneDir{$name} )
                {
                    $doneDir{$name} = 1;
                    my $tbl = GetTagTable( $$extInfo{SubDirectory}{TagTable} );
                    my %dirInfo = ( Parent => 'GIF' );
                    $verbose
                      and print $out
                      "Creating $name application extension block:\n";
                    $buff = $et->WriteDirectory( \%dirInfo, $tbl );
                }
                else {
                    next;
                }
                if ( defined $buff and length $buff ) {
                    ++$$et{CHANGED};
                    Write(
                        $outfile, "\x21\xff\x0b",
                        substr( $ext, 0, 8 ),
                        substr( $ext, 9, 3 )
                    ) or $err = 1;
                    my $pos = 0;
                    if ( not $$extTable{$ext}{IncludeLengthBytes} ) {
                        my $len = length $buff;
                        while ( $pos < length $buff ) {
                            my $n = length($buff) - $pos;
                            $n = 255 if $n > 255;
                            Write( $outfile, chr($n),
                                substr( $buff, $pos, $n ) )
                              or $err = 1;
                            $pos += $n;
                        }
                        Write( $outfile, "\0" ) or $err = 1;
                    }
                    elsif ( $$extTable{$ext}{IncludeLengthBytes} < 2 ) {
                        $pos += ord( substr( $buff, $pos, 1 ) ) + 1
                          while $pos < length $buff;
                        Write( $outfile, $buff,
                            "\0" x ( $pos - length($buff) + 1 ) )
                          or $err = 1;
                    }
                    else {
                        Write( $outfile, $buff,
                            pack( 'C*', 1, reverse( 0 .. 255 ), 0 ) )
                          or $err = 1;
                    }
                    ++$doneDir{$name};
                }
                else {
                    $verbose and print $out "  -> no $name to add\n";
                }
            }
        }
        if ( ord($ch) == 0x2c ) {
            ++$frameCount;
            Write( $outfile, $ch ) or $err = 1 if $outfile;
            last unless $raf->Read( $buff, 8 ) == 8 and $raf->Read( $ch, 1 );
            Write( $outfile, $buff, $ch ) or $err = 1 if $outfile;
            if ( $verbose and not $outfile ) {
                my ( $left, $top, $w, $h ) = unpack( 'v*', $buff );
                print $out "Image: left=$left top=$top width=$w height=$h\n";
            }
            if ( ord($ch) & 0x80 ) {
                $length = 3 * ( 2 << ( ord($ch) & 0x07 ) );
                last unless $raf->Read( $buff, $length ) == $length;
                Write( $outfile, $buff ) or $err = 1 if $outfile;
            }
            last unless $raf->Read( $buff, 1 );
            Write( $outfile, $buff ) or $err = 1 if $outfile;
            for ( ; ; ) {
                last unless $raf->Read( $ch, 1 );
                Write( $outfile, $ch ) or $err = 1 if $outfile;
                last unless ord($ch);
                last unless $raf->Read( $buff, ord($ch) );
                Write( $outfile, $buff ) or $err = 1 if $outfile;
            }
            next;
        }
        unless ( ord($ch) == 0x21 ) {
            if ($outfile) {
                Write( $outfile, $ch ) or $err = 1;
                while ( $raf->Read( $buff, 65536 ) ) {
                    Write( $outfile, $buff ) or $err = 1;
                }
            }
            $rtnVal = 1;
            last;
        }
        last unless $raf->Read( $s, 2 ) == 2;
        ( $a, $length ) = unpack( "C" x 2, $s );

        if ( $a == 0xfe ) {

            my $comment = '';
            while ($length) {
                last unless $raf->Read( $buff, $length ) == $length;
                $et->VerboseDump( \$buff ) unless $outfile;
                $comment .= $buff;
                last unless $raf->Read( $ch, 1 );
                $length = ord($ch);
            }
            last if $length;
            if ($outfile) {
                my $isOverwriting;
                if ($setComment) {
                    if ($nvComment) {
                        $isOverwriting =
                          $et->IsOverwriting( $nvComment, $comment );
                        $newComment = $et->GetNewValue($nvComment)
                          if defined $newComment;
                    }
                    else {
                        $isOverwriting = 1;
                    }
                }
                if ($isOverwriting) {
                    ++$$et{CHANGED};
                    $et->VerboseValue( '- GIF:Comment', $comment );
                    $comment = $newComment;
                    $et->VerboseValue( '+ GIF:Comment', $comment )
                      if defined $comment;
                    undef $nvComment;
                }
                else {
                    undef $setComment;
                }
                if ( defined $comment ) {
                    Write( $outfile, "\x21\xfe" ) or $err = 1;
                    my $len = length($comment);
                    my $n;
                    for ( $n = 0 ; $n < $len ; $n += 255 ) {
                        my $size = $len - $n;
                        $size > 255 and $size = 255;
                        my $str = substr( $comment, $n, $size );
                        Write( $outfile, pack( 'C', $size ), $str ) or $err = 1;
                    }
                    Write( $outfile, "\0" ) or $err = 1;
                }
                undef $newComment;
            }
            else {
                $rtnVal = 1;
                $et->FoundTag( 'Comment', $comment ) if $comment;
                undef $comment;
                last if $et->Options('FastScan');
            }
            next;

        }
        elsif ( $a == 0xff and $length == 0x0b ) {

            last unless $raf->Read( $buff, $length ) == $length;
            my $hdr = "$ch$s$buff";
            my $tag = substr( $buff, 0, 8 ) . '/' . substr( $buff, 8 );
            $tag =~ tr/\0-\x1f//d;
            $verbose and print $out "Application Extension: $tag\n";

            my $extInfo = $$extTable{$tag};
            my ( $subdir, $inclLen, $justCopy, $name );
            if ($extInfo) {
                if ( $outfile and $$newExt{ $$extInfo{TagID} } ) {
                    delete $$newExt{ $$extInfo{TagID} };

                }
                else {
                    $subdir = $$extInfo{SubDirectory};
                }
                $inclLen = $$extInfo{IncludeLengthBytes};
                $name    = $$extInfo{Name};
                $justCopy = 1 if $outfile and not $$extInfo{Writable};
            }
            else {
                $justCopy = 1 if $outfile;
            }
            Write( $outfile, $hdr ) or $err = 1 if $justCopy;

            my $dat = '';
            for ( ; ; ) {
                $raf->Read( $ch, 1 )                    or last Block;
                $length = ord($ch)                      or last;
                $raf->Read( $buff, $length ) == $length or last Block;
                Write( $outfile, $ch, $buff ) or $err = 1 if $justCopy;
                $dat .= $inclLen ? $ch . $buff : $buff;
            }
            if ($justCopy) {
                Write( $outfile, "\0" ) or $err = 1;
                next;
            }
            elsif ($inclLen) {
                if (    $$extInfo{Terminator}
                    and $dat =~ /$$extInfo{Terminator}/g )
                {
                    $dat = substr( $dat, 0, pos($dat) );
                }
                elsif ( $dat =~ /\0/g ) {
                    $dat = substr( $dat, 0, pos($dat) - 1 );
                }
            }
            if ($subdir) {
                my %dirInfo = (
                    DataPt  => \$dat,
                    DataLen => length $dat,
                    DirLen  => length $dat,
                    DirName => $name,
                    Parent  => 'GIF',
                );
                my $subTable = GetTagTable( $$subdir{TagTable} );
                unless ($outfile) {
                    $et->ProcessDirectory( \%dirInfo, $subTable );
                    next;
                }
                next if $justCopy;
                if ( $doneDir{$name} and $doneDir{$name} > 1 ) {
                    $et->Warn("Duplicate $name block created");
                }
                $buff = $et->WriteDirectory( \%dirInfo, $subTable );
                if ( defined $buff ) {
                    next unless length $buff;
                    $dat = $buff;
                }
                $doneDir{$name} = 1;
            }
            elsif ( $outfile and not $justCopy ) {
                my $nvHash = $et->GetNewValueHash($extInfo);
                if ( $nvHash and $et->IsOverwriting( $nvHash, $dat ) ) {
                    ++$$et{CHANGED};
                    my $val = $et->GetNewValue($extInfo);
                    $et->VerboseValue( "- GIF:$name", $dat );
                    next unless defined $val and length $val;
                    $dat = $val;
                    $et->VerboseValue( "+ GIF:$name", $dat );
                    $doneDir{$name} = 1;
                }
            }
            elsif ( not $outfile ) {
                $et->HandleTag( $extTable, $tag, $dat );
                next;
            }
            Write( $outfile, $hdr ) or $err = 1;
            if ($inclLen) {
                $et->Error("$name contained NULL character")
                  if $inclLen and $dat =~ /\0/;
                if ( $inclLen > 1 ) {
                    $dat .= pack( 'C*', 1, reverse( 0 .. 255 ) ) if $inclLen;
                }
                else {
                    my $pos = 0;
                    $pos += ord( substr( $dat, $pos, 1 ) ) + 1
                      while $pos < length $dat;
                    $dat .= "\0" x ( $pos - length($dat) );
                }
                Write( $outfile, $dat ) or $err = 1;
            }
            else {
                my $pos = 0;
                my $len = length $dat;
                while ( $pos < $len ) {
                    my $n = $len - $pos;
                    $n = 255 if $n > 255;
                    Write( $outfile, chr($n), substr( $dat, $pos, $n ) )
                      or $err = 1;
                    $pos += $n;
                }
            }
            Write( $outfile, "\0" ) or $err = 1;
            next;

        }
        elsif ( $a == 0xf9 and $length == 4 ) {

            last unless $raf->Read( $buff, $length ) == $length;
            my $delay = Get16u( \$buff, 1 );
            $delayTime += $delay;
            $verbose
              and printf $out "Graphic Control: delay=%.2f\n", $delay / 100;
            my $bits = Get8u( \$buff, 0 );
            $et->HandleTag( $tagTablePtr, 'TransparentColor',
                Get8u( \$buff, 3 ) )
              if $bits & 0x01;
            $raf->Seek( -$length, 1 ) or last;

        }
        elsif ( $a == 0x01 and $length == 12 ) {

            last unless $raf->Read( $buff, $length ) == $length;
            Write( $outfile, $ch, $s, $buff ) or $err = 1 if $outfile;
            if ( $verbose and not $outfile ) {
                my ( $left, $top, $w, $h ) = unpack( 'v4', $buff );
                print $out "Text: left=$left top=$top width=$w height=$h\n";
            }
            my $text = '';
            for ( ; ; ) {
                last unless $raf->Read( $ch, 1 );
                $length = ord($ch) or last;
                last unless $raf->Read( $buff, $length ) == $length;
                Write( $outfile, $ch, $buff ) or $err = 1 if $outfile;
                $text .= $buff;
            }
            Write( $outfile, "\0" ) or $err = 1 if $outfile;
            $et->HandleTag( $tagTablePtr, 'Text', $text );
            next;
        }
        Write( $outfile, $ch, $s ) or $err = 1 if $outfile;
        while ($length) {
            last unless $raf->Read( $buff, $length ) == $length;
            Write( $outfile, $buff ) or $err = 1 if $outfile;
            last unless $raf->Read( $ch, 1 );
            Write( $outfile, $ch ) or $err = 1 if $outfile;
            $length = ord($ch);
        }
    }
    unless ($outfile) {
        $et->HandleTag( $tagTablePtr, 'FrameCount', $frameCount )
          if $frameCount > 1;
        $et->HandleTag( $tagTablePtr, 'Duration', $delayTime / 100 )
          if $delayTime;
    }

    $rtnVal = -1 if $rtnVal and $err;
    return $rtnVal;
}

1;

__END__

