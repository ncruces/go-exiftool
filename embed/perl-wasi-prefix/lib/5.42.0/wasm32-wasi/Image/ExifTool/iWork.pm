
package Image::ExifTool::iWork;

use strict;
use vars            qw($VERSION);
use Image::ExifTool qw(:DataAccess :Utils);
use Image::ExifTool::XMP;
use Image::ExifTool::ZIP;

$VERSION = '1.06';

my %iWorkType = (
    NUMBERS     => 'NUMBERS',
    PAGES       => 'PAGES',
    KEY         => 'KEY',
    KTH         => 'KTH',
    NMBTEMPLATE => 'NMBTEMPLATE',
    'ls:document'      => 'NUMBERS',
    'sl:document'      => 'PAGES',
    'key:presentation' => 'KEY',
);

my %mimeType = (
    'NUMBERS'        => 'application/x-iwork-numbers-sffnumbers',
    'PAGES'          => 'application/x-iwork-pages-sffpages',
    'KEY'            => 'application/x-iWork-keynote-sffkey',
    'NMBTEMPLATE'    => 'application/x-iwork-numbers-sfftemplate',
    'PAGES.TEMPLATE' => 'application/x-iwork-pages-sfftemplate',
    'KTH'            => 'application/x-iWork-keynote-sffkth',
);

%Image::ExifTool::iWork::Main = (
    GROUPS       => { 0 => 'XML', 1 => 'XML', 2 => 'Document' },
    PROCESS_PROC => \&Image::ExifTool::XMP::ProcessXMP,
    VARS         => { ID_FMT => 'none' },
    NOTES        => q{
        The Apple iWork '09 file format is a ZIP archive containing XML files
        similar to the Office Open XML (OOXML) format.  Metadata tags in iWork
        files are extracted even if they don't appear below.
    },
    authors   => { Name => 'Author', Groups => { 2 => 'Author' } },
    comment   => {},
    copyright => { Groups => { 2 => 'Author' } },
    keywords  => {},
    projects  => { List => 1 },
    title     => {},
);

sub GetTagID($) {
    my $props = shift;
    return 0 if $$props[-1] =~ /^\w+:ID$/;
    return $$props[0] =~ /^.*?:(.*)/ ? $1 : $$props[0];
}

sub FoundTag($$$$;$) {
    my ( $et, $tagTablePtr, $props, $val, $attrs ) = @_;
    return 0 unless @$props;
    my $verbose = $et->Options('Verbose');

    $et->VPrint( 0, "  | - Tag '", join( '/', @$props ), "'\n" )
      if $verbose > 1;

    $val = Image::ExifTool::XMP::UnescapeXML($val);
    $val = $et->Decode( $val, 'UTF8' );
    my $tag = GetTagID($props) or return 0;

    unless ( $$tagTablePtr{$tag} ) {
        $et->VPrint( 0, "  [adding $tag]\n" ) if $verbose;
        AddTagToTable( $tagTablePtr, $tag, { Name => ucfirst $tag } );
    }
    $et->HandleTag( $tagTablePtr, $tag, $val );

    return 1;
}

sub Process_iWork($$) {
    my ( $et, $dirInfo ) = @_;
    my $zip = $$dirInfo{ZIP};
    my ( $type, $index, $indexFile, $status );

    local $SIG{'__WARN__'} = \&Image::ExifTool::ZIP::WarnProc;
    $type = $iWorkType{ $$et{FILE_EXT} } if $$et{FILE_EXT};
    unless ($type) {
        my @members = $zip->membersMatching('^index\.(xml|apxl)$');
        if (@members) {
            ( $index, $status ) = $zip->contents( $members[0] );
            unless ($status) {
                $indexFile = $members[0]->fileName();
                if ( $index =~ /^\s*<\?xml version=[^<]+<(\w+:\w+)/s ) {
                    $type = $iWorkType{$1} if $iWorkType{$1};
                }
            }
        }
        else {
            @members =
              $zip->membersMatching('(?i)^.*\.(pages|numbers|key)/Index.*');
            if (@members) {
                my $tmp = $members[0]->fileName();
                $type = $iWorkType{ uc $1 } if $tmp =~ /\.(pages|numbers|key)/i;
            }
        }
        $type or $type = 'ZIP';
    }
    $et->SetFileType( $type, $mimeType{$type} );

    my @members = $zip->members();
    my $docNum  = 0;
    my $member;
    foreach $member (@members) {
        my $file = $member->fileName();
        next unless defined $file;
        $et->VPrint( 0, "File: $file\n" );
        $$et{DOC_NUM} = ++$docNum;
        Image::ExifTool::ZIP::HandleMember( $et, $member );

        next
          unless $file =~
m{^(index\.(xml|apxl)|QuickLook/Thumbnail\.jpg|[^/]+/preview(-micro|-web)?.jpg)$}i;
        my ( $buff, $buffPt );
        if ( $indexFile and $indexFile eq $file ) {
            $buffPt = \$index;
        }
        else {
            ( $buff, $status ) = $zip->contents($member);
            $status and $et->Warn("Error extracting $file"), next;
            $buffPt = \$buff;
        }
        if ( $file =~ /\.jpg$/ ) {
            my $type =
                ( $file =~ /preview-(\w+)/ )
              ? ( $1 eq 'web' ? 'Other' : 'Thumbnail' )
              : 'Preview';
            $et->FoundTag( $type . 'Image', $buffPt );
            next;
        }
        next unless $$buffPt =~ /<(\w+):metadata>/g;
        my $ns = $1;
        my $p1 = pos $$buffPt;
        next unless $$buffPt =~ m{</${ns}:metadata>}g;
        $$buffPt = '<?xml version="1.0"?>'
          . substr( $$buffPt, $p1, pos($$buffPt) - $p1 );
        my %dirInfo = (
            DataPt       => $buffPt,
            DirLen       => length $$buffPt,
            DataLen      => length $$buffPt,
            XMPParseOpts => {
                FoundProc => \&FoundTag,
            },
        );
        my $tagTablePtr = GetTagTable('Image::ExifTool::iWork::Main');
        $et->ProcessDirectory( \%dirInfo, $tagTablePtr );
        undef $$buffPt;
    }
    delete $$et{DOC_NUM};
    return 1;
}

1;

__END__


