
package Image::ExifTool::Geotag;

use strict;
use vars            qw($VERSION);
use Image::ExifTool qw(:Public);
use Image::ExifTool::GPS;

$VERSION = '1.87';

sub JITTER() { return 2 }

sub GetTime($);
sub SetGeoValues($$;$);
sub PrintFixTime($);
sub PrintFix($@);
sub InitUserTags($);

my %xmlTag = (
    lat                 => 'lat',
    latitude            => 'lat',
    latitudedegrees     => 'lat',
    lon                 => 'lon',
    longitude           => 'lon',
    longitudedegrees    => 'lon',
    ele                 => 'alt',
    elevation           => 'alt',
    alt                 => 'alt',
    altitude            => 'alt',
    altitudemeters      => 'alt',
    'time'              => 'time',
    fix                 => 'fixtype',
    hdop                => 'hdop',
    vdop                => 'vdop',
    pdop                => 'pdop',
    sat                 => 'nsats',
    atemp               => 'atemp',
    when                => 'time',
    coordinates         => 'coords',
    coord               => 'coords',
    begin               => 'begin',
    end                 => 'time',
    course              => 'dir',
    pitch               => 'pitch',
    roll                => 'roll',
    speed               => 'speed',
    accuracy_horizontal => 'err',

    wpt        => '',
    trkpt      => '',
    rtept      => '',
    trackpoint => '',
    placemark  => '',
);

my %userTag;

my %cyclical = ( lon => 1, track => 1, dir  => 1, pitch => 1, roll => 1 );
my %cyc180   = ( lon => 1, pitch => 1, roll => 1 );

my %fixInfoKeys = (
    'pos'  => [ 'lat',   'lon' ],
    track  => [ 'track', 'speed' ],
    alt    => ['alt'],
    orient => [ 'dir', 'pitch', 'roll' ],
    atemp  => ['atemp'],
    err    => ['err'],
    dop    => [ 'hdop', 'vdop', 'pdop' ],
);

my %keyCategory = (
    dir   => 'orient',
    pitch => 'orient',
    roll  => 'orient',
    hdop  => 'dop',
    pdop  => 'dop',
    vdop  => 'dop',
);

my %sepTags = (
    dir   => 1,
    pitch => 1,
    roll  => 1,
    track => 1,
    speed => 1,
    hdop => 1,
    pdop => 1,
    vdop => 1,
);

my %speedConv = (
    'K'    => 1.852,
    'M'    => 1.150779448,
    'k'    => 'K',
    'm'    => 'M',
    'km/h' => 'K',
    'mph'  => 'M',
);

my %otherConv = (
    'km/h' => 1.852,
    'mph'  => 1.150779448,
    'm/s'  => 0.514444,
);

my $secPerDay = 24 * 3600;

sub SplitCSV($$) {
    my ( $line, $delim ) = @_;
    my @toks = split /\Q$delim/, $line;
    my ( @vals, $v );
    while (@toks) {
        ( $v = shift @toks ) =~ s/^ +//;
        if ( $v =~ s/^"// ) {
            while ( $v !~ /("+)\s*$/ or not length($1) & 1 ) {
                last unless @toks;
                $v .= $delim . shift @toks;
            }
            $v =~ s/"\s*$//;
            $v =~ s/""/"/g;
        }
        push @vals, $v;
    }
    return @vals;
}

sub LoadTrackLog($$;$) {
    local ( $_, $/, *EXIFTOOL_TRKFILE );
    my ( $et, $val ) = @_;
    my ( $raf, $from, $time, $isDate, $noDate, $noDateChanged, $lastDate,
        $dateFlarm );
    my ( $nmeaStart, $fixSecs, @fixTimes, $lastFix, %nmea, @csvHeadings,
        $sortFixes );
    my (
        $canCut, $cutPDOP, $cutHDOP,   $cutSats, $e0,
        $e1,     @tmp,     $trackFile, $trackTime
    );
    my ( $scaleSpeed, $startTime );

    unless ( eval { require Time::Local } ) {
        return 'Geotag feature requires Time::Local installed';
    }
    InitUserTags($et);

    my $geotag = $et->GetNewValue('Geotag') || {};

    my $points = $$geotag{Points};
    $points or $points = $$geotag{Points} = {};

    my $has = $$geotag{Has};
    $has or $has = $$geotag{Has} = { 'pos' => 1 };

    my $format = '';
    if ( $val =~ /^(\xef\xbb\xbf)?<(\?xml|gpx)[\s>]/ ) {
        $format = 'XML';
        $/      = '>';
    }
    elsif ( $val =~ /(\x0d\x0a|\x0d|\x0a)/ ) {
        $/ = $1;
    }
    else {
        if ( $et->Open( \*EXIFTOOL_TRKFILE, $val ) ) {
            $trackFile = $val;
            $raf       = File::RandomAccess->new( \*EXIFTOOL_TRKFILE );
            unless ( $raf->Read( $_, 256 ) ) {
                close EXIFTOOL_TRKFILE;
                return "Empty track file '${val}'";
            }
            if (/^(\xef\xbb\xbf)?<(\?xml|gpx)[\s>]/) {
                $format = 'XML';
                $/      = '>';
            }
            elsif (/(\x0d\x0a|\x0d|\x0a)/) {
                $/ = $1;
            }
            else {
                close EXIFTOOL_TRKFILE;
                return "Invalid track file '${val}'";
            }
            $raf->Seek( 0, 0 );
            $from = "file '${val}'";
        }
        elsif ( $val eq 'DATETIMEONLY' ) {
            $$geotag{DateTimeOnly} = 1;
            $$geotag{IsDate}       = 1;
            $et->VPrint( 0, 'Geotagging date/time only' );
            return $geotag;
        }
        else {
            return "Error opening GPS file '${val}'";
        }
    }
    unless ($from) {
        $raf  = File::RandomAccess->new( \$val );
        $from = 'data';
    }

    my $maxHDOP = $et->Options('GeoMaxHDOP');
    my $maxPDOP = $et->Options('GeoMaxPDOP');
    my $minSats = $et->Options('GeoMinSats');
    my $isCut   = $maxHDOP || $maxPDOP || $minSats;

    my $numPoints = 0;
    my $skipped   = 0;
    my $lastSecs  = 0;
    my $fix       = {};
    my $csvDelim  = $et->Options('CSVDelim');
    $csvDelim = ',' unless defined $csvDelim;
    my ( @saveFix, @saveTime, $timeSpan );
    for ( ; ; ) {
        $raf->ReadLine($_) or last;
        if ( not $format ) {
            s/^\xef\xbb\xbf//;
            if (/^\xff\xfe|\xfe\xff/) {
                return "ExifTool doesn't yet read UTF16-format track logs";
            }
            if (/^<(\?xml|gpx)[\s>]/) {
                $format = 'XML';
            }
            elsif (/^.*\$([A-Z]{2}(RMC|GGA|GLL|ZDA)|PMGNTRK),/) {
                $format    = 'NMEA';
                $nmeaStart = $2 || $1;
            }
            elsif (/^A(FLA|XSY|FIL)/) {
                $nmeaStart = 'B';
                next;
            }
            elsif (/^HFDTE(?:DATE:)?(\d{2})(\d{2})(\d{2})/) {
                my $year = $3 + ( $3 >= 70 ? 1900 : 2000 );
                $dateFlarm = Time::Local::timegm( 0, 0, 0, $1, $2 - 1, $year );
                $nmeaStart = 'B';
                $format    = 'IGC';
                next;
            }
            elsif ( $nmeaStart and /^B/ ) {
                $format = 'IGC';
            }
            elsif (/^TP,D,/) {
                $format = 'Winplus';
            }
            elsif ( /^\s*\d+\s+.*\sypr\s*$/ and ( @tmp = split ) == 12 ) {
                $format = 'Bramor';
            }
            elsif (
                (
                    ( /\b(GPS)?Date/i and /\b(GPS)?(Date)?Time/i )
                    or /\bTime\(seconds\)/i
                )
                and /\Q$csvDelim/
              )
            {
                chomp;
                @csvHeadings = SplitCSV( $_, $csvDelim );
                my $isColumbus =
                  ( $csvHeadings[0] and $csvHeadings[0] eq 'INDEX' );
                $format = 'CSV';
                foreach (@csvHeadings) {
                    my $head = $_;
                    my $param;
                    my $xtra = '';
                    s/^GPS ?//;
                    if (/^Time ?\(seconds\)$/i) {

                        $param = 'runtime';
                        if (    $trackFile
                            and $trackFile =~
/(\d{4})-(\d{2})-(\d{2})[^\/]+(\d{2})-(\d{2})-(\d{2})[^\/]*$/
                          )
                        {
                            $trackTime =
                              Image::ExifTool::TimeLocal( $6, $5, $4, $3,
                                $2 - 1, $1 );
                            my $utc = PrintFixTime($trackTime);
                            my $tzs = Image::ExifTool::TimeZoneString(
                                [ $6, $5, $4, $3, $2 - 1, $1 - 1900 ],
                                $trackTime );
                            $et->VPrint( 2,
"  DJI start time:  $utc (local timezone is $tzs)\n"
                            );
                        }
                        else {
                            return
'Error getting start time from file name for DJI CSV track file';
                        }
                    }
                    elsif (/^Date ?Time/i) {
                        $param = 'datetime';
                    }
                    elsif (/^Date/i) {
                        $param = 'date';
                    }
                    elsif (/^Time(?! ?\(text\))/i) {
                        $param = 'time';
                    }
                    elsif (/^(Pos)?Lat/i) {
                        $param = 'lat';
                        /ref$/i and $param .= 'ref';
                    }
                    elsif (/^(Pos)?Lon/i) {
                        $param = 'lon';
                        /ref$/i and $param .= 'ref';
                    }
                    elsif (/^(Pos)?(Alt|Height)/i) {
                        $param = 'alt';
                    }
                    elsif (/^Speed/i) {
                        $param = 'speed';
                        if (m{\((mph|km/h|m/s)\)}) {
                            $scaleSpeed = $otherConv{$1};
                            $xtra       = " in $1";
                        }
                        elsif ($isColumbus) {
                            $scaleSpeed = $otherConv{'km/h'};
                            $xtra       = " in km/h";
                        }
                        else {
                            $xtra = ' in knots';
                        }
                    }
                    elsif (/^(Angle)?(Heading|Track|Bearing)/i) {
                        $param = 'track';
                    }
                    elsif ( /^(Angle)?Pitch/i or /^Camera ?Elevation ?Angle/i )
                    {
                        $param = 'pitch';
                    }
                    elsif (/^(Angle)?Roll/i) {
                        $param = 'roll';
                    }
                    elsif (/^Img ?Dir/i) {
                        $param = 'dir';
                    }
                    elsif ( $userTag{ lc $_ } ) {
                        $param = $userTag{ lc $_ };
                    }
                    if ($param) {
                        $et->VPrint( 2,
                            "CSV column '${head}' is $param$xtra\n" );
                        $_ = $param;
                    }
                    else {
                        $et->VPrint( 2, "CSV column '${head}' ignored\n" );
                        $_ = '';
                    }
                }
                next;
            }
            elsif (
                /"(timelineObjects|placeVisit|activitySegment|latitudeE7)"\s*:/)
            {
                $format    = 'JSON';
                $sortFixes = 1;
            }
            elsif (/"(durationMinutesOffsetFromStartTime|startTime)"\s*:/) {
                $format = 'JSON';
                $raf->Seek( 0, 0 );
            }
            else {
                last if ++$skipped > 50;
                next;
            }
        }
        if ( $format eq 'XML' ) {
            my ( @args, $arg, $tok, $td, $value );
            if (/^([^<]+<\/[^>]+>)/) {
                s/^\s+</</;

                s{(\S+)\s+(\S+)\s+(\S+)(</gx:coord>)}{$1,$2,$3$4};
                push @args, $_;
            }
            else {
                s/\s*=\s*(['"])\s*/=$1/g;
                push @args, split;
            }
            foreach $arg (@args) {
                if ( $arg =~ /^(\w+:)?(\w+)=(['"])(.*?)\3/g ) {
                    my $tag = $xmlTag{ lc $2 };
                    $tag = $userTag{ lc $2 } unless defined $tag;
                    if ($tag) {
                        $$fix{$tag} = $4;
                        if ( $keyCategory{$tag} ) {
                            $$has{ $keyCategory{$tag} } = 1;
                        }
                        elsif ( $tag eq 'alt' ) {
                            undef $$fix{alt}
                              if defined $$fix{alt}
                              and $$fix{alt} !~ /^[+-]?\d+\.?\d*/;
                            $$has{alt} = 1 if $$fix{alt};
                        }
                        elsif ($tag eq 'atemp'
                            or $tag eq 'speed'
                            or $tag eq 'err' )
                        {
                            $$has{$tag} = 1;
                        }
                    }
                }
                while ( $arg =~ m{([^<>]*)<(/)?(\w+:)?(\w+)(>|$)}g ) {
                    $tok = lc $4;
                    my $tag = $xmlTag{$tok};
                    $tag = $userTag{$tok} unless defined $tag;
                    if ( defined $tag and not $tag ) {
                        if ( not $2 ) {
                            $lastFix = $fix = {};
                            undef @saveFix;
                            next;
                        }
                        elsif ( $fix and $lastFix and %$fix ) {
                            foreach ( keys %$fix ) {
                                $$lastFix{$_} = $$fix{$_}
                                  unless defined $$lastFix{$_};
                            }
                            undef $lastFix;
                        }
                    }
                    if ( length $1 ) {
                        if ($tag) {
                            if ( $tag eq 'coords' ) {
                                if (    defined $$fix{lon}
                                    and defined $$fix{lat}
                                    and defined $$fix{alt} )
                                {
                                    push @saveFix,
                                      [ @$fix{ 'lon', 'lat', 'alt' } ];
                                }
                                @$fix{ 'lon', 'lat', 'alt' } = split ',', $1;
                                $$has{alt} = 1 if $$fix{alt};
                            }
                            else {
                                if ( $tok eq 'when' and $$fix{'time'} ) {
                                    push @saveTime, $1;
                                }
                                else {
                                    $$fix{$tag} = $1;
                                }
                                if ( $keyCategory{$tag} ) {
                                    $$has{ $keyCategory{$tag} } = 1;
                                }
                                elsif ( $tag eq 'alt' ) {
                                    undef $$fix{alt}
                                      if defined $$fix{alt}
                                      and $$fix{alt} !~ /^[+-]?\d+\.?\d*/;
                                    $$has{alt} = 1 if $$fix{alt};
                                }
                                elsif ($tag eq 'atemp'
                                    or $tag eq 'speed'
                                    or $tag eq 'err' )
                                {
                                    $$has{$tag} = 1;
                                }
                            }
                        }
                        next;
                    }
                    elsif ( $tok eq 'td' ) {
                        $td = 1;
                    }
                    next unless defined $$fix{lat} and defined $$fix{lon};
                    unless ( defined $$fix{'time'} ) {
                        next unless @saveTime;
                        $$fix{'time'} = shift @saveTime;
                    }
                    unless ($$fix{lat} =~ /^[+-]?\d+\.?\d*/
                        and $$fix{lon} =~ /^[+-]?\d+\.?\d*/ )
                    {
                        $e0
                          or $et->VPrint( 0,
                            "Coordinate format error in $from\n" ), $e0 = 1;
                        next;
                    }
                    unless ( defined( $time = GetTime( $$fix{'time'} ) ) ) {
                        $e1
                          or
                          $et->VPrint( 0, "Timestamp format error in $from\n" ),
                          $e1 = 1;
                        next;
                    }
                    $isDate = 1;
                    $canCut = 1
                      if defined $$fix{pdop}
                      or defined $$fix{hdop}
                      or defined $$fix{nsats};
                    if ( $$fix{begin} ) {
                        my $begin = GetTime( $$fix{begin} );
                        undef $$fix{begin};
                        if ( defined $begin and $begin < $time ) {
                            $$fix{span} = $timeSpan = ( $timeSpan || 0 ) + 1;
                            my $i;
                            @saveFix
                              or push @saveFix,
                              [ @$fix{ 'lon', 'lat', 'alt' } ];
                            for ( $i = 0 ; $i < @saveFix ; ++$i ) {
                                my $t =
                                  $begin +
                                  ( $time - $begin ) *
                                  ( $i / scalar(@saveFix) );
                                my %f;
                                @f{ 'lon', 'lat', 'alt' } = @{ $saveFix[$i] };
                                $t += 0.001 if not $i and $$points{$t};
                                $f{span} = $timeSpan;
                                $$points{$t} = \%f;
                                push @fixTimes, $t;
                            }
                        }
                    }
                    $$points{$time} = $fix;
                    push @fixTimes, $time;
                    $fix = {};
                    undef @saveFix;
                    ++$numPoints;
                }
            }
            $$fix{'time'} = "$1T$2Z"
              if $td
              and not $$fix{'time'}
              and /[\s>](\d{4}-\d{2}-\d{2})[T ](\d{2}:\d{2}:\d{2}(\.\d+)?)/;
            next;
        }
        elsif ( $format eq 'Winplus' ) {
/^TP,D,\s*([-+]?\d+\.\d*),\s*([-+]?\d+\.\d*),\s*(\d+)\/(\d+)\/(\d{4}),\s*(\d+):(\d+):(\d+)/
              or next;
            $$fix{lat} = $1;
            $$fix{lon} = $2;
            $time      = Time::Local::timegm( $8, $7, $6, $4, $3 - 1, $5 );
          DoneFix: $isDate = 1;
            $$points{$time} = $fix;
            push @fixTimes, $time;
            $fix = {};
            ++$numPoints;
            next;
        }
        elsif ( $format eq 'Bramor' ) {
            my @parts = split ' ', $_;
            next unless @parts == 12 and $parts[11] eq 'ypr';
            my @d = split m{/}, $parts[6];
            my @t = split m{:}, $parts[7];
            next unless @d == 3 and @t == 3;
            @$fix{qw(lat lon alt track dir pitch roll)} =
              @parts[ 2, 3, 4, 5, 8, 9, 10 ];
            $time =
              Time::Local::timegm( 0, $t[1], $t[0], $d[0], $d[1] - 1, $d[2] ) +
              $t[2];
            @$has{qw(alt track orient)} = ( 1, 1, 1 );
            goto DoneFix;
        }
        elsif ( $format eq 'CSV' ) {
            chomp;
            my @vals = SplitCSV( $_, $csvDelim );
            my ( $param, $date, $secs, %neg );
            foreach $param (@csvHeadings) {
                my $val = shift @vals;
                last unless defined $val and length($val);
                next unless $param;
                if ( $param eq 'datetime' ) {
                    $val =~ s/^(\d{2})[^\d](\d{2})[^\d](\d{4}) /$3:$2:$1 /;
                    local $SIG{'__WARN__'} = sub { };
                    my $dateTime = $et->InverseDateTime($val);
                    if ($dateTime) {
                        $date = Image::ExifTool::GetUnixTime( $val, 2 );
                        $secs = 0;
                    }
                }
                elsif ( $param eq 'date' ) {
                    if ( $val =~ m{^(\d{2})/(\d{2})/(\d{4})$} ) {
                        $date = Time::Local::timegm( 0, 0, 0, $1, $2 - 1, $3 );
                    }
                    elsif ( $val =~ /(\d{4}).*?(\d{2}).*?(\d{2})/ ) {
                        $date = Time::Local::timegm( 0, 0, 0, $3, $2 - 1, $1 );
                    }
                    elsif ( $val =~ /^(\d{2})(\d{2})(\d{2})$/ ) {
                        $date =
                          Time::Local::timegm( 0, 0, 0, $3, $2 - 1, $1 + 2000 );
                    }
                }
                elsif ( $param eq 'time' ) {
                    if ( $val =~
/^(\d{1,2}):(\d{2}):(\d{2}(\.\d+)?).*?(([-+])(\d{1,2}):?(\d{2}))?/
                      )
                    {
                        $secs = ( ( $1 * 60 ) + $2 ) * 60 + $3;
                        $secs += ( $7 * 60 + $8 ) * ( $6 eq '-' ? 60 : -60 )
                          if $5;
                    }
                    elsif ( $val =~ /^(\d{2})(\d{2})(\d{2})$/ ) {
                        $secs = ( ( $1 * 60 ) + $2 ) * 60 + $3;
                    }
                }
                elsif ( $param eq 'lat' or $param eq 'lon' ) {
                    $$fix{$param} = Image::ExifTool::GPS::ToDegrees( $val, 1 );
                }
                elsif ( $param eq 'latref' ) {
                    $neg{lat} = 1 if $val =~ /^S/i;
                }
                elsif ( $param eq 'lonref' ) {
                    $neg{lon} = 1 if $val =~ /^W/i;
                }
                elsif ( $param eq 'runtime' ) {
                    $date = $trackTime;
                    $secs = $val;
                }
                elsif ( $param =~ /^_/ ) {
                    $$fix{$param} = $val;
                }
                else {
                    $val /= $scaleSpeed if $scaleSpeed and $param eq 'speed';
                    $$fix{$param} = $val;
                    $$has{$param} = 1 if $sepTags{$param};
                }
            }
            foreach $param ( keys %neg ) {
                next unless defined $$fix{$param};
                $$fix{$param} = -abs( $$fix{$param} );
            }
            if (    $date
                and defined $secs
                and defined $$fix{lat}
                and defined $$fix{lon} )
            {
                $time         = $date + $secs;
                $$has{alt}    = 1 if defined $$fix{alt};
                $$has{track}  = 1 if defined $$fix{track};
                $$has{orient} = 1 if defined $$fix{pitch};
                goto DoneFix;
            }
            next;
        }
        elsif ( $format eq 'JSON' ) {
            if (
/"(latitudeE7|longitudeE7|latE7|lngE7|timestamp|startTime|point|durationMinutesOffsetFromStartTime|time)"\s*:\s*"?(.*?)"?,?\s*[\x0d\x0a]/
              )
            {
                if ( $1 eq 'timestamp' or $1 eq 'time' ) {
                    $time = GetTime($2);
                    goto DoneFix if $time and $$fix{lat} and $$fix{lon};
                }
                elsif ( $1 eq 'startTime' ) {
                    $startTime = GetTime($2);
                }
                elsif ( $1 eq 'latitudeE7' or $1 eq 'latE7' ) {
                    $$fix{lat} = $2 * 1e-7;
                }
                elsif ( $1 eq 'longitudeE7' or $1 eq 'lngE7' ) {
                    $$fix{lon} = $2 * 1e-7;
                }
                elsif ( $1 eq 'point' ) {
                    my $point  = $2;
                    my @coords = $point =~ /[-+]?\d+\.\d+/g;
                    @$fix{ 'lat', 'lon' } = @coords[ 0, 1 ] if @coords == 2;
                }
                elsif ( $1 eq 'durationMinutesOffsetFromStartTime'
                    and defined $startTime )
                {
                    $time = $startTime + $2 * 60;
                    goto DoneFix if $time and $$fix{lat} and $$fix{lon};
                }
            }
            next;
        }
        my ( %fix, $secs, $date, $nmea );
        if ( $format eq 'NMEA' ) {
            next
              unless /^(.*)\$([A-Z]{2}(RMC|GGA|GLL|GSA|ZDA)|PMGNTRK|PTNTHPR),/;
            $nmea = $3 || $2;
            $_    = substr( $_, length($1) ) if length($1);
        }
        if ( $format eq 'IGC' ) {
/^B(\d{2})(\d{2})(\d{2})(\d{2})(\d{2})(\d{3})([NS])(\d{3})(\d{2})(\d{3})([EW])([AV])(\d{5})(\d{5})/
              or next;
            $fix{lat} =
              ( $4 + ( $5 + $6 / 1000 ) / 60 ) * ( $7 eq 'N' ? 1 : -1 );
            $fix{lon} =
              ( $8 + ( $9 + $10 / 1000 ) / 60 ) * ( $11 eq 'E' ? 1 : -1 );
            $fix{alt} = $12 eq 'A' ? $14 : undef;
            $secs = ( ( $1 * 60 ) + $2 ) * 60 + $3;
            if ($dateFlarm) {
                $dateFlarm += $secPerDay if $secs < $lastSecs - JITTER();
                $date = $dateFlarm;
            }
            $nmea = 'B';
        }
        elsif ( $nmea eq 'RMC' ) {
/^\$[A-Z]{2}RMC,(\d{2})(\d{2})(\d+(\.\d*)?),A?,(\d*?)(\d{1,2}\.\d+),([NS]),(\d*?)(\d{1,2}\.\d+),([EW]),(\d*\.?\d*),(\d*\.?\d*),(\d{2})(\d{2})(\d+)/
              or next;
            next if $13 > 31 or $14 > 12 or $15 > 99;
            $fix{lat}   = ( ( $5 || 0 ) + $6 / 60 ) * ( $7 eq 'N'  ? 1 : -1 );
            $fix{lon}   = ( ( $8 || 0 ) + $9 / 60 ) * ( $10 eq 'E' ? 1 : -1 );
            $fix{speed} = $11 if length $11;
            $fix{track} = $12 if length $12;
            my $year = $15 + ( $15 >= 70 ? 1900 : 2000 );
            $secs = ( ( $1 * 60 ) + $2 ) * 60 + $3;
            $date = Time::Local::timegm( 0, 0, 0, $13, $14 - 1, $year );
        }
        elsif ( $nmea eq 'GGA' ) {
/^\$[A-Z]{2}GGA,(\d{2})(\d{2})(\d+(\.\d*)?),(\d*?)(\d{1,2}\.\d+),([NS]),(\d*?)(\d{1,2}\.\d+),([EW]),[1-6]?,(\d+)?,(\.\d+|\d+\.?\d*)?,(-?\d+\.?\d*)?,M?/
              or next;
            $fix{lat} = ( ( $5 || 0 ) + $6 / 60 ) * ( $7 eq 'N'  ? 1 : -1 );
            $fix{lon} = ( ( $8 || 0 ) + $9 / 60 ) * ( $10 eq 'E' ? 1 : -1 );
            @fix{qw(nsats hdop alt)} = ( $11, $12, $13 );
            $secs                    = ( ( $1 * 60 ) + $2 ) * 60 + $3;
            $canCut                  = 1;
        }
        elsif ( $nmea eq 'GLL' ) {
/^\$[A-Z]{2}GLL,(\d*?)(\d{1,2}\.\d+),([NS]),(\d*?)(\d{1,2}\.\d+),([EW]),(\d{2})(\d{2})(\d+(\.\d*)?),A/
              or next;
            $fix{lat} = ( ( $1 || 0 ) + $2 / 60 ) * ( $3 eq 'N' ? 1 : -1 );
            $fix{lon} = ( ( $4 || 0 ) + $5 / 60 ) * ( $6 eq 'E' ? 1 : -1 );
            $secs     = ( ( $7 * 60 ) + $8 ) * 60 + $9;
        }
        elsif ( $nmea eq 'GSA' ) {
/^\$[A-Z]{2}GSA,[AM],([23]),((?:\d*,){11}(?:\d*)),(\d+\.?\d*|\.\d+)?,(\d+\.?\d*|\.\d+)?,(\d+\.?\d*|\.\d+)?\*/
              or next;
            @fix{qw(fixtype sats pdop hdop vdop)} =
              ( $1 . 'd', $2, $3, $4, $5 );
            my @a = ( $fix{sats} =~ /\d+/g );
            $fix{nsats} = scalar @a;
            $canCut = 1;
        }
        elsif ( $nmea eq 'ZDA' ) {
            /^\$[A-Z]{2}ZDA,(\d{2})(\d{2})(\d{2}(\.\d*)?),(\d+),(\d+),(\d+)/
              or next;
            $secs = ( ( $1 * 60 ) + $2 ) * 60 + $3;
            $date = Time::Local::timegm( 0, 0, 0, $5, $6 - 1, $7 );
        }
        elsif ( $nmea eq 'PMGNTRK' ) {
/^\$PMGNTRK,(\d+)(\d{2}\.\d+),([NS]),(\d+)(\d{2}\.\d+),([EW]),(-?\d+\.?\d*),([MF]),(\d{2})(\d{2})(\d+(\.\d*)?),A,(?:[^,]*,(\d{2})(\d{2})(\d+))?/
              or next;
            $fix{lat} = ( $1 + $2 / 60 ) * ( $3 eq 'N' ? 1 : -1 );
            $fix{lon} = ( $4 + $5 / 60 ) * ( $6 eq 'E' ? 1 : -1 );
            $fix{alt} = $8 eq 'M' ? $7 : $7 * 12 * 0.0254;
            $secs     = ( ( $9 * 60 ) + $10 ) * 60 + $11;
            if ( defined $15 ) {
                next if $13 > 31 or $14 > 12 or $15 > 99;

                my $year = $15 + ( $15 >= 70 ? 1900 : 2000 );
                $date = Time::Local::timegm( 0, 0, 0, $13, $14 - 1, $year );
            }
        }
        elsif ( $nmea eq 'PTNTHPR' ) {
            /^\$PTNTHPR,(-?[\d.]+),[MNO],(-?[\d.]+),[MNO],(-?[\d.]+),[MNO]/
              or next;
            @fix{qw(dir pitch roll)} = ( $1, $2, $3 );

        }
        else {
            next;
        }
        $nmea{$nmea} = 1;
        if ( defined $secs and not defined $date and defined $lastDate ) {
            if ( $secs < $lastSecs - JITTER() ) {
                $lastSecs -= $secPerDay;
                $lastDate += $secPerDay;
            }
            if ( $secs - $lastSecs < 10 ) {
                $date = $lastDate;
            }
            else {
                undef $lastDate;
                undef $lastSecs;
            }
        }
        if ( defined $date ) {
            $lastDate = $date;
            $lastSecs = $secs;
        }
        if (
            $nmea eq $nmeaStart or (
                defined $secs and (
                    not defined $fixSecs
                    or
                    ( $secs >= $fixSecs and $secs - $fixSecs >= 10 )
                    or
                    ( $secs < $fixSecs and $secs + $secPerDay - $fixSecs >= 10 )
                )
            )
          )
        {
            $fix     = \%fix;
            $fixSecs = $secs;
            undef $noDateChanged;
            if ( defined $date ) {
                $fix{isDate} = $isDate = 1;
                $time = $date + $secs;
            }
            elsif ( defined $secs ) {
                $time   = $secs;
                $noDate = $noDateChanged = 1;
            }
            else {
                next;
            }
        }
        else {
            foreach ( keys %fix ) {
                $$fix{$_} = $fix{$_} unless defined $$fix{$_};
            }
            if ( defined $date ) {
                next if $$fix{isDate};
                if ( defined $fixSecs ) {
                    delete $$points{$fixSecs};
                    pop @fixTimes if @fixTimes and $fixTimes[-1] == $fixSecs;
                    --$numPoints;
                    $date -= $secPerDay if $secs < $fixSecs;
                }
                else {
                    $fixSecs = $secs;
                }
                $time = $date + $fixSecs;
                $$fix{isDate} = $isDate = 1;
                $noDate = 0 if $noDateChanged;
            }
            elsif ( defined $secs and not defined $fixSecs ) {
                $time   = $fixSecs       = $secs;
                $noDate = $noDateChanged = 1;
            }
            else {
                next;
            }
        }
        $$points{$time} = $fix;
        push @fixTimes, $time;
        ++$numPoints;
    }
    $raf->Close();

    if ( $noDate and not $$geotag{NoDate} ) {
        if ($isDate) {
            $et->Warn(
                'Fixes are date-less -- will use time-only interpolation');
        }
        else {
            $et->Warn(
                'Some fixes are date-less -- may use time-only interpolation');
        }
        $$geotag{NoDate} = 1;
    }
    $$geotag{IsDate} = 1 if $isDate;

    if ( $isCut and $canCut ) {
        $cutPDOP = $cutHDOP = $cutSats = 0;
        my @goodTimes;
        foreach (@fixTimes) {
            $fix = $$points{$_} or next;
            if ( $maxPDOP and $$fix{pdop} and $$fix{pdop} > $maxPDOP ) {
                delete $$points{$_};
                ++$cutPDOP;
            }
            elsif ( $maxHDOP and $$fix{hdop} and $$fix{hdop} > $maxHDOP ) {
                delete $$points{$_};
                ++$cutHDOP;
            }
            elsif ( $minSats
                and defined $$fix{nsats}
                and $$fix{nsats} ne ''
                and $$fix{nsats} < $minSats )
            {
                delete $$points{$_};
                ++$cutSats;
            }
            else {
                push @goodTimes, $_;
            }
        }
        @fixTimes = @goodTimes;
        $numPoints -= $cutPDOP;
        $numPoints -= $cutHDOP;
        $numPoints -= $cutSats;
    }
    @fixTimes = sort { $a <=> $b } @fixTimes if $sortFixes;
    while (@fixTimes) {
        $fix = $$points{ $fixTimes[0] } or shift(@fixTimes), next;
        $$fix{first} = 1;
        last;
    }
    my $verbose = $et->Options('Verbose');
    if ($verbose) {
        my $out = $et->Options('TextOut');
        $format or $format = 'unknown';
        print $out
          "Loaded $numPoints points from $format-format GPS track log $from\n";
        print $out "Ignored $cutPDOP points due to GeoMaxPDOP cut\n"
          if $cutPDOP;
        print $out "Ignored $cutHDOP points due to GeoMaxHDOP cut\n"
          if $cutHDOP;
        print $out "Ignored $cutSats points due to GeoMinSats cut\n"
          if $cutSats;
        if ( $numPoints and $verbose > 1 ) {
            my @lbl = ( 'start:', 'end:  ' );
            @lbl = reverse @lbl if $fixTimes[0] > $fixTimes[-1];
            print $out "  GPS track $lbl[0] "
              . PrintFixTime( $fixTimes[0] ) . "\n";
            if ( $verbose > 3 ) {
                print $out PrintFix( $points, $_ ) foreach @fixTimes;
            }
            print $out "  GPS track $lbl[1] "
              . PrintFixTime( $fixTimes[-1] ) . "\n";
        }
    }
    if ($numPoints) {
        delete $$geotag{Times};
        $$has{alt}    = 1 if $nmea{GGA} or $nmea{PMGNTRK} or $nmea{B};
        $$has{track}  = 1 if $nmea{RMC};
        $$has{orient} = 1 if $nmea{PTNTHPR};
        return $geotag;
    }
    return "No track points found in GPS $from";
}

sub GetTime($) {
    my $timeStr = shift;
    $timeStr =~ /^(\d{4})-(\d+)-(\d+)T(\d+):(\d+):(\d+)(\.\d+)?(.*)/
      or return undef;
    my $time = Time::Local::timegm( $6, $5, $4, $3, $2 - 1, $1 );
    $time += $7 if $7;
    my $tz = $8;
    if ( $tz =~ /^([-+])(\d+):(\d{2})\b/ or $tz =~ /^([-+])(\d{2})(\d{2})?\b/ )
    {
        $tz = ( $2 * 60 + ( $3 || 0 ) ) * 60;
        $tz   *= -1 if $1 eq '+';
        $time += $tz;
    }
    return $time;
}

sub ApplySyncCorr($$) {
    my ( $et, $time ) = @_;
    my $sync = $et->GetNewValue('Geosync');
    if ( ref $sync eq 'HASH' ) {
        my $syncTimes = $$sync{Times};
        if ($syncTimes) {
            my ( $i0, $i1 ) = ( 0, scalar(@$syncTimes) - 1 );
            while ( $i1 > $i0 + 1 ) {
                my $pt = int( ( $i0 + $i1 ) / 2 );
                ( $time < $$syncTimes[$pt] ? $i1 : $i0 ) = $pt;
            }
            my ( $t0, $t1 ) = ( $$syncTimes[$i0], $$syncTimes[$i1] );
            my $syncPoints = $$sync{Points};
            my $f          = $t1 == $t0 ? 0 : ( $time - $t0 ) / ( $t1 - $t0 );
            $sync = $$syncPoints{$t1} * $f + $$syncPoints{$t0} * ( 1 - $f );
        }
        else {
            $sync = $$sync{Offset};
        }
        $_[1] += $sync;
    }
    else {
        undef $sync;
    }
    return $sync;
}

sub ScanOutwards($$$$$$) {
    my ( $key, $times, $points, $i, $dir, $maxSecs ) = @_;
    my $t0 = $$times[$i];
    for ( ; ; ) {
        $i += $dir;
        last if $i < 0 or $i >= scalar @$times;
        my $t = $$times[$i];
        last if abs( $t - $t0 ) > $maxSecs;
        my $p = $$points{$t};
        my $v = $$p{$key};
        return ( $t, $p, $v ) if defined $v;
    }
    return ();
}

sub FindFix($$$$$$$) {
    my ( $et, $key, $times, $points, $i, $dir, $maxSecs ) = @_;
    my ( $t, $p );
    if ($dir) {
        ( $t, $p ) = ScanOutwards( $key, $times, $points, $i, $dir, $maxSecs );
    }
    else {
        my ( $t1, $p1 ) =
          ScanOutwards( $key, $times, $points, $i, -1, $maxSecs );
        my ( $t2, $p2 ) =
          ScanOutwards( $key, $times, $points, $i, 1, $maxSecs );
        if ( defined $t1 ) {
            if ( defined $t2 ) {
                ( $t, $p ) =
                  ( $t - $t1 < $t2 - $t ) ? ( $t1, $p1 ) : ( $t2, $p2 );
            }
            else {
                ( $t, $p ) = ( $t1, $p1 );
            }
        }
        elsif ( defined $t2 ) {
            ( $t, $p ) = ( $t2, $p2 );
        }
    }
    if ( defined $p and $$et{OPTIONS}{Verbose} > 2 ) {
        $et->VPrint( 2, "  Taking $key from fix:\n", PrintFix( $points, $t ) );
    }
    return $p;
}

sub SetGeoValues($$;$) {
    local $_;
    my ( $et, $val, $writeGroup ) = @_;
    my $geotag  = $et->GetNewValue('Geotag');
    my $verbose = $et->Options('Verbose');
    my ( $fix, $time, $fsec, $noDate, $secondTry, $iExt, $iDir );

    $val =~ s/^\S+\s+// if $val and $geotag and not $$geotag{IsDate};

    my $geoMaxIntSecs = $et->Options('GeoMaxIntSecs');
    my $geoMaxExtSecs = $et->Options('GeoMaxExtSecs');

    defined $geoMaxIntSecs or $geoMaxIntSecs = 1800;
    defined $geoMaxExtSecs or $geoMaxExtSecs = 1800;

    my $times  = $$geotag{Times};
    my $points = $$geotag{Points};
    my $has    = $$geotag{Has};
    my $err    = '';
    while ( defined $val ) {
        unless ( defined $geotag ) {
            $err = 'No GPS track loaded';
            last;
        }
        unless ($times) {
            my @times = sort { $a <=> $b } keys %$points;
            $times = $$geotag{Times} = \@times;
        }
        unless ( $times and @$times or $$geotag{DateTimeOnly} ) {
            $err = 'GPS track is empty';
            last;
        }
        unless ( eval { require Time::Local } ) {
            $err = 'Geotag feature requires Time::Local installed';
            last;
        }
        my ( $year, $mon, $day, $hr, $min, $sec, $fs, $tz, $t0, $t1, $t2 );
        if ( $val =~
/^(\d{4}):(\d+):(\d+)\s+(\d+):(\d+):(\d+)(\.\d*)?(Z|([-+])(\d+):(\d+))?/
          )
        {
            ( $year, $mon, $day, $hr, $min, $sec, $fs, $tz, $t0, $t1, $t2 ) =
              ( $1, $2, $3, $4, $5, $6, $7, $8, $9, $10, $11 );
        }
        elsif ( $val =~ /^(\d{2}):(\d+):(\d+)(\.\d*)?(Z|([-+])(\d+):(\d+))?/ ) {
            ( $hr, $min, $sec, $fs, $tz, $t0, $t1, $t2 ) =
              ( $1, $2, $3, $4, $5, $6, $7, $8 );
            ( $year, $mon, $day ) = ( 1970, 1, 2 );
            $noDate = 1;
        }
        else {
            $err =
              'Invalid date/time (use YYYY:mm:dd HH:MM:SS[.ss][+/-HH:MM|Z])';
            last;
        }
        if ($tz) {
            $time =
              Time::Local::timegm( $sec, $min, $hr, $day, $mon - 1, $year );
            if ( $tz ne 'Z' ) {
                my $tzmin = $t1 * 60 + $t2;
                $time -= ( $t0 eq '-' ? -$tzmin : $tzmin ) * 60;
            }
        }
        else {
            $time = Image::ExifTool::TimeLocal( $sec, $min, $hr, $day, $mon - 1,
                $year );
        }
        $time += $fs if $fs and $fs ne '.';

        $time -= int( $time / $secPerDay ) * $secPerDay if $noDate;

        my $sync = ApplySyncCorr( $et, $time );

        $fsec = ( $time =~ /(\.\d+)$/ ) ? $1 : '';

        if ( $et->Options('Verbose') > 1 and not $secondTry ) {
            my $out = $et->Options('TextOut');
            my $str = '';
            $str .= sprintf( " (incl. Geosync offset of %+.3f sec)", $sync )
              if defined $sync;
            unless ($tz) {
                my $tzs = Image::ExifTool::TimeZoneString(
                    [ $sec, $min, $hr, $day, $mon - 1, $year - 1900 ], $time );
                $str .= " (local timezone is $tzs)";
            }
            print $out '  Geotime value:   ' . PrintFixTime($time) . "$str\n";
        }
        if ( not $times or not @$times ) {
            $fix = {};

        }
        elsif ( $time < $$times[0] ) {
            if ( $time < $$times[0] - $geoMaxExtSecs ) {
                $err or $err = 'Time is too far before track';
                $et->VPrint(
                    2,
                    '  Track start:     ',
                    PrintFixTime( $$times[0] ), "\n"
                ) if $verbose > 2;
                $fix = {} if $$geotag{DateTimeOnly};
            }
            else {
                $fix  = $$points{ $$times[0] };
                $iExt = 0;
                $iDir = 1;
                $et->VPrint(
                    2,
                    "  Taking pos from fix:\n",
                    PrintFix( $points, $$times[0] )
                ) if $verbose > 2;
            }
        }
        elsif ( $time > $$times[-1] ) {
            if ( $time > $$times[-1] + $geoMaxExtSecs ) {
                $err or $err = 'Time is too far beyond track';
                $et->VPrint(
                    2,
                    '  Track end:       ',
                    PrintFixTime( $$times[-1] ), "\n"
                ) if $verbose > 2;
                $fix = {} if $$geotag{DateTimeOnly};
            }
            else {
                $fix  = $$points{ $$times[-1] };
                $iExt = $#$times;
                $iDir = -1;
                $et->VPrint(
                    2,
                    "  Taking pos from fix:\n",
                    PrintFix( $points, $$times[-1] )
                ) if $verbose > 2;
            }
        }
        else {
            my ( $i0, $i1 ) = ( 0, scalar(@$times) - 1 );
            while ( $i1 > $i0 + 1 ) {
                my $pt = int( ( $i0 + $i1 ) / 2 );
                ( $time < $$times[$pt] ? $i1 : $i0 ) = $pt;
            }
            my $t0 = $$times[$i0];
            my $t1 = $$times[$i1];
            my $p1 = $$points{$t1};
            my $maxSecs =
              ( $$p1{first} and $geoMaxIntSecs )
              ? $geoMaxExtSecs
              : $geoMaxIntSecs;
            my $tn;
            if ( $time - $t0 < $t1 - $time ) {
                $tn   = $t0;
                $iExt = $i0;
            }
            else {
                $tn   = $t1;
                $iExt = $i1;
            }
            if (
                $t1 - $t0 > $maxSecs
                and (  not $$p1{span}
                    or not $$points{$t0}{span}
                    or $$p1{span} != $$points{$t0}{span} )
              )
            {
                if ( abs( $time - $tn ) > $geoMaxExtSecs ) {
                    $err or $err = 'Time is too far from nearest GPS fix';
                    $et->VPrint(
                        2, '  Nearest fix:     ',
                        PrintFixTime($tn), ' (', int( abs $time - $tn ),
                        " sec away)\n"
                    ) if $verbose > 2;
                    $fix = {} if $$geotag{DateTimeOnly};
                }
                else {
                    $fix = $$points{$tn};
                    $et->VPrint(
                        2,
                        "  Taking pos from fix:\n",
                        PrintFix( $points, $tn )
                    ) if $verbose > 2;
                }
            }
            else {
                my $f0 = $t1 == $t0 ? 0 : ( $time - $t0 ) / ( $t1 - $t0 );
                my $p0 = $$points{$t0};
                $et->VPrint(
                    2,
                    "  Interpolating between fixes (f=$f0):\n",
                    PrintFix( $points, $t0, $t1 )
                ) if $verbose > 2;
                $fix = {};
                $$fix{$_} = $$points{$tn}{$_} foreach values %userTag;
                my ( $category, $key );
              Category:
                foreach $category (qw{pos track alt orient atemp err dop}) {
                    next unless $$has{$category};
                    my ( $f, $p0b, $p1b, $f0b );
                    foreach $key ( @{ $fixInfoKeys{$category} } ) {
                        my $v0 = $$p0{$key};
                        my $v1 = $$p1{$key};
                        if ( defined $v0 and defined $v1 ) {
                            $f = $f0;
                        }
                        elsif ( defined $f0b ) {
                            $v0 = $$p0b{$key};
                            $v1 = $$p1b{$key};
                            next unless defined $v0 and defined $v1;
                            $f = $f0b;
                        }
                        else {
                            next if $sepTags{$key};

                            my ( $t0b, $t1b );
                            if ( defined $v0 ) {
                                $t0b = $t0;
                                $p0b = $p0;
                            }
                            else {
                                ( $t0b, $p0b, $v0 ) =
                                  ScanOutwards( $key, $times, $points, $i0, -1,
                                    $maxSecs );
                                next Category unless defined $t0b;
                            }
                            if ( defined $v1 ) {
                                $t1b = $t1;
                                $p1b = $p1;
                            }
                            else {
                                ( $t1b, $p1b, $v1 ) =
                                  ScanOutwards( $key, $times, $points, $i1, 1,
                                    $maxSecs );
                                next Category unless defined $t1b;
                            }
                            $f = $f0b =
                              $t1b == $t0b
                              ? 0
                              : ( $time - $t0b ) / ( $t1b - $t0b );
                            $et->VPrint(
                                2,
"  Interpolating $category between fixes (f=$f):\n",
                                PrintFix( $points, $t0b, $t1b )
                            ) if $verbose > 2;
                        }
                        if ( $cyclical{$key} and abs( $v1 - $v0 ) > 180 ) {
                            $v0 < $v1 ? $v0 += 360 : $v1 += 360;
                            $$fix{$key} = $v1 * $f + $v0 * ( 1 - $f );
                            my $max = $cyc180{$key} ? 180 : 360;
                            $$fix{$key} -= 360 if $$fix{$key} >= $max;
                        }
                        else {
                            $$fix{$key} = $v1 * $f + $v0 * ( 1 - $f );
                        }
                    }
                }
            }
        }
        if ($fix) {
            $err = '';
        }
        elsif ( $$geotag{NoDate} and not $noDate and $val =~ s/^\S+\s+// ) {
            $secondTry = 1;
            next;
        }
        last;
    }
    if ($fix) {
        my ( $gpsDate, $gpsAlt, $gpsAltRef );
        my @t       = gmtime( int $time );
        my $gpsTime = sprintf( '%.2d:%.2d:%.2d', $t[2], $t[1], $t[0] ) . $fsec;
        $gpsDate = sprintf( '%.2d:%.2d:%.2d', $t[5] + 1900, $t[4] + 1, $t[3] )
          unless $noDate;
        my $alt = $$fix{alt};
        if ( not defined $alt and $$has{alt} and defined $iExt ) {
            my $tFix = FindFix( $et, 'alt', $times, $points, $iExt, $iDir,
                $geoMaxExtSecs );
            $alt = $$tFix{alt} if $tFix;
        }
        my ( $xmp, $exif, $qt, @r );
        my %opts = ( Type => 'ValueConv' );
        if ($writeGroup) {
            $opts{Group} = $writeGroup;
            $xmp         = ( $writeGroup =~ /xmp/i );
            $exif        = ( $writeGroup =~ /^(exif|gps)$/i );
            $qt = $writeGroup =~ /^(quicktime|keys|itemlist|userdata)$/i;
        }
        my $coords = "$$fix{lat} $$fix{lon}";
        if ( defined $alt ) {
            $gpsAlt    = abs $alt;
            $gpsAltRef = ( $alt < 0 ? 1 : 0 );
            $coords .= " $alt";
        }
        @r = $et->SetNewValue( GPSCoordinates => $coords, %opts );
        my $nvHash;
        my $geoloc = $et->GetNewValue( 'Geolocate', \$nvHash );
        if ( $geoloc and $geoloc =~ /\bgeotag\b/i ) {
            my $tag = ( $$nvHash{WantGroup} ? "$$nvHash{WantGroup}:" : '' )
              . 'Geolocate';
            my $parms = join ',', grep m(/), split /\s*,\s*/, $geoloc;
            $parms and $parms = ",$parms,both";
            $et->SetNewValue( $tag => "$$fix{lat},$$fix{lon}$parms" );
        }
        return $err if $qt;

        @r = $et->SetNewValue( GPSLatitude    => $$fix{lat}, %opts );
        @r = $et->SetNewValue( GPSLongitude   => $$fix{lon}, %opts );
        @r = $et->SetNewValue( GPSAltitude    => $gpsAlt,    %opts );
        @r = $et->SetNewValue( GPSAltitudeRef => $gpsAltRef, %opts );
        if ( $$has{track} or $$has{speed} ) {
            my $type = $$has{track} ? 'track' : 'speed';
            my $tFix = $fix;
            if ( not defined $$fix{$type} and defined $iExt ) {
                my $p = FindFix( $et, $type, $times, $points, $iExt, $iDir,
                    $geoMaxExtSecs );
                $tFix = $p if $p;
            }
            @r = $et->SetNewValue( GPSTrack => $$tFix{track}, %opts );
            @r = $et->SetNewValue(
                GPSTrackRef => ( defined $$tFix{track} ? 'T' : undef ),
                %opts
            );
            my ( $spd, $ref );
            if ( defined( $spd = $$tFix{speed} ) ) {
                $ref = $$et{OPTIONS}{GeoSpeedRef};
                if ( $ref and defined $speedConv{$ref} ) {
                    $ref = $speedConv{$ref} if $speedConv{ $speedConv{$ref} };
                    $spd *= $speedConv{$ref};
                }
                else {
                    $ref = 'N';
                }
            }
            @r = $et->SetNewValue( GPSSpeed    => $spd, %opts );
            @r = $et->SetNewValue( GPSSpeedRef => $ref, %opts );
        }
        if ( $$has{orient} ) {
            my $tFix = $fix;
            if ( not defined $$fix{dir} and defined $iExt ) {
                my $p = FindFix( $et, 'dir', $times, $points, $iExt, $iDir,
                    $geoMaxExtSecs );
                $tFix = $p if $p;
            }
            @r = $et->SetNewValue( GPSImgDirection => $$tFix{dir}, %opts );
            @r = $et->SetNewValue(
                GPSImgDirectionRef => ( defined $$tFix{dir} ? 'T' : undef ),
                %opts
            );
            @r =
              $et->SetNewValue( CameraElevationAngle => $$tFix{pitch}, %opts );
            @r = $et->SetNewValue( GPSPitch => $$tFix{pitch}, %opts );
            @r = $et->SetNewValue( GPSRoll  => $$tFix{roll},  %opts );
        }
        if ( $$has{atemp} ) {
            my $tFix = $fix;
            if ( not defined $$fix{atemp} and defined $iExt ) {
                my $p = FindFix( $et, 'atemp', $times, $points, $iExt, $iDir,
                    $geoMaxExtSecs );
                $tFix = $p if $p;
            }
            @r = $et->SetNewValue( AmbientTemperature => $$tFix{atemp}, %opts );
        }
        if ( $$has{err} ) {
            @r = $et->SetNewValue( GPSHPositioningError => $$fix{err}, %opts );
        }
        if ( $$has{dop} ) {
            my ( $dop, $mm );
            if ( defined $$fix{pdop} ) {
                $dop = $$fix{pdop};
                $mm  = 3;
            }
            elsif ( defined $$fix{hdop} ) {
                if ( defined $$fix{vdop} ) {
                    $dop = sqrt(
                        $$fix{hdop} * $$fix{hdop} + $$fix{vdop} * $$fix{vdop} );
                    $mm = 3;
                }
                else {
                    $dop = $$fix{hdop};
                    $mm  = 2;
                }
            }
            if ( defined $dop ) {
                $et->SetNewValue( GPSMeasureMode => $mm,  %opts );
                $et->SetNewValue( GPSDOP         => $dop, %opts );
                my $hposErr = $$et{OPTIONS}{GeoHPosErr};
                if ($hposErr) {
                    $hposErr =~ s/gpsdop/GPSDOP/i;
                    my $GPSDOP = $dop;
                    local $SIG{'__WARN__'} = \&Image::ExifTool::SetWarning;
                    undef $Image::ExifTool::evalWarning;
                    $hposErr = eval $hposErr;
                    my $err = Image::ExifTool::GetWarning() || $@;
                    if ($err) {
                        $err = Image::ExifTool::CleanWarning($err);
                        $et->Warn(
                            "Error calculating GPSHPositioningError: $err", 1 );
                    }
                    else {
                        $et->SetNewValue(
                            GPSHPositioningError => $hposErr,
                            %opts
                        );
                    }
                }
            }
        }
        unless ($xmp) {
            my ( $latRef, $lonRef );
            $latRef = ( $$fix{lat} > 0 ? 'N' : 'S' ) if defined $$fix{lat};
            $lonRef = ( $$fix{lon} > 0 ? 'E' : 'W' ) if defined $$fix{lon};
            @r      = $et->SetNewValue( GPSLatitudeRef  => $latRef,  %opts );
            @r      = $et->SetNewValue( GPSLongitudeRef => $lonRef,  %opts );
            @r      = $et->SetNewValue( GPSDateStamp    => $gpsDate, %opts );
            @r      = $et->SetNewValue( GPSTimeStamp    => $gpsTime, %opts );
            $opts{EditOnly} = 1;
            $opts{Group}    = 'XMP';
        }
        unless ($exif) {
            @r = $et->SetNewValue( GPSDateTime => "$gpsDate $gpsTime", %opts );
        }
        foreach ( sort values %userTag ) {
            @r = $et->SetNewValue( substr( $_, 1 ) => $$fix{$_} )
              if defined $$fix{$_};
        }
    }
    else {
        my %opts = ( IgnorePermanent => 1 );
        $opts{Replace} = 2 if defined $val;

        InitUserTags($et);
        foreach ( values %userTag ) {
            my @r = $et->SetNewValue( substr( $_, 1 ), undef, %opts );
        }
        $opts{Group} = $writeGroup if $writeGroup;
        foreach (
            qw(GPSLatitude GPSLatitudeRef GPSLongitude GPSLongitudeRef
            GPSAltitude GPSAltitudeRef GPSDateStamp GPSTimeStamp GPSDateTime
            GPSTrack GPSTrackRef GPSSpeed GPSSpeedRef GPSImgDirection
            GPSImgDirectionRef GPSPitch GPSRoll CameraElevationAngle
            AmbientTemperature GPSHPositioningError GPSCoordinates
            GPSMeasureMode GPSDOP)
          )
        {
            my @r = $et->SetNewValue( $_, undef, %opts );
        }
    }
    return $err;
}

sub ConvertGeosync($$) {
    my ( $et, $val ) = @_;
    my $sync = $et->GetNewValue('Geosync') || {};
    my ( $syncFile, $gpsTime, $imgTime );

    if ( $val =~ /(.*?)\@(.*)/ ) {
        $gpsTime = $1;
        ( -f $2 ? $syncFile : $imgTime ) = $2;
    }
    elsif ( $val !~ /^\d/ or $val !~ /:/ ) {
        $syncFile = $val if -f $val;
    }
    if ( $gpsTime or defined $syncFile ) {
        if ( defined $syncFile ) {
            my @timeTags =
              qw(SubSecDateTimeOriginal SubSecCreateDate SubSecModifyDate
              DateTimeOriginal CreateDate ModifyDate FileModifyDate);
            my $info = ImageInfo( $syncFile, { PrintConv => 0 },
                @timeTags, 'GPSDateTime', 'GPSTimeStamp' );
            $$info{Error} and warn("$$info{Err}\n"), return undef;
            unless ($gpsTime) {
                $gpsTime = $$info{GPSDateTime} || $$info{GPSTimeStamp};
                $gpsTime .= 'Z' if $gpsTime and not $$info{GPSDateTime};
            }
            $gpsTime or warn("No GPSTimeStamp in '$syncFile\n"), return undef;
            my $tag;
            foreach $tag (@timeTags) {
                if ( $$info{$tag} ) {
                    $imgTime = $$info{$tag};
                    $et->VPrint( 2,
                        "Geosyncing with $tag from '${syncFile}'\n" );
                    last;
                }
            }
            $imgTime
              or warn("No image timestamp in '${syncFile}'\n"), return undef;
        }
        my ( $imgDateTime, $gpsDateTime, $noDate );
        if ( $imgTime =~ /^(\d+:\d+:\d+)\s+\d+/ ) {
            $imgDateTime = $imgTime;
            my $date = $1;
            if ( $gpsTime =~ /^\d+:\d+:\d+\s+\d+/ ) {
                $gpsDateTime = $gpsTime;
            }
            else {
                $gpsDateTime = "$date $gpsTime";
            }
        }
        elsif ( $gpsTime =~ /^(\d+:\d+:\d+)\s+\d+/ ) {
            $imgDateTime = "$1 $imgTime";
            $gpsDateTime = $gpsTime;
        }
        else {
            my @tm = localtime;
            my $date =
              sprintf( '%.4d:%.2d:%.2d', $tm[5] + 1900, $tm[4] + 1, $tm[3] );
            $gpsDateTime = "$date $gpsTime";
            $imgDateTime = "$date $imgTime";
            $noDate      = 1;
        }
        my $imgSecs = Image::ExifTool::GetUnixTime( $imgDateTime, 1 );
        defined $imgSecs
          or warn("Invalid image time '${imgTime}'\n"), return undef;
        my $gpsSecs = Image::ExifTool::GetUnixTime( $gpsDateTime, 1 );
        defined $gpsSecs
          or warn("Invalid GPS time '${gpsTime}'\n"), return undef;
        $gpsSecs += $1 if $gpsTime =~ /(\.\d+)/;
        $imgSecs += $1 if $imgTime =~ /(\.\d+)/;
        if ( $gpsDateTime ne $gpsTime or $imgDateTime ne $imgTime ) {
            my $diff = ( $imgSecs - $gpsSecs ) % ( 24 * 3600 );
            $diff -= 24 * 3600 if $diff > 12 * 3600;
            $diff += 24 * 3600 if $diff < -12 * 3600;
            if ( $gpsDateTime ne $gpsTime ) {
                $gpsSecs = $imgSecs - $diff;
            }
            else {
                $imgSecs = $gpsSecs + $diff;
            }
        }
        $$sync{Offset} = $gpsSecs - $imgSecs;
        unless ($noDate) {
            $$sync{Points} or $$sync{Points} = {};
            $$sync{Points}{$imgSecs} = $$sync{Offset};
            if ( $et->Options('Verbose') > 1 ) {
                $et->VPrint(
                    1,                      "Added Geosync point:\n",
                    '  GPS time stamp:  ',  PrintFixTime($gpsSecs),
                    "\n",                   '  Image date/time: ',
                    PrintFixTime($imgSecs), "\n"
                );
            }
            my @times = keys %{ $$sync{Points} };
            if ( @times > 1 ) {
                @times = sort { $a <=> $b } @times;
                $$sync{Times} = \@times;
            }
        }
    }
    else {
        my @vals = $val =~ /(?=\d|\.\d)\d*(?:\.\d*)?/g;
        @vals
          or warn("Invalid value (please refer to geotag documentation)\n"),
          return undef;
        my $secs = 0;
        my $mult;
        foreach $mult ( 1, 60, 3600, $secPerDay ) {
            $secs += $mult * pop(@vals);
            last unless @vals;
        }
        $$sync{Offset} = $val =~ /^\s*-/ ? -$secs : $secs;
    }
    return $sync;
}

sub PrintFixTime($) {
    my $time = shift;
    return Image::ExifTool::ConvertUnixTime( $time, undef, 3 ) . ' UTC';
}

sub PrintFix($@) {
    local $_;
    my $points = shift;
    my $str    = '';
    while (@_) {
        my $time = shift;
        $str .= '    ' . PrintFixTime($time) . ' -';
        my $fix = $$points{$time};
        if ($fix) {
            foreach ( sort keys %$fix ) {
                $str .= " $_=$$fix{$_}"
                  unless $_ eq 'time'
                  or not defined $$fix{$_};
            }
        }
        $str .= "\n";
    }
    return $str;
}

sub InitUserTags($) {
    my $et = shift;
    %userTag = ();
    if ( $$et{OPTIONS}{GeoUserTag} ) {
        foreach ( split /\s*,\s*/, $$et{OPTIONS}{GeoUserTag} ) {
            next unless /^(.+)=(.+)$/;
            $xmlTag{ lc $2 }
              and $et->Warn(
                "User-defined GPX tag '${2}' conflicts with existing tag"),
              next;
            $userTag{ lc $2 } = "_$1";
        }
    }
}

1;

__END__

