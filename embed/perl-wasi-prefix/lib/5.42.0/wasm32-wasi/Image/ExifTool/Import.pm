package Image::ExifTool::Import;

use strict;
require Exporter;

use vars qw($VERSION @ISA @EXPORT_OK);

$VERSION   = '1.14';
@ISA       = qw(Exporter);
@EXPORT_OK = qw(ReadCSV ReadJSON);

sub ReadJSONObject($;$);

my %unescapeJSON =
  ( 't' => "\t", 'n' => "\n", 'r' => "\r", 'b' => "\b", 'f' => "\f" );
my $charset;

sub ReadCSV($$;$$) {
    local ( $_, $/ );
    my ( $file, $database, $missingValue, $delim ) = @_;
    my ( $buff, @tags, $found, $err, $raf, $openedFile );

    if ( UNIVERSAL::isa( $file, 'File::RandomAccess' ) ) {
        $raf  = $file;
        $file = 'CSV file';
    }
    elsif ( ref $file eq 'GLOB' ) {
        $raf  = File::RandomAccess->new($file);
        $file = 'CSV file';
    }
    else {
        open CSVFILE, $file or return "Error opening CSV file '${file}'";
        binmode CSVFILE;
        $openedFile = 1;
        $raf        = File::RandomAccess->new( \*CSVFILE );
    }
    $delim = ',' unless defined $delim;
    while ( $raf->Read( $buff, 65536 ) ) {
        $buff =~ /(\x0d\x0a|\x0d|\x0a)/ and $/ = $1, last;
    }
    $raf->Seek( 0, 0 );
    while ( $raf->ReadLine($buff) ) {
        my ( @vals, $v, $i, %fileInfo );
        my @toks = split /\Q$delim/, $buff;
        while (@toks) {
            ( $v = shift @toks ) =~ s/^ +//;
            if ( $v =~ s/^"// ) {
                while ( $v !~ /("+)\s*$/ or not length($1) & 1 ) {
                    if (@toks) {
                        $v .= $delim . shift @toks;
                    }
                    else {
                        $raf->ReadLine($buff) or last;
                        @toks = split /\Q$delim/, $buff;
                        last unless @toks;
                        $v .= shift @toks;
                    }
                }
                $v =~ s/"\s*$//;
                $v =~ s/""/"/g;
            }
            else {
                $v =~ s/[ \n\r]+$//;
            }
            push @vals, $v;
        }
        if (@tags) {
            $fileInfo{_ordered_keys_} = [];
            for ( $i = 0 ; $i < @vals and $i < @tags ; ++$i ) {
                next
                  unless length $vals[$i]
                  or defined $missingValue and $missingValue eq '';
                $fileInfo{ $tags[$i] } =
                  ( defined $missingValue and $vals[$i] eq $missingValue )
                  ? undef
                  : $vals[$i];
                push @{ $fileInfo{_ordered_keys_} }, $tags[$i];
            }
            if ( $fileInfo{SourceFile} ) {
                $$database{ $fileInfo{SourceFile} } = \%fileInfo;
                $found = 1;
            }
        }
        else {
            foreach (@vals) {
                last unless length $_;
                @tags or s/^\xef\xbb\xbf//;
                /^([-_0-9A-Z]+:)*[-_0-9A-Z]+#?$/i
                  or $err = "Invalid tag name '${_}'", last;
                push( @tags, $_ );
            }
            last if $err;
            @tags or $err = 'No tags found', last;
            $tags[0] = 'SourceFile' if lc $tags[0] eq 'sourcefile';
        }
    }
    close CSVFILE if $openedFile;
    undef $raf;
    $err = 'No SourceFile column' unless $found or $err;
    return $err ? "$err in $file" : undef;
}

sub ToUTF8($) {
    require Image::ExifTool::Charset;
    return Image::ExifTool::Charset::Recompose( undef, [ $_[0] ], $charset );
}

sub ReadJSONObject($;$) {
    my ( $raf, $buffPt ) = @_;
    my ( $pos, $readMore, $rtnVal, $tok, $key, $didBOM );
    if ($buffPt) {
        $pos = pos $$buffPt;
        $pos = pos($$buffPt) = 0 unless defined $pos;
    }
    else {
        my $buff = '';
        $buffPt = \$buff;
        $pos    = 0;
    }
  Tok: for ( ; ; ) {
        last unless defined $pos;
        if ( $pos >= length $$buffPt or $readMore ) {
            last unless defined $raf;
            my $offset = length($$buffPt) - $pos;
            if ($offset) {
                my $buff;
                $raf->Read( $buff, 65536 ) or $$buffPt = '', last;
                $$buffPt = substr( $$buffPt, $pos ) . $buff;
            }
            else {
                $raf->Read( $$buffPt, 65536 ) or $$buffPt = '', last;
            }
            unless ($didBOM) {
                $$buffPt =~ s/^\xef\xbb\xbf//;
                $didBOM = 1;
            }
            $pos      = pos($$buffPt) = 0;
            $readMore = 0;
        }
        unless ($tok) {
            $$buffPt =~ /(\S)/g or $pos = length($$buffPt), next;
            $tok                        = $1;
            $pos                        = pos $$buffPt;
        }
        if ( $tok eq '{' ) {
            $rtnVal = { _ordered_keys_ => [] } unless defined $rtnVal;
            for ( ; ; ) {
                unless ( defined $key ) {
                    $key = ReadJSONObject( $raf, $buffPt );
                    $pos = pos $$buffPt;
                }
                if ( defined $key ) {
                    $$buffPt =~ /(\S)/g or $readMore = 1, next Tok;
                    $1 eq ':' or return undef;
                    my $val = ReadJSONObject( $raf, $buffPt );
                    $pos = pos $$buffPt;
                    return undef unless defined $val;
                    $$rtnVal{$key} = $val;
                    push @{ $$rtnVal{_ordered_keys_} }, $key;
                    undef $key;
                }
                $$buffPt =~ /(\S)/g or $readMore = 1, next Tok;
                last if $1 eq '}';
                $1 eq ',' or return undef;
            }
        }
        elsif ( $tok eq '[' ) {
            $rtnVal = [] unless defined $rtnVal;
            for ( ; ; ) {
                my $item = ReadJSONObject( $raf, $buffPt );
                $pos = pos $$buffPt;
                push @$rtnVal, $item if defined $item;
                $$buffPt =~ /(\S)/g or $readMore = 1, next Tok;
                last if $1 eq ']';
                $1 eq ',' or return undef;
            }
        }
        elsif ( $tok eq '"' ) {
            for ( ; ; ) {
                $$buffPt =~ /(\\*)"/g or $readMore = 1, next Tok;
                last unless length($1) & 1;
            }
            $rtnVal = substr( $$buffPt, $pos, pos($$buffPt) - $pos - 1 );
            $rtnVal =~ s/\\u([0-9a-f]{4})/ToUTF8(hex $1)/ige;
            $rtnVal =~ s/\\(.)/$unescapeJSON{$1}||$1/sge;
            if ( $rtnVal =~ /^base64:[A-Za-z0-9+\/]*={0,2}$/
                and length($rtnVal) % 4 == 3 )
            {
                require Image::ExifTool::XMP;
                $rtnVal =
                  ${ Image::ExifTool::XMP::DecodeBase64( substr( $rtnVal, 7 ) )
                  };
            }
        }
        elsif ( $tok eq ']' or $tok eq '}' or $tok eq ',' ) {
            pos($$buffPt) = pos($$buffPt) - 1;
        }
        else {
            $$buffPt =~ /([\s:,\}\]])/g or $readMore = 1, next;
            pos($$buffPt) = pos($$buffPt) - 1;
            $rtnVal = $tok . substr( $$buffPt, $pos, pos($$buffPt) - $pos );
        }
        last;
    }
    return $rtnVal;
}

sub ReadJSON($$;$$) {
    local $_;
    my ( $file, $database, $missingValue, $chset ) = @_;
    my ( $raf, $openedFile );

    $charset = $chset || 'UTF8';
    if ( UNIVERSAL::isa( $file, 'File::RandomAccess' ) ) {
        $raf  = $file;
        $file = 'JSON file';
    }
    elsif ( ref $file eq 'GLOB' ) {
        $raf  = File::RandomAccess->new($file);
        $file = 'JSON file';
    }
    elsif ( ref $file eq 'SCALAR' ) {
        $raf  = File::RandomAccess->new($file);
        $file = 'in memory';
    }
    else {
        open JSONFILE, $file or return "Error opening JSON file '${file}'";
        binmode JSONFILE;
        $openedFile = 1;
        $raf        = File::RandomAccess->new( \*JSONFILE );
    }
    my $obj = ReadJSONObject($raf);
    close JSONFILE if $openedFile;
    unless ( ref $obj eq 'ARRAY' ) {
        ref $obj eq 'HASH' or return "Format error in JSON file '${file}'";
        $obj = [$obj];
    }
    my ( $info, $found );
    foreach $info (@$obj) {
        next unless ref $info eq 'HASH';
        unless ( defined $$info{SourceFile} ) {
            my ($key) = grep /^SourceFile$/i, keys %$info;
            if ($key) {
                $$info{SourceFile} = $$info{$key};
                delete $$info{$key};
            }
            else {
                $$info{SourceFile} = '*';
            }
        }
        if ( defined $missingValue ) {
            $$info{$_} eq $missingValue and $$info{$_} = undef
              foreach keys %$info;
        }
        $$database{ $$info{SourceFile} } = $info;
        $found = 1;
    }
    return $found ? undef : "No valid JSON objects in '${file}'";
}

1;

__END__

