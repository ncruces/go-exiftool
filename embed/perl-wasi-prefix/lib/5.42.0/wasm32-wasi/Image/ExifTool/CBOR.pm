
package Image::ExifTool::CBOR;
use strict;
use vars            qw($VERSION);
use Image::ExifTool qw(:DataAccess :Utils);
use Image::ExifTool::JSON;

$VERSION = '1.04';

sub ProcessCBOR($$$);
sub ReadCBORValue($$$$);

my %cborType6 = (
    0  => 'date/time string',
    1  => 'epoch-based date/time',
    2  => 'positive bignum',
    3  => 'negative bignum',
    4  => 'decimal fraction',
    5  => 'bigfloat',
    16 => 'COSE Encrypt0',
    17 => 'COSE Mac0',
    18 => 'COSE Sign1',
    19 => 'COSE Countersignature',
    21 => 'expected base64url encoding',
    22 => 'expected base64 encoding',
    23 => 'expected base16 encoding',
    24 => 'encoded CBOR data',
    25 => 'string number',
    26 => 'serialized Perl',
    27 => 'serialized code',
    28 => 'shared value',
    29 => 'shared value number',
    30 => 'rational',
    31 => 'missing array value',
    32 => 'URI',
    33 => 'base64url',
    34 => 'base64',
    35 => 'regular expression',
    36 => 'MIME message',
    55799 => 'CBOR magic number',
);

my %cborType7 = (
    20 => 'False',
    21 => 'True',
    22 => 'null',
    23 => 'undef',
);

%Image::ExifTool::CBOR::Main = (
    GROUPS       => { 0      => 'JUMBF', 1 => 'CBOR', 2 => 'Other' },
    VARS         => { ID_FMT => 'none' },
    PROCESS_PROC => \&ProcessCBOR,
    NOTES        => q{
        The tags below are extracted from CBOR (Concise Binary Object
        Representation) metadata.  The C2PA specification uses this format for some
        metadata.  As well as these tags, ExifTool will read any existing tags.
    },
    'dc:title'  => 'Title',
    'dc:format' => 'Format',
    authorName       => { Name => 'AuthorName', Groups => { 2 => 'Author' } },
    authorIdentifier =>
      { Name => 'AuthorIdentifier', Groups => { 2 => 'Author' } },
    documentID    => {},
    instanceID    => {},
    thumbnailHash => { List => 1 },
    thumbnailUrl  => { Name => 'ThumbnailURL' },
    relationship  => {}
);

sub ReadCBORValue($$$$) {
    my ( $et, $dataPt, $pos, $end ) = @_;
    return ( undef, 'Truncated CBOR data', $pos ) if $pos >= $end;
    my $verbose   = $$et{OPTIONS}{Verbose};
    my $indent    = $$et{INDENT};
    my $dumpStart = $pos;
    my $fmt       = Get8u( $dataPt, $pos++ );
    my $dat       = $fmt & 0x1f;
    my ( $num, $val, $err, $size );
    $fmt >>= 5;

    if ( $dat < 24 ) {
        $num = $dat;
    }
    elsif ( $dat == 31 ) {
        $num = -1;
        $et->VPrint( 1, "$$et{INDENT} (indefinite count):\n" );
    }
    else {
        my $format =
          { 24 => 'int8u', 25 => 'int16u', 26 => 'int32u', 27 => 'int64u' }
          ->{$dat};
        return ( undef, "Invalid CBOR integer type $dat", $pos ) unless $format;
        $size = Image::ExifTool::FormatSize($format);
        return ( undef, 'Truncated CBOR integer value', $pos )
          if $pos + $size > $end;
        $num = ReadValue( $dataPt, $pos, $format, 1, $size );
        $pos += $size;
    }
    my $pre = '';
    if ( defined $$et{cbor_pre} and $fmt != 6 ) {
        $pre = $$et{cbor_pre};
        delete $$et{cbor_pre};
    }
    if ( $fmt == 0 ) {
        $val = $num;
        $et->VPrint( 1, "$$et{INDENT} ${pre}int+: $val\n" );
    }
    elsif ( $fmt == 1 ) {
        $val = -1 * $num;
        $et->VPrint( 1, "$$et{INDENT} ${pre}int-: $val\n" );
    }
    elsif ( $fmt == 2 or $fmt == 3 ) {
        return ( undef, 'Truncated CBOR string value', $pos )
          if $pos + $num > $end;
        if ( $num < 0 ) {
            my $string = '';
            $$et{INDENT} .= '  ';
            for ( ; ; ) {
                ( $val, $err, $pos ) =
                  ReadCBORValue( $et, $dataPt, $pos, $end );
                return ( undef, $err, $pos ) if $err;
                last                         if not defined $val;

                $string .= $val;
            }
            $$et{INDENT} = $indent;
            return ( $string, undef, $pos );
        }
        else {
            $val = substr( $$dataPt, $pos, $num );
        }
        $pos += $num;
        if ( $fmt == 2 ) {
            $et->VPrint( 1,
                    "$$et{INDENT} ${pre}byte: <binary data "
                  . length($val)
                  . " bytes>\n" );
            my $dat = $val;
            $val = \$dat;
        }
        else {
            $val = $et->Decode( $val, 'UTF8' );
            $et->VPrint( 1, "$$et{INDENT} ${pre}text: '${val}'\n" );
        }
    }
    elsif ( $fmt == 4 or $fmt == 5 ) {
        if ( $fmt == 4 ) {
            $et->VPrint( 1, "$$et{INDENT} ${pre}list: <$num elements>\n" );
        }
        else {
            $et->VPrint( 1, "$$et{INDENT} ${pre}hash: <$num pairs>\n" );
            $num *= 2;
        }
        $$et{INDENT} .= '  ';
        my $i = 0;
        my @list;
        Image::ExifTool::HexDump(
            $dataPt, $pos - $dumpStart,
            Start   => $dumpStart,
            DataPos => $$et{cbor_datapos},
            Prefix  => $$et{INDENT},
            Out     => $et->Options('TextOut'),
        ) if $verbose > 2;
        while ($num) {
            $$et{cbor_pre} = "$i) ";
            if ( $fmt == 4 ) {
                ++$i;
            }
            elsif ( $num & 0x01 ) {
                $$et{cbor_pre} = ' ' x length( $$et{cbor_pre} );
                ++$i;
            }
            ( $val, $err, $pos ) = ReadCBORValue( $et, $dataPt, $pos, $end );
            return ( undef, $err, $pos ) if $err;
            if ( not defined $val ) {
                return ( undef, 'Unexpected list terminator', $pos )
                  unless $num < 0;
                last;
            }
            push @list, $val;
            --$num;
        }
        $dumpStart = $pos;
        $$et{INDENT} = $indent;
        if ( $fmt == 5 ) {
            my ( $i, @keys );
            my %hash = ( _ordered_keys_ => \@keys );
            for ( $i = 0 ; $i < @list - 1 ; $i += 2 ) {
                $hash{ $list[$i] } = $list[ $i + 1 ];
                push @keys, $list[$i];
            }
            $val = \%hash;
        }
        else {
            $val = \@list;
        }
    }
    elsif ( $fmt == 6 ) {
        if ($verbose) {
            my $str = "$num (" . ( $cborType6{$num} || 'unknown' ) . ')';
            my $spc = $$et{cbor_pre} ? ( ' ' x length $$et{cbor_pre} ) : '';
            $et->VPrint( 1, "$$et{INDENT} $spc<CBOR optional type $str>\n" );
            Image::ExifTool::HexDump(
                $dataPt, $pos - $dumpStart,
                Start   => $dumpStart,
                DataPos => $$et{cbor_datapos},
                Prefix  => $$et{INDENT} . '  ',
                Out     => $et->Options('TextOut'),
            ) if $verbose > 2;
        }
        ( $val, $err, $pos ) = ReadCBORValue( $et, $dataPt, $pos, $end );
        return ( undef, $err, $pos ) if $err;
        $dumpStart = $pos;
        if ( $num == 0 and not ref $val ) {
            require Image::ExifTool::XMP;
            $val = Image::ExifTool::XMP::ConvertXMPDate($val);
        }
        elsif ( $num == 1 and not ref $val ) {
            if ( Image::ExifTool::IsFloat($val) ) {
                my $dec = ( $val == int($val) ) ? undef : 6;
                $val = Image::ExifTool::ConvertUnixTime( $val, 1, $dec );
            }
        }
        elsif ( ( $num == 2 or $num == 3 ) and ref($val) eq 'SCALAR' ) {
            my $big = 0;
            $big = 256 * $big + Get8u( $val, $_ )
              foreach 0 .. ( length($$val) - 1 );
            $val = $num == 2 ? $big : -$big;
        }
        elsif ( ( $num == 4 or $num == 5 )
            and ref($val) eq 'ARRAY'
            and @$val == 2
            and Image::ExifTool::IsInt( $$val[0] )
            and Image::ExifTool::IsInt( $$val[1] ) )
        {
            $val = $$val[1] * ( $num == 4 ? 10 : 2 )**$$val[0];
        }
    }
    elsif ( $fmt == 7 ) {
        if ( $dat == 31 ) {
            undef $val;
        }
        elsif ( $dat < 24 ) {
            $val = $cborType7{$num};
            $val = "Unknown ($val)" unless defined $val;
        }
        elsif ( $dat == 25 ) {
            my $exp  = ( $num >> 10 ) & 0x1f;
            my $mant = $num & 0x3ff;
            if ( $exp == 0 ) {
                $val = $mant**-24;
                $val *= -1 if $num & 0x8000;
            }
            elsif ( exp != 31 ) {
                $val = ( $mant + 1024 )**( $exp - 25 );
                $val *= -1 if $num & 0x8000;
            }
            else {
                $val = $mant == 0 ? '<inf>' : '<nan>';
            }
        }
        elsif ( $dat == 26 ) {
            $val = GetFloat( $dataPt, $pos - $size );
        }
        elsif ( $dat == 27 ) {
            $val = GetDouble( $dataPt, $pos - $size );
        }
        else {
            return ( undef, "Invalid CBOR type 7 variant $num", $pos );
        }
        $et->VPrint( 1,
                "$$et{INDENT} ${pre}typ7: "
              . ( defined $val ? $val : '<break>' )
              . "\n" );
    }
    else {
        return ( undef, "Unknown CBOR format $fmt", $pos );
    }
    Image::ExifTool::HexDump(
        $dataPt, $pos - $dumpStart,
        Start   => $dumpStart,
        DataPos => $$et{cbor_datapos},
        Prefix  => $$et{INDENT} . '  ',
        MaxLen  => $verbose < 5 ? ( $verbose == 3 ? 96 : 2048 ) : undef,
        Out     => $et->Options('TextOut'),
    ) if $verbose > 2;
    return ( $val, $err, $pos );
}

sub ProcessCBOR($$$) {
    my ( $et, $dirInfo, $tagTablePtr ) = @_;
    my $dataPt = $$dirInfo{DataPt};
    my $pos    = $$dirInfo{DirStart};
    my $end    = $pos + $$dirInfo{DirLen};
    my ( $val, $err, $tag, $i );

    $et->VerboseDir( 'CBOR', undef, $$dirInfo{DirLen} );
    SetByteOrder('MM');

    $$et{cbor_datapos} = $$dirInfo{DataPos} + $$dirInfo{Base};

    while ( $pos < $end ) {
        ( $val, $err, $pos ) = ReadCBORValue( $et, $dataPt, $pos, $end );
        $err and $et->Warn($err), last;
        if ( ref $val eq 'HASH' ) {
            foreach $tag ( @{ $$val{_ordered_keys_} } ) {
                Image::ExifTool::JSON::ProcessTag( $et, $tagTablePtr, $tag,
                    $$val{$tag} );
            }
        }
        elsif ( ref $val eq 'ARRAY' ) {
            for ( $i = 0 ; $i < @$val ; ++$i ) {
                Image::ExifTool::JSON::ProcessTag( $et, $tagTablePtr, "Item$i",
                    $$val[$i] );
            }
        }
        elsif ( $val eq '0' ) {
            $et->VPrint( 1, "$$et{INDENT} <CBOR end>\n" );
            last;
        }
        else {
            $et->VPrint( 1, "$$et{INDENT} Unknown value: $val\n" );
        }
    }
    return 1;
}

1;

__END__


