
package Image::ExifTool::FITS;

use strict;
use vars            qw($VERSION);
use Image::ExifTool qw(:DataAccess :Utils);

$VERSION = '1.02';

%Image::ExifTool::FITS::Main = (
    GROUPS => { 2 => 'Image' },
    NOTES  => q{
        This table lists some standard Flexible Image Transport System (FITS) tags,
        but ExifTool will extract any other tags found.  See
        L<https://fits.gsfc.nasa.gov/fits_standard.html> for the specification.
    },
    TELESCOP   => 'Telescope',
    BACKGRND   => 'Background',
    INSTRUME   => 'Instrument',
    OBJECT     => 'Object',
    OBSERVER   => 'Observer',
    DATE       => { Name => 'CreateDate', Groups => { 2 => 'Time' } },
    AUTHOR     => { Name => 'Author',     Groups => { 2 => 'Author' } },
    REFERENC   => 'Reference',
    'DATE-OBS' => { Name => 'ObservationDate',    Groups => { 2 => 'Time' } },
    'TIME-OBS' => { Name => 'ObservationTime',    Groups => { 2 => 'Time' } },
    'DATE-END' => { Name => 'ObservationDateEnd', Groups => { 2 => 'Time' } },
    'TIME-END' => { Name => 'ObservationTimeEnd', Groups => { 2 => 'Time' } },
    COMMENT    => {
        Name      => 'Comment',
        PrintConv => '$val =~ s/^ +//; $val',
        Notes     =>
'leading spaces are removed if L<PrintConv|../ExifTool.html#PrintConv> is enabled'
    },
    HISTORY => {
        Name      => 'History',
        PrintConv => '$val =~ s/^ +//; $val',
        Notes     =>
'leading spaces are removed if L<PrintConv|../ExifTool.html#PrintConv> is enabled'
    },
);

sub ProcessFITS($$) {
    my ( $et, $dirInfo ) = @_;
    my $raf = $$dirInfo{RAF};
    my ( $buff, $tag, $continue );

    return 0
      unless $raf->Read( $buff, 80 ) == 80 and $buff =~ /^SIMPLE  = {20}T/;
    $et->SetFileType();
    my $tagTablePtr = GetTagTable('Image::ExifTool::FITS::Main');

    for ( ; ; ) {
        $raf->Read( $buff, 80 ) == 80
          or $et->Warn('Truncated FITS header'), last;
        my $key = substr( $buff, 0, 8 );
        $key =~ s/ +$//;
        if ( $key eq 'CONTINUE' ) {
            defined $continue
              or $et->Warn('Unexpected FITS CONTINUE keyword'), next;
        }
        else {
            if ( defined $continue ) {
                $et->HandleTag( $tagTablePtr, $tag, $continue . '&' );
                undef $continue;
            }
            last if $key eq 'END';
            $key =~ /^[-_A-Z0-9]*$/
              or $et->Warn('Format error in FITS header'), last;
            if ( $key eq 'COMMENT' or $key eq 'HISTORY' ) {
                my $val = substr( $buff, 8 );
                $val =~ s/ +$//;
                $et->HandleTag( $tagTablePtr, $key, $val );
                next;
            }
            next unless substr( $buff, 8, 2 ) eq '= ';
            $tag = $Image::ExifTool::specialTags{$key} ? "_$key" : $key;
            unless ( $$tagTablePtr{$tag} ) {
                my $name = ucfirst lc $tag;
                $name =~ s/_(.)/\U$1/g;
                AddTagToTable( $tagTablePtr, $tag, { Name => $name } );
            }
        }
        my $val = substr( $buff, 10 );
        if ( $val =~ /^'(.*?)'(.*)/ ) {
            ( $val, $buff ) = ( $1, $2 );
            while ( $buff =~ /^('.*?)'(.*)/ ) {
                $val .= $1;
                $buff = $2;
            }
            $val =~ s/ +$//;
            if ( defined $continue ) {
                $val = $continue . $val;
                undef $continue;
            }
            $val =~ s/\&$// and $continue = $val, next;
        }
        elsif ( defined $continue ) {
            $et->Warn('Invalid FITS CONTINUE value');
            next;
        }
        else {
            $val =~ s/ *(\/.*)?$//;
            next unless length $val;
            $val =~ s/^ +//;

            $val =~ tr/DE/e/
              if $val =~ /^[+-]?(?=\d|\.\d)\d*(\.\d*)?([ED]([+-]?\d+))?$/;
        }
        $et->HandleTag( $tagTablePtr, $tag, $val );
    }
    return 1;
}

1;

__END__


