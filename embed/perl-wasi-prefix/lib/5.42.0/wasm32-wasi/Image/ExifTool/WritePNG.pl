package Image::ExifTool::PNG;

use strict;

my @crcTable;

sub CalculateCRC($;$$$) {
    my ( $dataPt, $crc, $pos, $len ) = @_;
    $crc = 0                       unless defined $crc;
    $pos = 0                       unless defined $pos;
    $len = length($$dataPt) - $pos unless defined $len;
    $crc ^= 0xffffffff;

    unless (@crcTable) {
        my ( $c, $n, $k );
        for ( $n = 0 ; $n < 256 ; ++$n ) {
            for ( $k = 0, $c = $n ; $k < 8 ; ++$k ) {
                $c = ( $c & 1 ) ? 0xedb88320 ^ ( $c >> 1 ) : $c >> 1;
            }
            $crcTable[$n] = $c;
        }
    }
    foreach ( unpack( "x${pos}C$len", $$dataPt ) ) {
        $crc = $crcTable[ ( $crc ^ $_ ) & 0xff ] ^ ( $crc >> 8 );
    }
    return $crc ^ 0xffffffff;
}

sub HexEncode($) {
    my $dataPt = shift;
    my $len    = length($$dataPt);
    my $hex    = '';
    my $pos;
    for ( $pos = 0 ; $pos < $len ; $pos += 36 ) {
        my $n = $len - $pos;
        $n > 36 and $n = 36;
        $hex .= unpack( 'H*', substr( $$dataPt, $pos, $n ) ) . "\n";
    }
    return $hex;
}

sub WriteProfile($$$;$) {
    my ( $outfile, $rawType, $dataPt, $profile ) = @_;
    my ( $buff, $prefix, $chunk, $deflate );
    if ( $rawType ne $stdCase{exif} and eval { require Compress::Zlib } ) {
        $deflate = Compress::Zlib::deflateInit();
    }
    if ( not defined $profile ) {
        if ( ref $rawType ) {
            return 0 unless $deflate;
            $chunk  = 'iCCP';
            $prefix = "$$rawType\0\0";
        }
        else {
            $chunk = $rawType;
            if ( $rawType eq $stdCase{zxif} ) {
                $prefix = "\0" . pack( 'N', length $$dataPt );
            }
            else {
                $prefix = '';
            }
        }
        if ($deflate) {
            $buff = $deflate->deflate($$dataPt);
            return 0 unless defined $buff;
            $buff .= $deflate->flush();
            $dataPt = \$buff;
        }
    }
    else {
        my $txtHdr = sprintf( "\n$profile profile\n%8d\n", length($$dataPt) );
        $buff   = $txtHdr . HexEncode($dataPt);
        $chunk  = 'tEXt';
        $prefix = "Raw profile type $rawType\0";
        $dataPt = \$buff;
        if ($deflate) {
            my $buf2 = $deflate->deflate($buff);
            if ( defined $buf2 ) {
                $dataPt = \$buf2;
                $buf2 .= $deflate->flush();
                $chunk = 'zTXt';
                $prefix .= "\0";
            }
        }
    }
    my $hdr =
      pack( 'Na4', length($prefix) + length($$dataPt), $chunk ) . $prefix;
    my $crc = CalculateCRC( \$hdr, undef, 4 );
    $crc = CalculateCRC( $dataPt, $crc );
    return Write( $outfile, $hdr, $$dataPt, pack( 'N', $crc ) );
}

sub Add_iCCP($$) {
    my ( $et, $outfile ) = @_;
    if ( $$et{ADD_DIRS}{ICC_Profile} ) {
        my $tagTablePtr =
          Image::ExifTool::GetTagTable('Image::ExifTool::ICC_Profile::Main');
        my %dirInfo = ( Parent => 'PNG', DirName => 'ICC_Profile' );
        my $buff    = $et->WriteDirectory( \%dirInfo, $tagTablePtr );
        if ( defined $buff and length $buff ) {
            my $profileName =
              $et->GetNewValue( $Image::ExifTool::PNG::Main{'iCCP-name'} );
            $profileName = 'icm' unless defined $profileName;
            if ( WriteProfile( $outfile, \$profileName, \$buff ) ) {
                $et->VPrint( 0,
                    "Created ICC profile with name '${profileName}'\n" );
                delete $$et{ADD_DIRS}{ICC_Profile};
            }
        }
    }
    if ( $$et{ADD_PNG} ) {
        my ( $tag, %addBeforePLTE );
        foreach $tag (qw(sRGB gAMA)) {
            next unless $$et{ADD_PNG}{$tag};
            $addBeforePLTE{$tag} = $$et{ADD_PNG}{$tag};
            delete $$et{ADD_PNG}{$tag};
        }
        if (%addBeforePLTE) {
            my $save = $$et{ADD_PNG};
            $$et{ADD_PNG} = \%addBeforePLTE;
            AddChunks( $et, $outfile );
            $$et{ADD_PNG} = $save;
        }
    }
    return 1;
}

sub DoneDir($$$;$) {
    my ( $et, $dir, $outBuff, $nonStandard ) = @_;
    my $saveDir = $dir;
    $dir = 'EXIF' if $dir eq 'IFD0';
    if ( not $nonStandard ) {
        delete $$et{ADD_DIRS}{$dir};
        delete $$et{ADD_DIRS}{IFD0} if $dir eq 'EXIF';
    }
    elsif ( $$et{DEL_GROUP}{$dir} or $$et{DEL_GROUP}{$saveDir} ) {
        $et->VPrint( 0, "  Deleting non-standard $dir\n" );
        $$outBuff = '';
    }
}

sub BuildTextChunk($$$$$) {
    my ( $et, $tag, $tagInfo, $val, $lang ) = @_;
    my ( $xtra, $compVal, $iTXt, $comp );
    if ( $$tagInfo{SubDirectory} ) {
        if ( $$tagInfo{Name} eq 'XMP' ) {
            $iTXt = 2;

        }
        else {
            $comp = 2;
        }
    }
    else {
        $comp = 1 if $et->Options('Compress');
        if ($lang) {
            $iTXt = 1;
            $tag =~ s/-$lang$//;
        }
        elsif ( $$et{OPTIONS}{Charset} ne 'Latin' and $val =~ /[\x80-\xff]/ ) {
            $iTXt = 1;
        }
        elsif ( $$tagInfo{iTXt} ) {
            $iTXt = 1;
        }
    }
    if ($comp) {
        my $warn;
        if ( eval { require Compress::Zlib } ) {
            my $deflate = Compress::Zlib::deflateInit();
            $compVal = $deflate->deflate($val) if $deflate;
            if ( defined $compVal ) {
                $compVal .= $deflate->flush();
                unless ( length($compVal) < length($val) ) {
                    undef $compVal;
                    $warn = 'uncompressed data is smaller';
                }
            }
            else {
                $warn = 'deflate error';
            }
        }
        else {
            $warn = 'Compress::Zlib not available';
        }
        if ( $warn and $comp == 1 ) {
            $et->Warn( "PNG:$$tagInfo{Name} not compressed ($warn)", 1 );
        }
    }
    if ($iTXt) {
        $$et{TextChunkType} = 'iTXt';
        $xtra =
          ( defined $compVal ? "\x01\0" : "\0\0" ) . ( $lang || '' ) . "\0\0";
        $val = $et->Encode( $val, 'UTF8' ) if $iTXt == 1;
    }
    elsif ( defined $compVal ) {
        $$et{TextChunkType} = 'zTXt';
        $xtra = "\0";
    }
    else {
        $$et{TextChunkType} = 'tEXt';
        $xtra = '';
    }
    return $tag . "\0" . $xtra . ( defined $compVal ? $compVal : $val );
}

sub AddChunks($$;@) {
    my ( $et, $outfile, @add ) = @_;
    my ( $addTags, $tag, $dir, $err, $tagTablePtr, $specified );

    if (@add) {
        $addTags   = {};
        $specified = 1;
    }
    else {
        $addTags = $$et{ADD_PNG};
        delete $$et{ADD_PNG};

        @add = sort keys %{ $$et{ADD_DIRS} };
    }
    foreach $tag ( sort keys %$addTags ) {
        my $tagInfo = $$addTags{$tag};
        next if $$tagInfo{FakeTag};
        my $nvHash = $et->GetNewValueHash($tagInfo);
        next unless $$nvHash{IsCreating} or $et->IsOverwriting($nvHash) > 0;
        my $val = $et->GetNewValue($nvHash);
        if ( defined $val ) {
            next if $$nvHash{EditOnly};
            my $data;
            if ( $$tagInfo{Table} eq \%Image::ExifTool::PNG::TextualData ) {
                $data = BuildTextChunk( $et, $tag, $tagInfo, $val,
                    $$tagInfo{LangCode} );
                $data = $$et{TextChunkType} . $data;
                delete $$et{TextChunkType};
            }
            else {
                $data = "$tag$val";
            }
            my $hdr  = pack( 'N', length($data) - 4 );
            my $cbuf = pack( 'N', CalculateCRC( \$data, undef ) );
            Write( $outfile, $hdr, $data, $cbuf ) or $err = 1;
            $et->VerboseValue( "+ PNG:$$tagInfo{Name}", $val );
            ++$$et{CHANGED};
        }
    }
    foreach $dir (@add) {
        next unless $$et{ADD_DIRS}{$dir};
        my $buff;
        my %dirInfo = (
            Parent  => 'PNG',
            DirName => $dir,
        );
        if ( $dir eq 'IFD0' ) {
            next unless $specified;
            my $chunk = $stdCase{exif};
            $et->VPrint( 0, "Creating $chunk chunk:\n" );
            $$et{TIFF_TYPE} = 'APP1';
            $tagTablePtr =
              Image::ExifTool::GetTagTable('Image::ExifTool::Exif::Main');
            $buff = $et->WriteDirectory( \%dirInfo, $tagTablePtr,
                \&Image::ExifTool::WriteTIFF );
            if ( defined $buff and length $buff ) {
                WriteProfile( $outfile, $chunk, \$buff ) or $err = 1;
            }
        }
        elsif ( $dir eq 'XMP' ) {
            $et->VPrint( 0, "Creating XMP iTXt chunk:\n" );
            $tagTablePtr =
              Image::ExifTool::GetTagTable('Image::ExifTool::XMP::Main');
            $dirInfo{ReadOnly} = 1;
            $buff = $et->WriteDirectory( \%dirInfo, $tagTablePtr );
            if (
                    defined $buff
                and length $buff
                and
                Image::ExifTool::XMP::ValidateXMP( \$buff, 'r' )
              )
            {
                $buff = "iTXtXML:com.adobe.xmp\0\0\0\0\0" . $buff;
                my $hdr  = pack( 'N', length($buff) - 4 );
                my $cbuf = pack( 'N', CalculateCRC( \$buff, undef ) );
                Write( $outfile, $hdr, $buff, $cbuf ) or $err = 1;
            }
        }
        elsif ( $dir eq 'IPTC' ) {
            $et->Warn( 'Creating non-standard IPTC in PNG', 1 );
            $et->VPrint( 0, "Creating IPTC profile:\n" );
            $dirInfo{DirName} = 'Photoshop';
            $tagTablePtr =
              Image::ExifTool::GetTagTable('Image::ExifTool::Photoshop::Main');
            $buff = $et->WriteDirectory( \%dirInfo, $tagTablePtr );
            if ( defined $buff and length $buff ) {
                WriteProfile( $outfile, 'iptc', \$buff, 'IPTC' ) or $err = 1;
            }
        }
        elsif ( $dir eq 'ICC_Profile' ) {
            $et->VPrint( 0, "Creating ICC profile:\n" );
            $tagTablePtr = Image::ExifTool::GetTagTable(
                'Image::ExifTool::ICC_Profile::Main');
            $buff = $et->WriteDirectory( \%dirInfo, $tagTablePtr );
            if ( defined $buff and length $buff ) {
                WriteProfile( $outfile, 'icm', \$buff, 'ICC' ) or $err = 1;
                $et->Warn('Wrote ICC as a raw profile (no Compress::Zlib)');
            }
        }
        elsif ( $dir eq 'PNG-pHYs' ) {
            $et->VPrint( 0,
                "Creating pHYs chunk (default 2834 pixels per meter):\n" );
            $tagTablePtr = Image::ExifTool::GetTagTable(
                'Image::ExifTool::PNG::PhysicalPixel');
            my $blank = "\0\0\x0b\x12\0\0\x0b\x12\x01";
            $dirInfo{DataPt} = \$blank;
            $buff = $et->WriteDirectory( \%dirInfo, $tagTablePtr );
            if ( defined $buff and length $buff ) {
                $buff = 'pHYs' . $buff;
                my $hdr  = pack( 'N', length($buff) - 4 );
                my $cbuf = pack( 'N', CalculateCRC( \$buff, undef ) );
                Write( $outfile, $hdr, $buff, $cbuf ) or $err = 1;
            }
        }
        else {
            next;
        }
        delete $$et{ADD_DIRS}{$dir};
    }
    return not $err;
}

1;

__END__

