
package Data::Dumper;

use strict;
use warnings;

use 5.008_001;
require Exporter;

use constant IS_PRE_516_PERL     => "$]" < 5.016;
use constant SUPPORTS_CORE_BOOLS => defined &builtin::is_bool;

use Carp ();

our (
    $Indent,    $Trailingcomma, $Purity,     $Pad,     $Varname,
    $Useqq,     $Terse,         $Freezer,    $Toaster, $Deepcopy,
    $Quotekeys, $Bless,         $Maxdepth,   $Pair,    $Sortkeys,
    $Deparse,   $Sparseseen,    $Maxrecurse, $Useperl
);

our ( @ISA, @EXPORT, @EXPORT_OK, $VERSION );

BEGIN {
    $VERSION = '2.192';

    @ISA       = qw(Exporter);
    @EXPORT    = qw(Dumper);
    @EXPORT_OK = qw(DumperX);

    eval {
        require XSLoader;
        XSLoader::load('Data::Dumper');
        1;
    }
      or $Useperl = 1;
}

my $IS_ASCII = ord 'A' == 65;

$Indent        = 2       unless defined $Indent;
$Trailingcomma = 0       unless defined $Trailingcomma;
$Purity        = 0       unless defined $Purity;
$Pad           = ""      unless defined $Pad;
$Varname       = "VAR"   unless defined $Varname;
$Useqq         = 0       unless defined $Useqq;
$Terse         = 0       unless defined $Terse;
$Freezer       = ""      unless defined $Freezer;
$Toaster       = ""      unless defined $Toaster;
$Deepcopy      = 0       unless defined $Deepcopy;
$Quotekeys     = 1       unless defined $Quotekeys;
$Bless         = "bless" unless defined $Bless;
$Maxdepth   = 0      unless defined $Maxdepth;
$Pair       = ' => ' unless defined $Pair;
$Useperl    = 0      unless defined $Useperl;
$Sortkeys   = 0      unless defined $Sortkeys;
$Deparse    = 0      unless defined $Deparse;
$Sparseseen = 0      unless defined $Sparseseen;
$Maxrecurse = 1000   unless defined $Maxrecurse;

sub new {
    my ( $c, $v, $n ) = @_;

    Carp::croak("Usage:  PACKAGE->new(ARRAYREF, [ARRAYREF])")
      unless ( defined($v) && ( ref($v) eq 'ARRAY' ) );
    $n = [] unless ( defined($n) && ( ref($n) eq 'ARRAY' ) );

    my ($s) = {
        level         => 0,
        indent        => $Indent,
        trailingcomma => $Trailingcomma,
        pad           => $Pad,
        xpad          => "",
        apad          => "",
        sep           => "",
        pair          => $Pair,
        seen          => {},
        todump        => $v,
        names         => $n,
        varname       => $Varname,
        purity        => $Purity,
        useqq         => $Useqq,
        terse         => $Terse,
        freezer       => $Freezer,
        toaster       => $Toaster,
        deepcopy      => $Deepcopy,
        quotekeys     => $Quotekeys,
        'bless'       => $Bless,
        maxdepth   => $Maxdepth,
        maxrecurse => $Maxrecurse,
        useperl    => $Useperl,
        sortkeys   => $Sortkeys,
        deparse    => $Deparse,
        noseen     => $Sparseseen,
    };

    if ( $Indent > 0 ) {
        $s->{xpad} = "  ";
        $s->{sep}  = "\n";
    }
    return bless( $s, $c );
}

sub format_refaddr {
    require Scalar::Util;
    pack "J", Scalar::Util::refaddr(shift);
}

sub Seen {
    my ( $s, $g ) = @_;
    if ( defined($g) && ( ref($g) eq 'HASH' ) ) {
        my ( $k, $v, $id );
        while ( ( $k, $v ) = each %$g ) {
            if ( defined $v ) {
                if ( ref $v ) {
                    $id = format_refaddr($v);
                    if ( $k =~ /^[*](.*)$/ ) {
                        $k =
                            ( ref $v eq 'ARRAY' ) ? ( "\\\@" . $1 )
                          : ( ref $v eq 'HASH' )  ? ( "\\\%" . $1 )
                          : ( ref $v eq 'CODE' )  ? ( "\\\&" . $1 )
                          :                         ( "\$" . $1 );
                    }
                    elsif ( $k !~ /^\$/ ) {
                        $k = "\$" . $k;
                    }
                    $s->{seen}{$id} = [ $k, $v ];
                }
                else {
                    Carp::carp(
                        "Only refs supported, ignoring non-ref item \$$k");
                }
            }
            else {
                Carp::carp(
                    "Value of ref must be defined; ignoring undefined item \$$k"
                );
            }
        }
        return $s;
    }
    else {
        return map { @$_ } values %{ $s->{seen} };
    }
}

sub Values {
    my ( $s, $v ) = @_;
    if ( defined($v) ) {
        if ( ref($v) eq 'ARRAY' ) {
            $s->{todump} = [@$v];
            return $s;
        }
        else {
            Carp::croak("Argument to Values, if provided, must be array ref");
        }
    }
    else {
        return @{ $s->{todump} };
    }
}

sub Names {
    my ( $s, $n ) = @_;
    if ( defined($n) ) {
        if ( ref($n) eq 'ARRAY' ) {
            $s->{names} = [@$n];
            return $s;
        }
        else {
            Carp::croak("Argument to Names, if provided, must be array ref");
        }
    }
    else {
        return @{ $s->{names} };
    }
}

sub DESTROY { }

sub Dump {
    return &Dumpxs
      unless $Data::Dumper::Useperl
      || ( ref( $_[0] ) && $_[0]->{useperl} )
      || ( !$IS_ASCII && "$]" < 5.021_010 );
    return &Dumpperl;
}

our @post;

sub Dumpperl {
    my ($s) = shift;
    my ( @out, $val, $name );
    my ($i) = 0;
    local (@post);

    $s = $s->new(@_) unless ref $s;

    for $val ( @{ $s->{todump} } ) {
        @post = ();
        $name = $s->{names}[ $i++ ];
        $name = $s->_refine_name( $name, $val, $i );

        my $valstr;
        {
            local ( $s->{apad} ) = $s->{apad};
            $s->{apad} .= ' ' x ( length($name) + 3 )
              if $s->{indent} >= 2 and !$s->{terse};
            $valstr = $s->_dump( $val, $name );
        }

        $valstr = "$name = " . $valstr . ';' if @post or !$s->{terse};
        my $out = $s->_compose_out( $valstr, \@post );

        push @out, $out;
    }
    return wantarray ? @out : join( '', @out );
}

sub _quote {
    my $val = shift;
    $val =~ s/([\\\'])/\\$1/g;
    return "'" . $val . "'";
}

use constant _bad_vsmg => defined &_vstring
  && ( _vstring( ~v0 ) || '' ) eq "v0";

sub _dump {
    my ( $s, $val, $name ) = @_;
    my ( $out, $type, $id, $sname );

    $type = ref $val;
    $out  = "";

    if ($type) {

        my $freezer = $s->{freezer};
        if ( $freezer and UNIVERSAL::can( $val, $freezer ) ) {
            eval { $val->$freezer() };
            warn "WARNING(Freezer method call failed): $@" if $@;
        }

        require Scalar::Util;
        my $realpack = Scalar::Util::blessed($val);
        my $realtype = $realpack ? Scalar::Util::reftype($val) : ref $val;
        $id = format_refaddr($val);

        if ( exists $s->{seen}{$id} ) {
            if ( $s->{purity} and $s->{level} > 0 ) {
                $out =
                    ( $realtype eq 'HASH' )  ? '{}'
                  : ( $realtype eq 'ARRAY' ) ? '[]'
                  :                            'do{my $o}';
                push @post, $name . " = " . $s->{seen}{$id}[0];
            }
            else {
                $out = $s->{seen}{$id}[0];
                if ( $name =~ /^([\@\%])/ ) {
                    my $start = $1;
                    if ( $out =~ /^\\$start/ ) {
                        $out = substr( $out, 1 );
                    }
                    else {
                        $out = $start . '{' . $out . '}';
                    }
                }
            }
            return $out;
        }
        else {
            $s->{seen}{$id} = [
                (
                      ( $name =~ /^[@%]/ ) ? ( '\\' . $name )
                    : ( $realtype eq 'CODE' and $name =~ /^[*](.*)$/ )
                    ? ( '\\&' . $1 )
                    : $name
                ),
                $val
            ];
        }
        my $no_bless = 0;
        my $is_regex = 0;
        if (
            $realpack
            and ( $] >= 5.009005 ? re::is_regexp($val) : $realpack eq 'Regexp' )
          )
        {
            $is_regex = 1;
            $no_bless = $realpack eq 'Regexp';
        }

        if (    !$s->{purity}
            and defined( $s->{maxdepth} )
            and $s->{maxdepth} > 0
            and $s->{level} >= $s->{maxdepth} )
        {
            return qq['$val'];
        }

        if (    $s->{maxrecurse} > 0
            and $s->{level} >= $s->{maxrecurse} )
        {
            die "Recursion limit of $s->{maxrecurse} exceeded";
        }

        my ($blesspad);
        if ( $realpack and !$no_bless ) {
            $out      = $s->{'bless'} . '( ';
            $blesspad = $s->{apad};
            $s->{apad} .= '       ' if ( $s->{indent} >= 2 );
        }

        $s->{level}++;
        my $ipad = $s->{xpad} x $s->{level};

        if ($is_regex) {
            my $pat;
            my $flags = "";
            if ( defined( *re::regexp_pattern{CODE} ) ) {
                ( $pat, $flags ) = re::regexp_pattern($val);
            }
            else {
                $pat = "$val";
            }
            $pat =~ s <
                     (\\.)           # anything backslash escaped
                   | (\$)(?![)|]|\z) # any unescaped $, except $| $) and end
                   | /               # any unescaped /
                  >
                  {
                      $1 ? $1
                          : $2 ? '${\q($)}'
                          : '\\/'
                  }gex;
            $out .= "qr/$pat/$flags";
        }
        elsif ($realtype eq 'SCALAR'
            || $realtype eq 'REF'
            || $realtype eq 'VSTRING' )
        {
            if ($realpack) {
                $out .=
                  'do{\\(my $o = ' . $s->_dump( $$val, "\${$name}" ) . ')}';
            }
            else {
                $out .= '\\' . $s->_dump( $$val, "\${$name}" );
            }
        }
        elsif ( $realtype eq 'GLOB' ) {
            $out .= '\\' . $s->_dump( $$val, "*{$name}" );
        }
        elsif ( $realtype eq 'ARRAY' ) {
            my ( $pad, $mname );
            my ($i) = 0;
            $out .= ( $name =~ /^\@/ ) ? '(' : '[';
            $pad = $s->{sep} . $s->{pad} . $s->{apad};
            ( $name =~ /^\@(.*)$/ ) ? ( $mname = "\$" . $1 ) :
              ( $name =~ /^\\?[\%\@\*\$][^{].*[]}]$/ )
              ? ( $mname = $name )
              : ( $mname = $name . '->' );
            $mname .= '->' if $mname =~ /^\*.+\{[A-Z]+\}$/;
            for my $v (@$val) {
                $sname = $mname . '[' . $i . ']';
                $out .= $pad . $ipad . '#' . $i
                  if $s->{indent} >= 3;
                $out .= $pad . $ipad . $s->_dump( $v, $sname );
                $out .= ","
                  if $i++ < $#$val
                  || ( $s->{trailingcomma} && $s->{indent} >= 1 );
            }
            $out .= $pad . ( $s->{xpad} x ( $s->{level} - 1 ) ) if $i;
            $out .= ( $name =~ /^\@/ ) ? ')' : ']';
        }
        elsif ( $realtype eq 'HASH' ) {
            my ( $k, $v, $pad, $lpad, $mname, $pair );
            $out .= ( $name =~ /^\%/ ) ? '(' : '{';
            $pad  = $s->{sep} . $s->{pad} . $s->{apad};
            $lpad = $s->{apad};
            $pair = $s->{pair};
            ( $name =~ /^\%(.*)$/ ) ? ( $mname = "\$" . $1 ) :
              ( $name =~ /^\\?[\%\@\*\$][^{].*[]}]$/ )
              ? ( $mname = $name )
              : ( $mname = $name . '->' );
            $mname .= '->' if $mname =~ /^\*.+\{[A-Z]+\}$/;
            my $sortkeys = defined( $s->{sortkeys} ) ? $s->{sortkeys} : '';
            my $keys     = [];

            if ($sortkeys) {
                if ( ref( $s->{sortkeys} ) eq 'CODE' ) {
                    $keys = $s->{sortkeys}($val);
                    unless ( ref($keys) eq 'ARRAY' ) {
                        Carp::carp(
                            "Sortkeys subroutine did not return ARRAYREF");
                        $keys = [];
                    }
                }
                else {
                    $keys = [ sort keys %$val ];
                }
            }

            keys(%$val);

            my $key;
            while (
                ( $k, $v ) =
                 !$sortkeys ? ( each %$val )
                : @$keys    ? ( $key = shift(@$keys), $val->{$key} )
                :             ()
              )
            {
                my $nk = $s->_dump( $k, "" );

                if ( $s->{quotekeys} && $nk =~ /^(?:0|-?[1-9][0-9]{0,8})\z/ ) {
                    $nk = $s->{useqq} ? qq("$nk") : qq('$nk');
                }
                elsif (!$s->{quotekeys}
                    and $nk =~ /^[\"\']([A-Za-z_]\w*)[\"\']$/ )
                {
                    $nk = $1;
                }

                $sname = $mname . '{' . $nk . '}';
                $out .= $pad . $ipad . $nk . $pair;

                $s->{apad} .= ( " " x ( length($nk) + 4 ) )
                  if $s->{indent} >= 2;
                $out .= $s->_dump( $val->{$k}, $sname ) . ",";
                $s->{apad} = $lpad
                  if $s->{indent} >= 2;
            }
            if ( substr( $out, -1 ) eq ',' ) {
                chop $out if !$s->{trailingcomma} || !$s->{indent};
                $out .= $pad . ( $s->{xpad} x ( $s->{level} - 1 ) );
            }
            $out .= ( $name =~ /^\%/ ) ? ')' : '}';
        }
        elsif ( $realtype eq 'CODE' ) {
            if ( $s->{deparse} ) {
                require B::Deparse;
                my $sub = 'sub ' . ( B::Deparse->new )->coderef2text($val);
                my $pad =
                    $s->{sep}
                  . $s->{pad}
                  . $s->{apad}
                  . $s->{xpad} x ( $s->{level} - 1 );
                $sub =~ s/\n/$pad/gs;
                $out .= $sub;
            }
            else {
                $out .= 'sub { "DUMMY" }';
                Carp::carp("Encountered CODE ref, using dummy placeholder")
                  if $s->{purity};
            }
        }
        else {
            Carp::croak("Can't handle '$realtype' type");
        }

        if ( $realpack and !$no_bless ) {
            $out .= ', ' . _quote($realpack) . ' )';
            $out .= '->' . $s->{toaster} . '()'
              if $s->{toaster} ne '';
            $s->{apad} = $blesspad;
        }
        $s->{level}--;
    }
    else {

        my $ref = \$_[1];
        my $v;
        if ( $name ne '' ) {
            $id = format_refaddr($ref);
            if ( exists $s->{seen}{$id} ) {
                if ( $s->{seen}{$id}[2] ) {
                    $out = $s->{seen}{$id}[0];
                    return "\${$out}";
                }
            }
            else {
                $s->{seen}{$id} = [ "\\$name", $ref ];
            }
        }
        $ref = \$val;
        if ( ref($ref) eq 'GLOB' ) {
            my $name = substr( $val, 1 );
            $name =~ s/^main::(?!\z)/::/;
            if ( $name =~
/\A(?:[A-Z_a-z][0-9A-Z_a-z]*)?::(?:[0-9A-Z_a-z]+::)*[0-9A-Z_a-z]*\z/
                && $name ne 'main::' )
            {
                $sname = $name;
            }
            else {
                local $s->{useqq} =
                  IS_PRE_516_PERL && ( $s->{useqq} || $name =~ /[^\x00-\x7f]/ )
                  ? 1
                  : $s->{useqq};
                $sname = $s->_dump(
                    $name eq 'main::'
                    ? ''
                    : $name, "",
                );
                $sname = '{' . $sname . '}';
            }
            if ( $s->{purity} ) {
                my $k;
                local ( $s->{level} ) = 0;
                for $k (qw(SCALAR ARRAY HASH)) {
                    my $gval = *$val{$k};
                    next unless defined $gval;
                    next if $k eq "SCALAR" && !defined $$gval;

                    my $postlen = scalar @post;
                    $post[$postlen] = "\*$sname = ";
                    local ( $s->{apad} ) = " " x length( $post[$postlen] )
                      if $s->{indent} >= 2;
                    $post[$postlen] .= $s->_dump( $gval, "\*$sname\{$k\}" );
                }
            }
            $out .= '*' . $sname;
        }
        elsif ( !defined($val) ) {
            $out .= "undef";
        }
        elsif (
            SUPPORTS_CORE_BOOLS && do {

                BEGIN {
                    SUPPORTS_CORE_BOOLS
                      and warnings->unimport("experimental::builtin");
                }
                builtin::is_bool($val);
            }
          )
        {
            $out .= $val ? '!!1' : '!!0';
        }
        elsif ( defined &_vstring
            and $v = _vstring($val)
            and !_bad_vsmg || eval $v eq $val )
        {
            $out .= $v;
        }
        elsif (!defined &_vstring
            and ref $ref eq 'VSTRING'
            || eval { Scalar::Util::isvstring($val) } )
        {
            $out .= sprintf "v%vd", $val;
        }
        elsif ( $val =~ /^(?:0|-?[1-9][0-9]{0,8})\z/ ) {
            $out .= $val;
        }
        else {
            if ( $s->{useqq} or $val =~ tr/\0-\377//c ) {
                $out .= qquote( $val, $s->{useqq} );
            }
            else {
                $out .= _quote($val);
            }
        }
    }
    if ($id) {
        if ( $s->{deepcopy} ) {
            delete( $s->{seen}{$id} );
        }
        elsif ($name) {
            $s->{seen}{$id}[2] = 1;
        }
    }
    return $out;
}

sub Dumper {
    return Data::Dumper->Dump( [@_] );
}

sub DumperX {
    return Data::Dumper->Dumpxs( [@_], [] );
}

sub Reset {
    my ($s) = shift;
    $s->{seen} = {};
    return $s;
}

sub Indent {
    my ( $s, $v ) = @_;
    if ( @_ >= 2 ) {
        if ( $v == 0 ) {
            $s->{xpad} = "";
            $s->{sep}  = "";
        }
        else {
            $s->{xpad} = "  ";
            $s->{sep}  = "\n";
        }
        $s->{indent} = $v;
        return $s;
    }
    else {
        return $s->{indent};
    }
}

sub Trailingcomma {
    my ( $s, $v ) = @_;
    @_ >= 2 ? ( ( $s->{trailingcomma} = $v ), return $s ) : $s->{trailingcomma};
}

sub Pair {
    my ( $s, $v ) = @_;
    @_ >= 2 ? ( ( $s->{pair} = $v ), return $s ) : $s->{pair};
}

sub Pad {
    my ( $s, $v ) = @_;
    @_ >= 2 ? ( ( $s->{pad} = $v ), return $s ) : $s->{pad};
}

sub Varname {
    my ( $s, $v ) = @_;
    @_ >= 2 ? ( ( $s->{varname} = $v ), return $s ) : $s->{varname};
}

sub Purity {
    my ( $s, $v ) = @_;
    @_ >= 2 ? ( ( $s->{purity} = $v ), return $s ) : $s->{purity};
}

sub Useqq {
    my ( $s, $v ) = @_;
    @_ >= 2 ? ( ( $s->{useqq} = $v ), return $s ) : $s->{useqq};
}

sub Terse {
    my ( $s, $v ) = @_;
    @_ >= 2 ? ( ( $s->{terse} = $v ), return $s ) : $s->{terse};
}

sub Freezer {
    my ( $s, $v ) = @_;
    @_ >= 2 ? ( ( $s->{freezer} = $v ), return $s ) : $s->{freezer};
}

sub Toaster {
    my ( $s, $v ) = @_;
    @_ >= 2 ? ( ( $s->{toaster} = $v ), return $s ) : $s->{toaster};
}

sub Deepcopy {
    my ( $s, $v ) = @_;
    @_ >= 2 ? ( ( $s->{deepcopy} = $v ), return $s ) : $s->{deepcopy};
}

sub Quotekeys {
    my ( $s, $v ) = @_;
    @_ >= 2 ? ( ( $s->{quotekeys} = $v ), return $s ) : $s->{quotekeys};
}

sub Bless {
    my ( $s, $v ) = @_;
    @_ >= 2 ? ( ( $s->{'bless'} = $v ), return $s ) : $s->{'bless'};
}

sub Maxdepth {
    my ( $s, $v ) = @_;
    @_ >= 2 ? ( ( $s->{'maxdepth'} = $v ), return $s ) : $s->{'maxdepth'};
}

sub Maxrecurse {
    my ( $s, $v ) = @_;
    @_ >= 2 ? ( ( $s->{'maxrecurse'} = $v ), return $s ) : $s->{'maxrecurse'};
}

sub Useperl {
    my ( $s, $v ) = @_;
    @_ >= 2 ? ( ( $s->{'useperl'} = $v ), return $s ) : $s->{'useperl'};
}

sub Sortkeys {
    my ( $s, $v ) = @_;
    @_ >= 2 ? ( ( $s->{'sortkeys'} = $v ), return $s ) : $s->{'sortkeys'};
}

sub Deparse {
    my ( $s, $v ) = @_;
    @_ >= 2 ? ( ( $s->{'deparse'} = $v ), return $s ) : $s->{'deparse'};
}

sub Sparseseen {
    my ( $s, $v ) = @_;
    @_ >= 2 ? ( ( $s->{'noseen'} = $v ), return $s ) : $s->{'noseen'};
}

my %esc = (
    "\a" => "\\a",
    "\b" => "\\b",
    "\t" => "\\t",
    "\n" => "\\n",
    "\f" => "\\f",
    "\r" => "\\r",
    "\e" => "\\e",
);

my $low_controls = join "", map { quotemeta chr $_ } 0 .. ( ord(" ") - 1 );
$low_controls .=
  ( $] < 5.008 || $IS_ASCII )
  ? "\x7f"
  : chr utf8::unicode_to_native(0x9F);
my $low_controls_re = qr/[$low_controls]/;

sub qquote {
    local ($_) = shift;
    return qq("") unless defined && length;
    s/([\\\"\@\$])/\\$1/g;

    my $bytes;
    { use bytes; $bytes = length }
    s/([^[:ascii:]$low_controls])/sprintf("\\x{%x}",ord($1))/ge
      if $bytes > length;

    return qq("$_") unless /[[:^print:]]/;

    s/([\a\b\t\n\f\r\e])/$esc{$1}/g;

    s/($low_controls_re)(?!\d)/'\\'.sprintf('%o',ord($1))/eg;

    s/($low_controls_re)/'\\'.sprintf('%03o',ord($1))/eg;

    my $high = shift || "";
    if ( $high eq "iso8859" ) {

        s/([\200-\240])/'\\'.sprintf('%o',ord($1))/eg if $IS_ASCII;
    }
    elsif ( $high eq "utf8" ) {
    }
    elsif ( $high eq "8bit" ) {
    }
    else {
        s/([[:^ascii:]])/'\\'.sprintf('%03o',ord($1))/eg;
    }

    return qq("$_");
}

sub _refine_name {
    my $s = shift;
    my ( $name, $val, $i ) = @_;
    if ( defined $name ) {
        if ( $name =~ /^[*](.*)$/ ) {
            if ( defined $val ) {
                $name =
                    ( ref $val eq 'ARRAY' ) ? ( "\@" . $1 )
                  : ( ref $val eq 'HASH' )  ? ( "\%" . $1 )
                  : ( ref $val eq 'CODE' )  ? ( "\*" . $1 )
                  :                           ( "\$" . $1 );
            }
            else {
                $name = "\$" . $1;
            }
        }
        elsif ( $name !~ /^\$/ ) {
            $name = "\$" . $name;
        }
    }
    else {
        $name = "\$" . $s->{varname} . $i;
    }
    return $name;
}

sub _compose_out {
    my $s = shift;
    my ( $valstr, $postref ) = @_;
    my $out = "";
    $out .= $s->{pad} . $valstr . $s->{sep};
    if ( @{$postref} ) {
        $out .=
            $s->{pad}
          . join( ';' . $s->{sep} . $s->{pad}, @{$postref} ) . ';'
          . $s->{sep};
    }
    return $out;
}

1;
__END__

