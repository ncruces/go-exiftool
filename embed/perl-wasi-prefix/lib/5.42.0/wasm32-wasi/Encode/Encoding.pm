package Encode::Encoding;

use strict;
use warnings;
our $VERSION = do { my @r = ( q$Revision: 2.8 $ =~ /\d+/g ); sprintf "%d." . "%02d" x $#r, @r };

our @CARP_NOT = qw(Encode Encode::Encoder);

use Carp   ();
use Encode ();
use Encode::MIME::Name;

use constant DEBUG => !!$ENV{PERL_ENCODE_DEBUG};

sub Define {
    my $obj       = shift;
    my $canonical = shift;
    $obj = bless { Name => $canonical }, $obj unless ref $obj;

    Encode::define_encoding( $obj, $canonical, @_ );
}

sub name { return shift->{'Name'} }

sub mime_name {
    return Encode::MIME::Name::get_mime_name( shift->name );
}

sub renew {
    my $self  = shift;
    my $clone = bless {%$self} => ref($self);
    $clone->{renewed}++;
    DEBUG and warn $clone->{renewed};
    return $clone;
}

sub renewed { return $_[0]->{renewed} || 0 }

*new_sequence = \&renew;

sub needs_lines { 0 }

sub perlio_ok {
    return eval { require PerlIO::encoding } ? 1 : 0;
}

sub toUnicode   { shift->decode(@_) }
sub fromUnicode { shift->encode(@_) }

sub encode {
    my $obj   = shift;
    my $class = ref($obj) ? ref($obj) : $obj;
    Carp::croak( $class . "->encode() not defined!" );
}

sub decode {
    my $obj   = shift;
    my $class = ref($obj) ? ref($obj) : $obj;
    Carp::croak( $class . "->encode() not defined!" );
}

sub DESTROY { }

1;
__END__

