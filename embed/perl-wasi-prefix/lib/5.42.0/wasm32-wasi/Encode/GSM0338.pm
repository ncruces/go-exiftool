package Encode::GSM0338;

use strict;
use warnings;
use Carp;

use vars qw($VERSION);
$VERSION = do { my @r = ( q$Revision: 2.10 $ =~ /\d+/g ); sprintf "%d." . "%02d" x $#r, @r };

use Encode qw(:fallbacks);

use parent qw(Encode::Encoding);
__PACKAGE__->Define('gsm0338');

use utf8;

our %UNI2GSM = (
    "\x{000A}" => "\x0A",
    "\x{000C}" => "\x1B\x0A",
    "\x{000D}" => "\x0D",
    "\x{0020}" => "\x20",
    "\x{0021}" => "\x21",
    "\x{0022}" => "\x22",
    "\x{0023}" => "\x23",
    "\x{0024}" => "\x02",
    "\x{0025}" => "\x25",
    "\x{0026}" => "\x26",
    "\x{0027}" => "\x27",
    "\x{0028}" => "\x28",
    "\x{0029}" => "\x29",
    "\x{002A}" => "\x2A",
    "\x{002B}" => "\x2B",
    "\x{002C}" => "\x2C",
    "\x{002D}" => "\x2D",
    "\x{002E}" => "\x2E",
    "\x{002F}" => "\x2F",
    "\x{0030}" => "\x30",
    "\x{0031}" => "\x31",
    "\x{0032}" => "\x32",
    "\x{0033}" => "\x33",
    "\x{0034}" => "\x34",
    "\x{0035}" => "\x35",
    "\x{0036}" => "\x36",
    "\x{0037}" => "\x37",
    "\x{0038}" => "\x38",
    "\x{0039}" => "\x39",
    "\x{003A}" => "\x3A",
    "\x{003B}" => "\x3B",
    "\x{003C}" => "\x3C",
    "\x{003D}" => "\x3D",
    "\x{003E}" => "\x3E",
    "\x{003F}" => "\x3F",
    "\x{0040}" => "\x00",
    "\x{0041}" => "\x41",
    "\x{0042}" => "\x42",
    "\x{0043}" => "\x43",
    "\x{0044}" => "\x44",
    "\x{0045}" => "\x45",
    "\x{0046}" => "\x46",
    "\x{0047}" => "\x47",
    "\x{0048}" => "\x48",
    "\x{0049}" => "\x49",
    "\x{004A}" => "\x4A",
    "\x{004B}" => "\x4B",
    "\x{004C}" => "\x4C",
    "\x{004D}" => "\x4D",
    "\x{004E}" => "\x4E",
    "\x{004F}" => "\x4F",
    "\x{0050}" => "\x50",
    "\x{0051}" => "\x51",
    "\x{0052}" => "\x52",
    "\x{0053}" => "\x53",
    "\x{0054}" => "\x54",
    "\x{0055}" => "\x55",
    "\x{0056}" => "\x56",
    "\x{0057}" => "\x57",
    "\x{0058}" => "\x58",
    "\x{0059}" => "\x59",
    "\x{005A}" => "\x5A",
    "\x{005B}" => "\x1B\x3C",
    "\x{005C}" => "\x1B\x2F",
    "\x{005D}" => "\x1B\x3E",
    "\x{005E}" => "\x1B\x14",
    "\x{005F}" => "\x11",
    "\x{0061}" => "\x61",
    "\x{0062}" => "\x62",
    "\x{0063}" => "\x63",
    "\x{0064}" => "\x64",
    "\x{0065}" => "\x65",
    "\x{0066}" => "\x66",
    "\x{0067}" => "\x67",
    "\x{0068}" => "\x68",
    "\x{0069}" => "\x69",
    "\x{006A}" => "\x6A",
    "\x{006B}" => "\x6B",
    "\x{006C}" => "\x6C",
    "\x{006D}" => "\x6D",
    "\x{006E}" => "\x6E",
    "\x{006F}" => "\x6F",
    "\x{0070}" => "\x70",
    "\x{0071}" => "\x71",
    "\x{0072}" => "\x72",
    "\x{0073}" => "\x73",
    "\x{0074}" => "\x74",
    "\x{0075}" => "\x75",
    "\x{0076}" => "\x76",
    "\x{0077}" => "\x77",
    "\x{0078}" => "\x78",
    "\x{0079}" => "\x79",
    "\x{007A}" => "\x7A",
    "\x{007B}" => "\x1B\x28",
    "\x{007C}" => "\x1B\x40",
    "\x{007D}" => "\x1B\x29",
    "\x{007E}" => "\x1B\x3D",
    "\x{00A1}" => "\x40",
    "\x{00A3}" => "\x01",
    "\x{00A4}" => "\x24",
    "\x{00A5}" => "\x03",
    "\x{00A7}" => "\x5F",
    "\x{00BF}" => "\x60",
    "\x{00C4}" => "\x5B",
    "\x{00C5}" => "\x0E",
    "\x{00C6}" => "\x1C",
    "\x{00C7}" => "\x09",
    "\x{00C9}" => "\x1F",
    "\x{00D1}" => "\x5D",
    "\x{00D6}" => "\x5C",
    "\x{00D8}" => "\x0B",
    "\x{00DC}" => "\x5E",
    "\x{00DF}" => "\x1E",
    "\x{00E0}" => "\x7F",
    "\x{00E4}" => "\x7B",
    "\x{00E5}" => "\x0F",
    "\x{00E6}" => "\x1D",
    "\x{00E8}" => "\x04",
    "\x{00E9}" => "\x05",
    "\x{00EC}" => "\x07",
    "\x{00F1}" => "\x7D",
    "\x{00F2}" => "\x08",
    "\x{00F6}" => "\x7C",
    "\x{00F8}" => "\x0C",
    "\x{00F9}" => "\x06",
    "\x{00FC}" => "\x7E",
    "\x{0393}" => "\x13",
    "\x{0394}" => "\x10",
    "\x{0398}" => "\x19",
    "\x{039B}" => "\x14",
    "\x{039E}" => "\x1A",
    "\x{03A0}" => "\x16",
    "\x{03A3}" => "\x18",
    "\x{03A6}" => "\x12",
    "\x{03A8}" => "\x17",
    "\x{03A9}" => "\x15",
    "\x{20AC}" => "\x1B\x65",
);
our %GSM2UNI = reverse %UNI2GSM;
our $ESC     = "\x1b";

sub decode ($$;$) {
    my ( $obj, $bytes, $chk ) = @_;
    return undef unless defined $bytes;
    my $str = substr( $bytes, 0, 0 );
    while ( length $bytes ) {
        my $seq = '';
        my $c;
        do {
            $c = substr( $bytes, 0, 1, '' );
            $seq .= $c;
        } while ( length $bytes and $c eq $ESC );
        my $u =
            exists $GSM2UNI{$seq}          ? $GSM2UNI{$seq}
          : ( $chk && ref $chk eq 'CODE' ) ? $chk->( unpack 'C*', $seq )
          :                                  "\x{FFFD}";
        if ( not exists $GSM2UNI{$seq} and $chk and not ref $chk ) {
            if ( substr( $seq, 0, 1 ) eq $ESC
                and ( $chk & Encode::STOP_AT_PARTIAL ) )
            {
                $bytes .= $seq;
                last;
            }
            croak join( '', map { sprintf "\\x%02X", $_ } unpack 'C*', $seq )
              . ' does not map to Unicode'
              if $chk & Encode::DIE_ON_ERR;
            carp join( '', map { sprintf "\\x%02X", $_ } unpack 'C*', $seq )
              . ' does not map to Unicode'
              if $chk & Encode::WARN_ON_ERR;
            if ( $chk & Encode::RETURN_ON_ERR ) {
                $bytes .= $seq;
                last;
            }
        }
        $str .= $u;
    }
    $_[1] = $bytes if not ref $chk and $chk and !( $chk & Encode::LEAVE_SRC );
    return $str;
}

sub encode($$;$) {
    my ( $obj, $str, $chk ) = @_;
    return undef unless defined $str;
    my $bytes = substr( $str, 0, 0 );
    while ( length $str ) {
        my $u = substr( $str, 0, 1, '' );
        my $c;
        my $seq =
            exists $UNI2GSM{$u}            ? $UNI2GSM{$u}
          : ( $chk && ref $chk eq 'CODE' ) ? $chk->( ord($u) )
          :                                  $UNI2GSM{'?'};
        if ( not exists $UNI2GSM{$u} and $chk and not ref $chk ) {
            croak sprintf( "\\x{%04x} does not map to %s", ord($u), $obj->name )
              if $chk & Encode::DIE_ON_ERR;
            carp sprintf( "\\x{%04x} does not map to %s", ord($u), $obj->name )
              if $chk & Encode::WARN_ON_ERR;
            if ( $chk & Encode::RETURN_ON_ERR ) {
                $str .= $u;
                last;
            }
        }
        $bytes .= $seq;
    }
    $_[1] = $str if not ref $chk and $chk and !( $chk & Encode::LEAVE_SRC );
    return $bytes;
}

1;
__END__

