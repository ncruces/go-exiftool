package Encode::CN::HZ;

use strict;
use warnings;
use utf8 ();

use vars qw($VERSION);
$VERSION = do { my @r = ( q$Revision: 2.10 $ =~ /\d+/g ); sprintf "%d." . "%02d" x $#r, @r };

use Encode qw(:fallbacks);

use parent qw(Encode::Encoding);
__PACKAGE__->Define('hz');

sub needs_lines { 1 }

sub decode ($$;$) {
    my ( $obj, $str, $chk ) = @_;
    return undef unless defined $str;

    my $GB       = Encode::find_encoding('gb2312-raw');
    my $ret      = substr( $str, 0, 0 );
    my $in_ascii = 1;

    while ( length $str ) {
        if ($in_ascii) {
            if ( $str =~ s/^([\x00-\x7D\x7F]+)// ) {
                $ret .= $1;

            }
            elsif ( $str =~ s/^\x7E\x7E// ) {
                $ret .= '~';
            }
            elsif ( $str =~ s/^\x7E\cJ// ) {
                1;
            }
            elsif ( $str =~ s/^\x7E\x7B// ) {
                $in_ascii = 0;
            }
            else {
                last;
            }
        }
        else {
            no warnings 'uninitialized';
            if ( $str =~ s/^((?:[\x21-\x77][\x21-\x7E])+)// ) {
                my $prefix = $1;
                $ret .= $GB->decode( $prefix, $chk );
            }
            elsif ( $str =~ s/^\x7E\x7D// ) {
                $in_ascii = 1;
            }
            else {
                last;
            }
        }
    }
    $_[1] = '' if $chk;
    return $ret;
}

sub cat_decode {
    my ( $obj, undef, $src, $pos, $trm, $chk ) = @_;
    my ( $rdst, $rsrc, $rpos ) = \@_[ 1 .. 3 ];

    my $GB       = Encode::find_encoding('gb2312-raw');
    my $ret      = '';
    my $in_ascii = 1;

    my $ini_pos = pos($$rsrc);

    substr( $src, 0, $pos ) = '';

    my $ini_len = bytes::length($src);

    $src =~ s/^\x7E// if $trm eq "\x7E";

    while ( length $src ) {
        my $now;
        if ($in_ascii) {
            if ( $src =~ s/^([\x00-\x7D\x7F])// ) {
                $now = $1;
            }
            elsif ( $src =~ s/^\x7E\x7E// ) {
                $now = '~';
            }
            elsif ( $src =~ s/^\x7E\cJ// ) {
                next;
            }
            elsif ( $src =~ s/^\x7E\x7B// ) {
                $in_ascii = 0;
                next;
            }
            else {
                last;
            }
        }
        else {
            if ( $src =~ s/^((?:[\x21-\x77][\x21-\x7F])+)// ) {
                $now = $GB->decode( $1, $chk );
            }
            elsif ( $src =~ s/^\x7E\x7D// ) {
                $in_ascii = 1;
                next;
            }
            else {
                last;
            }
        }

        next if !defined $now;

        $ret .= $now;

        if ( $now eq $trm ) {
            $$rdst .= $ret;
            $$rpos = $ini_pos + $pos + $ini_len - bytes::length($src);
            pos($$rsrc) = $ini_pos;
            return 1;
        }
    }

    $$rdst .= $ret;
    $$rpos = $ini_pos + $pos + $ini_len - bytes::length($src);
    pos($$rsrc) = $ini_pos;
    return '';
}

sub encode($$;$) {
    my ( $obj, $str, $chk ) = @_;
    return undef unless defined $str;

    my $GB       = Encode::find_encoding('gb2312-raw');
    my $ret      = substr( $str, 0, 0 );
    my $in_ascii = 1;

    no warnings 'utf8';

    while ( length $str ) {
        if ( $str =~ s/^([[:ascii:]]+)// ) {
            my $tmp = $1;
            $tmp =~ s/~/~~/g;
            if ( !$in_ascii ) {
                $ret .= "\x7E\x7D";
                $in_ascii = 1;
            }
            $ret .= pack 'a*', $tmp;
        }
        elsif ( $str =~ s/(.)// ) {
            my $s   = $1;
            my $tmp = $GB->encode( $s, $chk || 0 );
            last if !defined $tmp;
            if ( length $tmp == 2 ) {
                if ($in_ascii) {
                    $ret .= "\x7E\x7B";
                    $in_ascii = 0;
                }
                $ret .= $tmp;
            }
            elsif ( length $tmp ) {
                if ( !$in_ascii ) {
                    $ret .= "\x7E\x7D";
                    $in_ascii = 1;
                }
                $ret .= $tmp;
            }
        }
        else {
            last;
        }
    }
    $_[1] = $str if $chk;

    if ( !$in_ascii ) {
        $ret .= "\x7E\x7D";
        $in_ascii = 1;
    }
    utf8::encode($ret);
    return $ret;
}

1;
__END__

