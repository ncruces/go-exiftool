
package Filter::Util::Call;

require 5.006;
require Exporter;

use XSLoader ();
use strict;
use warnings;

our @ISA        = qw(Exporter);
our @EXPORT     = qw( filter_add filter_del filter_read filter_read_exact);
our $VERSION    = "1.64";
our $XS_VERSION = $VERSION;
$VERSION = eval $VERSION;

sub filter_read_exact($) {
    my ($size) = @_;
    my ($left) = $size;
    my ($status);

    unless ( $size > 0 ) {
        require Carp;
        Carp::croak("filter_read_exact: size parameter must be > 0");
    }

    while ( $left and ( $status = filter_read($left) ) > 0 ) {
        $left = $size - length $_;
    }

    return 1 if $status == 0 and length $_;

    return $status;
}

sub filter_add($) {
    my ($obj) = @_;

    my $coderef = ( ref $obj eq 'CODE' );

    if ( !$coderef and ( !ref($obj) or ref($obj) =~ /^ARRAY|HASH$/ ) ) {
        $obj = bless( \$obj, (caller)[0] );
    }

    Filter::Util::Call::real_import( $obj, (caller)[0], $coderef );
}

XSLoader::load('Filter::Util::Call');

1;
__END__


