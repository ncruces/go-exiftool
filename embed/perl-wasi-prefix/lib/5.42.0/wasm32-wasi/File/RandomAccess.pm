
package File::RandomAccess;

use strict;
require 5.002;
require Exporter;

use vars qw($VERSION @ISA @EXPORT_OK);
$VERSION = '1.13';
@ISA     = qw(Exporter);

sub Read($$$);

my $CHUNK_SIZE   = 8192;
my $SKIP_SIZE    = 65536;
my $SLURP_CHUNKS = 16;

sub new($$;$) {
    my ( $that, $filePt, $isRandom ) = @_;
    my $class = ref($that) || $that;
    my $self;

    if ( ref $filePt eq 'SCALAR' ) {
        $self = {
            BUFF_PT => $filePt,
            BASE    => 0,
            POS     => 0,
            LEN     => length($$filePt),
            TESTED  => -1,
        };
        bless $self, $class;
    }
    else {
        my $buff = '';
        $self = {
            FILE_PT => $filePt,
            BUFF_PT => \$buff,
            BASE    => 0,
            POS     => 0,
            LEN     => 0,
            TESTED  => 0,
        };
        bless $self, $class;
        $self->SeekTest() unless $isRandom;
    }
    return $self;
}

sub Debug($) {
    my $self = shift;
    $self->{DEBUG} = {};
}

sub SeekTest($) {
    my $self = shift;
    unless ( $self->{TESTED} ) {
        my $fp = $self->{FILE_PT};
        if ( seek( $fp, 1, 1 ) and seek( $fp, -1, 1 ) ) {
            $self->{TESTED} = 1;
        }
        else {
            $self->{TESTED} = -1;
        }
    }
    return $self->{TESTED} == 1 ? 1 : 0;
}

sub Tell($) {
    my $self = shift;
    my $rtnVal;
    if ( $self->{TESTED} < 0 ) {
        $rtnVal = $self->{POS} + $self->{BASE};
    }
    else {
        $rtnVal = tell( $self->{FILE_PT} );
    }
    return $rtnVal;
}

sub Seek($$;$) {
    my ( $self, $num, $whence ) = @_;
    $whence = 0 unless defined $whence;
    my $rtnVal;
    if ( $self->{TESTED} < 0 ) {
        my $newPos;
        if ( $whence == 0 ) {
            $newPos = $num - $self->{BASE};
        }
        elsif ( $whence == 1 ) {
            $newPos = $num + $self->{POS};
        }
        elsif ( $self->{NoBuffer} and $self->{FILE_PT} ) {
            $newPos = -1;
        }
        else {
            $self->Slurp();
            $newPos = $num + $self->{LEN};
        }
        if (
            $newPos >= 0
            and
            ( not $self->{NoBuffer} or $newPos >= $self->{POS} )
          )
        {
            $self->{POS} = $newPos;
            $rtnVal = 1;
        }
    }
    else {
        $rtnVal = seek( $self->{FILE_PT}, $num, $whence );
    }
    return $rtnVal;
}

sub Read($$$) {
    my $self = shift;
    my $len  = $_[1];
    my $rtnVal;

    if ( $len & 0xf8000000 ) {
        return 0 if $len < 0;
        my $maxLen = 0x4000000;
        my $num    = Read( $self, $_[0], $maxLen );
        return $num if $num < $maxLen;
        for ( ; ; ) {
            $len -= $maxLen;
            last if $len <= 0;
            my $l = $len < $maxLen ? $len : $maxLen;
            my $buff;
            my $n = Read( $self, $buff, $l );
            last unless $n;
            $_[0] .= $buff;
            $num += $n;
            last if $n < $l;
        }
        return $num;
    }
    if ( $self->{TESTED} < 0 ) {
        $self->Purge() or return 0 if $self->{NoBuffer};
        my $buff;
        my $newPos = $self->{POS} + $len;
        my $num = $newPos - $self->{LEN};
        if ( $num > 0 and $self->{FILE_PT} ) {
            $num = ( ( $num - 1 ) | ( $CHUNK_SIZE - 1 ) ) + 1;
            $num = read( $self->{FILE_PT}, $buff, $num );
            if ($num) {
                ${ $self->{BUFF_PT} } .= $buff;
                $self->{LEN} += $num;
            }
            elsif ( not defined $num ) {
                $self->{ERROR} = $!;
            }
        }
        $num = $self->{LEN} - $self->{POS};
        if ( $len <= $num ) {
            $rtnVal = $len;
        }
        elsif ( $num <= 0 ) {
            $_[0] = '';
            return 0;
        }
        else {
            $rtnVal = $num;
        }
        $_[0] = substr( ${ $self->{BUFF_PT} }, $self->{POS}, $rtnVal );
        $self->{POS} += $rtnVal;
    }
    else {
        $_[0] = '' unless defined $_[0];
        $rtnVal = read( $self->{FILE_PT}, $_[0], $len );
        unless ( defined $rtnVal ) {
            $self->{ERROR} = $!;
            $rtnVal = 0;
        }
    }
    if ( $self->{DEBUG} ) {
        my $pos = $self->Tell() - $rtnVal;
        unless ( $self->{DEBUG}->{$pos} and $self->{DEBUG}->{$pos} > $rtnVal ) {
            $self->{DEBUG}->{$pos} = $rtnVal;
        }
    }
    return $rtnVal;
}

sub ReadLine($$) {
    my $self = shift;
    my $rtnVal;
    my $fp = $self->{FILE_PT};

    if ( $self->{TESTED} < 0 ) {
        my ( $num, $buff );
        $self->Purge() or return 0 if $self->{NoBuffer};
        my $pos = $self->{POS};
        if ($fp) {
            while ( $self->{LEN} <= $pos ) {
                $num = read( $fp, $buff, $CHUNK_SIZE );
                unless ($num) {
                    defined $num or $self->{ERROR} = $!;
                    return 0;
                }
                ${ $self->{BUFF_PT} } .= $buff;
                $self->{LEN} += $num;
            }
            for ( ; ; ) {
                $pos = index( ${ $self->{BUFF_PT} }, $/, $pos );
                if ( $pos >= 0 ) {
                    $pos += length($/);
                    last;
                }
                $pos = $self->{LEN};
                $num = read( $fp, $buff, $CHUNK_SIZE );
                unless ($num) {
                    defined $num or $self->{ERROR} = $!;
                    last;
                }
                ${ $self->{BUFF_PT} } .= $buff;
                $self->{LEN} += $num;
            }
        }
        else {
            $pos = index( ${ $self->{BUFF_PT} }, $/, $pos );
            if ( $pos < 0 ) {
                $pos = $self->{LEN};
                $self->{POS} = $pos if $self->{POS} > $pos;
            }
            else {
                $pos += length($/);
            }
        }
        $rtnVal      = $pos - $self->{POS};
        $_[0]        = substr( ${ $self->{BUFF_PT} }, $self->{POS}, $rtnVal );
        $self->{POS} = $pos;
    }
    else {
        $_[0] = <$fp>;
        if ( defined $_[0] ) {
            $rtnVal = length( $_[0] );
        }
        else {
            $rtnVal = 0;
        }
    }
    if ( $self->{DEBUG} ) {
        my $pos = $self->Tell() - $rtnVal;
        unless ( $self->{DEBUG}->{$pos} and $self->{DEBUG}->{$pos} > $rtnVal ) {
            $self->{DEBUG}->{$pos} = $rtnVal;
        }
    }
    return $rtnVal;
}

sub Slurp($) {
    my $self = shift;
    my $fp   = $self->{FILE_PT} || return;
    my ( $buff, $num );
    for ( ; ; ) {
        $num = read( $fp, $buff, $CHUNK_SIZE * $SLURP_CHUNKS );
        unless ($num) {
            defined $num or $self->{ERROR} = $!;
            last;
        }
        ${ $self->{BUFF_PT} } .= $buff;
        $self->{LEN} += $num;
    }
}

sub Purge($) {
    my $self = shift;
    return 1 unless $self->{FILE_PT};
    return 0 if $self->{POS} < 0;
    if ( $self->{POS} > $CHUNK_SIZE ) {
        my $purge = $self->{POS} - ( $self->{POS} % $CHUNK_SIZE );
        if ( $purge >= $self->{LEN} ) {
            while ( $self->{POS} > $self->{LEN} ) {
                $self->{BASE} += $self->{LEN};
                $self->{POS}  -= $self->{LEN};
                ${ $self->{BUFF_PT} } = '';
                $self->{LEN} =
                  read( $self->{FILE_PT}, ${ $self->{BUFF_PT} }, $SKIP_SIZE );
                if ( not defined $self->{LEN} ) {
                    $self->{ERROR} = $!;
                    last;
                }
                last if $self->{LEN} < $SKIP_SIZE;
            }
        }
        elsif ( $purge > 0 ) {
            ${ $self->{BUFF_PT} } = substr ${ $self->{BUFF_PT} }, $purge;
            $self->{BASE} += $purge;
            $self->{POS}  -= $purge;
            $self->{LEN}  -= $purge;
        }
    }
    return 1;
}

sub BinMode($) {
    my $self = shift;
    binmode( $self->{FILE_PT} ) if $self->{FILE_PT};
}

sub Close($) {
    my $self = shift;

    if ( $self->{DEBUG} ) {
        local $_;
        if ( $self->Seek( 0, 2 ) ) {
            $self->{DEBUG}->{ $self->Tell() } = 0;
            my $last;
            my $tot = 0;
            my $bad = 0;
            foreach ( sort { $a <=> $b } keys %{ $self->{DEBUG} } ) {
                my $pos = $_;
                my $len = $self->{DEBUG}->{$_};
                if ( defined $last and $last < $pos ) {
                    my $bytes = $pos - $last;
                    $tot += $bytes;
                    $self->Seek($last);
                    my $buff;
                    $self->Read( $buff, $bytes );
                    my $warn = '';
                    if ( $buff =~ /[^\0]/ ) {
                        $bad += ( $pos - $last );
                        $warn = ' - NON-ZERO!';
                    }
                    printf "0x%.8x - 0x%.8x (%d bytes)$warn\n", $last, $pos,
                      $bytes;
                }
                my $cur = $pos + $len;
                $last = $cur unless defined $last and $last > $cur;
            }
            print "$tot bytes missed";
            $bad and print ", $bad non-zero!";
            print "\n";
        }
        else {
            warn
              "File::RandomAccess DEBUG not working (file already closed?)\n";
        }
        delete $self->{DEBUG};
    }
    if ( $self->{FILE_PT} ) {
        close( $self->{FILE_PT} );
        delete $self->{FILE_PT};
    }
    my $emptyBuff = '';
    $self->{BUFF_PT} = \$emptyBuff;
    $self->{BASE}    = 0;
    $self->{LEN}     = 0;
    $self->{POS}     = 0;
}

1;
