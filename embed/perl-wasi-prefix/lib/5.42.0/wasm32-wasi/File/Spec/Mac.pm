package File::Spec::Mac;

use strict;
use Cwd ();
require File::Spec::Unix;

our $VERSION = '3.94';
$VERSION =~ tr/_//d;

our @ISA = qw(File::Spec::Unix);

sub case_tolerant { 1 }


sub canonpath {
    my ( $self, $path ) = @_;
    return $path;
}


sub catdir {
    my $self = shift;
    return '' unless @_;
    my @args = @_;
    my $first_arg;
    my $relative;

    if ( $args[0] eq '' ) {
        shift @args;
        $relative  = 0;
        $first_arg = $self->rootdir;

    }
    elsif ( $args[0] =~ /^[^:]+:/ ) {
        $relative  = 0;
        $first_arg = shift @args;
        $first_arg = "$first_arg:" unless ( $first_arg =~ /:\Z(?!\n)/ );

    }
    else {
        $relative = 1;
        if ( $args[0] =~ /^::+\Z(?!\n)/ ) {
            $first_arg = ':';
        }
        elsif ( $args[0] eq ':' ) {
            $first_arg = shift @args;
        }
        else {
            $first_arg = shift @args;
            $first_arg = "$first_arg:" unless ( $first_arg =~ /:\Z(?!\n)/ );
        }
    }

    my $result = $first_arg;
    while (@args) {
        my $arg = shift @args;
        unless ( ( $arg eq '' ) || ( $arg eq ':' ) ) {
            if ( $arg =~ /^::+\Z(?!\n)/ ) {
                my $updir_count = length($arg) - 1;
                while ( (@args) && ( $args[0] =~ /^::+\Z(?!\n)/ ) ) {
                    $arg = shift @args;
                    $updir_count += ( length($arg) - 1 );
                }
                $arg = ( ':' x $updir_count );
            }
            else {
                $arg =~ s/^://s;
                $arg = "$arg:" unless ( $arg =~ /:\Z(?!\n)/ );
            }
            $result .= $arg;
        }
    }

    if ( ($relative) && ( $result !~ /^:/ ) ) {
        $result = ":$result";
    }

    unless ($relative) {
        $result =~ s/([^:]+:)(:*)(.*)\Z(?!\n)/$1$3/;
    }

    return $result;
}


sub catfile {
    my $self = shift;
    return '' unless @_;
    my $file = pop @_;
    return $file unless @_;
    my $dir = $self->catdir(@_);
    $file =~ s/^://s;
    return $dir . $file;
}


sub curdir {
    return ":";
}


sub devnull {
    return "Dev:Null";
}


sub rootdir { '' }


sub tmpdir {
    my $cached = $_[0]->_cached_tmpdir('TMPDIR');
    return $cached if defined $cached;
    $_[0]->_cache_tmpdir( $_[0]->_tmpdir( $ENV{TMPDIR} ), 'TMPDIR' );
}


sub updir {
    return "::";
}


sub file_name_is_absolute {
    my ( $self, $file ) = @_;
    if ( $file =~ /:/ ) {
        return ( !( $file =~ m/^:/s ) );
    }
    elsif ( $file eq '' ) {
        return 1;
    }
    else {
        return 0;
    }
}


sub path {
    return unless exists $ENV{Commands};
    return split( /,/, $ENV{Commands} );
}


sub splitpath {
    my ( $self, $path, $nofile ) = @_;
    my ( $volume, $directory, $file );

    if ($nofile) {
        ( $volume, $directory ) = $path =~ m|^((?:[^:]+:)?)(.*)|s;
    }
    else {
        $path =~ m|^( (?: [^:]+: )? )
               ( (?: .*: )? )
               ( .* )
             |xs;
        $volume    = $1;
        $directory = $2;
        $file      = $3;
    }

    $volume    = '' unless defined($volume);
    $directory = ":$directory" if ( $volume && $directory );
    if ($directory) {
        $directory .= ':'          unless ( substr( $directory, -1 ) eq ':' );
        $directory = ":$directory" unless ( substr( $directory, 0, 1 ) eq ':' );
    }
    else {
        $directory = '';
    }
    $file = '' unless defined($file);

    return ( $volume, $directory, $file );
}


sub splitdir {
    my ( $self, $path ) = @_;
    my @result = ();
    my ( $head, $sep, $tail, $volume, $directories );

    return @result if ( ( !defined($path) ) || ( $path eq '' ) );
    return (':')   if ( $path eq ':' );

    ( $volume, $sep, $directories ) = $path =~ m|^((?:[^:]+:)?)(:*)(.*)|s;

    if ($volume) {
        push( @result, $volume );
        $sep .= ':';
    }

    while ( $sep || $directories ) {
        if ( length($sep) > 1 ) {
            my $updir_count = length($sep) - 1;
            for ( my $i = 0 ; $i < $updir_count ; $i++ ) {
                push( @result, '::' );
            }
        }
        $sep = '';
        if ($directories) {
            ( $head, $sep, $tail ) = $directories =~ m|^((?:[^:]+)?)(:*)(.*)|s;
            push( @result, $head );
            $directories = $tail;
        }
    }
    return @result;
}


sub catpath {
    my ( $self, $volume, $directory, $file ) = @_;

    if ( ( !$volume ) && ( !$directory ) ) {
        $file =~ s/^:// if $file;
        return $file;
    }

    my ( $dir_volume, $dir_dirs ) = $self->splitpath( $directory, 1 );

    $volume = $dir_volume unless length $volume;
    my $path = $volume;
    $path .= ':' unless ( substr( $path, -1 ) eq ':' );

    if ($directory) {
        $directory = $dir_dirs if $volume;
        $directory =~ s/^://;
        $path .= $directory;
        $path .= ':' unless ( substr( $path, -1 ) eq ':' );
    }

    if ($file) {
        $file =~ s/^://;
        $path .= $file;
    }

    return $path;
}


sub _resolve_updirs {
    my $path = shift @_;
    my $proceed;

    do {
        $proceed = ( $path =~ s/^(.*):[^:]+::(.*?)\z/$1:$2/ );
    } while ($proceed);

    return $path;
}

sub abs2rel {
    my ( $self, $path, $base ) = @_;

    if ( !$self->file_name_is_absolute($path) ) {
        $path = $self->rel2abs($path);
    }

    if ( !defined($base) || $base eq '' ) {
        $base = Cwd::getcwd();
    }
    elsif ( !$self->file_name_is_absolute($base) ) {
        $base = $self->rel2abs($base);
        $base = _resolve_updirs($base);
    }
    else {
        $base = _resolve_updirs($base);
    }

    my ( $path_vol, $path_dirs, $path_file ) = $self->splitpath($path);
    my ( $base_vol, $base_dirs ) = $self->splitpath($base);

    return $path unless lc($path_vol) eq lc($base_vol);

    my @pathchunks = $self->splitdir($path_dirs);
    my @basechunks = $self->splitdir($base_dirs);

    while (@pathchunks
        && @basechunks
        && lc( $pathchunks[0] ) eq lc( $basechunks[0] ) )
    {
        shift @pathchunks;
        shift @basechunks;
    }

    $path_dirs = $self->catdir( ':', @pathchunks );

    $base_dirs = ( ':' x @basechunks ) . ':';

    return $self->catpath( '', $self->catdir( $base_dirs, $path_dirs ),
        $path_file );
}


sub rel2abs {
    my ( $self, $path, $base ) = @_;

    if ( !$self->file_name_is_absolute($path) ) {
        if ( !defined($base) || $base eq '' ) {
            $base = Cwd::getcwd();
        }
        elsif ( !$self->file_name_is_absolute($base) ) {
            $base = $self->rel2abs($base);
        }

        my ( $path_dirs, $path_file ) = ( $self->splitpath($path) )[ 1, 2 ];

        my ( $base_vol, $base_dirs ) = $self->splitpath($base);

        $path_dirs = ':' if ( $path_dirs eq '' );
        $base_dirs =~ s/:$//;
        $base_dirs = $base_dirs . $path_dirs;

        $path = $self->catpath( $base_vol, $base_dirs, $path_file );
    }
    return $path;
}


1;
