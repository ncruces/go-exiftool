package File::Spec::OS2;

use strict;
use Cwd ();
require File::Spec::Unix;

our $VERSION = '3.94';
$VERSION =~ tr/_//d;

our @ISA = qw(File::Spec::Unix);

sub devnull {
    return "/dev/nul";
}

sub case_tolerant {
    return 1;
}

sub file_name_is_absolute {
    my ( $self, $file ) = @_;
    return scalar( $file =~ m{^([a-z]:)?[\\/]}is );
}

sub path {
    my $path = $ENV{PATH};
    $path =~ s:\\:/:g;
    my @path = split( ';', $path, -1 );
    foreach (@path) { $_ = '.' if $_ eq '' }
    return @path;
}

sub tmpdir {
    my $cached = $_[0]->_cached_tmpdir(qw 'TMPDIR TEMP TMP');
    return $cached if defined $cached;
    my @d = @ENV{qw(TMPDIR TEMP TMP)};
    $_[0]->_cache_tmpdir( $_[0]->_tmpdir( @d, '/tmp', '/' ),
        qw 'TMPDIR TEMP TMP' );
}

sub catdir {
    my $self = shift;
    my @args = @_;
    foreach (@args) {
        tr[\\][/];
        $_ .= "/" unless m{/$};
    }
    return $self->canonpath( join( '', @args ) );
}

sub canonpath {
    my ( $self, $path ) = @_;
    return unless defined $path;

    $path =~ s/^([a-z]:)/\l$1/s;
    $path =~ s|\\|/|g;
    $path =~ s|([^/])/+|$1/|g;
    $path =~ s|(/\.)+/|/|g;
    $path =~ s|^(\./)+(?=[^/])||s;
    $path =~ s|/\Z(?!\n)||
      unless $path =~ m#^([a-z]:)?/\Z(?!\n)#si;
    $path =~ s{^/\.\.$}{/};
    1 while $path =~ s{^/\.\.}{};
    return $path;
}

sub splitpath {
    my ( $self,   $path,      $nofile ) = @_;
    my ( $volume, $directory, $file )   = ( '', '', '' );
    if ($nofile) {
        $path =~ m{^( (?:[a-zA-Z]:|(?:\\\\|//)[^\\/]+[\\/][^\\/]+)? ) 
                 (.*)
             }xs;
        $volume    = $1;
        $directory = $2;
    }
    else {
        $path =~ m{^ ( (?: [a-zA-Z]: |
                      (?:\\\\|//)[^\\/]+[\\/][^\\/]+
                  )?
                )
                ( (?:.*[\\\\/](?:\.\.?\Z(?!\n))?)? )
                (.*)
             }xs;
        $volume    = $1;
        $directory = $2;
        $file      = $3;
    }

    return ( $volume, $directory, $file );
}

sub splitdir {
    my ( $self, $directories ) = @_;
    split m|[\\/]|, $directories, -1;
}

sub catpath {
    my ( $self, $volume, $directory, $file ) = @_;

    $volume .= $1
      if ( $volume =~ m@^([\\/])[\\/][^\\/]+[\\/][^\\/]+\Z(?!\n)@s
        && $directory =~ m@^[^\\/]@s );

    $volume .= $directory;

    if (   $volume !~ m@^[a-zA-Z]:\Z(?!\n)@s
        && $volume =~ m@[^\\/]\Z(?!\n)@
        && $file   =~ m@[^\\/]@ )
    {
        $volume =~ m@([\\/])@;
        my $sep = $1 ? $1 : '/';
        $volume .= $sep;
    }

    $volume .= $file;

    return $volume;
}

sub abs2rel {
    my ( $self, $path, $base ) = @_;

    if ( !$self->file_name_is_absolute($path) ) {
        $path = $self->rel2abs($path);
    }
    else {
        $path = $self->canonpath($path);
    }

    if ( !defined($base) || $base eq '' ) {
        $base = Cwd::getcwd();
    }
    elsif ( !$self->file_name_is_absolute($base) ) {
        $base = $self->rel2abs($base);
    }
    else {
        $base = $self->canonpath($base);
    }

    my ( $path_volume, $path_directories, $path_file ) =
      $self->splitpath( $path, 1 );
    my ( $base_volume, $base_directories ) = $self->splitpath( $base, 1 );
    return $path unless $path_volume eq $base_volume;

    my @pathchunks = $self->splitdir($path_directories);
    my @basechunks = $self->splitdir($base_directories);

    while (@pathchunks
        && @basechunks
        && lc( $pathchunks[0] ) eq lc( $basechunks[0] ) )
    {
        shift @pathchunks;
        shift @basechunks;
    }

    $path_directories = CORE::join( '/', @pathchunks );
    $base_directories = CORE::join( '/', @basechunks );

    $base_directories =~ s|[^\\/]+|..|g;

    if ( $path_directories ne '' && $base_directories ne '' ) {
        $path_directories = "$base_directories/$path_directories";
    }
    else {
        $path_directories = "$base_directories$path_directories";
    }

    return $self->canonpath(
        $self->catpath( "", $path_directories, $path_file ) );
}

sub rel2abs {
    my ( $self, $path, $base ) = @_;

    if ( !$self->file_name_is_absolute($path) ) {

        if ( !defined($base) || $base eq '' ) {
            $base = Cwd::getcwd();
        }
        elsif ( !$self->file_name_is_absolute($base) ) {
            $base = $self->rel2abs($base);
        }
        else {
            $base = $self->canonpath($base);
        }

        my ( $path_directories, $path_file ) =
          ( $self->splitpath( $path, 1 ) )[ 1, 2 ];

        my ( $base_volume, $base_directories ) = $self->splitpath( $base, 1 );

        $path = $self->catpath( $base_volume,
            $self->catdir( $base_directories, $path_directories ), $path_file );
    }

    return $self->canonpath($path);
}

1;
__END__

