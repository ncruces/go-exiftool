
package Term::ANSIColor;

use 5.008;
use strict;
use warnings;

use Exporter;

## no critic (ClassHierarchies::ProhibitExplicitISA)

## no critic (Modules::ProhibitAutomaticExportation)
our ( @EXPORT, @EXPORT_OK, %EXPORT_TAGS, @ISA, $VERSION );

our $AUTOLOAD;

BEGIN {
    $VERSION = '5.01';

    my @colorlist = qw(
      CLEAR           RESET             BOLD            DARK
      FAINT           ITALIC            UNDERLINE       UNDERSCORE
      BLINK           REVERSE           CONCEALED

      BLACK           RED               GREEN           YELLOW
      BLUE            MAGENTA           CYAN            WHITE
      ON_BLACK        ON_RED            ON_GREEN        ON_YELLOW
      ON_BLUE         ON_MAGENTA        ON_CYAN         ON_WHITE

      BRIGHT_BLACK    BRIGHT_RED        BRIGHT_GREEN    BRIGHT_YELLOW
      BRIGHT_BLUE     BRIGHT_MAGENTA    BRIGHT_CYAN     BRIGHT_WHITE
      ON_BRIGHT_BLACK ON_BRIGHT_RED     ON_BRIGHT_GREEN ON_BRIGHT_YELLOW
      ON_BRIGHT_BLUE  ON_BRIGHT_MAGENTA ON_BRIGHT_CYAN  ON_BRIGHT_WHITE
    );

    my @colorlist256 = (
        ( map { ( "ANSI$_", "ON_ANSI$_" ) } 0 .. 255 ),
        ( map { ( "GREY$_", "ON_GREY$_" ) } 0 .. 23 ),
    );
    for my $r ( 0 .. 5 ) {
        for my $g ( 0 .. 5 ) {
            push( @colorlist256,
                map { ( "RGB$r$g$_", "ON_RGB$r$g$_" ) } 0 .. 5 );
        }
    }

    @ISA         = qw(Exporter);
    @EXPORT      = qw(color colored);
    @EXPORT_OK   = qw(uncolor colorstrip colorvalid coloralias);
    %EXPORT_TAGS = (
        constants    => \@colorlist,
        constants256 => \@colorlist256,
        pushpop      => [ @colorlist, qw(PUSHCOLOR POPCOLOR LOCALCOLOR) ],
    );
    Exporter::export_ok_tags( 'pushpop', 'constants256' );
}

our $AUTOLOCAL;

our $AUTORESET;

our $EACHLINE;

#<<<
our %ATTRIBUTES = (
    'clear'          => 0,
    'reset'          => 0,
    'bold'           => 1,
    'dark'           => 2,
    'faint'          => 2,
    'italic'         => 3,
    'underline'      => 4,
    'underscore'     => 4,
    'blink'          => 5,
    'reverse'        => 7,
    'concealed'      => 8,

    'black'          => 30,   'on_black'          => 40,
    'red'            => 31,   'on_red'            => 41,
    'green'          => 32,   'on_green'          => 42,
    'yellow'         => 33,   'on_yellow'         => 43,
    'blue'           => 34,   'on_blue'           => 44,
    'magenta'        => 35,   'on_magenta'        => 45,
    'cyan'           => 36,   'on_cyan'           => 46,
    'white'          => 37,   'on_white'          => 47,

    'bright_black'   => 90,   'on_bright_black'   => 100,
    'bright_red'     => 91,   'on_bright_red'     => 101,
    'bright_green'   => 92,   'on_bright_green'   => 102,
    'bright_yellow'  => 93,   'on_bright_yellow'  => 103,
    'bright_blue'    => 94,   'on_bright_blue'    => 104,
    'bright_magenta' => 95,   'on_bright_magenta' => 105,
    'bright_cyan'    => 96,   'on_bright_cyan'    => 106,
    'bright_white'   => 97,   'on_bright_white'   => 107,
);
#>>>

for my $code ( 0 .. 15 ) {
    $ATTRIBUTES{"ansi$code"}    = "38;5;$code";
    $ATTRIBUTES{"on_ansi$code"} = "48;5;$code";
}

for my $r ( 0 .. 5 ) {
    for my $g ( 0 .. 5 ) {
        for my $b ( 0 .. 5 ) {
            my $code = 16 + ( 6 * 6 * $r ) + ( 6 * $g ) + $b;
            $ATTRIBUTES{"rgb$r$g$b"}    = "38;5;$code";
            $ATTRIBUTES{"on_rgb$r$g$b"} = "48;5;$code";
        }
    }
}

for my $n ( 0 .. 23 ) {
    my $code = $n + 232;
    $ATTRIBUTES{"grey$n"}    = "38;5;$code";
    $ATTRIBUTES{"on_grey$n"} = "48;5;$code";
}

our %ATTRIBUTES_R;
for my $attr ( reverse( sort( keys(%ATTRIBUTES) ) ) ) {
    $ATTRIBUTES_R{ $ATTRIBUTES{$attr} } = $attr;
}

for my $code ( 16 .. 255 ) {
    $ATTRIBUTES{"ansi$code"}    = "38;5;$code";
    $ATTRIBUTES{"on_ansi$code"} = "48;5;$code";
}

our %ALIASES;
if ( exists( $ENV{ANSI_COLORS_ALIASES} ) ) {
    my $spec = $ENV{ANSI_COLORS_ALIASES};
    $spec =~ s{ \A \s+ }{}xms;
    $spec =~ s{ \s+ \z }{}xms;

    ## no critic (ErrorHandling::RequireCarping)
    for my $definition ( split( m{\s*,\s*}xms, $spec ) ) {
        my ( $new, $old ) = split( m{\s*=\s*}xms, $definition, 2 );
        if ( !$new || !$old ) {
            warn qq{Bad color mapping "$definition"};
        }
        else {
            my $result = eval { coloralias( $new, $old ) };
            if ( !$result ) {
                my $error = $@;
                $error =~ s{ [ ] at [ ] .* }{}xms;
                warn qq{$error in "$definition"};
            }
        }
    }
}

our @COLORSTACK;

sub croak {
    my (@args) = @_;
    require Carp;
    Carp::croak(@args);
}

## no critic (ClassHierarchies::ProhibitAutoloading)
## no critic (Subroutines::RequireArgUnpacking)
## no critic (RegularExpressions::ProhibitEnumeratedClasses)
sub AUTOLOAD {
    my ( $sub, $attr ) = $AUTOLOAD =~ m{
        \A ( [a-zA-Z0-9:]* :: ([A-Z0-9_]+) ) \z
    }xms;

    if ( !( $attr && defined( $ATTRIBUTES{ lc $attr } ) ) ) {
        croak("undefined subroutine &$AUTOLOAD called");
    }

    if ( $ENV{ANSI_COLORS_DISABLED} || defined( $ENV{NO_COLOR} ) ) {
        return join( q{}, @_ );
    }

    $AUTOLOAD = $sub;

    my $escape = "\e[" . $ATTRIBUTES{ lc $attr } . 'm';

    my $eval_err = $@;

    ## no critic (BuiltinFunctions::ProhibitStringyEval)
    ## no critic (ValuesAndExpressions::ProhibitImplicitNewlines)
    my $eval_result = eval qq{
        sub $AUTOLOAD {
            if (\$ENV{ANSI_COLORS_DISABLED} || defined(\$ENV{NO_COLOR})) {
                return join(q{}, \@_);
            } elsif (\$AUTOLOCAL && \@_) {
                return PUSHCOLOR('$escape') . join(q{}, \@_) . POPCOLOR;
            } elsif (\$AUTORESET && \@_) {
                return '$escape' . join(q{}, \@_) . "\e[0m";
            } else {
                return '$escape' . join(q{}, \@_);
            }
        }
        1;
    };

    ## no critic (ErrorHandling::RequireCarping)
    if ( !$eval_result ) {
        die "failed to generate constant $attr: $@";
    }

    ## no critic (Variables::RequireLocalizedPunctuationVars)
    $@ = $eval_err;

    goto &$AUTOLOAD;
}

sub PUSHCOLOR {
    my (@text) = @_;
    my $text = join( q{}, @text );

    my ($color) = $text =~ m{ \A ( (?:\e\[ [\d;]+ m)+ ) }xms;

    if (@COLORSTACK) {
        $color = $COLORSTACK[-1] . $color;
    }

    push( @COLORSTACK, $color );
    return $text;
}

sub POPCOLOR {
    my (@text) = @_;
    pop(@COLORSTACK);
    if (@COLORSTACK) {
        return $COLORSTACK[-1] . join( q{}, @text );
    }
    else {
        return RESET(@text);
    }
}

sub LOCALCOLOR {
    my (@text) = @_;
    return PUSHCOLOR( join( q{}, @text ) ) . POPCOLOR();
}

sub color {
    my (@codes) = @_;

    if ( $ENV{ANSI_COLORS_DISABLED} || defined( $ENV{NO_COLOR} ) ) {
        return q{};
    }

    @codes = map { split } @codes;
    @codes = map { defined( $ALIASES{$_} ) ? @{ $ALIASES{$_} } : $_ } @codes;

    ## no critic (RegularExpressions::ProhibitEnumeratedClasses)
    my $attribute = q{};
    for my $code (@codes) {
        $code = lc($code);
        if ( defined( $ATTRIBUTES{$code} ) ) {
            $attribute .= $ATTRIBUTES{$code} . q{;};
        }
        elsif ( $code =~ m{ \A (on_)? r([0-9]+) g([0-9]+) b([0-9]+) \z }xms ) {
            my ( $r, $g, $b ) = ( $2 + 0, $3 + 0, $4 + 0 );
            if ( $r > 255 || $g > 255 || $b > 255 ) {
                croak("Invalid attribute name $code");
            }
            my $prefix = $1 ? '48' : '38';
            $attribute .= "$prefix;2;$r;$g;$b;";
        }
        else {
            croak("Invalid attribute name $code");
        }
    }

    chop($attribute);

    return ( $attribute ne q{} ) ? "\e[${attribute}m" : undef;
}

sub uncolor {
    my (@escapes) = @_;
    my ( @nums, @result );

    for my $escape (@escapes) {
        $escape =~ s{ \A \e\[ }{}xms;
        $escape =~ s{ m \z }   {}xms;
        my ($attrs) = $escape =~ m{ \A ((?:\d+;)* \d*) \z }xms;
        if ( !defined($attrs) ) {
            croak("Bad escape sequence $escape");
        }

        my $regex = qr{
            (
                0*[34]8 ; 0*2 ; \d+ ; \d+ ; \d+
              | 0*[34]8 ; 0*5 ; \d+
              | \d+
            )
            (?: ; | \z )
        }xms;
        push( @nums, $attrs =~ m{$regex}xmsg );
    }

    for my $num (@nums) {
        if ( $num =~ m{ \A 0*([34])8 ; 0*2 ; (\d+) ; (\d+) ; (\d+) \z }xms ) {
            my ( $r, $g, $b ) = ( $2 + 0, $3 + 0, $4 + 0 );
            if ( $r > 255 || $g > 255 || $b > 255 ) {
                croak("No name for escape sequence $num");
            }
            my $prefix = ( $1 == 4 ) ? 'on_' : q{};
            push( @result, "${prefix}r${r}g${g}b${b}" );
        }
        else {
            $num =~ s{ ( \A | ; ) 0+ (\d) }{$1$2}xmsg;
            my $name = $ATTRIBUTES_R{$num};
            if ( !defined($name) ) {
                croak("No name for escape sequence $num");
            }
            push( @result, $name );
        }
    }

    return @result;
}

sub colored {
    my ( $first, @rest ) = @_;
    my ( $string, @codes );
    if ( ref($first) && ref($first) eq 'ARRAY' ) {
        @codes  = @{$first};
        $string = join( q{}, @rest );
    }
    else {
        $string = $first;
        @codes  = @rest;
    }

    if ( $ENV{ANSI_COLORS_DISABLED} || defined( $ENV{NO_COLOR} ) ) {
        return $string;
    }

    my $attr = color(@codes);

    if ( defined($EACHLINE) ) {
        my @text = map { ( $_ ne $EACHLINE ) ? $attr . $_ . "\e[0m" : $_ }
          grep { length > 0 }
          split( m{ (\Q$EACHLINE\E) }xms, $string );
        return join( q{}, @text );
    }
    else {
        return $attr . $string . "\e[0m";
    }
}

sub coloralias {
    my ( $alias, @color ) = @_;
    if ( !@color ) {
        if ( exists( $ALIASES{$alias} ) ) {
            return join( q{ }, @{ $ALIASES{$alias} } );
        }
        else {
            return;
        }
    }

    ## no critic (RegularExpressions::ProhibitEnumeratedClasses)
    if ( $alias !~ m{ \A [a-zA-Z0-9._-]+ \z }xms ) {
        croak(qq{Invalid alias name "$alias"});
    }
    elsif ( $ATTRIBUTES{$alias} ) {
        croak(qq{Cannot alias standard color "$alias"});
    }

    @color = map { split } @color;
    @color = map { defined( $ALIASES{$_} ) ? @{ $ALIASES{$_} } : $_ } @color;

    for my $attribute (@color) {
        if ( !exists( $ATTRIBUTES{$attribute} ) ) {
            croak(qq{Invalid attribute name "$attribute"});
        }
    }

    $ALIASES{$alias} = [@color];
    return join( q{ }, @color );
}

sub colorstrip {
    my (@string) = @_;
    for my $string (@string) {
        $string =~ s{ \e\[ [\d;]* m }{}xmsg;
    }
    return wantarray ? @string : join( q{}, @string );
}

sub colorvalid {
    my (@codes) = @_;
    @codes = map { split( q{ }, lc ) } @codes;
    for my $code (@codes) {
        next if defined( $ATTRIBUTES{$code} );
        next if defined( $ALIASES{$code} );
        if ( $code =~ m{ \A (?: on_ )? r (\d+) g (\d+) b (\d+) \z }xms ) {
            next if ( $1 <= 255 && $2 <= 255 && $3 <= 255 );
        }
        return;
    }
    return 1;
}

1;
__END__


# Local Variables:
# copyright-at-end-flag: t
# End:
