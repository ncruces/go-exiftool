package stable;
$stable::VERSION = '0.035';
use strict;
use warnings;
use version ();

use experimental ();
use Carp         qw/croak carp/;

my %allow_at = (
    bitwise      => 5.022000,
    isa          => 5.032000,
    lexical_subs => 5.022000,
    postderef    => 5.020000,
    const_attr   => 5.022000,
    for_list     => 5.036000,
);

sub import {
    my ( $self, @pragmas ) = @_;

    for my $pragma (@pragmas) {
        my $min_ver = $allow_at{$pragma};
        croak "unknown stablized experiment $pragma" unless defined $min_ver;
        croak
"requested stablized experiment $pragma, which is stable at $min_ver but this is $]"
          unless $] >= $min_ver;
    }

    experimental->import(@pragmas);
    return;
}

sub unimport {
    my ( $self, @pragmas ) = @_;

    experimental->unimport(@pragmas);
    return;
}

1;

__END__

