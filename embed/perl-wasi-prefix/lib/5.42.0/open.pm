package open;
use warnings;

our $VERSION = '1.13';

require 5.008001;

my $locale_encoding;

sub _get_encname {
    return ( $1, Encode::resolve_alias($1) ) if $_[0] =~ /^:?encoding\((.+)\)$/;
    return;
}

sub croak {
    require Carp;
    goto &Carp::croak;
}

sub _drop_oldenc {
    my ( $h, @new ) = @_;
    return unless @new >= 1 && $new[-1] =~ /^:encoding\(.+\)$/;
    my @old = PerlIO::get_layers($h);
    return
         unless @old >= 3
      && $old[-1] eq 'utf8'
      && $old[-2] =~ /^encoding\(.+\)$/;
    require Encode;
    my ( $loname, $lcname ) = _get_encname( $old[-2] );
    unless ( defined $lcname ) {
        croak("open: Unknown encoding '$loname'");
    }
    my ( $voname, $vcname ) = _get_encname( $new[-1] );
    unless ( defined $vcname ) {
        croak("open: Unknown encoding '$voname'");
    }
    if ( $lcname eq $vcname ) {
        binmode( $h, ":pop" );
    }
}

sub import {
    my ( $class, @args ) = @_;
    croak("open: needs explicit list of PerlIO layers") unless @args;
    my $std;
    my ( $in, $out ) = split( /\0/, ( ${^OPEN} || "\0" ), -1 );
    while (@args) {
        my $type = shift(@args);
        my $dscp;
        if ( $type =~ /^:?(utf8|locale|encoding\(.+\))$/ ) {
            $type = 'IO';
            $dscp = ":$1";
        }
        elsif ( $type eq ':std' ) {
            $std = 1;
            next;
        }
        else {
            $dscp = shift(@args) || '';
        }
        my @val;
        foreach my $layer ( split( /\s+/, $dscp ) ) {
            $layer =~ s/^://;
            if ( $layer eq 'locale' ) {
                require Encode;
                require encoding;
                $locale_encoding = encoding::_get_locale_encoding()
                  unless defined $locale_encoding;
                (
                    warnings::warnif(
                        "layer", "Cannot figure out an encoding to use"
                    ),
                    last
                ) unless defined $locale_encoding;
                $layer = "encoding($locale_encoding)";
                $std   = 1;
            }
            else {
                my $target = $layer;
                $target =~ s/^(\w+)\(.+\)$/$1/;

                unless ( PerlIO::Layer::->find( $target, 1 ) ) {
                    warnings::warnif( "layer",
                        "Unknown PerlIO layer '$target'" );
                }
            }
            push( @val, ":$layer" );
            if ( $layer =~ /^(crlf|raw)$/ ) {
                $^H{"open_$type"} = $layer;
            }
        }
        if ( $type eq 'IN' ) {
            _drop_oldenc( *STDIN, @val ) if $std;
            $in = join( ' ', @val );
        }
        elsif ( $type eq 'OUT' ) {
            if ($std) {
                _drop_oldenc( *STDOUT, @val );
                _drop_oldenc( *STDERR, @val );
            }
            $out = join( ' ', @val );
        }
        elsif ( $type eq 'IO' ) {
            if ($std) {
                _drop_oldenc( *STDIN,  @val );
                _drop_oldenc( *STDOUT, @val );
                _drop_oldenc( *STDERR, @val );
            }
            $in = $out = join( ' ', @val );
        }
        else {
            croak "Unknown PerlIO layer class '$type' (need IN, OUT or IO)";
        }
    }
    ${^OPEN} = join( "\0", $in, $out );
    if ($std) {
        if ($in) {
            binmode STDIN, $in;
        }
        if ($out) {
            binmode( STDOUT, $out );
            binmode( STDERR, $out );
        }
    }
}

1;
__END__

