package Tie::RefHash;

our $VERSION = '1.41';

use Tie::Hash;
our @ISA = qw(Tie::Hash);
use strict;
use Carp ();

use Scalar::Util qw(refaddr);

BEGIN {
    use Config ();
    my $usethreads = $Config::Config{usethreads};
    *_HAS_THREADS = $usethreads                   ? sub () { 1 } : sub () { 0 };
    *_HAS_WEAKEN = defined(&Scalar::Util::weaken) ? sub () { 1 } : sub () { 0 };
}

my ( @thread_object_registry, $count );

sub TIEHASH {
    my $c = shift;
    my $s = [];
    bless $s, $c;
    while (@_) {
        $s->STORE( shift, shift );
    }

    if (_HAS_THREADS) {

        if (_HAS_WEAKEN) {
            push @thread_object_registry, $s;
            Scalar::Util::weaken( $thread_object_registry[-1] );

            if ( ++$count > 1000 ) {
                @thread_object_registry = grep defined, @thread_object_registry;
                Scalar::Util::weaken($_) for @thread_object_registry;
                $count = 0;
            }
        }
        else {
            $count++;
        }
    }

    return $s;
}

my $storable_format_version = join( "/", __PACKAGE__, "0.01" );

sub STORABLE_freeze {
    my ( $self, $is_cloning ) = @_;
    my ( $refs, $reg )        = @$self;
    return ( $storable_format_version, [ values %$refs ], $reg || {} );
}

sub STORABLE_thaw {
    my ( $self, $is_cloning, $version, $refs, $reg ) = @_;
    Carp::croak "incompatible versions of Tie::RefHash between freeze and thaw"
      unless $version eq $storable_format_version;

    @$self = ( {}, $reg );
    $self->_reindex_keys($refs);
}

sub CLONE {
    my $pkg = shift;

    if ( $count and not _HAS_WEAKEN ) {
        warn "Tie::RefHash is not threadsafe without Scalar::Util::weaken";
    }

    @thread_object_registry = grep defined && do { $_->_reindex_keys; 1 },
      @thread_object_registry;
    Scalar::Util::weaken($_) for @thread_object_registry;
    $count = 0;
}

sub _reindex_keys {
    my ( $self, $extra_keys ) = @_;
    %{ $self->[0] } = map +( refaddr( $_->[0] ) => $_ ),
      ( values( %{ $self->[0] } ), @{ $extra_keys || [] } );
}

sub FETCH {
    my ( $s, $k ) = @_;
    if ( ref $k ) {
        my $kstr = refaddr($k);
        if ( defined $s->[0]{$kstr} ) {
            $s->[0]{$kstr}[1];
        }
        else {
            undef;
        }
    }
    else {
        $s->[1]{$k};
    }
}

sub STORE {
    my ( $s, $k, $v ) = @_;
    if ( ref $k ) {
        $s->[0]{ refaddr($k) } = [ $k, $v ];
    }
    else {
        $s->[1]{$k} = $v;
    }
    $v;
}

sub DELETE {
    my ( $s, $k ) = @_;
    ( ref $k )
      ? ( delete( $s->[0]{ refaddr($k) } ) || [] )->[1]
      : delete( $s->[1]{$k} );
}

sub EXISTS {
    my ( $s, $k ) = @_;
    ( ref $k ) ? exists( $s->[0]{ refaddr($k) } ) : exists( $s->[1]{$k} );
}

sub FIRSTKEY {
    my $s = shift;
    keys %{ $s->[0] };
    keys %{ $s->[1] };
    $s->[2] = 0;
    $s->NEXTKEY;
}

sub NEXTKEY {
    my $s = shift;
    my ( $k, $v );
    if ( !$s->[2] ) {
        if ( ( $k, $v ) = each %{ $s->[0] } ) {
            return $v->[0];
        }
        else {
            $s->[2] = 1;
        }
    }
    return each %{ $s->[1] };
}

sub CLEAR {
    my $s = shift;
    $s->[2] = 0;
    %{ $s->[0] } = ();
    %{ $s->[1] } = ();
}

package Tie::RefHash::Nestable;
our @ISA = 'Tie::RefHash';

sub STORE {
    my ( $s, $k, $v ) = @_;
    if ( ref($v) eq 'HASH' and not tied %$v ) {
        my @elems = %$v;
        tie %$v, ref($s), @elems;
    }
    $s->SUPER::STORE( $k, $v );
}

1;

__END__

