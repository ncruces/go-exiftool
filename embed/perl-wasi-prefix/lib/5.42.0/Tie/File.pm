package Tie::File;

use strict;
use warnings;

use Carp ':DEFAULT', 'confess';
use POSIX 'SEEK_SET';
use Fcntl 'O_CREAT', 'O_RDWR', 'LOCK_EX', 'LOCK_SH', 'O_WRONLY', 'O_RDONLY';
use constant O_ACCMODE => O_RDONLY | O_RDWR | O_WRONLY;

our $VERSION = "1.10";
my $DEFAULT_MEMORY_SIZE                  = 1 << 21;
my $DEFAULT_AUTODEFER_THRESHHOLD         = 3;
my $DEFAULT_AUTODEFER_FILELEN_THRESHHOLD = 65536;

my %good_opt = map { $_ => 1, "-$_" => 1 }
  qw(memory dw_size mode recsep discipline
  autodefer autochomp autodefer_threshhold concurrent);

our $DIAGNOSTIC = 0;
our @OFF;
our @H;

sub TIEARRAY {
    if ( @_ % 2 != 0 ) {
        croak "usage: tie \@array, $_[0], filename, [option => value]...";
    }
    my ( $pack, $file, %opts ) = @_;

    for my $key ( keys %opts ) {
        unless ( $good_opt{$key} ) {
            croak("$pack: Unrecognized option '$key'\n");
        }
        my $okey = $key;
        if ( $key =~ s/^-+// ) {
            $opts{$key} = delete $opts{$okey};
        }
    }

    if ( $opts{concurrent} ) {
        croak("$pack: concurrent access not supported yet\n");
    }

    unless ( defined $opts{memory} ) {
        $opts{memory} = $DEFAULT_MEMORY_SIZE;
        $opts{memory} = $opts{dw_size}
          if defined $opts{dw_size} && $opts{dw_size} > $DEFAULT_MEMORY_SIZE;
    }
    $opts{dw_size} = $opts{memory} unless defined $opts{dw_size};
    if ( $opts{dw_size} > $opts{memory} ) {
        croak(
            "$pack: dw_size may not be larger than total memory allocation\n");
    }
    $opts{defer}        = 0 unless defined $opts{defer};
    $opts{deferred}     = {};
    $opts{deferred_s}   = 0;
    $opts{deferred_max} = -1;

    $opts{cache} = Tie::File::Cache->new( $opts{memory} );

    $opts{autodefer}            = 1 unless defined $opts{autodefer};
    $opts{autodeferring}        = 0;
    $opts{ad_history}           = [];
    $opts{autodefer_threshhold} = $DEFAULT_AUTODEFER_THRESHHOLD
      unless defined $opts{autodefer_threshhold};
    $opts{autodefer_filelen_threshhold} = $DEFAULT_AUTODEFER_FILELEN_THRESHHOLD
      unless defined $opts{autodefer_filelen_threshhold};

    $opts{offsets}  = [0];
    $opts{filename} = $file;
    unless ( defined $opts{recsep} ) {
        $opts{recsep} = _default_recsep();
    }
    $opts{recseplen} = length( $opts{recsep} );
    if ( $opts{recseplen} == 0 ) {
        croak "Empty record separator not supported by $pack";
    }

    $opts{autochomp} = 1 unless defined $opts{autochomp};

    $opts{mode}       = O_CREAT | O_RDWR unless defined $opts{mode};
    $opts{rdonly}     = ( ( $opts{mode} & O_ACCMODE ) == O_RDONLY );
    $opts{sawlastrec} = undef;

    my $fh;

    if ( UNIVERSAL::isa( $file, 'GLOB' ) ) {
        unless ( seek $file, 1, SEEK_SET ) {
            croak "$pack: your filehandle does not appear to be seekable";
        }
        seek $file, 0, SEEK_SET;
        $fh = $file;
    }
    elsif ( ref $file ) {
        croak "usage: tie \@array, $pack, filename, [option => value]...";
    }
    else {
        sysopen $fh, $file, $opts{mode}, 0666 or return;
        binmode $fh;
        ++$opts{ourfh};
    }
    { my $ofh = select $fh; $| = 1; select $ofh }
    if ( defined $opts{discipline} ) {
        eval { binmode( $fh, $opts{discipline} ) };
        croak $@ if $@ =~ /Unknown discipline|IO layers .* unavailable/;
        die      if $@;
    }
    $opts{fh} = $fh;

    bless \%opts => $pack;
}

sub FETCH {
    my ( $self, $n ) = @_;
    my $rec;

    $rec = $self->{deferred}{$n} if exists $self->{deferred}{$n};
    $rec = $self->_fetch($n) unless defined $rec;

    substr( $rec, -$self->{recseplen} ) = ""
      if defined $rec && $self->{autochomp};
    $rec;
}

sub _chomp {
    my $self = shift;
    return unless $self->{autochomp};
    if ( $self->{autochomp} ) {
        for (@_) {
            next unless defined;
            substr( $_, -$self->{recseplen} ) = "";
        }
    }
}

sub _chomp1 {
    my ( $self, $rec ) = @_;
    return $rec unless $self->{autochomp};
    return      unless defined $rec;
    substr( $rec, -$self->{recseplen} ) = "";
    $rec;
}

sub _fetch {
    my ( $self, $n ) = @_;

    {
        my $cached = $self->{cache}->lookup($n);
        return $cached if defined $cached;
    }

    if ( $#{ $self->{offsets} } < $n ) {
        return if $self->{eof};
        my $o = $self->_fill_offsets_to($n);
        return unless defined $o;
    }

    my $fh = $self->{FH};
    $self->_seek($n);
    my $rec = $self->_read_record;

    $self->{cache}->insert( $n, $rec ) if defined $rec && not $self->{flushing};
    $rec;
}

sub STORE {
    my ( $self, $n, $rec ) = @_;
    die "STORE called from _check_integrity!" if $DIAGNOSTIC;

    $self->_fixrecs($rec);

    if ( $self->{autodefer} ) {
        $self->_annotate_ad_history($n);
    }

    return $self->_store_deferred( $n, $rec ) if $self->_is_deferring;

    my $oldrec = $self->_fetch($n);

    if ( not defined $oldrec ) {
        $self->_extend_file_to( $n + 1 );
        $oldrec = $self->{recsep};
    }
    my $len_diff = length($rec) - length($oldrec);

    $self->_mtwrite( $rec, $self->{offsets}[$n], length($oldrec) );
    $self->_oadjust( [ $n, 1, $rec ] );
    $self->{cache}->update( $n, $rec );
}

sub _store_deferred {
    my ( $self, $n, $rec ) = @_;
    $self->{cache}->remove($n);
    my $old_deferred = $self->{deferred}{$n};

    if ( defined $self->{deferred_max} && $n > $self->{deferred_max} ) {
        $self->{deferred_max} = $n;
    }
    $self->{deferred}{$n} = $rec;

    my $len_diff = length($rec);
    $len_diff -= length($old_deferred) if defined $old_deferred;
    $self->{deferred_s} += $len_diff;
    $self->{cache}->adj_limit( -$len_diff );
    if ( $self->{deferred_s} > $self->{dw_size} ) {
        $self->_flush;
    }
    elsif ( $self->_cache_too_full ) {
        $self->_cache_flush;
    }
}

sub _delete_deferred {
    my ( $self, $n ) = @_;
    my $rec = delete $self->{deferred}{$n};
    return unless defined $rec;

    if ( defined $self->{deferred_max}
        && $n == $self->{deferred_max} )
    {
        undef $self->{deferred_max};
    }

    $self->{deferred_s} -= length $rec;
    $self->{cache}->adj_limit( length $rec );
}

sub FETCHSIZE {
    my $self = shift;
    my $n    = $self->{eof} ? $#{ $self->{offsets} } : $self->_fill_offsets;

    my $top_deferred = $self->_defer_max;
    $n = $top_deferred + 1 if defined $top_deferred && $n < $top_deferred + 1;
    $n;
}

sub STORESIZE {
    my ( $self, $len ) = @_;

    if ( $self->{autodefer} ) {
        $self->_annotate_ad_history('STORESIZE');
    }

    my $olen = $self->FETCHSIZE;
    return if $len == $olen;

    if ( $len > $olen ) {
        if ( $self->_is_deferring ) {
            for ( $olen .. $len - 1 ) {
                $self->_store_deferred( $_, $self->{recsep} );
            }
        }
        else {
            $self->_extend_file_to($len);
        }
        return;
    }

    if ( $self->_is_deferring ) {
        for ( grep $_ >= $len, keys %{ $self->{deferred} } ) {
            $self->_delete_deferred($_);
        }
        $self->{deferred_max} = $len - 1;
    }

    $self->_seek($len);
    $self->_chop_file;
    $#{ $self->{offsets} } = $len;

    $self->{cache}->remove( grep $_ >= $len, $self->{cache}->ckeys );
}

sub PUSH {
    my $self = shift;
    $self->SPLICE( $self->FETCHSIZE, scalar(@_), @_ );

}

sub POP {
    my $self = shift;
    my $size = $self->FETCHSIZE;
    return if $size == 0;
    scalar $self->SPLICE( $size - 1, 1 );
}

sub SHIFT {
    my $self = shift;
    scalar $self->SPLICE( 0, 1 );
}

sub UNSHIFT {
    my $self = shift;
    $self->SPLICE( 0, 0, @_ );
}

sub CLEAR {
    my $self = shift;

    if ( $self->{autodefer} ) {
        $self->_annotate_ad_history('CLEAR');
    }

    $self->_seekb(0);
    $self->_chop_file;
    $self->{cache}->set_limit( $self->{memory} );
    $self->{cache}->empty;
    @{ $self->{offsets} }  = (0);
    %{ $self->{deferred} } = ();
    $self->{deferred_s}   = 0;
    $self->{deferred_max} = -1;
}

sub EXTEND {
    my ( $self, $n ) = @_;

    return if $self->_is_deferring;

    $self->_fill_offsets_to($n);
    $self->_extend_file_to($n);
}

sub DELETE {
    my ( $self, $n ) = @_;

    if ( $self->{autodefer} ) {
        $self->_annotate_ad_history('DELETE');
    }

    my $lastrec = $self->FETCHSIZE - 1;
    my $rec     = $self->FETCH($n);
    $self->_delete_deferred($n) if $self->_is_deferring;
    if ( $n == $lastrec ) {
        $self->_seek($n);
        $self->_chop_file;
        $#{ $self->{offsets} }--;
        $self->{cache}->remove($n);
    }
    elsif ( $n < $lastrec ) {
        $self->STORE( $n, "" );
    }
    $rec;
}

sub EXISTS {
    my ( $self, $n ) = @_;
    return 1 if exists $self->{deferred}{$n};
    $n < $self->FETCHSIZE;
}

sub SPLICE {
    my $self = shift;

    if ( $self->{autodefer} ) {
        $self->_annotate_ad_history('SPLICE');
    }

    $self->_flush if $self->_is_deferring;
    if (wantarray) {
        $self->_chomp( my @a = $self->_splice(@_) );
        @a;
    }
    else {
        $self->_chomp1( scalar $self->_splice(@_) );
    }
}

sub DESTROY {
    my $self = shift;
    $self->flush           if $self->_is_deferring;
    $self->{cache}->delink if defined $self->{cache};
    if ( $self->{fh} and $self->{ourfh} ) {
        delete $self->{ourfh};
        close delete $self->{fh};
    }
}

sub _splice {
    my ( $self, $pos, $nrecs, @data ) = @_;
    my @result;

    $pos = 0 unless defined $pos;

    {
        my $oldsize = $self->FETCHSIZE;
        $nrecs = $oldsize unless defined $nrecs;
        my $oldpos = $pos;

        if ( $pos < 0 ) {
            $pos += $oldsize;
            if ( $pos < 0 ) {
                croak "Modification of non-creatable array value attempted, "
                  . "subscript $oldpos";
            }
        }

        if ( $pos > $oldsize ) {
            return unless @data;
            $pos = $oldsize;
        }

        if ( $nrecs < 0 ) {
            $nrecs = $oldsize - $pos + $nrecs;
            $nrecs = 0 if $nrecs < 0;
        }

        if ( $nrecs + $pos > $oldsize ) {
            $nrecs = $oldsize - $pos;
        }
    }

    $self->_fixrecs(@data);
    my $data    = join '', @data;
    my $datalen = length $data;
    my $oldlen  = 0;

    for ( $pos .. $pos + $nrecs - 1 ) {
        last unless defined $self->_fill_offsets_to($_);
        my $rec = $self->_fetch($_);
        last unless defined $rec;
        push @result, $rec;

        $oldlen += $self->{offsets}[ $_ + 1 ] - $self->{offsets}[$_]
          if defined $self->{offsets}[ $_ + 1 ];
    }
    $self->_fill_offsets_to( $pos + $nrecs );

    $self->_mtwrite( $data, $self->{offsets}[$pos], $oldlen );
    $self->_oadjust( [ $pos, $nrecs, @data ] );

    {

        for ( $pos .. $pos + $nrecs - 1 ) {
            my $new = $data[ $_ - $pos ];
            if ( defined $new ) {
                $self->{cache}->update( $_, $new );
            }
            else {
                $self->{cache}->remove($_);
            }
        }

        {
            my @oldkeys = grep $_ >= $pos + $nrecs, $self->{cache}->ckeys;
            my @newkeys = map $_ - $nrecs + @data, @oldkeys;
            $self->{cache}->rekey( \@oldkeys, \@newkeys );
        }

        $self->_cache_flush;
    }

    wantarray ? @result : @result ? $result[-1] : undef;
}

sub _twrite {
    my ( $self, $data, $pos, $len ) = @_;

    unless ( defined $pos ) {
        die "\$pos was undefined in _twrite";
    }

    my $len_diff = length($data) - $len;

    if ( $len_diff == 0 ) {
        my $fh = $self->{fh};
        $self->_seekb($pos);
        $self->_write_record($data);
        return;
    }

    my $bufsize = _bufsize($len_diff);
    my ( $writepos, $readpos ) = ( $pos, $pos + $len );
    my $next_block;
    my $more_data;

    do {
        $self->_seekb($readpos);
        my $br = read $self->{fh}, $next_block, $bufsize;
        $more_data = read $self->{fh}, my ($dummy), 1;
        $self->_seekb($writepos);
        $self->_write_record($data);
        $readpos  += $br;
        $writepos += length $data;
        $data = $next_block;
    } while $more_data;
    $self->_seekb($writepos);
    $self->_write_record($next_block);

    $self->_chop_file if $len_diff < 0;
}

sub _iwrite {
    my $self = shift;
    my ( $D, $s, $e ) = @_;
    my $d = length $D;
    my $c = $e - $s - $d;
    local *FH = $self->{fh};
    confess "Not enough space to insert $d bytes between $s and $e"
      if $c < 0;
    confess "[$s,$e) is an invalid insertion range" if $e < $s;

    $self->_seekb($s);
    read FH, my $buf, $e - $s;

    $D .= substr( $buf, 0, $c, "" );

    $self->_seekb($s);
    $self->_write_record($D);

    return $buf;
}

sub _mtwrite {
    my $self      = shift;
    my $unwritten = "";
    my $delta     = 0;

    @_ % 3 == 0
      or die "Arguments to _mtwrite did not come in groups of three";

    while (@_) {
        my ( $data, $pos, $len ) = splice @_, 0, 3;
        my $end = $pos + $len;
        $data = $unwritten . $data;
        $delta -= length($unwritten);
        $unwritten = "";
        $pos += $delta;
        my $dlen = length $data;
        $self->_seekb($pos);

        if ( $len >= $dlen ) {
            $self->_write_record($data);
            $delta += ( $dlen - $len );
            $data = "";
        }
        else {
            my $writable = substr( $data, 0, $len - $delta, "" );
            $self->_write_record($writable);
            $delta += ( $dlen - $len );
        }

        my $ndlen = length $data;
        if ( $delta == 0 ) {
            $self->_write_record($data);
        }
        elsif ( $delta < 0 ) {
            if (@_) {
                $self->_upcopy( $end, $end + $delta, $_[1] - $end );
            }
            else {
                $self->_upcopy( $end, $end + $delta );
            }
        }
        else {
            if (@_) {
                $unwritten = $self->_downcopy( $data, $end, $_[1] - $end );
            }
            else {
                $unwritten = $self->_downcopy( $data, $end );
            }
        }
    }
}

sub _upcopy {
    my $blocksize = 8192;
    my ( $self, $spos, $dpos, $len ) = @_;
    if ( $dpos > $spos ) {
        die "source ($spos) was upstream of destination ($dpos) in _upcopy";
    }
    elsif ( $dpos == $spos ) {
        return;
    }

    while ( !defined($len) || $len > 0 ) {
        my $readsize =
            !defined($len)    ? $blocksize
          : $len > $blocksize ? $blocksize
          :                     $len;

        my $fh = $self->{fh};
        $self->_seekb($spos);
        my $bytes_read = read $fh, my ($data), $readsize;
        $self->_seekb($dpos);
        if ( $data eq "" ) {
            $self->_chop_file;
            last;
        }
        $self->_write_record($data);
        $spos += $bytes_read;
        $dpos += $bytes_read;
        $len  -= $bytes_read if defined $len;
    }
}

sub _downcopy {
    my $blocksize = 8192;
    my ( $self, $data, $pos, $len ) = @_;
    my $fh = $self->{fh};

    while ( !defined $len || $len > 0 ) {
        my $readsize =
            !defined($len)    ? $blocksize
          : $len > $blocksize ? $blocksize
          :                     $len;
        $self->_seekb($pos);
        read $fh, my ($old), $readsize;
        my $last_read_was_short = length($old) < $readsize;
        $data .= $old;
        my $writable;
        if ($last_read_was_short) {
            $writable = $data;
            $data     = "";
        }
        else {
            $writable = substr( $data, 0, $readsize, "" );
        }
        last if $writable eq "";
        $self->_seekb($pos);
        $self->_write_record($writable);
        last if $last_read_was_short && $data eq "";
        $len -= $readsize if defined $len;
        $pos += $readsize;
    }
    return $data;
}

sub _oadjust {
    my $self       = shift;
    my $delta      = 0;
    my $delta_recs = 0;
    my $prev_end   = -1;

    for (@_) {
        my ( $pos, $nrecs, @data ) = @$_;
        $pos += $delta_recs;

        for my $i ( $prev_end + 2 .. $pos - 1 ) {
            $self->{offsets}[$i] += $delta;
        }

        $prev_end = $pos + @data - 1;

        my @newoff = ( $self->{offsets}[$pos] + $delta );
        for my $i ( 0 .. $#data ) {
            my $newlen = length $data[$i];
            push @newoff, $newoff[$i] + $newlen;
            $delta += $newlen;
        }

        for my $i ( $pos .. $pos + $nrecs - 1 ) {
            last if $i + 1 > $#{ $self->{offsets} };
            my $oldlen = $self->{offsets}[ $i + 1 ] - $self->{offsets}[$i];
            $delta -= $oldlen;
        }

        splice @{ $self->{offsets} }, $pos, $nrecs + 1, @newoff;

        $delta_recs += @data - $nrecs;
    }

    if ($delta) {
        for my $i ( $prev_end + 2 .. $#{ $self->{offsets} } ) {
            $self->{offsets}[$i] += $delta;
        }
    }

    $self->{offsets}[0] = 0 unless @{ $self->{offsets} };

    $self->_cache_flush;
}

sub _fixrecs {
    my $self = shift;
    for (@_) {
        $_ = "" unless defined $_;
        $_ .= $self->{recsep}
          unless substr( $_, -$self->{recseplen} ) eq $self->{recsep};
    }
}

sub _seek {
    my ( $self, $n ) = @_;
    my $o = $self->{offsets}[$n];
    defined($o)
      or confess("logic error: undefined offset for record $n");
    seek $self->{fh}, $o, SEEK_SET
      or confess "Couldn't seek filehandle: $!";
}

sub _seekb {
    my ( $self, $b ) = @_;
    seek $self->{fh}, $b, SEEK_SET
      or die "Couldn't seek filehandle: $!";
}

sub _fill_offsets_to {
    my ( $self, $n ) = @_;

    return $self->{offsets}[$n] if $self->{eof};

    my $fh = $self->{fh};
    local *OFF = $self->{offsets};
    my $rec;

    until ( $#OFF >= $n ) {
        $self->_seek(-1);
        $rec = $self->_read_record;
        if ( defined $rec ) {
            push @OFF, int( tell $fh );
        }
        else {
            $self->{eof} = 1;
            return;
        }
    }

    $OFF[$n];
}

sub _fill_offsets {
    my ($self) = @_;

    my $fh = $self->{fh};
    local *OFF = $self->{offsets};

    $self->_seek(-1);

    while ( defined $self->_read_record() ) {
        push @OFF, int( tell $fh );
    }

    $self->{eof} = 1;
    $#OFF;
}

sub _write_record {
    my ( $self, $rec ) = @_;
    my $fh = $self->{fh};
    local $\ = "";
    print $fh $rec
      or die "Couldn't write record: $!";
}

sub _read_record {
    my $self = shift;
    my $rec;
    {
        local $/ = $self->{recsep};
        my $fh = $self->{fh};
        $rec = <$fh>;
    }
    return unless defined $rec;
    if ( substr( $rec, -$self->{recseplen} ) ne $self->{recsep} ) {
        $self->{sawlastrec} = 1;
        unless ( $self->{rdonly} ) {
            local $\ = "";
            my $fh = $self->{fh};
            print $fh $self->{recsep};
        }
        $rec .= $self->{recsep};
    }
    $rec;
}

sub _rw_stats {
    my $self = shift;
    @{$self}{ '_read', '_written' };
}

sub _cache_flush {
    my ($self) = @_;
    $self->{cache}->reduce_size_to( $self->{memory} - $self->{deferred_s} );
}

sub _cache_too_full {
    my $self = shift;
    $self->{cache}->bytes + $self->{deferred_s} >= $self->{memory};
}

sub _extend_file_to {
    my ( $self, $n ) = @_;
    $self->_seek(-1);
    my $pos = $self->{offsets}[-1];

    my $extras = $n - $#{ $self->{offsets} };

    while ( $extras-- > 0 ) {
        $self->_write_record( $self->{recsep} );
        push @{ $self->{offsets} }, int( tell $self->{fh} );
    }
}

sub _chop_file {
    my $self = shift;
    truncate $self->{fh}, tell( $self->{fh} );
}

sub _bufsize {
    my $n = shift;
    return 8192 if $n <= 0;
    my $b = $n & ~8191;
    $b += 8192 if $n & 8191;
    $b;
}

sub flock {
    my ( $self, $op ) = @_;
    unless ( @_ <= 3 ) {
        my $pack = ref $self;
        croak "Usage: $pack\->flock([OPERATION])";
    }
    my $fh = $self->{fh};
    $op = LOCK_EX unless defined $op;
    my $locked = flock $fh, $op;

    if ( $locked && ( $op & ( LOCK_EX | LOCK_SH ) ) ) {
        $self->{offsets} = [0];
        $self->{cache}->empty;
    }

    $locked;
}

sub autochomp {
    my $self = shift;
    if (@_) {
        my $old = $self->{autochomp};
        $self->{autochomp} = shift;
        $old;
    }
    else {
        $self->{autochomp};
    }
}

sub offset {
    my ( $self, $n ) = @_;

    if ( $#{ $self->{offsets} } < $n ) {
        return if $self->{eof};
        my $o = $self->_fill_offsets_to($n);
        return unless defined $o;
    }

    $self->{offsets}[$n];
}

sub discard_offsets {
    my $self = shift;
    $self->{offsets} = [0];
}

sub defer {
    my $self = shift;
    $self->_stop_autodeferring;
    @{ $self->{ad_history} } = ();
    $self->{defer} = 1;
}

sub flush {
    my $self = shift;

    $self->_flush;
    $self->{defer} = 0;
}

sub _old_flush {
    my $self     = shift;
    my @writable = sort { $a <=> $b } ( keys %{ $self->{deferred} } );

    while (@writable) {
        my $first_rec = shift @writable;
        my $last_rec  = $first_rec + 1;
        ++$last_rec, shift @writable
          while @writable && $last_rec == $writable[0];
        --$last_rec;
        $self->_fill_offsets_to($last_rec);
        $self->_extend_file_to($last_rec);
        $self->_splice(
            $first_rec,
            $last_rec - $first_rec + 1,
            @{ $self->{deferred} }{ $first_rec .. $last_rec }
        );
    }

    $self->_discard;
}

sub _flush {
    my $self     = shift;
    my @writable = sort { $a <=> $b } ( keys %{ $self->{deferred} } );
    my @args;
    my @adjust;

    while (@writable) {
        my $first_rec = shift @writable;
        my $last_rec  = $first_rec + 1;
        ++$last_rec, shift @writable
          while @writable && $last_rec == $writable[0];
        --$last_rec;
        my $end = $self->_fill_offsets_to( $last_rec + 1 );
        if ( not defined $end ) {
            $self->_extend_file_to($last_rec);
            $end = $self->{offsets}[$last_rec];
        }
        my ($start) = $self->{offsets}[$first_rec];
        push @args,
          join( "", @{ $self->{deferred} }{ $first_rec .. $last_rec } ),
          $start,
          $end - $start;
        push @adjust, [
            $first_rec,
            $last_rec - $first_rec + 1,

            @{ $self->{deferred} }{ $first_rec .. $last_rec },
        ];
    }

    $self->_mtwrite(@args);
    $self->_discard;
    $self->_oadjust(@adjust);
}

sub discard {
    my $self = shift;
    $self->_discard;
    $self->{defer} = 0;
}

sub _discard {
    my $self = shift;
    %{ $self->{deferred} } = ();
    $self->{deferred_s}   = 0;
    $self->{deferred_max} = -1;
    $self->{cache}->set_limit( $self->{memory} );
}

sub _is_deferring {
    my $self = shift;
    $self->{defer} || $self->{autodeferring};
}

sub _defer_max {
    my $self = shift;
    return $self->{deferred_max} if defined $self->{deferred_max};
    my $max = -1;
    for my $key ( keys %{ $self->{deferred} } ) {
        $max = $key if $key > $max;
    }
    $self->{deferred_max} = $max;
    $max;
}

sub autodefer {
    my $self = shift;
    if (@_) {
        my $old = $self->{autodefer};
        $self->{autodefer} = shift;
        if ($old) {
            $self->_stop_autodeferring;
            @{ $self->{ad_history} } = ();
        }
        $old;
    }
    else {
        $self->{autodefer};
    }
}

sub _annotate_ad_history {
    my ( $self, $n ) = @_;
    return unless $self->{autodefer};
    return if $self->{defer};
    return unless $self->{offsets}[-1] >= $self->{autodefer_filelen_threshhold};

    local *H = $self->{ad_history};
    if ( $n eq 'CLEAR' ) {
        @H = ( -2, -1 );
        $self->_stop_autodeferring;
    }
    elsif ( $n =~ /^\d+$/ ) {
        if ( @H == 0 ) {
            @H = ( $n, $n );
        }
        else {
            if ( $H[1] == $n - 1 ) {
                $H[1]++;
                if ( $H[1] - $H[0] + 1 >= $self->{autodefer_threshhold} ) {
                    $self->{autodeferring} = 1;
                }
            }
            else {
                @H = ( $n, $n );
                $self->_stop_autodeferring;
            }
        }
    }
    else {
        @H = ();
        $self->_stop_autodeferring;
    }
}

sub _stop_autodeferring {
    my $self = shift;
    if ( $self->{autodeferring} ) {
        $self->_flush;
    }
    $self->{autodeferring} = 0;
}

sub _default_recsep {
    my $recsep = $/;
    if ( $^O eq 'MSWin32' ) {

        $recsep =~ s/\n/\r\n/g;
    }
    $recsep;
}

sub _ci_warn {
    my $msg = shift;
    $msg =~ s/\n/\\n/g;
    $msg =~ s/\r/\\r/g;
    print "# $msg\n";
}

sub _check_integrity {
    my ( $self, $file, $warn ) = @_;
    my $rsl  = $self->{recseplen};
    my $rs   = $self->{recsep};
    my $good = 1;
    local *_;
    local $DIAGNOSTIC = 1;

    if ( not defined $rs ) {
        _ci_warn("recsep is undef!");
        $good = 0;
    }
    elsif ( $rs eq "" ) {
        _ci_warn("recsep is empty!");
        $good = 0;
    }
    elsif ( $rsl != length $rs ) {
        my $ln = length $rs;
        _ci_warn("recsep <$rs> has length $ln, should be $rsl");
        $good = 0;
    }

    if ( not defined $self->{offsets}[0] ) {
        _ci_warn("offset 0 is missing!");
        $good = 0;

    }
    elsif ( $self->{offsets}[0] != 0 ) {
        _ci_warn("rec 0: offset <$self->{offsets}[0]> s/b 0!");
        $good = 0;
    }

    my $cached = 0;
    {
        local *F = $self->{fh};
        seek F, 0, SEEK_SET;
        local $. = 0;
        local $/ = $rs;

        while (<F>) {
            my $n      = $. - 1;
            my $cached = $self->{cache}->_produce($n);
            my $offset = $self->{offsets}[$.];
            my $ao     = tell F;
            if ( defined $offset && $offset != $ao ) {
                _ci_warn("rec $n: offset <$offset> actual <$ao>");
                $good = 0;
            }
            if ( defined $cached && $_ ne $cached && !$self->{deferred}{$n} ) {
                $good = 0;
                _ci_warn("rec $n: cached <$cached> actual <$_>");
            }
            if ( defined $cached && substr( $cached, -$rsl ) ne $rs ) {
                $good = 0;
                _ci_warn("rec $n in the cache is missing the record separator");
            }
            if ( !defined $offset && $self->{eof} ) {
                $good = 0;
                _ci_warn(
                    "The offset table was marked complete, but it is missing "
                      . "element $." );
            }
        }
        if ( @{ $self->{offsets} } > $. + 1 ) {
            $good = 0;
            my $n = @{ $self->{offsets} };
            _ci_warn("The offset table has $n items, but the file has only $.");
        }

        my $deferring = $self->_is_deferring;
        for my $n ( $self->{cache}->ckeys ) {
            my $r = $self->{cache}->_produce($n);
            $cached += length($r);
            next if $n + 1 <= $.;
            _ci_warn("spurious caching of record $n");
            $good = 0;
        }
        my $b = $self->{cache}->bytes;
        if ( $cached != $b ) {
            _ci_warn("cache size is $b, should be $cached");
            $good = 0;
        }
    }

    $good = 0 unless $self->{cache}->_check_integrity;

    if ( !$self->_is_deferring && %{ $self->{deferred} } ) {
        _ci_warn("deferred writing disabled, but deferbuffer nonempty");
        $good = 0;
    }

    my $deferred_s = 0;
    while ( my ( $n, $r ) = each %{ $self->{deferred} } ) {
        $deferred_s += length($r);
        if ( defined $self->{cache}->_produce($n) ) {
            _ci_warn("record $n is in the deferbuffer *and* the readcache");
            $good = 0;
        }
        if ( substr( $r, -$rsl ) ne $rs ) {
            _ci_warn(
                "rec $n in the deferbuffer is missing the record separator");
            $good = 0;
        }
    }

    if ( $deferred_s != $self->{deferred_s} ) {
        _ci_warn("buffer size is $self->{deferred_s}, should be $deferred_s");
        $good = 0;
    }

    if ( $deferred_s > $self->{dw_size} ) {
        _ci_warn( "buffer size is $self->{deferred_s} which exceeds the limit "
              . "of $self->{dw_size}" );
        $good = 0;
    }

    if ( $deferred_s + $cached > $self->{memory} ) {
        my $total = $deferred_s + $cached;
        _ci_warn( "total stored data size is $total which exceeds the limit "
              . "of $self->{memory}" );
        $good = 0;
    }

    if ( !$self->{autodefer} && @{ $self->{ad_history} } ) {
        _ci_warn("autodefer is disabled, but ad_history is nonempty");
        $good = 0;
    }
    if ( $self->{autodeferring} && $self->{defer} ) {
        _ci_warn("both autodeferring and explicit deferring are active");
        $good = 0;
    }
    if ( @{ $self->{ad_history} } == 0 ) {
    }
    elsif ( @{ $self->{ad_history} } == 2 ) {
        my @non_number = grep !/^-?\d+$/, @{ $self->{ad_history} };
        if (@non_number) {
            my $msg;
            {
                local $" = ')(';
                $msg =
                  "ad_history contains non-numbers (@{$self->{ad_history}})";
            }
            _ci_warn($msg);
            $good = 0;
        }
        elsif ( $self->{ad_history}[1] < $self->{ad_history}[0] ) {
            _ci_warn(
                "ad_history has nonsensical values @{$self->{ad_history}}");
            $good = 0;
        }
    }
    else {
        _ci_warn("ad_history has bad length <@{$self->{ad_history}}>");
        $good = 0;
    }

    $good;
}

package Tie::File::Cache;
$Tie::File::Cache::VERSION = $Tie::File::VERSION;
use Carp ':DEFAULT', 'confess';

use constant {
    HEAP  => 0,
    HASH  => 1,
    MAX   => 2,
    BYTES => 3,
};

sub new {
    my ( $pack, $max ) = @_;
    local *_;
    croak "missing argument to ->new" unless defined $max;
    my $self = [];
    bless $self => $pack;
    @$self = ( Tie::File::Heap->new($self), {}, $max, 0 );
    $self;
}

sub adj_limit {
    my ( $self, $n ) = @_;
    $self->[MAX] += $n;
}

sub set_limit {
    my ( $self, $n ) = @_;
    $self->[MAX] = $n;
}

sub _heap_move {
    my ( $self, $k, $n ) = @_;
    if ( defined $n ) {
        $self->[HASH]{$k} = $n;
    }
    else {
        delete $self->[HASH]{$k};
    }
}

sub insert {
    my ( $self, $key, $val ) = @_;
    local *_;
    croak "missing argument to ->insert" unless defined $key;
    unless ( defined $self->[MAX] ) {
        confess "undefined max";
    }
    confess "undefined val" unless defined $val;
    return if length($val) > $self->[MAX];

    my $oldnode = $self->[HASH]{$key};
    if ( defined $oldnode ) {
        my $oldval = $self->[HEAP]->set_val( $oldnode, $val );
        $self->[BYTES] -= length($oldval);
    }
    else {
        $self->[HEAP]->insert( $key, $val );
    }
    $self->[BYTES] += length($val);
    $self->flush if $self->[BYTES] > $self->[MAX];
}

sub expire {
    my $self     = shift;
    my $old_data = $self->[HEAP]->popheap;
    return unless defined $old_data;
    $self->[BYTES] -= length $old_data;
    $old_data;
}

sub remove {
    my ( $self, @keys ) = @_;
    my @result;

    for my $key (@keys) {
        next unless exists $self->[HASH]{$key};
        my $old_data = $self->[HEAP]->remove( $self->[HASH]{$key} );
        $self->[BYTES] -= length $old_data;
        push @result, $old_data;
    }
    @result;
}

sub lookup {
    my ( $self, $key ) = @_;
    local *_;
    croak "missing argument to ->lookup" unless defined $key;

    if ( exists $self->[HASH]{$key} ) {
        $self->[HEAP]->lookup( $self->[HASH]{$key} );
    }
    else {
        return;
    }
}

sub _produce {
    my ( $self, $key ) = @_;
    my $loc = $self->[HASH]{$key};
    return unless defined $loc;
    $self->[HEAP][$loc][2];
}

sub _promote {
    my ( $self, $key ) = @_;
    $self->[HEAP]->promote( $self->[HASH]{$key} );
}

sub empty {
    my ($self) = @_;
    %{ $self->[HASH] } = ();
    $self->[BYTES] = 0;
    $self->[HEAP]->empty;
}

sub is_empty {
    my ($self) = @_;
    keys %{ $self->[HASH] } == 0;
}

sub update {
    my ( $self, $key, $val ) = @_;
    local *_;
    croak "missing argument to ->update" unless defined $key;
    if ( length($val) > $self->[MAX] ) {
        my ($oldval) = $self->remove($key);
        $self->[BYTES] -= length($oldval) if defined $oldval;
    }
    elsif ( exists $self->[HASH]{$key} ) {
        my $oldval = $self->[HEAP]->set_val( $self->[HASH]{$key}, $val );
        $self->[BYTES] += length($val);
        $self->[BYTES] -= length($oldval) if defined $oldval;
    }
    else {
        $self->[HEAP]->insert( $key, $val );
        $self->[BYTES] += length($val);
    }
    $self->flush;
}

sub rekey {
    my ( $self, $okeys, $nkeys ) = @_;
    local *_;
    my %map;
    @map{@$okeys} = @$nkeys;
    croak "missing argument to ->rekey"          unless defined $nkeys;
    croak "length mismatch in ->rekey arguments" unless @$nkeys == @$okeys;
    my %adjusted;

    for ( 0 .. $#$okeys ) {
        $adjusted{ $nkeys->[$_] } = delete $self->[HASH]{ $okeys->[$_] };
    }
    while ( my ( $nk, $ix ) = each %adjusted ) {
        $self->[HEAP]->rekey( $ix, $nk );
        $self->[HASH]{$nk} = $ix;
    }
}

sub ckeys {
    my $self = shift;
    my @a    = keys %{ $self->[HASH] };
    @a;
}

sub bytes {
    my $self = shift;
    $self->[BYTES];
}

sub reduce_size_to {
    my ( $self, $max ) = @_;
    until ( $self->[BYTES] <= $max ) {
        my $old_data = $self->[HEAP]->popheap;
        return unless defined $old_data;
        $self->[BYTES] -= length $old_data;
    }
}

sub flush {
    my $self = shift;
    $self->reduce_size_to( $self->[MAX] ) if $self->[BYTES] > $self->[MAX];
}

sub _produce_lru {
    my $self = shift;
    $self->[HEAP]->expire_order;
}

BEGIN { *_ci_warn = \&Tie::File::_ci_warn }

sub _check_integrity {
    my $self = shift;
    my $good = 1;

    $self->[HEAP]->_check_integrity or $good = 0;

    my $bytes = 0;
    for my $k ( keys %{ $self->[HASH] } ) {
        if ( $k ne '0' && $k !~ /^[1-9][0-9]*$/ ) {
            $good = 0;
            _ci_warn "Cache hash key <$k> is non-numeric";
        }

        my $h = $self->[HASH]{$k};
        if ( !defined $h ) {
            $good = 0;
            _ci_warn "Heap index number for key $k is undefined";
        }
        elsif ( $h == 0 ) {
            $good = 0;
            _ci_warn "Heap index number for key $k is zero";
        }
        else {
            my $j = $self->[HEAP][$h];
            if ( !defined $j ) {
                $good = 0;
                _ci_warn "Heap contents key $k (=> $h) are undefined";
            }
            else {
                $bytes += length( $j->[2] );
                if ( $k ne $j->[1] ) {
                    $good = 0;
                    _ci_warn
                      "Heap contents key $k (=> $h) is $j->[1], should be $k";
                }
            }
        }
    }

    if ( $bytes != $self->[BYTES] ) {
        $good = 0;
        _ci_warn "Total data in cache is $bytes, expected $self->[BYTES]";
    }

    if ( $bytes > $self->[MAX] ) {
        $good = 0;
        _ci_warn "Total data in cache is $bytes, exceeds maximum $self->[MAX]";
    }

    return $good;
}

sub delink {
    my $self = shift;
    $self->[HEAP] = undef;
}

package Tie::File::Heap;
use Carp ':DEFAULT', 'confess';
$Tie::File::Heap::VERSION = $Tie::File::Cache::VERSION;
use constant {
    SEQ => 0,
    KEY => 1,
    DAT => 2,
};

sub new {
    my ( $pack, $cache ) = @_;
    die "$pack: Parent cache object $cache does not support _heap_move method"
      unless eval { $cache->can('_heap_move') };
    my $self = [ [ 0, $cache, 0 ] ];
    bless $self => $pack;
}

sub _nseq {
    my $self = shift;
    $self->[0][0]++;
}

sub _cache {
    my $self = shift;
    $self->[0][1];
}

sub _nelts {
    my $self = shift;
    $self->[0][2];
}

sub _nelts_inc {
    my $self = shift;
    ++$self->[0][2];
}

sub _nelts_dec {
    my $self = shift;
    --$self->[0][2];
}

sub is_empty {
    my $self = shift;
    $self->_nelts == 0;
}

sub empty {
    my $self = shift;
    $#$self       = 0;
    $self->[0][2] = 0;
    $self->[0][0] = 0;
}

sub _heap_move {
    my $self = shift;
    $self->_cache->_heap_move(@_);
}

sub insert {
    my ( $self, $key, $data, $seq ) = @_;
    $seq = $self->_nseq unless defined $seq;
    $self->_insert_new( [ $seq, $key, $data ] );
}

sub _insert_new {
    my ( $self, $item ) = @_;
    my $i = @$self;
    $i = int( $i / 2 ) until defined $self->[ $i / 2 ];
    $self->[$i] = $item;
    $self->[0][1]->_heap_move( $self->[$i][KEY], $i );
    $self->_nelts_inc;
}

sub _insert {
    my ( $self, $item, $i ) = @_;
    $i = 1 unless defined $i;
    until ( !defined $self->[$i] ) {
        if ( $self->[$i][SEQ] > $item->[SEQ] ) {
            ( $self->[$i], $item ) = ( $item, $self->[$i] );
            $self->[0][1]->_heap_move( $self->[$i][KEY], $i );
        }
        my $dir;
        $dir = 0 if !defined $self->[ 2 * $i ];
        $dir = 1 if !defined $self->[ 2 * $i + 1 ];
        $dir = int( rand(2) ) unless defined $dir;
        $i   = 2 * $i + $dir;
    }
    $self->[$i] = $item;
    $self->[0][1]->_heap_move( $self->[$i][KEY], $i );
    $self->_nelts_inc;
}

sub remove {
    my ( $self, $i ) = @_;
    $i = 1 unless defined $i;
    my $top = $self->[$i];
    return unless defined $top;
    while (1) {
        my $ii;
        my ( $L, $R ) = ( 2 * $i, 2 * $i + 1 );

        last unless defined $self->[$L] || defined $self->[$R];
        $ii = $R if not defined $self->[$L];
        $ii = $L if not defined $self->[$R];
        unless ( defined $ii ) {
            $ii = $self->[$L][SEQ] < $self->[$R][SEQ] ? $L : $R;
        }

        $self->[$i] = $self->[$ii];
        $self->[0][1]->_heap_move( $self->[$i][KEY], $i );
        $i = $ii;
    }
    $self->[0][1]->_heap_move( $top->[KEY], undef );
    undef $self->[$i];
    $self->_nelts_dec;
    return $top->[DAT];
}

sub popheap {
    my $self = shift;
    $self->remove(1);
}

sub promote {
    my ( $self, $n ) = @_;
    $self->[$n][SEQ] = $self->_nseq;
    my $i = $n;
    while (1) {
        my ( $L, $R ) = ( 2 * $i, 2 * $i + 1 );
        my $dir;
        last unless defined $self->[$L] || defined $self->[$R];
        $dir = $R unless defined $self->[$L];
        $dir = $L unless defined $self->[$R];
        unless ( defined $dir ) {
            $dir = $self->[$L][SEQ] < $self->[$R][SEQ] ? $L : $R;
        }
        @{$self}[ $i, $dir ] = @{$self}[ $dir, $i ];
        for ( $i, $dir ) {
            $self->[0][1]->_heap_move( $self->[$_][KEY], $_ )
              if defined $self->[$_];
        }
        $i = $dir;
    }
}

sub lookup {
    my ( $self, $n ) = @_;
    my $val = $self->[$n];
    $self->promote($n);
    $val->[DAT];
}

sub set_val {
    my ( $self, $n, $val ) = @_;
    my $oval = $self->[$n][DAT];
    $self->[$n][DAT] = $val;
    $self->promote($n);
    return $oval;
}

sub rekey {
    my ( $self, $n, $new_key ) = @_;
    $self->[$n][KEY] = $new_key;
}

sub _check_loc {
    my ( $self, $n ) = @_;
    unless ( 1 || defined $self->[$n] ) {
        confess "_check_loc($n) failed";
    }
}

BEGIN { *_ci_warn = \&Tie::File::_ci_warn }

sub _check_integrity {
    my $self = shift;
    my $good = 1;
    my %seq;

    unless ( eval { $self->[0][1]->isa("Tie::File::Cache") } ) {
        _ci_warn "Element 0 of heap corrupt";
        $good = 0;
    }
    $good = 0 unless $self->_satisfies_heap_condition(1);
    for my $i ( 2 .. $#{$self} ) {
        my $p = int( $i / 2 );
        if ( defined $self->[$i] && !defined $self->[$p] ) {
            _ci_warn "Element $i of heap defined, but parent $p isn't";
            $good = 0;
        }

        if ( defined $self->[$i] ) {
            if ( $seq{ $self->[$i][SEQ] } ) {
                my $seq = $self->[$i][SEQ];
                _ci_warn "Nodes $i and $seq{$seq} both have SEQ=$seq";
                $good = 0;
            }
            else {
                $seq{ $self->[$i][SEQ] } = $i;
            }
        }
    }

    return $good;
}

sub _satisfies_heap_condition {
    my $self = shift;
    my $n    = shift || 1;
    my $good = 1;
    for ( 0, 1 ) {
        my $c = $n * 2 + $_;
        next unless defined $self->[$c];
        if ( $self->[$n][SEQ] >= $self->[$c] ) {
            _ci_warn "Node $n of heap does not predate node $c";
            $good = 0;
        }
        $good = 0 unless $self->_satisfies_heap_condition($c);
    }
    return $good;
}

sub expire_order {
    my $self  = shift;
    my @nodes = sort { $a->[SEQ] <=> $b->[SEQ] } $self->_nodes;
    map { $_->[KEY] } @nodes;
}

sub _nodes {
    my $self = shift;
    my $i    = shift || 1;
    return unless defined $self->[$i];
    ( $self->[$i], $self->_nodes( $i * 2 ), $self->_nodes( $i * 2 + 1 ) );
}

1;

__END__


