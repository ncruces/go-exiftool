use strict;

package Tie::Memoize;
use Tie::Hash;
our @ISA     = 'Tie::ExtraHash';
our $VERSION = '1.1';

our $exists_token = \undef;

sub croak { require Carp; goto &Carp::croak }

sub FETCH {
    my ( $h, $key ) = ( $_[0][0], $_[1] );
    my $res = $h->{$key};
    return $res if defined $res;
    return $res if exists $h->{$key};
    my $cache = $_[0][1]{$key};
    return if defined $cache and not $cache;
    my @res = $_[0][2]->( $key, $_[0][4] );
    $_[0][1]{$key} = 0, return unless @res;
    delete $_[0][1]{$key};
    $_[0][0]{$key} = $res[0];
}

sub EXISTS {
    my ( $a, $key ) = ( shift, shift );
    return 1 if exists $a->[0]{$key};
    my $cache = $a->[1]{$key};
    return $cache if defined $cache;
    my @res = $a->[3]( $key, $a->[4] );
    $a->[1]{$key} = 0, return unless @res;

    return ( $a->[1]{$key} = 1 ) if $a->[5];

    $a->[0]{$key} = $res[0];
    return 1;
}

sub TIEHASH {
    croak 'syntax: tie %hash, \'Tie::AutoLoad\', \&fetch_subr' if @_ < 2;
    croak
'syntax: tie %hash, \'Tie::AutoLoad\', \&fetch_subr, $data, \&exists_subr, \%data_cache, \%existence_cache'
      if @_ > 6;
    push @_, undef if @_ < 3;
    push @_, $_[1] if @_ < 4;
    push @_, {} while @_ < 6;
    bless [ @_[ 4, 5, 1, 3, 2 ], $_[1] ne $_[3] ], $_[0];
}

1;


