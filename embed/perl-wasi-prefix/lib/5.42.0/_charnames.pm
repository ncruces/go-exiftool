
package _charnames;
use strict;
use warnings;
our $VERSION = '1.50';
use unicore::Name;

use bytes ();
use re "/aa";

$Carp::Internal{ (__PACKAGE__) } = 1;

my %system_aliases = (

    'SINGLE-SHIFT 2' => chr utf8::unicode_to_native(0x8E),
    'SINGLE-SHIFT 3' => chr utf8::unicode_to_native(0x8F),
    'PRIVATE USE 1'  => chr utf8::unicode_to_native(0x91),
    'PRIVATE USE 2'  => chr utf8::unicode_to_native(0x92),
);

my $HANGUL_JUNGSEONG_O_E_utf8 = chr 0x1180;
my $HANGUL_JUNGSEONG_OE_utf8  = chr 0x116C;

my $txt;

my %full_names_cache;

my %loose_names_cache;

my $decimal_qr = qr/^[1-9]\d*$/;

my $hex_qr = qr/^(?:[Uu]\+|0[xX])?([[:xdigit:]]+)$/;

sub croak {
    require Carp;
    goto &Carp::croak;
}

sub carp {
    require Carp;
    goto &Carp::carp;
}

sub populate_txt() {
    return if $txt;

    $txt = do "unicore/Name.pl";
    Internals::SvREADONLY( $txt, 1 );
}

sub alias (@) {
    my @errors;
    my $nbsp = chr utf8::unicode_to_native(0xA0);

    my $alias = ref $_[0] ? $_[0] : {@_};
    foreach my $name ( sort keys %$alias ) {

        my $value = $alias->{$name};
        next unless defined $value;

        if ( $value !~ $decimal_qr && $value =~ $hex_qr ) {
            my $temp = CORE::hex $1;
            $temp  = utf8::unicode_to_native($temp) if $value =~ /^[Uu]\+/;
            $value = $temp;
        }
        if ( $value =~ $decimal_qr ) {
            no warnings qw(non_unicode surrogate nonchar);
            $^H{charnames_ord_aliases}{$name} = chr $value;

            $^H{charnames_inverse_ords}{ sprintf( "%05X", $value ) } = $name;
        }
        else {
            my $ok_portion = "";
            $ok_portion = $1 if $name =~ / ^ (
                                            \p{_Perl_Charname_Begin}
                                            \p{_Perl_Charname_Continue}*
                                         ) /x;

            if ( length $ok_portion < length $name ) {
                my $first_bad = substr( $name, length($ok_portion), 1 );
                push @errors,
                    "Invalid character in charnames alias definition; "
                  . "marked by <-- HERE in '$ok_portion$first_bad<-- HERE "
                  . substr( $name, length($ok_portion) + 1 ) . "'";
            }
            else {
                if ( $name =~ / ( .* \s ) ( \s* ) $ /x ) {
                    push @errors,
                        "charnames alias definitions may not contain "
                      . "trailing white-space; marked by <-- HERE in "
                      . "'$1 <-- HERE "
                      . $2 . "'";
                    next;
                }

                if ( $name =~ / ( .*? \s{2} ) ( .+ ) /x ) {
                    push @errors,
                        "charnames alias definitions may not contain a "
                      . "sequence of multiple spaces; marked by <-- HERE "
                      . "in '$1 <-- HERE "
                      . $2 . "'";
                    next;
                }

                $^H{charnames_name_aliases}{$name} = $value;
            }
        }
    }

    if (@errors) {
        croak join "\n", @errors;
    }

    return;
}

sub not_legal_use_bytes_msg {
    my ( $name, $utf8 ) = @_;
    my $return;

    if ( length($utf8) == 1 ) {
        $return =
          sprintf( "Character 0x%04x with name '%s' is", ord $utf8, $name );
    }
    else {
        $return = sprintf(
            "String with name '%s' (and ordinals %s) contains character(s)",
            $name,
            join( " ", map { sprintf "0x%04X", ord $_ } split( //, $utf8 ) ) );
    }
    return $return . " above 0xFF with 'use bytes' in effect";
}

sub alias_file ($) {
    require File::Spec;
    my ( $arg, $file ) = @_;
    if ( -f $arg && File::Spec->file_name_is_absolute($arg) ) {
        $file = $arg;
    }
    elsif ( $arg =~ m/ ^ \p{_Perl_IDStart} \p{_Perl_IDCont}* $/x ) {
        $file = "unicore/${arg}_alias.pl";
    }
    else {
        croak "Charnames alias file names can only have identifier characters";
    }
    if ( my @alias = do $file ) {
        @alias == 1 && !defined $alias[0]
          and croak "$file cannot be used as alias file for charnames";
        @alias % 2
          and croak "$file did not return a (valid) list of alias pairs";
        alias(@alias);
        return (1);
    }
    0;
}

my %dummy_H = (
    charnames_stringified_names => "",
    charnames_stringified_ords  => "",
    charnames_scripts           => "",
    charnames_full              => 1,
    charnames_loose             => 0,
    charnames_short             => 0,
);

sub lookup_name ($$$;$) {
    my ( $name, $wants_ord, $runtime, $regex_loose ) = @_;
    $regex_loose //= 0;

    my $result;
    my $save_input;

    if ( $runtime && !$regex_loose ) {

        my $hints_ref = ( caller($runtime) )[10];

        $hints_ref = \%dummy_H
          if !defined $hints_ref
          || ( !defined $hints_ref->{charnames_full}
            && !defined $hints_ref->{charnames_loose} );

        %{ $^H{charnames_name_aliases} } = split ',',
          $hints_ref->{charnames_stringified_names};
        %{ $^H{charnames_ord_aliases} } = split ',',
          $hints_ref->{charnames_stringified_ords};
        $^H{charnames_scripts} = $hints_ref->{charnames_scripts};
        $^H{charnames_full}    = $hints_ref->{charnames_full};
        $^H{charnames_loose}   = $hints_ref->{charnames_loose};
        $^H{charnames_short}   = $hints_ref->{charnames_short};
    }

    my $loose = $regex_loose || $^H{charnames_loose};
    my $lookup_name;

    if ( !$regex_loose && exists $^H{charnames_ord_aliases}{$name} ) {
        $result = $^H{charnames_ord_aliases}{$name};
    }
    elsif ( !$regex_loose && exists $^H{charnames_name_aliases}{$name} ) {
        $name       = $^H{charnames_name_aliases}{$name};
        $save_input = $lookup_name = $name;

        if ($loose) {
            $loose = 0;
            $^H{charnames_full} = 1;
        }
    }
    else {

        $lookup_name = $name;
        if ($loose) {
            $lookup_name = uc $lookup_name;

            $lookup_name =~ s/_//g;

            $lookup_name =~ s/ (?<= \S  ) - (?= \S  )//gx;

            $lookup_name =~ s/\s//g;
        }

        if ( exists $system_aliases{$lookup_name} ) {
            $result = $system_aliases{$lookup_name};
        }
    }

    my @off;

    if ( !defined $result ) {

        if ( !$loose && $^H{charnames_full} && exists $full_names_cache{$name} )
        {
            $result = $full_names_cache{$name};
        }
        elsif ( $loose && exists $loose_names_cache{$name} ) {
            $result = $loose_names_cache{$name};
        }
        else {

            my $cache_ref;

            populate_txt() unless $txt;

            if (
                ( $loose || $^H{charnames_full} )
                && (
                    defined(
                        my $ord = charnames::name_to_code_point_special(
                            $lookup_name, $loose
                        )
                    )
                )
              )
            {
                $result = chr $ord;
            }
            else {

                $lookup_name = quotemeta $lookup_name;

                if ($loose) {

                    $lookup_name =~
                      s/ (?! \\ -)    # Don't do this to the \- sequence
                             ( [^-\\] )   # Nor the "-" within that sequence,
                                          # nor the "\" that quotes metachars,
                                          # but otherwise put the char into $1
                             (?=.)        # And don't do it for the final char
                           /$1\[- \]?/gx;

                    $lookup_name =~ s/\\ -/(?:- | -)/xg;
                }

                if ( ( $loose || $^H{charnames_full} )
                    && $txt =~ /^$lookup_name$/m )
                {
                    @off = ( $-[0], $+[0] );
                    $cache_ref =
                      ($loose) ? \%loose_names_cache : \%full_names_cache;
                }
                elsif ($regex_loose) {
                    return;
                }
                else {

                    my $scripts_trie = "";
                    my $name_has_uppercase;
                    my @scripts;
                    if (
                        ( $^H{charnames_short} )
                        && $lookup_name =~ /^ (?: \\ \s)*   # Quoted space
                                    (.+?)         # $1 = the script
                                    (?: \\ \s)*
                                    \\ :          # Quoted colon
                                    (?: \\ \s)*
                                    (.+?)         # $2 = the name
                                    (?: \\ \s)* $
                                  /xs
                      )
                    {
                        $scripts_trie = "\U$1";
                        $lookup_name  = $2;

                        $save_input = $name if !defined $save_input;
                        $name =~ s/.*?://;
                        $name_has_uppercase = $name =~ /[[:upper:]]/;
                    }
                    else {

                        @scripts = split( /\|/, $^H{charnames_scripts} );

                        $name_has_uppercase = $name =~ /[[:upper:]]/;
                    }
                    my $case = $name_has_uppercase ? "CAPITAL" : "SMALL";

                    if (@scripts) {
                      SCRIPTS: foreach my $script (@scripts) {
                            if ( $txt =~
/^ (?: $script ) \ (?:$case\ )? LETTER \ \U$lookup_name $/xm
                              )
                            {
                                @off = ( $-[0], $+[0] );
                                last SCRIPTS;
                            }
                        }
                        return unless (@off);
                    }
                    else {
                        return
                          if (!$scripts_trie
                            || $txt !~
/^ (?: $scripts_trie ) \ (?:$case\ )? LETTER \ \U$lookup_name $/xm
                          );
                        @off = ( $-[0], $+[0] );
                    }
                }

                if ( substr( $txt, $off[0] - 7, 1 ) eq "\n" ) {
                    $result = chr CORE::hex substr( $txt, $off[0] - 6, 5 );

                    $result = $HANGUL_JUNGSEONG_O_E_utf8
                      if $loose
                      && $result eq $HANGUL_JUNGSEONG_OE_utf8
                      && $name =~ m/O \s* - [-\s]* E/ix;
                }
                else {

                    my $charstart = rindex( $txt, "\n", $off[0] - 7 ) + 1;
                    $result = pack( "W*",
                        map { CORE::hex }
                          split " ",
                        substr( $txt, $charstart, $off[0] - $charstart - 1 ) );
                }
            }

            $cache_ref->{$name} = $result if defined $cache_ref;
        }
    }

    if ($wants_ord) {
        return ord($result) if length $result == 1;
    }
    elsif ( !utf8::is_utf8($result) ) {

        return $result
          if !$runtime
          || ( caller $runtime )[8] & $bytes::hint_bits
          || $result !~ /[[:^ascii:]]/;
        utf8::upgrade($result);
        return $result;
    }
    else {

        my $in_bytes = !$regex_loose

          && (
              ($runtime)
            ? ( caller $runtime )[8] & $bytes::hint_bits
            : $^H & $bytes::hint_bits
          );
        return $result if ( !$in_bytes || utf8::downgrade( $result, 1 ) );

    }

    if (@off) {
        $name = substr( $txt, $off[0], $off[1] - $off[0] ) if @off;
    }
    else {
        $name = ( defined $save_input ) ? $save_input : $_[0];
    }

    if ($wants_ord) {
        carp
"charnames::vianame() doesn't handle named sequences ($name).  Use charnames::string_vianame() instead";
        return;
    }

    if ($runtime) {
        carp not_legal_use_bytes_msg( $name, $result );
        return;
    }
    else {
        croak not_legal_use_bytes_msg( $name, $result );
    }

}

sub charnames {

    return lookup_name( $_[0], 0, 0 );
}

sub _loose_regcomp_lookup {
    return lookup_name( $_[0], 0, 1, 1 );
}

sub _get_names_info {
    populate_txt() unless $txt;

    return ( \$txt, \@charnames::code_points_ending_in_code_point );
}

sub import {
    shift;

    populate_txt() unless $txt;

    if ( not @_ ) {
        carp("'use charnames' needs explicit imports list");
    }
    $^H{charnames}              = \&charnames;
    $^H{charnames_ord_aliases}  = {};
    $^H{charnames_name_aliases} = {};
    $^H{charnames_inverse_ords} = {};

    my ( $promote, %h, @args ) = (0);
    while ( my $arg = shift ) {
        if ( $arg eq ":alias" ) {
            @_
              or croak ":alias needs an argument in charnames";
            my $alias = shift;
            if ( ref $alias ) {
                ref $alias eq "HASH"
                  or croak
                  "Only HASH reference supported as argument to :alias";
                alias($alias);
                $promote = 1;
                next;
            }
            if ( $alias =~ m{:(\w+)$} ) {
                $1 eq "full" || $1 eq "loose" || $1 eq "short"
                  and croak
                  ":alias cannot use existing pragma :$1 (reversed order?)";
                alias_file($1) and $promote = 1;
                next;
            }
            alias_file($alias) and $promote = 1;
            next;
        }
        if ( substr( $arg, 0, 1 ) eq ':'
            and !( $arg eq ":full" || $arg eq ":short" || $arg eq ":loose" ) )
        {
            warn "unsupported special '$arg' in charnames";
            next;
        }
        push @args, $arg;
    }

    @args == 0 && $promote and @args = (":full");
    @h{@args} = (1) x @args;

    $^H{charnames_full}  = delete $h{':full'}  || 0;
    $^H{charnames_loose} = delete $h{':loose'} || 0;
    $^H{charnames_short} = delete $h{':short'} || 0;
    my @scripts = map { uc quotemeta } grep { /^[^:]/ } @args;

    if ( warnings::enabled('utf8') && @scripts ) {
        for my $script (@scripts) {
            if ( not $txt =~ m/^$script (?:CAPITAL |SMALL )?LETTER /m ) {
                warnings::warn( 'utf8', "No such script: '$script'" );
                $script = quotemeta $script;
            }
        }
    }

    $^H{charnames_stringified_ords} = join ",", %{ $^H{charnames_ord_aliases} };
    $^H{charnames_stringified_names} = join ",",
      %{ $^H{charnames_name_aliases} };
    $^H{charnames_stringified_inverse_ords} = join ",",
      %{ $^H{charnames_inverse_ords} };

    if ( $^H{charnames_loose} ) {
        for ( my $i = 0 ; $i < @scripts ; $i++ ) {
            $scripts[$i] =~ s/[_ -]//g;
            $scripts[$i] =~ s/ ( [^\\] ) (?= . ) /$1\\ ?/gx;
        }
    }

    my %letters_by_script =
      map { $_ => [ ( $txt =~ m/$_(?: (?:small|capital))? letter (.*)/ig ) ] }
      @scripts;
  SCRIPTS: foreach my $this_script (@scripts) {
        my @other_scripts       = grep { $_ ne $this_script } @scripts;
        my @this_script_letters = @{ $letters_by_script{$this_script} };
        my @other_script_letters =
          map { @{ $letters_by_script{$_} } } @other_scripts;
        foreach my $this_letter (@this_script_letters) {
            if ( grep { $_ eq $this_letter } @other_script_letters ) {
                warn "charnames: some short character names may clash in ["
                  . join( ', ', sort @scripts )
                  . "], for example $this_letter\n";
                last SCRIPTS;
            }
        }
    }

    $^H{charnames_scripts} = join "|", @scripts;
}

my %viacode;

my $no_name_code_points_re = join "|",
  map { sprintf( "%05X", utf8::unicode_to_native($_) ) } 0x80, 0x81, 0x84, 0x99;
$no_name_code_points_re = qr/$no_name_code_points_re/;

sub viacode {

    if ( @_ != 1 ) {
        carp "charnames::viacode() expects one argument";
        return;
    }

    my $arg = shift;

    my $hex;
    if ( $arg =~ $decimal_qr ) {
        $hex = sprintf "%05X", $arg;
    }
    elsif ( $arg =~ $hex_qr ) {
        $hex = CORE::hex $1;
        $hex = utf8::unicode_to_native($hex) if $arg =~ /^[Uu]\+/;
        $hex = sprintf "%05X", $hex;
    }
    else {
        carp("unexpected arg \"$arg\" to charnames::viacode()");
        return;
    }

    return $viacode{$hex} if exists $viacode{$hex};

    my $return;

    if ( length($hex) <= 5 || CORE::hex($hex) <= 0x10FFFF ) {
        populate_txt() unless $txt;

        my $algorithmic =
          charnames::code_point_to_name_special( CORE::hex $hex );
        if ( defined $algorithmic ) {
            $viacode{$hex} = $algorithmic;
            return $algorithmic;
        }

        if ( $txt =~ m/^$hex\n/m ) {

            $return = substr( $txt, $+[0], index( $txt, "\n", $+[0] ) - $+[0] );

            if ( $hex !~ / ^ $no_name_code_points_re $ /x ) {
                $viacode{$hex} = $return;
                return $return;
            }

        }
    }

    my $H_ref = ( caller(1) )[10];
    return
      if !defined $return
      && ( !defined $H_ref
        || !exists $H_ref->{charnames_stringified_inverse_ords} );

    my %code_point_aliases;
    if ( defined $H_ref->{charnames_stringified_inverse_ords} ) {
        %code_point_aliases = split ',',
          $H_ref->{charnames_stringified_inverse_ords};
        return $code_point_aliases{$hex} if exists $code_point_aliases{$hex};
    }

    return $return if defined $return;

    if ( CORE::hex($hex) > 0x10FFFF
        && warnings::enabled('non_unicode') )
    {
        carp
"Unicode characters only allocated up to U+10FFFF (you asked for U+$hex)";
    }
    return;

}

1;

