package autodie::Scope::GuardStack;

use strict;
use warnings;

use autodie::Scope::Guard;

our $VERSION = '2.37';

my $H_KEY_STEM = __PACKAGE__ . '/guard';
my $COUNTER    = 0;

sub new {
    my ($class) = @_;

    return bless( [], $class );
}

sub push_hook {
    my ( $self, $hook ) = @_;
    my $h_key = $H_KEY_STEM . ( $COUNTER++ );
    my $size  = @{$self};
    $^H{$h_key} = autodie::Scope::Guard->new(
        sub {
            $self->_pop_hook while $self && @{$self} > $size;
        }
    );
    push( @{$self}, [ $hook, $h_key ] );
    return;
}

sub _pop_hook {
    my ($self) = @_;
    my ( $hook, $key ) = @{ pop( @{$self} ) };
    my $ref = delete( $^H{$key} );
    $hook->();
    return;
}

sub DESTROY {
    my ($self) = @_;

    $self->_pop_hook while @{$self};
    return;
}

1;

__END__

