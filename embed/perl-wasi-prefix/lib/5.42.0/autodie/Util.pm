package autodie::Util;

use strict;
use warnings;

use Exporter 5.57 qw(import);

use autodie::Scope::GuardStack;

our @EXPORT_OK = qw(
  fill_protos
  install_subs
  make_core_trampoline
  on_end_of_compile_scope
);

our $VERSION = '2.37';

my $H_STACK_KEY = __PACKAGE__ . '/stack';

sub on_end_of_compile_scope {
    my ($hook) = @_;

    $^H |= 0x020000;

    my $stack = $^H{$H_STACK_KEY};
    if ( not defined($stack) ) {
        $stack = autodie::Scope::GuardStack->new;
        $^H{$H_STACK_KEY} = $stack;
    }

    $stack->push_hook($hook);
    return;
}

sub fill_protos {
    my ($proto) = @_;
    my ( $n, $isref, @out, @out1, $seen_semi ) = -1;
    if ( $proto =~ m{^\s* (?: [;] \s*)? \@}x ) {
        return ( [ 0, '@_' ] );
    }

    while ( $proto =~ /\S/ ) {
        $n++;
        push( @out1, [ $n, @out ] ) if $seen_semi;
        push( @out, $1 . "{\$_[$n]}" ), next if $proto =~ s/^\s*\\([\@%\$\&])//;
        push( @out, "\$_[$n]" ),       next if $proto =~ s/^\s*([_*\$&])//;
        push( @out, "\@_[$n..\$#_]" ), last if $proto =~ s/^\s*(;\s*)?\@//;
        $seen_semi = 1, $n--, next if $proto =~ s/^\s*;//;
        die "Internal error: Unknown prototype letters: \"$proto\"";
    }
    push( @out1, [ $n + 1, @out ] );
    return @out1;
}

sub make_core_trampoline {
    my ( $call, $pkg, $proto_str ) = @_;
    my $trampoline_code = 'sub {';
    my $trampoline_sub;
    my @protos = fill_protos($proto_str);

    foreach my $proto (@protos) {
        local $" = ", ";
        my ( $count, @args ) = @$proto;
        if ( @args && $args[-1] =~ m/[@#]_/ ) {
            $trampoline_code .= qq/
                if (\@_ >= $count) {
                    return $call(@args);
                }
             /;
        }
        else {
            $trampoline_code .= qq<
                if (\@_ == $count) {
                    return $call(@args);
                }
             >;
        }
    }

    $trampoline_code .=
qq< require Carp; Carp::croak("Internal error in Fatal/autodie.  Leak-guard failure"); } >;
    my $E;

    {
        local $@;
        $trampoline_sub = eval "package $pkg;\n $trampoline_code";    ## no critic
        $E              = $@;
    }
    die "Internal error in Fatal/autodie: Leak-guard installation failure: $E"
      if $E;

    return $trampoline_sub;
}

sub install_subs {
    my ( $target_pkg, $subs_to_reinstate ) = @_;

    my $pkg_sym = "${target_pkg}::";

    foreach my $sub_name ( sort keys( %{$subs_to_reinstate} ) ) {

        no strict qw(refs);    ## no critic

        my $sub_ref = $subs_to_reinstate->{$sub_name};

        my $full_path = ${pkg_sym} . ${sub_name};
        my $oldglob   = *$full_path;

        delete( $pkg_sym->{$sub_name} );

        no warnings qw(once);
        local *alias = *$full_path;
        use warnings qw(once);

        foreach my $slot (qw( SCALAR ARRAY HASH IO )) {
            next unless defined( *$oldglob{$slot} );
            *alias = *$oldglob{$slot};
        }

        if ($sub_ref) {
            *$full_path = $sub_ref;
        }
    }

    return;
}

1;

__END__

