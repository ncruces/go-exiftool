package Module::Metadata;

sub __clean_eval { eval $_[0] }
use strict;
use warnings;

our $VERSION = '1.000038';

use Carp qw/croak/;
use File::Spec;

BEGIN {
    eval {
        require Fcntl;
        Fcntl->import('SEEK_SET');
        1;
    } or *SEEK_SET = sub { 0 }
}
use version 0.87;

BEGIN {
    if ( $INC{'Log/Contextual.pm'} ) {
        require "Log/Contextual/WarnLogger.pm";
        Log::Contextual->import(
            'log_info',
            '-default_logger' => Log::Contextual::WarnLogger->new(
                { env_prefix => 'MODULE_METADATA', }
            ),
        );
    }
    else {
        *log_info = sub (&) { warn $_[0]->() };
    }
}
use File::Find qw(find);

my $V_NUM_REGEXP = qr{v?[0-9._]+};

my $PKG_FIRST_WORD_REGEXP = qr{ # the FIRST word in a package name
  [a-zA-Z_]                     # the first word CANNOT start with a digit
    (?:
      [\w']?                    # can contain letters, digits, _, or ticks
      \w                        # But, NO multi-ticks or trailing ticks
    )*
}x;

my $PKG_ADDL_WORD_REGEXP = qr{ # the 2nd+ word in a package name
  \w                           # the 2nd+ word CAN start with digits
    (?:
      [\w']?                   # and can contain letters or ticks
      \w                       # But, NO multi-ticks or trailing ticks
    )*
}x;

my $PKG_NAME_REGEXP = qr{ # match a package name
  (?: :: )?               # a pkg name can start with arisdottle
  $PKG_FIRST_WORD_REGEXP  # a package word
  (?:
    (?: :: )+             ### arisdottle (allow one or many times)
    $PKG_ADDL_WORD_REGEXP ### a package word
  )*                      # ^ zero, one or many times
  (?:
    ::                    # allow trailing arisdottle
  )?
}x;

my $PKG_REGEXP = qr{   # match a package declaration
  ^[\s\{;]*             # intro chars on a line
  package               # the word 'package'
  \s+                   # whitespace
  ($PKG_NAME_REGEXP)    # a package name
  \s*                   # optional whitespace
  ($V_NUM_REGEXP)?      # optional version number
  \s*                   # optional whitespace
  [;\{]                 # semicolon line terminator or block start (since 5.16)
}x;

my $CLASS_REGEXP = qr{  # match a class declaration (core since 5.38)
  ^[\s\{;]*             # intro chars on a line
  class                 # the word 'class'
  \s+                   # whitespace
  ($PKG_NAME_REGEXP)    # a package name
  \s*                   # optional whitespace
  ($V_NUM_REGEXP)?      # optional version number
  \s*                   # optional whitespace
  [;\{]                 # semicolon line terminator or block start
}x;

my $VARNAME_REGEXP = qr{ # match fully-qualified VERSION name
  ([\$*])         # sigil - $ or *
  (
    (             # optional leading package name
      (?:::|\')?  # possibly starting like just :: (a la $::VERSION)
      (?:\w+(?:::|\'))*  # Foo::Bar:: ...
    )?
    VERSION
  )\b
}x;

my $VERS_REGEXP = qr{ # match a VERSION definition
  (?:
    \(\s*$VARNAME_REGEXP\s*\) # with parens
  |
    $VARNAME_REGEXP           # without parens
  )
  \s*
  =[^=~>]  # = but not ==, nor =~, nor =>
}x;

sub new_from_file {
    my $class    = shift;
    my $filename = File::Spec->rel2abs(shift);

    return undef unless defined($filename) && -f $filename;
    return $class->_init( undef, $filename, @_ );
}

sub new_from_handle {
    my $class    = shift;
    my $handle   = shift;
    my $filename = shift;
    return undef unless defined($handle) && defined($filename);
    $filename = File::Spec->rel2abs($filename);

    return $class->_init( undef, $filename, @_, handle => $handle );

}

sub new_from_module {
    my $class  = shift;
    my $module = shift;
    my %props  = @_;

    $props{inc} ||= \@INC;
    my $filename = $class->find_module_by_name( $module, $props{inc} );
    return undef unless defined($filename) && -f $filename;
    return $class->_init( $module, $filename, %props );
}

{

    my $compare_versions = sub {
        my ( $v1, $op, $v2 ) = @_;
        $v1 = version->new($v1)
          unless UNIVERSAL::isa( $v1, 'version' );

        my $eval_str = "\$v1 $op \$v2";
        my $result   = eval $eval_str;
        log_info { "error comparing versions: '$eval_str' $@" } if $@;

        return $result;
    };

    my $normalize_version = sub {
        my ($version) = @_;
        if ( $version =~ /[=<>!,]/ ) {

        }
        elsif ( ref $version eq 'version' ) {
            $version = $version->is_qv ? $version->normal : $version->stringify;
        }
        elsif ( $version =~ /^[^v][^.]*\.[^.]+\./ ) {

            $version = "v$version";
        }
        else {
        }
        return $version;
    };

    my $resolve_module_versions = sub {
        my $packages = shift;

        my ( $file, $version );
        my $err = '';
        foreach my $p (@$packages) {
            if ( defined( $p->{version} ) ) {
                if ( defined($version) ) {
                    if ( $compare_versions->( $version, '!=', $p->{version} ) )
                    {
                        $err .= "  $p->{file} ($p->{version})\n";
                    }
                    else {
                    }
                }
                else {
                    $file    = $p->{file};
                    $version = $p->{version};
                }
            }
            $file ||= $p->{file} if defined( $p->{file} );
        }

        if ($err) {
            $err = "  $file ($version)\n" . $err;
        }

        my %result = (
            file    => $file,
            version => $version,
            err     => $err
        );

        return \%result;
    };

    sub provides {
        my $class = shift;

        croak "provides() requires key/value pairs \n" if @_ % 2;
        my %args = @_;

        croak "provides() takes only one of 'dir' or 'files'\n"
          if $args{dir} && $args{files};

        croak "provides() requires a 'version' argument"
          unless defined $args{version};

        croak "provides() does not support version '$args{version}' metadata"
          unless grep $args{version} eq $_, qw/1.4 2/;

        $args{prefix} = 'lib' unless defined $args{prefix};

        my $p;
        if ( $args{dir} ) {
            $p = $class->package_versions_from_directory( $args{dir} );
        }
        else {
            croak "provides() requires 'files' to be an array reference\n"
              unless ref $args{files} eq 'ARRAY';
            $p = $class->package_versions_from_directory( $args{files} );
        }

        if ( length $args{prefix} ) {
            $args{prefix} =~ s{/$}{};
            for my $v ( values %$p ) {
                $v->{file} = "$args{prefix}/$v->{file}";
            }
        }

        return $p;
    }

    sub package_versions_from_directory {
        my ( $class, $dir, $files ) = @_;

        my @files;

        if ($files) {
            @files = @$files;
        }
        else {
            find(
                {
                    wanted => sub {
                        push @files, $_ if -f $_ && /\.pm$/;
                    },
                    no_chdir => 1,
                },
                $dir
            );
        }

        my ( %prime, %alt );
        foreach my $file (@files) {
            my $mapped_filename = File::Spec->abs2rel( $file, $dir );
            my @path            = File::Spec->splitdir($mapped_filename);
            ( my $prime_package = join( '::', @path ) ) =~ s/\.pm$//;

            my $pm_info = $class->new_from_file($file);

            foreach my $package ( $pm_info->packages_inside ) {
                next if $package eq 'main';
                next if $package eq 'DB';
                next if grep /^_/, split( /::/, $package );

                my $version = $pm_info->version($package);

                $prime_package = $package if lc($prime_package) eq lc($package);
                if ( $package eq $prime_package ) {
                    if ( exists( $prime{$package} ) ) {
                        croak
"Unexpected conflict in '$package'; multiple versions found.\n";
                    }
                    else {
                        $mapped_filename = "$package.pm"
                          if lc("$package.pm") eq lc($mapped_filename);
                        $prime{$package}{file}    = $mapped_filename;
                        $prime{$package}{version} = $version
                          if defined($version);
                    }
                }
                else {
                    push(
                        @{ $alt{$package} },
                        {
                            file    => $mapped_filename,
                            version => $version,
                        }
                    );
                }
            }
        }

        foreach my $package ( keys(%alt) ) {
            my $result = $resolve_module_versions->( $alt{$package} );

            if ( exists( $prime{$package} ) ) {

                if ( $result->{err} ) {
                    log_info {
                        "Found conflicting versions for package '$package'\n"
                          . "  $prime{$package}{file} ($prime{$package}{version})\n"
                          . $result->{err}
                    };

                }
                elsif ( defined( $result->{version} ) ) {

                    if ( exists( $prime{$package}{version} )
                        && defined( $prime{$package}{version} ) )
                    {
                        if (
                            $compare_versions->(
                                $prime{$package}{version}, '!=',
                                $result->{version}
                            )
                          )
                        {

                            log_info {
"Found conflicting versions for package '$package'\n"
                                  . "  $prime{$package}{file} ($prime{$package}{version})\n"
                                  . "  $result->{file} ($result->{version})\n"
                            };
                        }

                    }
                    else {
                        $prime{$package}{file}    = $result->{file};
                        $prime{$package}{version} = $result->{version};
                    }

                }
                else {
                }

            }
            else {

                if ( $result->{err} ) {
                    log_info {
                        "Found conflicting versions for package '$package'\n"
                          . $result->{err}
                    };
                }

                $prime{$package}{file}    = $result->{file};
                $prime{$package}{version} = $result->{version}
                  if defined( $result->{version} );
            }
        }

        for ( grep defined $_->{version}, values %prime ) {
            $_->{version} = $normalize_version->( $_->{version} );
        }

        return \%prime;
    }
}

sub _init {
    my $class    = shift;
    my $module   = shift;
    my $filename = shift;
    my %props    = @_;

    my $handle = delete $props{handle};
    my ( %valid_props, @valid_props );
    @valid_props = qw( collect_pod inc decode_pod );
    @valid_props{@valid_props} = delete( @props{@valid_props} );
    warn "Unknown properties: @{[keys %props]}\n" if scalar(%props);

    my %data = (
        module       => $module,
        filename     => $filename,
        version      => undef,
        packages     => [],
        versions     => {},
        pod          => {},
        pod_headings => [],
        collect_pod  => 0,

        %valid_props,
    );

    my $self = bless( \%data, $class );

    if ( not $handle ) {
        my $filename = $self->{filename};
        open $handle, '<', $filename
          or croak("Can't open '$filename': $!");

        $self->_handle_bom( $handle, $filename );
    }
    $self->_parse_fh($handle);

    @{ $self->{packages} } = __uniq( @{ $self->{packages} } );

    unless ( $self->{module} and length( $self->{module} ) ) {
        if ( $self->{filename} =~ /\.pm$/ ) {
            my ( $v, $d, $f ) = File::Spec->splitpath( $self->{filename} );
            $f =~ s/\..+$//;
            my @candidates = grep /(^|::)$f$/, @{ $self->{packages} };
            $self->{module} = shift(@candidates);
        }
        else {
            if (   ( grep /main/, @{ $self->{packages} } )
                or ( grep /main/, keys %{ $self->{versions} } ) )
            {
                $self->{module} = 'main';
            }
            else {
                $self->{module} = $self->{packages}[0] || '';
            }
        }
    }

    $self->{version} = $self->{versions}{ $self->{module} }
      if defined( $self->{module} );

    return $self;
}

sub _do_find_module {
    my $class  = shift;
    my $module = shift || croak 'find_module_by_name() requires a package name';
    my $dirs   = shift || \@INC;

    my $file = File::Spec->catfile( split( /::/, $module ) );
    foreach my $dir (@$dirs) {
        my $testfile = File::Spec->catfile( $dir, $file );
        return [ File::Spec->rel2abs($testfile), $dir ]
          if -e $testfile and !-d _;

        $testfile .= '.pm';
        return [ File::Spec->rel2abs($testfile), $dir ]
          if -e $testfile;
    }
    return;
}

sub find_module_by_name {
    my $found = shift()->_do_find_module(@_) or return;
    return $found->[0];
}

sub find_module_dir_by_name {
    my $found = shift()->_do_find_module(@_) or return;
    return $found->[1];
}

sub _parse_version_expression {
    my $self = shift;
    my $line = shift;

    my ( $sigil, $variable_name, $package );
    if ( $line =~ /$VERS_REGEXP/o ) {
        ( $sigil, $variable_name, $package ) =
          $2 ? ( $1, $2, $3 ) : ( $4, $5, $6 );
        if ($package) {
            $package = ( $package eq '::' ) ? 'main' : $package;
            $package =~ s/::$//;
        }
    }

    return ( $sigil, $variable_name, $package );
}

sub _handle_bom {
    my ( $self, $fh, $filename ) = @_;

    my $pos = tell $fh;
    return unless defined $pos;

    my $buf   = ' ' x 2;
    my $count = read $fh, $buf, length $buf;
    return unless defined $count and $count >= 2;

    my $encoding;
    if ( $buf eq "\x{FE}\x{FF}" ) {
        $encoding = 'UTF-16BE';
    }
    elsif ( $buf eq "\x{FF}\x{FE}" ) {
        $encoding = 'UTF-16LE';
    }
    elsif ( $buf eq "\x{EF}\x{BB}" ) {
        $buf   = ' ';
        $count = read $fh, $buf, length $buf;
        if ( defined $count and $count >= 1 and $buf eq "\x{BF}" ) {
            $encoding = 'UTF-8';
        }
    }

    if ( defined $encoding ) {
        if ( "$]" >= 5.008 ) {
            binmode( $fh, ":encoding($encoding)" );
        }
    }
    else {
        seek $fh, $pos, SEEK_SET
          or croak( sprintf "Can't reset position to the top of '$filename'" );
    }

    return $encoding;
}

sub _parse_fh {
    my ( $self, $fh ) = @_;

    my ( $in_pod, $seen_end, $need_vers ) = ( 0, 0, 0 );
    my ( @packages, %vers, %pod, @pod );
    my $package  = 'main';
    my $pod_sect = '';
    my $pod_data = '';
    my $in_end   = 0;
    my $encoding = '';

    while ( defined( my $line = <$fh> ) ) {
        my $line_num = $.;

        chomp($line);

        my $is_cut;
        if ( $line =~ /^=([a-zA-Z].*)/ ) {
            my $cmd = $1;
            $is_cut = $cmd =~ /^cut(?:[^a-zA-Z]|$)/;
            $in_pod = !$is_cut;
        }

        if ($in_pod) {

            if ( $line =~ /^=head[1-4]\s+(.+)\s*$/ ) {
                push( @pod, $1 );
                if ( $self->{collect_pod} && length($pod_data) ) {
                    $pod{$pod_sect} = $pod_data;
                    $pod_data = '';
                }
                $pod_sect = $1;
            }
            elsif ( $self->{collect_pod} ) {
                if ( $self->{decode_pod} && $line =~ /^=encoding ([\w-]+)/ ) {
                    $encoding = $1;
                }
                $pod_data .= "$line\n";
            }
            next;
        }
        elsif ($is_cut) {
            if ( $self->{collect_pod} && length($pod_data) ) {
                $pod{$pod_sect} = $pod_data;
                $pod_data = '';
            }
            $pod_sect = '';
            next;
        }

        next if $in_end;

        next if $line =~ /^\s*#/;

        if ( $line eq '__END__' ) {
            $in_end++;
            next;
        }

        last if $line eq '__DATA__';

        my ( $version_sigil, $version_fullname, $version_package ) =
          index( $line, 'VERSION' ) >= 1
          ? $self->_parse_version_expression($line)
          : ();

        if ( $line =~ /$PKG_REGEXP/o or $line =~ /$CLASS_REGEXP/ ) {
            $package = $1;
            my $version = $2;
            push( @packages, $package )
              unless grep( $package eq $_, @packages );
            $need_vers = defined $version ? 0 : 1;

            if ( not exists $vers{$package} and defined $version ) {
                my $dwim_version = eval { _dwim_version($version) };
                croak
"Version '$version' from $self->{filename} does not appear to be valid:\n$line\n\nThe fatal error was: $@\n"
                  unless defined $dwim_version;
                $vers{$package} = $dwim_version;
            }
        }

        elsif ( $version_fullname && $version_package ) {
            $need_vers = 0 if $version_package eq $package;

            unless ( defined $vers{$version_package}
                && length $vers{$version_package} )
            {
                $vers{$version_package} =
                  $self->_evaluate_version_line( $version_sigil,
                    $version_fullname, $line );
            }
        }

        elsif ($package eq 'main'
            && $version_fullname
            && !exists( $vers{main} ) )
        {
            $need_vers = 0;
            my $v =
              $self->_evaluate_version_line( $version_sigil, $version_fullname,
                $line );
            $vers{$package} = $v;
            push( @packages, 'main' );
        }

        elsif ( $package eq 'main' && !exists( $vers{main} ) && $line =~ /\w/ )
        {
            $need_vers = 1;
            $vers{main} = '';
            push( @packages, 'main' );
        }

        elsif ( $version_fullname && $need_vers ) {
            $need_vers = 0;
            my $v =
              $self->_evaluate_version_line( $version_sigil, $version_fullname,
                $line );

            unless ( defined $vers{$package} && length $vers{$package} ) {
                $vers{$package} = $v;
            }
        }
    }

    if ( $self->{collect_pod} && length($pod_data) ) {
        $pod{$pod_sect} = $pod_data;
    }

    if ( $self->{decode_pod} && $encoding ) {
        require Encode;
        $_ = Encode::decode( $encoding, $_ ) for values %pod;
    }

    $self->{versions}     = \%vers;
    $self->{packages}     = \@packages;
    $self->{pod}          = \%pod;
    $self->{pod_headings} = \@pod;
}

sub __uniq (@) {
    my ( %seen, $key );
    grep !$seen{ $key = $_ }++, @_;
}

{
    my $pn = 0;

    sub _evaluate_version_line {
        my $self = shift;
        my ( $sigil, $variable_name, $line ) = @_;

        $pn++;
        my $eval = qq{ my \$dummy = q#  Hide from _packages_inside()
    #; package Module::Metadata::_version::p${pn};
    use version;
    sub {
      local $sigil$variable_name;
      $line;
      return \$$variable_name if defined \$$variable_name;
      return \$Module::Metadata::_version::p${pn}::$variable_name;
    };
  };

        $eval = $1 if $eval =~ m{^(.+)}s;

        local $^W;
        my $vsub = __clean_eval($eval);
        if ( $@ =~ /Can't locate/ && -d 'lib' ) {
            local @INC = ( 'lib', @INC );
            $vsub = __clean_eval($eval);
        }
        warn "Error evaling version line '$eval' in $self->{filename}: $@\n"
          if $@;

        ( ref($vsub) eq 'CODE' )
          or croak "failed to build version sub for $self->{filename}";

        my $result = eval { $vsub->() };
        croak
"Could not get version from $self->{filename} by executing:\n$eval\n\nThe fatal error was: $@\n"
          if $@;

        my $version = eval { _dwim_version($result) };

        croak
"Version '$result' from $self->{filename} does not appear to be valid:\n$eval\n\nThe fatal error was: $@\n"
          unless defined $version;

        return $version;
    }
}

{
    my @version_prep = (
        sub { return shift },

        sub {
            my $v = shift;
            $v =~ s{([0-9])[a-z-].*$}{$1}i;
            return $v;
        },

        sub {
            my $v          = shift;
            my $num_dots   = () = $v =~ m{(\.)}g;
            my $num_unders = () = $v =~ m{(_)}g;
            my $leading_v  = substr( $v, 0, 1 ) eq 'v';
            if ( !$leading_v && $num_dots < 2 && $num_unders > 1 ) {
                $v =~ s{_}{}g;
                $num_unders = () = $v =~ m{(_)}g;
            }
            return $v;
        },

        sub {
            my $v = shift;
            no warnings 'numeric';
            return 0 + $v;
        },

    );

    sub _dwim_version {
        my ($result) = shift;

        return $result if ref($result) eq 'version';

        my ( $version, $error );
        for my $f (@version_prep) {
            $result  = $f->($result);
            $version = eval { version->new($result) };
            $error ||= $@ if $@;
            last          if defined $version;
        }

        croak $error unless defined $version;

        return $version;
    }
}

sub name { $_[0]->{module} }

sub filename        { $_[0]->{filename} }
sub packages_inside { @{ $_[0]->{packages} } }
sub pod_inside      { @{ $_[0]->{pod_headings} } }
sub contains_pod    { 0 + @{ $_[0]->{pod_headings} } }

sub version {
    my $self = shift;
    my $mod  = shift || $self->{module};
    my $vers;
    if (   defined($mod)
        && length($mod)
        && exists( $self->{versions}{$mod} ) )
    {
        return $self->{versions}{$mod};
    }
    else {
        return undef;
    }
}

sub pod {
    my $self = shift;
    my $sect = shift;
    if (   defined($sect)
        && length($sect)
        && exists( $self->{pod}{$sect} ) )
    {
        return $self->{pod}{$sect};
    }
    else {
        return undef;
    }
}

sub is_indexable {
    my ( $self, $package ) = @_;

    my @indexable_packages = grep $_ ne 'main', $self->packages_inside;

    return !!grep $_ eq $package, @indexable_packages if $package;

    return !!@indexable_packages;
}

1;

__END__

