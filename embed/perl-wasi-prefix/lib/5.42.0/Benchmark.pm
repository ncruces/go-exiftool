package Benchmark;

use strict;


sub _doeval { no strict; eval shift }

use Carp;
use Exporter;

our ( @ISA, @EXPORT, @EXPORT_OK, %EXPORT_TAGS, $VERSION );

@ISA       = qw(Exporter);
@EXPORT    = qw(timeit timethis timethese timediff timestr);
@EXPORT_OK = qw(timesum cmpthese countit
  clearcache clearallcache disablecache enablecache);
%EXPORT_TAGS = ( all => [ @EXPORT, @EXPORT_OK ] );

$VERSION = 1.27;

my $hirestime;

sub mytime () { time }

init();

BEGIN {
    if ( eval { require Time::HiRes } ) {
        $hirestime = \&Time::HiRes::time;
    }
}

sub import {
    my $class = shift;
    if ( grep { $_ eq ":hireswallclock" } @_ ) {
        @_ = grep { $_ ne ":hireswallclock" } @_;
        local $^W = 0;
        *mytime = $hirestime if defined $hirestime;
    }
    Benchmark->export_to_level( 1, $class, @_ );
}

our ( $Debug, $Min_Count, $Min_CPU, $Default_Format, $Default_Style,
    %_Usage, %Cache, $Do_Cache );

sub init {
    $Debug          = 0;
    $Min_Count      = 4;
    $Min_CPU        = 0.4;
    $Default_Format = '5.2f';
    $Default_Style  = 'auto';
    disablecache();
    clearallcache();
}

sub debug { $Debug = ( $_[1] != 0 ); }

sub usage {
    my $calling_sub = ( caller(1) )[3];
    $calling_sub =~ s/^Benchmark:://;
    return $_Usage{$calling_sub} || '';
}

$_Usage{clearcache} = <<'USAGE';
usage: clearcache($count);
USAGE

sub clearcache {
    die usage unless @_ == 1;
    delete $Cache{"$_[0]c"};
    delete $Cache{"$_[0]s"};
}

$_Usage{clearallcache} = <<'USAGE';
usage: clearallcache();
USAGE

sub clearallcache {
    die usage if @_;
    %Cache = ();
}

$_Usage{enablecache} = <<'USAGE';
usage: enablecache();
USAGE

sub enablecache {
    die usage if @_;
    $Do_Cache = 1;
}

$_Usage{disablecache} = <<'USAGE';
usage: disablecache();
USAGE

sub disablecache {
    die usage if @_;
    $Do_Cache = 0;
}

sub new {
    my @t = ( mytime, times, @_ == 2 ? $_[1] : 0 );
    print STDERR "new=@t\n" if $Debug;
    bless \@t;
}

sub cpu_p { my ( $r, $pu, $ps, $cu, $cs ) = @{ $_[0] }; $pu + $ps; }
sub cpu_c { my ( $r, $pu, $ps, $cu, $cs ) = @{ $_[0] }; $cu + $cs; }
sub cpu_a { my ( $r, $pu, $ps, $cu, $cs ) = @{ $_[0] }; $pu + $ps + $cu + $cs; }
sub real  { my ( $r, $pu, $ps, $cu, $cs ) = @{ $_[0] }; $r; }
sub iters { $_[0]->[5]; }

sub elapsed {
    my ( $self, $style ) = @_;
    $style = "" unless defined $style;

    return $self->cpu_c if $style eq 'nop';
    return $self->cpu_p if $style eq 'noc';
    return $self->cpu_a;
}

$_Usage{timediff} = <<'USAGE';
usage: $result_diff = timediff($result1, $result2);
USAGE

sub timediff {
    my ( $a, $b ) = @_;

    die usage unless ref $a and ref $b;

    my @r;
    for ( my $i = 0 ; $i < @$a ; ++$i ) {
        push( @r, $a->[$i] - $b->[$i] );
    }
    bless \@r;
}

$_Usage{timesum} = <<'USAGE';
usage: $sum = timesum($result1, $result2);
USAGE

sub timesum {
    my ( $a, $b ) = @_;

    die usage unless ref $a and ref $b;

    my @r;
    for ( my $i = 0 ; $i < @$a ; ++$i ) {
        push( @r, $a->[$i] + $b->[$i] );
    }
    bless \@r;
}

$_Usage{timestr} = <<'USAGE';
usage: $formatted_result = timestr($result1);
USAGE

sub timestr {
    my ( $tr, $style, $f ) = @_;

    die usage unless ref $tr;

    my @t = @$tr;
    warn "bad time value (@t)" unless @t == 6;
    my ( $r, $pu, $ps, $cu, $cs, $n ) = @t;
    my ( $pt, $ct, $tt ) = ( $tr->cpu_p, $tr->cpu_c, $tr->cpu_a );
    $f = $Default_Format unless defined $f;
    $style ||= $Default_Style;
    return '' if $style eq 'none';
    $style = ( $ct > 0 ) ? 'all' : 'noc' if $style eq 'auto';
    my $s = "@t $style";
    my $w = $hirestime ? "%2g" : "%2d";
    $s = sprintf(
        "$w wallclock secs (%$f usr %$f sys + %$f cusr %$f csys = %$f CPU)",
        $r, $pu, $ps, $cu, $cs, $tt )
      if $style eq 'all';
    $s = sprintf( "$w wallclock secs (%$f usr + %$f sys = %$f CPU)",
        $r, $pu, $ps, $pt )
      if $style eq 'noc';
    $s = sprintf( "$w wallclock secs (%$f cusr + %$f csys = %$f CPU)",
        $r, $cu, $cs, $ct )
      if $style eq 'nop';
    my $elapsed = $tr->elapsed($style);
    $s .= sprintf( " @ %$f/s (n=$n)", $n / ($elapsed) ) if $n && $elapsed;
    $s;
}

sub timedebug {
    my ( $msg, $t ) = @_;
    print STDERR "$msg", timestr($t), "\n" if $Debug;
}

$_Usage{runloop} = <<'USAGE';
usage: runloop($number, [$string | $coderef])
USAGE

sub runloop {
    my ( $n, $c ) = @_;

    $n += 0;
    croak "negative loopcount $n" if $n < 0;
    confess usage unless defined $c;
    my ( $t0, $t1, $td );

    my $curpack = caller(0);
    my ( $i, $pack ) = 0;
    while ( $pack = caller( ++$i ) ) {
        last if $pack ne $curpack;
    }

    my ( $subcode, $subref );
    if ( ref $c eq 'CODE' ) {
        $subcode = "sub { for (1 .. $n) { local \$_; package $pack; &\$c; } }";
        $subref  = eval $subcode;
    }
    else {
        $subcode = "sub { for (1 .. $n) { local \$_; package $pack; $c;} }";
        $subref  = _doeval($subcode);
    }
    croak "runloop unable to compile '$c': $@\ncode: $subcode\n" if $@;
    print STDERR "runloop $n '$subcode'\n"                       if $Debug;

    my $tbase = Benchmark->new(0)->[1];
    my $limit = 1;
    while ( ( $t0 = Benchmark->new(0) )->[1] == $tbase ) {
        for ( my $i = 0 ; $i < $limit ; $i++ ) { my $x = $i / 1.5 }
        $limit *= 1.1;
    }
    $subref->();
    $t1 = Benchmark->new($n);
    $td = &timediff( $t1, $t0 );
    timedebug( "runloop:", $td );
    $td;
}

$_Usage{timeit} = <<'USAGE';
usage: $result = timeit($count, 'code' );        or
       $result = timeit($count, sub { code } );
USAGE

sub timeit {
    my ( $n, $code ) = @_;
    my ( $wn, $wc, $wd );

    die usage
      unless defined $code
      and ( !ref $code or ref $code eq 'CODE' );

    printf STDERR "timeit $n $code\n" if $Debug;
    my $cache_key = $n . ( ref($code) ? 'c' : 's' );
    if ( $Do_Cache && exists $Cache{$cache_key} ) {
        $wn = $Cache{$cache_key};
    }
    else {
        $wn = &runloop( $n, ref($code) ? sub { } : '' );
        $wn->[5] = 0;
        $Cache{$cache_key} = $wn;
    }

    $wc = &runloop( $n, $code );

    $wd = timediff( $wc, $wn );
    timedebug( "timeit: ", $wc );
    timedebug( "      - ", $wn );
    timedebug( "      = ", $wd );

    $wd;
}

my $default_for = 3;
my $min_for     = 0.1;

$_Usage{countit} = <<'USAGE';
usage: $result = countit($time, 'code' );        or
       $result = countit($time, sub { code } );
USAGE

sub countit {
    my ( $tmax, $code ) = @_;

    die usage unless @_;

    if ( not defined $tmax or $tmax == 0 ) {
        $tmax = $default_for;
    }
    elsif ( $tmax < 0 ) {
        $tmax = -$tmax;
    }

    die "countit($tmax, ...): timelimit cannot be less than $min_for.\n"
      if $tmax < $min_for;

    my ( $n, $tc );

    my $zeros = 0;
    for ( $n = 1 ; ; $n *= 2 ) {
        my $t0 = Benchmark->new(0);
        my $td = timeit( $n, $code );
        my $t1 = Benchmark->new(0);
        $tc = $td->[1] + $td->[2];
        if ( $tc <= 0 and $n > 1024 ) {
            my $d = timediff( $t1, $t0 );
            if ( $d->[1] + $d->[2] > 8
                || ++$zeros > 16 )
            {
                die
"Timing is consistently zero in estimation loop, cannot benchmark. N=$n\n";
            }
        }
        else {
            $zeros = 0;
        }
        last if $tc > 0.1;
    }

    my $nmin = $n;

    my $tpra = 0.1 * $tmax;
    while ( $tc < $tpra ) {
        $n = int( $tpra * 1.05 * $n / $tc );
        my $td     = timeit( $n, $code );
        my $new_tc = $td->[1] + $td->[2];
        $tc = $new_tc > 1.2 * $tc ? $new_tc : 1.2 * $tc;
    }

    my $ntot  = 0;
    my $rtot  = 0;
    my $utot  = 0.0;
    my $stot  = 0.0;
    my $cutot = 0.0;
    my $cstot = 0.0;
    my $ttot  = 0.0;

    $n     = int( $n * ( 1.05 * $tmax / $tc ) );
    $zeros = 0;
    while () {
        my $td = timeit( $n, $code );
        $ntot  += $n;
        $rtot  += $td->[0];
        $utot  += $td->[1];
        $stot  += $td->[2];
        $cutot += $td->[3];
        $cstot += $td->[4];
        $ttot = $utot + $stot;
        last if $ttot >= $tmax;

        if ( $ttot <= 0 ) {
            ++$zeros > 16
              and die "Timing is consistently zero, cannot benchmark. N=$n\n";
        }
        else {
            $zeros = 0;
        }
        $ttot = 0.01 if $ttot < 0.01;
        my $r = $tmax / $ttot - 1;
        $n = int( $r * $ntot );
        $n = $nmin if $n < $nmin;
    }

    return bless [ $rtot, $utot, $stot, $cutot, $cstot, $ntot ];
}

sub n_to_for {
    my $n = shift;
    return $n == 0 ? $default_for : $n < 0 ? -$n : undef;
}

$_Usage{timethis} = <<'USAGE';
usage: $result = timethis($time, 'code' );        or
       $result = timethis($time, sub { code } );
USAGE

sub timethis {
    my ( $n, $code, $title, $style ) = @_;
    my ( $t, $forn );

    die usage
      unless defined $code
      and ( !ref $code or ref $code eq 'CODE' );

    if ( $n > 0 ) {
        croak "non-integer loopcount $n, stopped" if int($n) < $n;
        $t     = timeit( $n, $code );
        $title = "timethis $n" unless defined $title;
    }
    else {
        my $fort = n_to_for($n);
        $t     = countit( $fort, $code );
        $title = "timethis for $fort" unless defined $title;
        $forn  = $t->[-1];
    }
    local $| = 1;
    $style = "" unless defined $style;
    printf( "%10s: ", $title ) unless $style eq 'none';
    print timestr( $t, $style, $Default_Format ), "\n" unless $style eq 'none';

    $n = $forn if defined $forn;

    if ( $t->elapsed($style) < 0 ) {
        print
          "            (warning: too few iterations for a reliable count)\n";
        $t->[$_] = 0 for 1 .. 4;
    }

    print "            (warning: too few iterations for a reliable count)\n"
      if $n < $Min_Count
      || ( $t->real < 1 && $n < 1000 )
      || $t->cpu_a < $Min_CPU;
    $t;
}

$_Usage{timethese} = <<'USAGE';
usage: timethese($count, { Name1 => 'code1', ... });        or
       timethese($count, { Name1 => sub { code1 }, ... });
USAGE

sub timethese {
    my ( $n, $alt, $style ) = @_;
    die usage unless ref $alt eq 'HASH';

    my @names = sort keys %$alt;
    $style = "" unless defined $style;
    print "Benchmark: " unless $style eq 'none';
    if ( $n > 0 ) {
        croak "non-integer loopcount $n, stopped" if int($n) < $n;
        print "timing $n iterations of" unless $style eq 'none';
    }
    else {
        print "running" unless $style eq 'none';
    }
    print " ", join( ', ', @names ) unless $style eq 'none';
    unless ( $n > 0 ) {
        my $for = n_to_for($n);
        print ", each" if $n > 1 && $style ne 'none';
        print " for at least $for CPU seconds" unless $style eq 'none';
    }
    print "...\n" unless $style eq 'none';

    my %results;
    foreach my $name (@names) {
        $results{$name} = timethis( $n, $alt->{$name}, $name, $style );
    }

    return \%results;
}

$_Usage{cmpthese} = <<'USAGE';
usage: cmpthese($count, { Name1 => 'code1', ... });        or
       cmpthese($count, { Name1 => sub { code1 }, ... });  or
       cmpthese($result, $style);
USAGE

sub cmpthese {
    my ( $results, $style );

    if ( ref $_[0] eq 'HASH' ) {
        ( $results, $style ) = @_;
    }
    else {
        my ( $count, $code ) = @_[ 0, 1 ];
        $style = $_[2] if defined $_[2];

        die usage unless ref $code eq 'HASH';

        $results = timethese( $count, $code, ( $style || "none" ) );
    }

    $style = "" unless defined $style;

    my @vals = map { [ $_, @{ $results->{$_} } ] } keys %$results;

    for (@vals) {
        my $tmp_bm  = bless [ @{$_}[ 1 .. $#$_ ] ];
        my $elapsed = $tmp_bm->elapsed($style);
        my $rate = $_->[6] / ( ($elapsed) + 0.000000000000001 );
        $_->[7] = $rate;
    }

    @vals = sort { $a->[7] <=> $b->[7] } @vals;

    my $display_as_rate = @vals ? ( $vals[ $#vals >> 1 ]->[7] > 1 ) : 0;

    my @rows;
    my @col_widths;

    my @top_row =
      ( '', $display_as_rate ? 'Rate' : 's/iter', map { $_->[0] } @vals );

    push @rows, \@top_row;
    @col_widths = map { length($_) } @top_row;

    for my $row_val (@vals) {
        my @row;

        push @row, $row_val->[0];
        $col_widths[0] = length( $row_val->[0] )
          if length( $row_val->[0] ) > $col_widths[0];

        my $row_rate = $row_val->[7];

        my $rate = $display_as_rate ? $row_rate : 1 / $row_rate;

        my $format =
            $rate >= 100 ? "%0.0f"
          : $rate >= 10  ? "%0.1f"
          : $rate >= 1   ? "%0.2f"
          : $rate >= 0.1 ? "%0.3f"
          :                "%0.2e";

        $format .= "/s"
          if $display_as_rate;

        my $formatted_rate = sprintf( $format, $rate );
        push @row, $formatted_rate;
        $col_widths[1] = length($formatted_rate)
          if length($formatted_rate) > $col_widths[1];

        my $skip_rest = 0;
        for ( my $col_num = 0 ; $col_num < @vals ; ++$col_num ) {
            my $col_val = $vals[$col_num];
            my $out;
            if ($skip_rest) {
                $out = '';
            }
            elsif ( $col_val->[0] eq $row_val->[0] ) {
                $out = "--";
            }
            else {
                my $col_rate = $col_val->[7];
                $out = sprintf( "%.0f%%", 100 * $row_rate / $col_rate - 100 );
            }
            push @row, $out;
            $col_widths[ $col_num + 2 ] = length($out)
              if length($out) > $col_widths[ $col_num + 2 ];

            $col_widths[ $col_num + 2 ] = length( $col_val->[0] )
              if length( $col_val->[0] ) > $col_widths[ $col_num + 2 ];
        }
        push @rows, \@row;
    }

    return \@rows if $style eq "none";

    my @sorted_width_refs =
      sort { $$a <=> $$b } map { \$_ } @col_widths[ 2 .. $#col_widths ];
    my $max_width = ${ $sorted_width_refs[-1] };

    my $total = @col_widths - 1;
    for (@col_widths) { $total += $_ }

  STRETCHER:
    while ( $total < 80 ) {
        my $min_width = ${ $sorted_width_refs[0] };
        last
          if $min_width == $max_width;
        for (@sorted_width_refs) {
            last
              if $$_ > $min_width;
            ++$$_;
            ++$total;
            last STRETCHER
              if $total >= 80;
        }
    }

    my $format = join( ' ', map { "%${_}s" } @col_widths ) . "\n";
    substr( $format, 1, 0 ) = '-';
    for (@rows) {
        printf $format, @$_;
    }

    return \@rows;
}

1;
