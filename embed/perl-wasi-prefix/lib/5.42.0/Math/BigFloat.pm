package Math::BigFloat;

use 5.006001;
use strict;
use warnings;

use Carp         qw< carp croak >;
use Scalar::Util qw< blessed >;
use Math::BigInt qw< >;

our $VERSION = '2.005002';
$VERSION =~ tr/_//d;

require Exporter;
our @ISA       = qw< Math::BigInt >;
our @EXPORT_OK = qw< bpi >;

use overload

  '+' => sub { $_[0]->copy()->badd( $_[1] ); },

  '-' => sub {
    my $c = $_[0]->copy();
    $_[2]
      ? $c->bneg()->badd( $_[1] )
      : $c->bsub( $_[1] );
  },

  '*' => sub { $_[0]->copy()->bmul( $_[1] ); },

  '/' => sub {
    $_[2]
      ? ref( $_[0] )->new( $_[1] )->bdiv( $_[0] )
      : $_[0]->copy()->bdiv( $_[1] );
  },

  '%' => sub {
    $_[2]
      ? ref( $_[0] )->new( $_[1] )->bmod( $_[0] )
      : $_[0]->copy()->bmod( $_[1] );
  },

  '**' => sub {
    $_[2]
      ? ref( $_[0] )->new( $_[1] )->bpow( $_[0] )
      : $_[0]->copy()->bpow( $_[1] );
  },

  '<<' => sub {
    $_[2]
      ? ref( $_[0] )->new( $_[1] )->bblsft( $_[0] )
      : $_[0]->copy()->bblsft( $_[1] );
  },

  '>>' => sub {
    $_[2]
      ? ref( $_[0] )->new( $_[1] )->bbrsft( $_[0] )
      : $_[0]->copy()->bbrsft( $_[1] );
  },

  '+=' => sub { $_[0]->badd( $_[1] ); },

  '-=' => sub { $_[0]->bsub( $_[1] ); },

  '*=' => sub { $_[0]->bmul( $_[1] ); },

  '/=' => sub { scalar $_[0]->bdiv( $_[1] ); },

  '%=' => sub { $_[0]->bmod( $_[1] ); },

  '**=' => sub { $_[0]->bpow( $_[1] ); },

  '<<=' => sub { $_[0]->bblsft( $_[1] ); },

  '>>=' => sub { $_[0]->bbrsft( $_[1] ); },

  '<' => sub {
    $_[2]
      ? ref( $_[0] )->new( $_[1] )->blt( $_[0] )
      : $_[0]->blt( $_[1] );
  },

  '<=' => sub {
    $_[2]
      ? ref( $_[0] )->new( $_[1] )->ble( $_[0] )
      : $_[0]->ble( $_[1] );
  },

  '>' => sub {
    $_[2]
      ? ref( $_[0] )->new( $_[1] )->bgt( $_[0] )
      : $_[0]->bgt( $_[1] );
  },

  '>=' => sub {
    $_[2]
      ? ref( $_[0] )->new( $_[1] )->bge( $_[0] )
      : $_[0]->bge( $_[1] );
  },

  '==' => sub { $_[0]->beq( $_[1] ); },

  '!=' => sub { $_[0]->bne( $_[1] ); },

  '<=>' => sub {
    my $cmp = $_[0]->bcmp( $_[1] );
    defined($cmp) && $_[2] ? -$cmp : $cmp;
  },

  'cmp' => sub {
    $_[2]
      ? "$_[1]" cmp $_[0]->bstr()
      : $_[0]->bstr() cmp "$_[1]";
  },

  '&' => sub {
    $_[2]
      ? ref( $_[0] )->new( $_[1] )->band( $_[0] )
      : $_[0]->copy()->band( $_[1] );
  },

  '&=' => sub { $_[0]->band( $_[1] ); },

  '|' => sub {
    $_[2]
      ? ref( $_[0] )->new( $_[1] )->bior( $_[0] )
      : $_[0]->copy()->bior( $_[1] );
  },

  '|=' => sub { $_[0]->bior( $_[1] ); },

  '^' => sub {
    $_[2]
      ? ref( $_[0] )->new( $_[1] )->bxor( $_[0] )
      : $_[0]->copy()->bxor( $_[1] );
  },

  '^=' => sub { $_[0]->bxor( $_[1] ); },

  'neg' => sub { $_[0]->copy()->bneg(); },

  '~' => sub { $_[0]->copy()->bnot(); },

  '++' => sub { $_[0]->binc() },

  '--' => sub { $_[0]->bdec() },

  'atan2' => sub {
    $_[2]
      ? ref( $_[0] )->new( $_[1] )->batan2( $_[0] )
      : $_[0]->copy()->batan2( $_[1] );
  },

  'cos' => sub { $_[0]->copy()->bcos(); },

  'sin' => sub { $_[0]->copy()->bsin(); },

  'exp' => sub { $_[0]->copy()->bexp( $_[1] ); },

  'abs' => sub { $_[0]->copy()->babs(); },

  'log' => sub { $_[0]->copy()->blog(); },

  'sqrt' => sub { $_[0]->copy()->bsqrt(); },

  'int' => sub { $_[0]->copy()->bint(); },

  'bool' => sub { $_[0]->is_zero() ? '' : 1; },

  '""' => sub { $_[0]->bstr(); },

  '0+' => sub { $_[0]->numify(); },

  '=' => sub { $_[0]->copy(); },

  ;

our $accuracy   = undef;
our $precision  = undef;
our $round_mode = 'even';
our $div_scale  = 40;

our $upgrade   = undef;
our $downgrade = undef;

our $_trap_nan = 0;
our $_trap_inf = 0;

my $nan = 'NaN';

my $LIB = Math::BigInt->config('lib');

my $IMPORT = 0;

my $LOG_10 =
  '2.3025850929940456840179914546843642076011014886287729760333279009675726097';
my $LOG_10_A = length($LOG_10) - 1;
my $LOG_2 =
  '0.6931471805599453094172321214581765680755001343602552541206800094933936220';
my $LOG_2_A = length($LOG_2) - 1;
my $HALF    = '0.5';

our $rnd_mode;
our $AUTOLOAD;

sub TIESCALAR {
    my ($class) = @_;
    bless \$round_mode, $class;
}

sub FETCH {
    return $round_mode;
}

sub STORE {
    $rnd_mode = ( ref $_[0] )->round_mode( $_[1] );
}

BEGIN {
    *objectify = \&Math::BigInt::objectify;

    $rnd_mode = 'even';
    tie $rnd_mode, 'Math::BigFloat';

    *as_number = \&as_int;
}

sub DESTROY {
}

sub AUTOLOAD {

    my $name = $AUTOLOAD;
    $name =~ s/^(.*):://;
    my $class = $1 || __PACKAGE__;

    $class->import() if $IMPORT == 0;

    my $bname = $name;
    $bname =~ s/^f/b/;

    if ( $bname ne $name && Math::BigFloat->can($bname) ) {
        no strict 'refs';
        return &{"Math::BigFloat::$bname"}(@_);
    }

    elsif ( Math::BigInt->can($bname) ) {
        no strict 'refs';
        return &{"Math::BigInt::$bname"}(@_);
    }

    else {
        croak("Can't call $class->$name(), not a valid method");
    }
}

sub isa {
    my ( $self, $class ) = @_;
    return if $class =~ /^Math::BigInt/;
    UNIVERSAL::isa( $self, $class );
}

sub config {
    my $self  = shift;
    my $class = ref($self) || $self || __PACKAGE__;

    if ( @_ == 1 && ref( $_[0] ) ne 'HASH' ) {
        my $param = shift;
        return $class if $param eq 'class';
        return $LIB   if $param eq 'with';
        return $self->SUPER::config($param);
    }

    my $cfg = $self->SUPER::config(@_);

    unless ( ref($self) ) {
        $cfg->{class} = $class;
        $cfg->{with}  = $LIB;
    }

    $cfg;
}

sub new {

    my $self    = shift;
    my $selfref = ref $self;
    my $class   = $selfref || $self;

    $class->import() if $IMPORT == 0;

    return $class->bzero() unless @_;

    my ( $wanted, @r ) = @_;

    if ( !defined($wanted) ) {
        return $class->bzero(@r);
    }

    if ( !ref($wanted) && $wanted eq "" ) {
        return $class->bnan(@r);
    }

    $self = bless {}, $class;

    if ( defined( blessed($wanted) ) && $wanted->can('as_float') ) {
        my $tmp = $wanted->as_float(@r);
        for my $attr ( 'sign', '_m', '_es', '_e' ) {
            $self->{$attr} = $tmp->{$attr};
        }
        return $self->round(@r);
    }

    $wanted = "$wanted";

    if (
        $wanted =~ / ^
          \s*                           # optional leading whitespace
          ( [+-]? )                     # optional sign
          0*                            # optional leading zeros
          ( [1-9] (?: [0-9]* [1-9] )? ) # significand
          \s*                           # optional trailing whitespace
          $
        /x
      )
    {
        my $dng = $class->downgrade();
        return $dng->new( $1 . $2 ) if $dng && $dng ne $class;
        $self->{sign} = $1 || '+';
        $self->{_m}   = $LIB->_new($2);
        $self->{_es}  = '+';
        $self->{_e}   = $LIB->_zero();
        $self->round(@r)
          unless @r >= 2 && !defined $r[0] && !defined $r[1];
        return $self;
    }

    if (
        $wanted =~ / ^
                     \s*
                     ( [+-]? )
                     inf (?: inity )?
                     \s*
                     \z
                   /ix
      )
    {
        my $sgn = $1 || '+';
        return $class->binf( $sgn, @r );
    }

    if (
        $wanted =~ / ^
                     \s*
                     ( [+-]? )
                     nan
                     \s*
                     \z
                   /ix
      )
    {
        return $class->bnan(@r);
    }

    my @parts;

    if (

        $wanted =~ /^\s*[+-]?0?[Xx]/
        and @parts = $class->_hex_str_to_flt_lib_parts($wanted)

        or

        $wanted =~ /^\s*[+-]?0?[Oo]/
        and @parts = $class->_oct_str_to_flt_lib_parts($wanted)

        or

        $wanted =~ /^\s*[+-]?0?[Bb]/
        and @parts = $class->_bin_str_to_flt_lib_parts($wanted)

        or

        @parts = $class->_dec_str_to_flt_lib_parts($wanted)
        or

        $wanted =~ /^\s*[+-]?0_*\d/
        and @parts = $class->_oct_str_to_flt_lib_parts($wanted)
      )
    {
        ( $self->{sign}, $self->{_m}, $self->{_es}, $self->{_e} ) = @parts;

        $self->round(@r)
          unless @r >= 2 && !defined( $r[0] ) && !defined( $r[1] );

        $self->_dng()
          if ( $self->is_int()
            || $self->is_inf()
            || $self->is_nan() );

        return $self;
    }

    return $class->bnan(@r);
}

sub from_dec {
    my $self    = shift;
    my $selfref = ref $self;
    my $class   = $selfref || $self;

    $class->import() if $IMPORT == 0;

    return $self if $selfref && $self->modify('from_dec');

    my $str = shift;
    my @r   = @_;

    if ( my @parts = $class->_dec_str_to_flt_lib_parts($str) ) {

        unless ($selfref) {
            $self = bless {}, $class;
        }

        ( $self->{sign}, $self->{_m}, $self->{_es}, $self->{_e} ) = @parts;

        $self->round(@r)
          unless @r >= 2 && !defined( $r[0] ) && !defined( $r[1] );

        $self->_dng()
          if ( $self->is_int()
            || $self->is_inf()
            || $self->is_nan() );

        return $self;
    }

    return $self->bnan(@r);
}

sub from_hex {
    my $self    = shift;
    my $selfref = ref $self;
    my $class   = $selfref || $self;

    $class->import() if $IMPORT == 0;

    return $self if $selfref && $self->modify('from_hex');

    my $str = shift;
    my @r   = @_;

    if ( my @parts = $class->_hex_str_to_flt_lib_parts($str) ) {

        unless ($selfref) {
            $self = bless {}, $class;
        }

        ( $self->{sign}, $self->{_m}, $self->{_es}, $self->{_e} ) = @parts;

        $self->round(@r)
          unless @r >= 2 && !defined( $r[0] ) && !defined( $r[1] );

        $self->_dng()
          if ( $self->is_int()
            || $self->is_inf()
            || $self->is_nan() );
        return $self;
    }

    return $self->bnan(@r);
}

sub from_oct {
    my $self    = shift;
    my $selfref = ref $self;
    my $class   = $selfref || $self;

    $class->import() if $IMPORT == 0;

    return $self if $selfref && $self->modify('from_oct');

    my $str = shift;
    my @r   = @_;

    if ( my @parts = $class->_oct_str_to_flt_lib_parts($str) ) {

        unless ($selfref) {
            $self = bless {}, $class;
        }

        ( $self->{sign}, $self->{_m}, $self->{_es}, $self->{_e} ) = @parts;

        $self->round(@r)
          unless @r >= 2 && !defined( $r[0] ) && !defined( $r[1] );

        $self->_dng()
          if ( $self->is_int()
            || $self->is_inf()
            || $self->is_nan() );
        return $self;
    }

    return $self->bnan(@r);
}

sub from_bin {
    my $self    = shift;
    my $selfref = ref $self;
    my $class   = $selfref || $self;

    $class->import() if $IMPORT == 0;

    return $self if $selfref && $self->modify('from_bin');

    my $str = shift;
    my @r   = @_;

    if ( my @parts = $class->_bin_str_to_flt_lib_parts($str) ) {

        unless ($selfref) {
            $self = bless {}, $class;
        }

        ( $self->{sign}, $self->{_m}, $self->{_es}, $self->{_e} ) = @parts;

        $self->round(@r)
          unless @r >= 2 && !defined( $r[0] ) && !defined( $r[1] );

        $self->_dng()
          if ( $self->is_int()
            || $self->is_inf()
            || $self->is_nan() );
        return $self;
    }

    return $self->bnan(@r);
}

sub from_bytes {
    my $self    = shift;
    my $selfref = ref $self;
    my $class   = $selfref || $self;

    $class->import() if $IMPORT == 0;

    return $self if $selfref && $self->modify('from_bytes');

    my $str = shift;
    my @r   = @_;

    $self = $class->bzero(@r) unless $selfref;

    $self->{sign} = "+";
    $self->{_m}   = $LIB->_from_bytes($str);
    $self->{_es}  = "+";
    $self->{_e}   = $LIB->_zero();
    $self->bnorm();

    $self->_dng();
    return $self;
}

sub from_ieee754 {
    my $self    = shift;
    my $selfref = ref $self;
    my $class   = $selfref || $self;

    $class->import() if $IMPORT == 0;

    return $self if $selfref && $self->modify('from_ieee754');

    my $in     = shift;
    my $format = shift;
    my $enc;
    my $k;
    my $b;
    my @r = @_;

    if ( $format =~ /^binary(\d+)\z/ ) {
        $k = $1;
        $b = 2;
    }
    elsif ( $format =~ /^decimal(\d+)(dpd|bcd)?\z/ ) {
        $k   = $1;
        $b   = 10;
        $enc = $2 || 'dpd';
    }
    elsif ( $format eq 'half' ) {
        $k = 16;
        $b = 2;
    }
    elsif ( $format eq 'single' ) {
        $k = 32;
        $b = 2;
    }
    elsif ( $format eq 'double' ) {
        $k = 64;
        $b = 2;
    }
    elsif ( $format eq 'quadruple' ) {
        $k = 128;
        $b = 2;
    }
    elsif ( $format eq 'octuple' ) {
        $k = 256;
        $b = 2;
    }
    elsif ( $format eq 'sexdecuple' ) {
        $k = 512;
        $b = 2;
    }

    if ( $b == 2 ) {

        my $p;
        my $t;
        my $w;

        if ( $k == 16 ) {
            $p = 11;
            $t = 10;
            $w = 5;
        }
        elsif ( $k == 32 ) {
            $p = 24;
            $t = 23;
            $w = 8;
        }
        elsif ( $k == 64 ) {
            $p = 53;
            $t = 52;
            $w = 11;
        }
        else {
            if ( $k < 128 || $k != 32 * sprintf( '%.0f', $k / 32 ) ) {
                croak "Number of bits must be 16, 32, 64, or >= 128 and",
                  " a multiple of 32";
            }
            $p = $k - sprintf( '%.0f', 4 * log($k) / log(2) ) + 13;
            $t = $p - 1;
            $w = $k - $t - 1;
        }

        my $emax = $class->new(2)->bpow( $w - 1 )->bdec();
        my $emin = 1 - $emax;
        my $bias = $emax;

        unless ( defined $in ) {
            carp("Input is undefined");
            return $self->bzero(@r);
        }

        my $len = CORE::length $in;
        if ( 8 * $len == $k ) {
            $in = unpack "B*", $in;
        }
        elsif ( 4 * $len == $k ) {
            if ( $in =~ /([^\da-f])/i ) {
                croak "Illegal hexadecimal digit '$1'";
            }
            $in = unpack "B*", pack "H*", $in;
        }
        elsif ( $len == $k ) {
            if ( $in =~ /([^01])/ ) {
                croak "Illegal binary digit '$1'";
            }
        }
        else {
            croak "Unknown input -- $in";
        }

        my $sign = substr( $in, 0, 1 ) eq '1' ? '-' : '+';
        my $expo = $class->from_bin( substr( $in, 1, $w ) );
        my $mant = $class->from_bin( substr( $in, $w + 1 ) );

        my $x;

        $expo->bsub($bias);

        if ( $expo < $emin ) {
            if ( $mant == 0 ) {
                $x = $class->bzero();
            }
            else {

                $x = $class->new("0.5");
                $x->bpow( $bias + $t - 1 )->bmul($mant);
                $x->bneg() if $sign eq '-';
            }
        }

        elsif ( $expo > $emax ) {
            if ( $mant == 0 ) {
                $x = $class->binf($sign);
            }
            else {
                $x = $class->bnan(@r);
            }
        }

        else {
            $mant = $class->new(2)->bpow($t)->badd($mant);
            if ( $expo < $t ) {
                $x = $class->new("0.5");
                $x->bpow( $t - $expo )->bmul($mant);
            }
            else {
                $x = $class->new(2);
                $x->bpow( $expo - $t )->bmul($mant);
            }
            $x->bneg() if $sign eq '-';
        }

        if ($selfref) {
            $self->{sign} = $x->{sign};
            $self->{_m}   = $x->{_m};
            $self->{_es}  = $x->{_es};
            $self->{_e}   = $x->{_e};
        }
        else {
            $self = $x;
        }

        $self->round(@r);
        $self->_dng()
          if ( $self->is_int()
            || $self->is_inf()
            || $self->is_nan() );
        return $self;
    }

    croak("The format '$format' is not yet supported.");
}

sub from_base {
    my $self    = shift;
    my $selfref = ref $self;
    my $class   = $selfref || $self;

    $class->import() if $IMPORT == 0;

    return $self if $selfref && $self->modify('from_base');

    my ( $str, $base, $cs, @r ) = @_;

    $base = $class->new($base) unless ref($base);

    croak("the base must be a finite integer >= 2")
      if $base < 2 || !$base->is_int();

    $self = $class->bzero() unless $selfref;

    unless ( defined $cs ) {
        return $self->from_bin( $str, @r ) if $base == 2;
        return $self->from_oct( $str, @r ) if $base == 8;
        return $self->from_hex( $str, @r ) if $base == 16;
        return $self->from_dec( $str, @r ) if $base == 10;
    }

    croak("from_base() requires a newer version of the $LIB library.")
      unless $LIB->can('_from_base');

    my $base_lib = $LIB->_lsft( $LIB->_copy( $base->{_m} ), $base->{_e}, 10 );
    $self->{sign} = '+';
    $self->{_m}  = $LIB->_from_base( $str, $base_lib, defined($cs) ? $cs : () );
    $self->{_es} = "+";
    $self->{_e}  = $LIB->_zero();
    $self->bnorm();

    $self->bround(@r);
    $self->_dng();
    return $self;
}

sub bzero {

    unless (
        @_
        && ( defined( blessed( $_[0] ) ) && $_[0]->isa(__PACKAGE__)
            || $_[0] =~ /^[a-z]\w*(?:::[a-z]\w*)*$/i )
      )
    {
        unshift @_, __PACKAGE__;
    }

    my $self    = shift;
    my $selfref = ref $self;
    my $class   = $selfref || $self;

    $class->import() if $IMPORT == 0;

    return $self if $selfref && $self->modify('bzero');

    my $dng = $class->downgrade();
    if ( $dng && $dng ne $class ) {
        return $self->_dng()->bzero(@_) if $selfref;
        return $dng->bzero(@_);
    }

    my @r = @_;

    $self = bless {}, $class unless $selfref;

    $self->{sign} = '+';
    $self->{_m}   = $LIB->_zero();
    $self->{_es}  = '+';
    $self->{_e}   = $LIB->_zero();

    if (@r) {
        if ( @r >= 2 && defined( $r[0] ) && defined( $r[1] ) ) {
            carp "can't specify both accuracy and precision";
            return $self->bnan();
        }
        $self->{accuracy}  = $r[0];
        $self->{precision} = $r[1];
    }
    else {
        unless ($selfref) {
            $self->{accuracy}  = $class->accuracy();
            $self->{precision} = $class->precision();
        }
    }

    return $self;
}

sub bone {

    unless (
        @_
        && ( defined( blessed( $_[0] ) ) && $_[0]->isa(__PACKAGE__)
            || $_[0] =~ /^[a-z]\w*(?:::[a-z]\w*)*$/i )
      )
    {
        unshift @_, __PACKAGE__;
    }

    my $self    = shift;
    my $selfref = ref $self;
    my $class   = $selfref || $self;

    $class->import() if $IMPORT == 0;

    return $self if $selfref && $self->modify('bone');

    my $dng = $class->downgrade();
    if ( $dng && $dng ne $class ) {
        return $self->_dng()->bone(@_) if $selfref;
        return $dng->bone(@_);
    }

    my $sign = '+';
    if ( defined( $_[0] ) && $_[0] =~ /^\s*([+-])\s*$/ ) {
        $sign = $1;
        shift;
    }

    my @r = @_;

    $self = bless {}, $class unless $selfref;

    $self->{sign} = $sign;
    $self->{_m}   = $LIB->_one();
    $self->{_es}  = '+';
    $self->{_e}   = $LIB->_zero();

    if (@r) {
        if ( @r >= 2 && defined( $r[0] ) && defined( $r[1] ) ) {
            carp "can't specify both accuracy and precision";
            return $self->bnan();
        }
        $self->{accuracy}  = $_[0];
        $self->{precision} = $_[1];
    }
    else {
        unless ($selfref) {
            $self->{accuracy}  = $class->accuracy();
            $self->{precision} = $class->precision();
        }
    }

    return $self;
}

sub binf {

    unless (
        @_
        && ( defined( blessed( $_[0] ) ) && $_[0]->isa(__PACKAGE__)
            || $_[0] =~ /^[a-z]\w*(?:::[a-z]\w*)*$/i )
      )
    {
        unshift @_, __PACKAGE__;
    }

    my $self    = shift;
    my $selfref = ref $self;
    my $class   = $selfref || $self;

    {
        no strict 'refs';
        if ( ${"${class}::_trap_inf"} ) {
            croak("Tried to create +-inf in $class->binf()");
        }
    }

    $class->import() if $IMPORT == 0;

    return $self if $selfref && $self->modify('binf');

    my $sign = '+';
    if ( defined( $_[0] ) && $_[0] =~ /^\s*([+-])(inf|$)/i ) {
        $sign = $1;
        shift;
    }

    my @r = @_;

    my $dng = $class->downgrade();
    if ( $dng && $dng ne $class ) {
        return $self->_dng()->binf( $sign, @r ) if $selfref;
        return $dng->binf( $sign, @r );
    }

    $self = bless {}, $class unless $selfref;

    $self->{sign} = $sign . 'inf';
    $self->{_m}   = $LIB->_zero();
    $self->{_es}  = '+';
    $self->{_e}   = $LIB->_zero();

    if (@r) {
        if ( @r >= 2 && defined( $r[0] ) && defined( $r[1] ) ) {
            carp "can't specify both accuracy and precision";
            return $self->bnan();
        }
        $self->{accuracy}  = $r[0];
        $self->{precision} = $r[1];
    }
    else {
        unless ($selfref) {
            $self->{accuracy}  = $class->accuracy();
            $self->{precision} = $class->precision();
        }
    }

    return $self;
}

sub bnan {

    unless (
        @_
        && ( defined( blessed( $_[0] ) ) && $_[0]->isa(__PACKAGE__)
            || $_[0] =~ /^[a-z]\w*(?:::[a-z]\w*)*$/i )
      )
    {
        unshift @_, __PACKAGE__;
    }

    my $self    = shift;
    my $selfref = ref $self;
    my $class   = $selfref || $self;

    {
        no strict 'refs';
        if ( ${"${class}::_trap_nan"} ) {
            croak("Tried to create NaN in $class->bnan()");
        }
    }

    $class->import() if $IMPORT == 0;

    return $self if $selfref && $self->modify('bnan');

    my $dng = $class->downgrade();
    if ( $dng && $dng ne $class ) {
        return $self->_dng()->bnan(@_) if $selfref;
        return $dng->bnan(@_);
    }

    my @r = @_;

    $self = bless {}, $class unless $selfref;

    $self->{sign} = $nan;
    $self->{_m}   = $LIB->_zero();
    $self->{_es}  = '+';
    $self->{_e}   = $LIB->_zero();

    if (@r) {
        if ( @r >= 2 && defined( $r[0] ) && defined( $r[1] ) ) {
            carp "can't specify both accuracy and precision";
            return $self->bnan();
        }
        $self->{accuracy}  = $r[0];
        $self->{precision} = $r[1];
    }
    else {
        unless ($selfref) {
            $self->{accuracy}  = $class->accuracy();
            $self->{precision} = $class->precision();
        }
    }

    return $self;
}

sub bpi {

    unless (
        @_
        && ( defined( blessed( $_[0] ) ) && $_[0]->isa(__PACKAGE__)
            || $_[0] =~ /^[a-z]\w*(?:::[a-z]\w*)*$/i )
      )
    {
        unshift @_, __PACKAGE__;
    }

    my $self    = shift;
    my $selfref = ref $self;
    my $class   = $selfref || $self;
    my @r       = @_;

    $class->import() if $IMPORT == 0;

    if ($selfref) {
        return $self if $self->modify('bpi');
    }
    else {
        $self = bless {}, $class;
    }

    ( $self, @r ) = $self->_find_round_parameters(@r);

    my $n =
        defined $r[0] ? $r[0]
      : defined $r[1] ? 1 - $r[1]
      :                 $self->div_scale();

    my $rmode = defined $r[2] ? $r[2] : $self->round_mode();

    my $pi;

    if ( $n <= 1000 ) {

        my $all_digits = <<EOF;
314159265358979323846264338327950288419716939937510582097494459230781640628
620899862803482534211706798214808651328230664709384460955058223172535940812
848111745028410270193852110555964462294895493038196442881097566593344612847
564823378678316527120190914564856692346034861045432664821339360726024914127
372458700660631558817488152092096282925409171536436789259036001133053054882
046652138414695194151160943305727036575959195309218611738193261179310511854
807446237996274956735188575272489122793818301194912983367336244065664308602
139494639522473719070217986094370277053921717629317675238467481846766940513
200056812714526356082778577134275778960917363717872146844090122495343014654
958537105079227968925892354201995611212902196086403441815981362977477130996
051870721134999999837297804995105973173281609631859502445945534690830264252
230825334468503526193118817101000313783875288658753320838142061717766914730
359825349042875546873115956286388235378759375195778185778053217122680661300
192787661119590921642019893809525720106548586327886593615338182796823030195
EOF

        my $round_up;

        my $nchrs = $n + int( $n / 75 );

        my $digits = substr( $all_digits, 0, $nchrs );

        if ( $rmode eq 'trunc' ) {
            $round_up = 0;
        }
        else {
            my $next_digit = substr( $all_digits, $nchrs, 1 );
            $round_up = $next_digit lt '5' ? 0 : 1;
        }

        $digits =~ tr/0-9//cd;

        if ($round_up) {
            my $last_digit = substr( $digits, -1, 1 );
            if ( $last_digit lt '9' ) {
                substr( $digits, -1, 1 ) = ++$last_digit;
            }
            else {
                $digits =~ s{([0-8])(9+)$}
                            { ($1 + 1) . ("0" x CORE::length($2)) }e;
            }
        }

        $pi = bless {
            sign => '+',
            _m   => $LIB->_new($digits),
            _es  => CORE::length($digits) > 1 ? '-' : '+',
            _e   => $LIB->_new( $n - 1 ),
        }, $class;

    }
    else {

        $n += 8;

        $HALF = $class->new($HALF) unless ref($HALF);
        my ( $an, $bn, $tn, $pn ) = (
            $class->bone,
            $HALF->copy()->bsqrt($n),
            $HALF->copy()->bmul($HALF),
            $class->bone
        );
        while ( $pn < $n ) {
            my $prev_an = $an->copy();
            $an->badd($bn)->bmul( $HALF, $n );
            $bn->bmul($prev_an)->bsqrt($n);
            $prev_an->bsub($an);
            $tn->bsub( $pn * $prev_an * $prev_an );
            $pn->badd($pn);
        }
        $an->badd($bn);
        $an->bmul( $an, $n )->bdiv( 4 * $tn, $n );

        $an->round(@r);
        $pi = $an;
    }

    if ( defined $r[0] ) {
        $pi->accuracy( $r[0] );
    }
    elsif ( defined $r[1] ) {
        $pi->precision( $r[1] );
    }

    $pi->_dng() if ( $pi->is_int()
        || $pi->is_inf()
        || $pi->is_nan() );

    %$self = %$pi;
    bless $self, ref($pi);
    return $self;
}

sub copy {
    my ( $x, $class );
    if ( ref( $_[0] ) ) {
        $x     = shift;
        $class = ref($x);
    }
    else {
        $class = shift;
        $x     = shift;
    }

    carp "Rounding is not supported for ", ( caller(0) )[3], "()" if @_;

    my $copy = bless {}, $class;

    $copy->{sign} = $x->{sign};
    $copy->{_es}  = $x->{_es};
    $copy->{_m}   = $LIB->_copy( $x->{_m} );
    $copy->{_e}   = $LIB->_copy( $x->{_e} );

    $copy->{accuracy}  = $x->{accuracy}  if exists $x->{accuracy};
    $copy->{precision} = $x->{precision} if exists $x->{precision};

    return $copy;
}

sub as_int {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    my $upg = Math::BigInt->upgrade();
    my $dng = Math::BigInt->downgrade();
    Math::BigInt->upgrade(undef);
    Math::BigInt->downgrade(undef);

    my $y;
    if ( $x->isa("Math::BigInt") ) {
        $y = $x->copy();
    }
    else {
        if ( $x->is_inf() ) {
            $y = Math::BigInt->binf( $x->sign() );
        }
        elsif ( $x->is_nan() ) {
            $y = Math::BigInt->bnan();
        }
        else {
            $y = Math::BigInt->new( $x->copy()->bint()->bdstr() );
        }

        ( $y->{accuracy}, $y->{precision} ) =
          ( $x->{accuracy}, $x->{precision} );
    }

    $y->round(@r);

    Math::BigInt->upgrade($upg);
    Math::BigInt->downgrade($dng);

    return $y;
}

sub as_rat {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    require Math::BigRat;
    my $upg = Math::BigRat->upgrade();
    my $dng = Math::BigRat->downgrade();
    Math::BigRat->upgrade(undef);
    Math::BigRat->downgrade(undef);

    my $y;
    if ( $x->isa("Math::BigRat") ) {
        $y = $x->copy();
    }
    else {

        if ( $x->is_inf() ) {
            $y = Math::BigRat->binf( $x->sign() );
        }
        elsif ( $x->is_nan() ) {
            $y = Math::BigRat->bnan();
        }
        else {
            $y = Math::BigRat->new( $x->bfstr() );
        }

        ( $y->{accuracy}, $y->{precision} ) =
          ( $x->{accuracy}, $x->{precision} );
    }

    $y->round(@r);

    Math::BigRat->upgrade($upg);
    Math::BigRat->downgrade($dng);

    return $y;
}

sub as_float {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    require Math::BigFloat;
    my $upg = Math::BigFloat->upgrade();
    my $dng = Math::BigFloat->downgrade();
    Math::BigFloat->upgrade(undef);
    Math::BigFloat->downgrade(undef);

    my $y;
    if ( $x->isa("Math::BigFloat") ) {
        $y = $x->copy();
    }
    else {
        if ( $x->is_inf() ) {
            $y = Math::BigFloat->binf( $x->sign() );
        }
        elsif ( $x->is_nan() ) {
            $y = Math::BigFloat->bnan();
        }
        else {
            if ( $x->isa("Math::BigRat") ) {
                if ( $x->is_int() ) {
                    $y = Math::BigFloat->new( $x->bdstr() );
                }
                else {
                    my ( $num, $den ) = $x->fparts();
                    my $str = $num->as_float()->bdiv( $den, @r )->bdstr();
                    $y = Math::BigFloat->new($str);
                }
            }
            else {
                $y = Math::BigFloat->new( $x->bdstr() );
            }
        }

        ( $y->{accuracy}, $y->{precision} ) =
          ( $x->{accuracy}, $x->{precision} );
    }

    $y->round(@r);

    Math::BigFloat->upgrade($upg);
    Math::BigFloat->downgrade($dng);

    return $y;
}

sub is_zero {
    my ( undef, $x ) = ref( $_[0] ) ? ( undef, @_ ) : objectify( 1, @_ );

    return 0 if $x->{sign} ne '+';
    return 1 if $LIB->_is_zero( $x->{_m} );
    return 0;
}

sub is_one {
    my ( undef, $x, $sign ) = ref( $_[0] ) ? ( undef, @_ ) : objectify( 1, @_ );

    if ( defined($sign) ) {
        croak 'is_one(): sign argument must be "+" or "-"'
          unless $sign eq '+' || $sign eq '-';
    }
    else {
        $sign = '+';
    }

    return 0 if $x->{sign} ne $sign;
    $LIB->_is_zero( $x->{_e} ) && $LIB->_is_one( $x->{_m} ) ? 1 : 0;
}

sub is_odd {
    my ( undef, $x ) = ref( $_[0] ) ? ( undef, @_ ) : objectify( 1, @_ );

    return 0 unless $x->is_finite();
    $LIB->_is_zero( $x->{_e} ) && $LIB->_is_odd( $x->{_m} ) ? 1 : 0;
}

sub is_even {
    my ( undef, $x ) = ref( $_[0] ) ? ( undef, @_ ) : objectify( 1, @_ );

    return 0 unless $x->is_finite();
    ( $x->{_es} eq '+' )
      && ( $LIB->_is_even( $x->{_m} ) ) ? 1 : 0;
}

sub is_int {
    my ( undef, $x ) = ref( $_[0] ) ? ( undef, @_ ) : objectify( 1, @_ );

    return 0 unless $x->is_finite();
    return $x->{_es} eq '+' ? 1 : 0;
}

sub bcmp {

    my ( $class, $x, $y, @r ) =
      ref( $_[0] ) && ref( $_[0] ) eq ref( $_[1] )
      ? ( ref( $_[0] ), @_ )
      : objectify( 2, @_ );

    carp "Rounding is not supported for ", ( caller(0) )[3], "()" if @r;

    return if $x->is_nan() || $y->is_nan();

    return 0
      if ( $x->is_inf("+") && $y->is_inf("+")
        || $x->is_inf("-") && $y->is_inf("-") );
    return +1 if $x->is_inf("+");
    return -1 if $x->is_inf("-");
    return -1 if $y->is_inf("+");
    return +1 if $y->is_inf("-");

    return +1 if $x->{sign} eq '+' && $y->{sign} eq '-';
    return -1 if $x->{sign} eq '-' && $y->{sign} eq '+';

    my $xz = $x->is_zero();
    my $yz = $y->is_zero();
    return 0  if $xz && $yz;
    return -1 if $xz && $y->{sign} eq '+';
    return +1 if $yz && $x->{sign} eq '+';

    my $cmp;

    my $mxl = $LIB->_len( $x->{_m} );
    my $myl = $LIB->_len( $y->{_m} );

    if ( $mxl == $myl ) {

        if ( $x->{_es} eq '+' && $y->{_es} eq '-' ) {
            $cmp = +1;
        }
        elsif ( $x->{_es} eq '-' && $y->{_es} eq '+' ) {
            $cmp = -1;
        }

        else {
            $cmp = $LIB->_acmp( $x->{_e}, $y->{_e} );
            $cmp = -$cmp if $x->{_es} eq '-';
        }

        $cmp = -$cmp if $x->{sign} eq '-';
        return $cmp  if $cmp;

    }

    my $ex;
    my $ey;

    if ( $x->{_es} eq '+' ) {

        if ( $y->{_es} eq '+' ) {
            $ex = $LIB->_copy( $x->{_e} );
            $ey = $LIB->_copy( $y->{_e} );
        }

        else {
            $ex = $LIB->_copy( $x->{_e} );
            $ex = $LIB->_add( $ex, $y->{_e} );
            $ey = $LIB->_zero();
        }

    }
    else {

        if ( $y->{_es} eq '+' ) {
            $ex = $LIB->_zero();
            $ey = $LIB->_copy( $y->{_e} );
            $ey = $LIB->_add( $ey, $x->{_e} );
        }

        else {
            $ex = $LIB->_copy( $y->{_e} );
            $ey = $LIB->_copy( $x->{_e} );
        }

    }

    $ex = $LIB->_add( $ex, $LIB->_new($mxl) );
    $ey = $LIB->_add( $ey, $LIB->_new($myl) );

    $cmp = $LIB->_acmp( $ex, $ey );
    $cmp = -$cmp if $x->{sign} eq '-';
    return $cmp if $cmp;

    my $mx = $x->{_m};
    my $my = $y->{_m};

    if ( $mxl > $myl ) {
        $my = $LIB->_lsft( $LIB->_copy($my), $LIB->_new( $mxl - $myl ), 10 );
    }
    elsif ( $mxl < $myl ) {
        $mx = $LIB->_lsft( $LIB->_copy($mx), $LIB->_new( $myl - $mxl ), 10 );
    }

    $cmp = $LIB->_acmp( $mx, $my );
    $cmp = -$cmp if $x->{sign} eq '-';
    return $cmp;

}

sub bacmp {

    my ( $class, $x, $y, @r ) =
      ref( $_[0] ) && ref( $_[0] ) eq ref( $_[1] )
      ? ( ref( $_[0] ), @_ )
      : objectify( 2, @_ );

    carp "Rounding is not supported for ", ( caller(0) )[3], "()" if @r;

    if ( $x->{sign} !~ /^[+-]$/ || $y->{sign} !~ /^[+-]$/ ) {
        return   if ( $x->is_nan() || $y->is_nan() );
        return 0 if ( $x->is_inf() && $y->is_inf() );
        return 1 if ( $x->is_inf() && !$y->is_inf() );
        return -1;
    }

    my $xz = $x->is_zero();
    my $yz = $y->is_zero();
    return 0  if $xz && $yz;
    return -1 if $xz && !$yz;
    return 1  if $yz && !$xz;

    my $lxm = $LIB->_len( $x->{_m} );
    my $lym = $LIB->_len( $y->{_m} );
    my ( $xes, $yes ) = ( 1, 1 );
    $xes = -1 if $x->{_es} ne '+';
    $yes = -1 if $y->{_es} ne '+';
    my $lx = $lxm + $xes * $LIB->_num( $x->{_e} );
    my $ly = $lym + $yes * $LIB->_num( $y->{_e} );
    my $l  = $lx - $ly;
    return $l <=> 0 if $l != 0;

    my $diff = $lxm - $lym;
    my $xm   = $x->{_m};
    my $ym   = $y->{_m};
    if ( $diff > 0 ) {
        $ym = $LIB->_copy( $y->{_m} );
        $ym = $LIB->_lsft( $ym, $LIB->_new($diff), 10 );
    }
    elsif ( $diff < 0 ) {
        $xm = $LIB->_copy( $x->{_m} );
        $xm = $LIB->_lsft( $xm, $LIB->_new( -$diff ), 10 );
    }
    $LIB->_acmp( $xm, $ym );
}

sub bneg {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    return $x if $x->modify('bneg');

    $x->{sign} =~ tr/+-/-+/
      unless $x->{sign} eq '+' && $LIB->_is_zero( $x->{_m} );

    $x->round(@r);
    $x->_dng() if ( $x->is_int()
        || $x->is_inf()
        || $x->is_nan() );
    return $x;
}

sub bnorm {

    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    carp "Rounding is not supported for ", ( caller(0) )[3], "()" if @r;

    if ( $x->{sign} !~ /^[+-]$/ ) {
        $x->round(@r);
        $x->_dng();
        return $x;
    }

    my $zeros = $LIB->_zeros( $x->{_m} );
    if ( $zeros != 0 ) {
        my $z = $LIB->_new($zeros);
        $x->{_m} = $LIB->_rsft( $x->{_m}, $z, 10 );
        if ( $x->{_es} eq '-' ) {
            if ( $LIB->_acmp( $x->{_e}, $z ) >= 0 ) {
                $x->{_e}  = $LIB->_sub( $x->{_e}, $z );
                $x->{_es} = '+' if $LIB->_is_zero( $x->{_e} );
            }
            else {
                $x->{_e}  = $LIB->_sub( $LIB->_copy($z), $x->{_e} );
                $x->{_es} = '+';
            }
        }
        else {
            $x->{_e} = $LIB->_add( $x->{_e}, $z );
        }
    }
    else {
        if ( $LIB->_is_zero( $x->{_m} ) ) {
            $x->{sign} = '+';
            $x->{_es}  = '+';
            $x->{_e}   = $LIB->_zero();
        }
    }

    $x->_dng() if $x->is_int();
    return $x;
}

sub binc {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    return $x if $x->modify('binc');

    if ( $x->is_inf() || $x->is_nan() ) {
        $x->round(@r);
        $x->_dng();
        return $x;
    }

    if ( $x->{_es} eq '-' ) {
        return $x->badd( $class->bone(), @r );
    }

    if ( !$LIB->_is_zero( $x->{_e} ) ) {
        $x->{_m}  = $LIB->_lsft( $x->{_m}, $x->{_e}, 10 );
        $x->{_e}  = $LIB->_zero();
        $x->{_es} = '+';
    }

    if ( $x->{sign} eq '+' ) {
        $x->{_m} = $LIB->_inc( $x->{_m} );
        return $x->bnorm()->bround(@r);
    }
    elsif ( $x->{sign} eq '-' ) {
        $x->{_m}   = $LIB->_dec( $x->{_m} );
        $x->{sign} = '+' if $LIB->_is_zero( $x->{_m} );
        return $x->bnorm()->bround(@r);
    }

    $x->_dng() if ( $x->is_int()
        || $x->is_inf()
        || $x->is_nan() );
    return $x;
}

sub bdec {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    return $x if $x->modify('bdec');

    if ( $x->is_inf() || $x->is_nan() ) {
        $x->round(@r);
        $x->_dng();
        return $x;
    }

    if ( $x->{_es} eq '-' ) {
        return $x->badd( $class->bone('-'), @r );
    }

    if ( !$LIB->_is_zero( $x->{_e} ) ) {
        $x->{_m}  = $LIB->_lsft( $x->{_m}, $x->{_e}, 10 );
        $x->{_e}  = $LIB->_zero();
        $x->{_es} = '+';
    }

    my $zero = $x->is_zero();
    if ( ( $x->{sign} eq '-' ) || $zero ) {
        $x->{_m}   = $LIB->_inc( $x->{_m} );
        $x->{sign} = '-' if $zero;
        $x->{sign} = '+' if $LIB->_is_zero( $x->{_m} );
        $x->bnorm();
    }
    elsif ( $x->{sign} eq '+' ) {
        $x->{_m} = $LIB->_dec( $x->{_m} );
        $x->bnorm();
    }

    $x->round(@r);
    $x->_dng() if ( $x->is_int()
        || $x->is_inf()
        || $x->is_nan() );
    return $x;
}

sub badd {
    my ( $class, $x, $y, @r ) =
      ref( $_[0] ) && ref( $_[0] ) eq ref( $_[1] )
      ? ( ref( $_[0] ), @_ )
      : objectify( 2, @_ );

    return $x if $x->modify('badd');

    unless ( $x->is_finite() && $y->is_finite() ) {

        return $x->bnan(@r) if $x->is_nan() || $y->is_nan();

        return $x->is_inf("+")
          ? (
              $y->is_inf("-")
            ? $x->bnan(@r)
            : $x->binf( "+", @r )
          )
          : $x->is_inf("-") ? (
              $y->is_inf("+")
            ? $x->bnan(@r)
            : $x->binf( "-", @r )
          )
          : (
              $y->is_inf("+")
            ? $x->binf( "+", @r )
            : $x->binf( "-", @r )
          );
    }

    return $x->_upg()->badd( $y, @r ) if $class->upgrade();

    $r[3] = $y;

    if ( $y->is_zero() ) {
        $x->round(@r);
    }

    elsif ( $x->is_zero() ) {
        $x->{_e}   = $LIB->_copy( $y->{_e} );
        $x->{_es}  = $y->{_es};
        $x->{_m}   = $LIB->_copy( $y->{_m} );
        $x->{sign} = $y->{sign} || $nan;
        $x->round(@r);
    }

    else {

        my $e = $y->{_e};
        $e = $LIB->_zero() if !defined $e;
        $e = $LIB->_copy($e);

        my $es;

        ( $e, $es ) = $LIB->_ssub( $e, $y->{_es} || '+', $x->{_e}, $x->{_es} );

        my $add = $LIB->_copy( $y->{_m} );

        if ( $es eq '-' ) {
            $x->{_m} = $LIB->_lsft( $x->{_m}, $e, 10 );
            ( $x->{_e}, $x->{_es} ) =
              $LIB->_sadd( $x->{_e}, $x->{_es}, $e, $es );
        }
        elsif ( !$LIB->_is_zero($e) ) {
            $add = $LIB->_lsft( $add, $e, 10 );
        }

        if ( $x->{sign} eq $y->{sign} ) {
            $x->{_m} = $LIB->_add( $x->{_m}, $add );
        }
        else {
            ( $x->{_m}, $x->{sign} ) =
              $LIB->_sadd( $x->{_m}, $x->{sign}, $add, $y->{sign} );
        }

        $x->bnorm()->round(@r);
    }

    $x->_dng() if ( $x->is_int()
        || $x->is_inf()
        || $x->is_nan() );
    return $x;
}

sub bsub {
    my ( $class, $x, $y, @r ) =
      ref( $_[0] ) && ref( $_[0] ) eq ref( $_[1] )
      ? ( ref( $_[0] ), @_ )
      : objectify( 2, @_ );

    return $x if $x->modify('bsub');

    $r[3] = $y;

    unless ( $x->is_finite() && $y->is_finite() ) {

        return $x->bnan(@r) if $x->is_nan() || $y->is_nan();

        return $x->is_inf("+")
          ? (
              $y->is_inf("+")
            ? $x->bnan(@r)
            : $x->binf( "+", @r )
          )
          : $x->is_inf("-") ? (
              $y->is_inf("-")
            ? $x->bnan(@r)
            : $x->binf( "-", @r )
          )
          : (
              $y->is_inf("+")
            ? $x->binf( "-", @r )
            : $x->binf( "+", @r )
          );
    }

    $x->badd( $y->copy()->bneg(), @r );
    return $x;
}

sub bmul {

    my ( $class, $x, $y, @r ) =
      ref( $_[0] ) && ref( $_[0] ) eq ref( $_[1] )
      ? ( ref( $_[0] ), @_ )
      : objectify( 2, @_ );

    return $x if $x->modify('bmul');

    return $x->bnan(@r) if $x->is_nan() || $y->is_nan();

    if ( ( $x->{sign} =~ /^[+-]inf$/ ) || ( $y->{sign} =~ /^[+-]inf$/ ) ) {
        return $x->bnan(@r) if $x->is_zero() || $y->is_zero();
        return $x->binf(@r) if ( $x->{sign} =~ /^\+/ && $y->{sign} =~ /^\+/ );
        return $x->binf(@r) if ( $x->{sign} =~ /^-/  && $y->{sign} =~ /^-/ );
        return $x->binf( '-', @r );
    }

    return $x->_upg()->bmul( $y, @r ) if $class->upgrade();

    $x->{_m} = $LIB->_mul( $x->{_m}, $y->{_m} );
    ( $x->{_e}, $x->{_es} ) =
      $LIB->_sadd( $x->{_e}, $x->{_es}, $y->{_e}, $y->{_es} );

    $r[3] = $y;

    $x->{sign} = $x->{sign} ne $y->{sign} ? '-' : '+';
    $x->bnorm->round(@r);

    $x->_dng() if ( $x->is_int()
        || $x->is_inf()
        || $x->is_nan() );
    return $x;
}

*bdiv = \&bfdiv;
*bmod = \&bfmod;

sub bfdiv {

    my ( $class, $x, $y, @r ) =
      ref( $_[0] ) && ref( $_[0] ) eq ref( $_[1] )
      ? ( ref( $_[0] ), @_ )
      : objectify( 2, @_ );

    return $x if $x->modify('bfdiv');

    my $wantarray = wantarray;

    if ( $x->is_nan() || $y->is_nan() ) {
        return $wantarray
          ? ( $x->bnan(@r), $class->bnan(@r) )
          : $x->bnan(@r);
    }

    if ( $y->is_zero() ) {
        my $rem;
        if ($wantarray) {
            $rem = $x->copy()->round(@r);
            $rem->_dng() if $rem->is_int();
        }
        if ( $x->is_zero() ) {
            $x->bnan(@r);
        }
        else {
            $x->binf( $x->{sign}, @r );
        }
        return $wantarray ? ( $x, $rem ) : $x;
    }

    if ( $x->is_inf() ) {
        my $rem;
        $rem = $class->bnan(@r) if $wantarray;
        if ( $y->is_inf() ) {
            $x->bnan(@r);
        }
        else {
            my $sign = $x->bcmp(0) == $y->bcmp(0) ? '+' : '-';
            $x->binf( $sign, @r );
        }
        return $wantarray ? ( $x, $rem ) : $x;
    }

    if ( $y->is_inf() ) {
        my $rem;
        if ($wantarray) {
            if ( $x->is_zero() || $x->bcmp(0) == $y->bcmp(0) ) {
                $rem = $x->copy()->round(@r);
                $rem->_dng() if $rem->is_int();
                $x->bzero(@r);
            }
            else {
                $rem = $class->binf( $y->{sign}, @r );
                $x->bone( '-', @r );
            }
        }
        else {
            $x->bzero(@r);
        }
        return $wantarray ? ( $x, $rem ) : $x;
    }

    my $fallback = 0;
    my ( @params, $scale );
    ( $x, @params ) = $x->_find_round_parameters( $r[0], $r[1], $r[2], $y );

    if ( $x->is_nan() ) {
        $x->round(@r);
        return $wantarray ? ( $x, $class->bnan(@r) ) : $x;
    }

    if ( scalar @params == 0 ) {
        $params[0] = $class->div_scale();
        $scale     = $params[0] + 4;
        $params[2] = $r[2];
        $fallback  = 1;
    }
    else {
        $scale = abs( $params[0] || $params[1] ) + 4;
    }

    my $dng = Math::BigFloat->downgrade();
    Math::BigFloat->downgrade(undef);

    my $rem;
    $rem = $class->bzero() if $wantarray;

    $y = $class->new($y) unless $y->isa('Math::BigFloat');

    my $lx = $LIB->_len( $x->{_m} );
    my $ly = $LIB->_len( $y->{_m} );
    $scale = $lx if $lx > $scale;
    $scale = $ly if $ly > $scale;
    my $diff = $ly - $lx;
    $scale += $diff if $diff > 0;

    my $xsign = $x->{sign};
    my $ysign = $y->{sign};

    $y->{sign} =~ tr/+-/-+/;
    my $same = $xsign ne $x->{sign};
    $y->{sign} = $ysign;

    if ($same) {
        $x->bone();
    }
    else {
        $rem = $x->copy() if $wantarray;

        $x->{sign} = $x->{sign} ne $y->{sign} ? '-' : '+';

        $y = $class->new($y) unless $y->isa('Math::BigFloat');

        $x->{_m} = $LIB->_lsft( $x->{_m}, $LIB->_new($scale), 10 );
        $x->{_m} = $LIB->_div( $x->{_m}, $y->{_m} );

        ( $x->{_e}, $x->{_es} ) =
          $LIB->_ssub( $x->{_e}, $x->{_es}, $y->{_e}, $y->{_es} );

        ( $x->{_e}, $x->{_es} ) =
          $LIB->_ssub( $x->{_e}, $x->{_es}, $LIB->_new($scale), '+' );

        $x->bnorm();
    }

    if ( defined $params[0] ) {
        $x->{accuracy} = undef;
        $x->bround( $params[0], $params[2] );
    }
    else {
        $x->{precision} = undef;
        $x->bfround( $params[1], $params[2] );
    }
    if ($fallback) {
        $x->{accuracy}  = undef;
        $x->{precision} = undef;
    }

    Math::BigFloat->downgrade($dng);

    if ($wantarray) {
        $x->bfloor();
        $rem->bfmod( $y, @params );
        if ($fallback) {
            $rem->{accuracy}  = undef;
            $rem->{precision} = undef;
        }
        $x->_dng()   if $x->is_int();
        $rem->_dng() if $rem->is_int();
        return $x, $rem;
    }

    $x->_dng() if $x->is_int();
    $x;
}

sub bfmod {

    my ( $class, $x, $y, @r ) =
      ref( $_[0] ) && ref( $_[0] ) eq ref( $_[1] )
      ? ( ref( $_[0] ), @_ )
      : objectify( 2, @_ );

    return $x if $x->modify('bfmod');

    return $x->bnan(@r) if $x->is_nan() || $y->is_nan();

    if ( $y->is_zero() ) {
        return $x->round(@r);
    }

    if ( $x->is_inf() ) {
        return $x->bnan(@r);
    }

    if ( $y->is_inf() ) {
        if ( $x->is_zero() || $x->bcmp(0) == $y->bcmp(0) ) {
            return $x->round(@r);
        }
        else {
            return $x->binf( $y->sign(), @r );
        }
    }

    return $x->bzero(@r)
      if $x->is_zero()
      || (
        $x->is_int()
        &&
        ( $LIB->_is_zero( $y->{_e} ) && $LIB->_is_one( $y->{_m} ) )
      );

    my $cmp = $x->bacmp($y);
    if ( $cmp == 0 ) {
        return $x->bzero(@r);
    }

    my $ecmp = $LIB->_scmp( $x->{_e}, $x->{_es}, $y->{_e}, $y->{_es} );

    my $ym = $y->{_m};

    if ( $ecmp > 0 ) {

        my ( $de, $ds ) =
          $LIB->_ssub( $LIB->_copy( $x->{_e} ), $x->{_es}, $y->{_e},
            $y->{_es} );

        $x->{_m} = $LIB->_lsft( $x->{_m}, $de, 10 );

        $x->{_m} = $LIB->_mod( $x->{_m}, $ym );

        ( $x->{_e}, $x->{_es} ) = $LIB->_ssub( $x->{_e}, $x->{_es}, $de, $ds );

    }
    elsif ( $ecmp < 0 ) {

        my ( $de, $ds ) =
          $LIB->_ssub( $LIB->_copy( $y->{_e} ), $y->{_es}, $x->{_e},
            $x->{_es} );

        $ym = $LIB->_lsft( $LIB->_copy($ym), $de, 10 );

        $x->{_m} = $LIB->_mod( $x->{_m}, $ym );

    }
    else {

        $x->{_m} = $LIB->_mod( $x->{_m}, $ym );
    }

    if ( $LIB->_is_zero( $x->{_m} ) ) {
        $x->{sign} = '+';
    }
    else {
        $x->{_m} = $LIB->_sub( $ym, $x->{_m}, 1 )
          if $x->{sign} ne $y->{sign};
        $x->{sign} = $y->{sign};
    }

    $x->bnorm();
    $x->round( $r[0], $r[1], $r[2], $y );
    $x->_dng() if $x->is_int();
    return $x;
}

sub btdiv {

    my ( $class, $x, $y, @r ) =
      ref( $_[0] ) && ref( $_[0] ) eq ref( $_[1] )
      ? ( ref( $_[0] ), @_ )
      : objectify( 2, @_ );

    return $x if $x->modify('btdiv');

    my $wantarray = wantarray;

    if ( $x->is_nan() || $y->is_nan() ) {
        return $wantarray
          ? ( $x->bnan(@r), $class->bnan(@r) )
          : $x->bnan(@r);
    }

    if ( $y->is_zero() ) {
        my $rem;
        if ($wantarray) {
            $rem = $x->copy(@r);
        }
        if ( $x->is_zero() ) {
            $x->bnan(@r);
        }
        else {
            $x->binf( $x->{sign}, @r );
        }
        return $wantarray ? ( $x, $rem ) : $x;
    }

    if ( $x->is_inf() ) {
        my $rem;
        $rem = $class->bnan(@r) if $wantarray;
        if ( $y->is_inf() ) {
            $x->bnan(@r);
        }
        else {
            my $sign = $x->bcmp(0) == $y->bcmp(0) ? '+' : '-';
            $x->binf( $sign, @r );
        }
        return $wantarray ? ( $x, $rem ) : $x;
    }

    if ( $y->is_inf() ) {
        my $rem;
        if ($wantarray) {
            $rem = $x->copy()->round(@r);
            $rem->_dng() if $rem->is_int();
        }
        $x->bzero(@r);
        return $wantarray ? ( $x, $rem ) : $x;
    }

    my $fallback = 0;
    my ( @params, $scale );
    ( $x, @params ) = $x->_find_round_parameters( $r[0], $r[1], $r[2], $y );

    if ( $x->is_nan() ) {
        $x->round(@r);
        return $wantarray ? ( $x, $class->bnan(@r) ) : $x;
    }

    if ( scalar @params == 0 ) {
        $params[0] = $class->div_scale();
        $scale     = $params[0] + 4;
        $params[2] = $r[2];
        $fallback  = 1;
    }
    else {
        $scale = abs( $params[0] || $params[1] ) + 4;
    }

    my $dng = Math::BigFloat->downgrade();
    Math::BigFloat->downgrade(undef);

    my $rem;
    $rem = $class->bzero() if $wantarray;

    $y = $class->new($y) unless $y->isa('Math::BigFloat');

    my $lx = $LIB->_len( $x->{_m} );
    my $ly = $LIB->_len( $y->{_m} );
    $scale = $lx if $lx > $scale;
    $scale = $ly if $ly > $scale;
    my $diff = $ly - $lx;
    $scale += $diff if $diff > 0;

    my $xsign = $x->{sign};
    my $ysign = $y->{sign};

    $y->{sign} =~ tr/+-/-+/;
    my $same = $xsign ne $x->{sign};
    $y->{sign} = $ysign;

    if ($same) {
        $x->bone();
    }
    else {
        $rem = $x->copy() if $wantarray;

        $x->{sign} = $x->{sign} ne $y->{sign} ? '-' : '+';

        $y = $class->new($y) unless $y->isa('Math::BigFloat');

        $x->{_m} = $LIB->_lsft( $x->{_m}, $LIB->_new($scale), 10 );
        $x->{_m} = $LIB->_div( $x->{_m}, $y->{_m} );

        ( $x->{_e}, $x->{_es} ) =
          $LIB->_ssub( $x->{_e}, $x->{_es}, $y->{_e}, $y->{_es} );

        ( $x->{_e}, $x->{_es} ) =
          $LIB->_ssub( $x->{_e}, $x->{_es}, $LIB->_new($scale), '+' );

        $x->bnorm();
    }

    if ( defined $params[0] ) {
        $x->{accuracy} = undef;
        $x->bround( $params[0], $params[2] );
    }
    else {
        $x->{precision} = undef;
        $x->bfround( $params[1], $params[2] );
    }
    if ($fallback) {
        $x->{accuracy}  = undef;
        $x->{precision} = undef;
    }

    Math::BigFloat->downgrade($dng);

    if ($wantarray) {
        $x->bint();
        $rem->btmod( $y, @params );

        if ($fallback) {
            $rem->{accuracy}  = undef;
            $rem->{precision} = undef;
        }
        $x->_dng()   if $x->is_int();
        $rem->_dng() if $rem->is_int();
        return $x, $rem;
    }

    $x->_dng() if $x->is_int();
    $x;
}

sub btmod {

    my ( $class, $x, $y, @r ) =
      ref( $_[0] ) && ref( $_[0] ) eq ref( $_[1] )
      ? ( ref( $_[0] ), @_ )
      : objectify( 2, @_ );

    return $x if $x->modify('btmod');

    return $x->bnan(@r) if $x->is_nan() || $y->is_nan();

    if ( $y->is_zero() ) {
        return $x->round(@r);
    }

    if ( $x->is_inf() ) {
        return $x->bnan(@r);
    }

    if ( $y->is_inf() ) {
        return $x->round(@r);
    }

    return $x->bzero(@r)
      if $x->is_zero()
      || (
        $x->is_int()
        &&
        ( $LIB->_is_zero( $y->{_e} ) && $LIB->_is_one( $y->{_m} ) )
      );

    my $cmp = $x->bacmp($y);
    if ( $cmp == 0 ) {
        return $x->bzero(@r);
    }

    my $ecmp = $LIB->_scmp( $x->{_e}, $x->{_es}, $y->{_e}, $y->{_es} );

    if ( $ecmp > 0 ) {

        my ( $de, $ds ) =
          $LIB->_ssub( $LIB->_copy( $x->{_e} ), $x->{_es}, $y->{_e},
            $y->{_es} );

        $x->{_m} = $LIB->_lsft( $x->{_m}, $de, 10 );

        $x->{_m} = $LIB->_mod( $x->{_m}, $y->{_m} );

        ( $x->{_e}, $x->{_es} ) = $LIB->_ssub( $x->{_e}, $x->{_es}, $de, $ds );

    }
    elsif ( $ecmp < 0 ) {

        my ( $de, $ds ) =
          $LIB->_ssub( $LIB->_copy( $y->{_e} ), $y->{_es}, $x->{_e},
            $x->{_es} );

        my $ym = $LIB->_lsft( $LIB->_copy( $y->{_m} ), $de, 10 );

        $x->{_m} = $LIB->_mod( $x->{_m}, $ym );

    }
    else {

        $x->{_m} = $LIB->_mod( $x->{_m}, $y->{_m} );
    }

    $x->{sign} = '+' if $LIB->_is_zero( $x->{_m} );

    $x->bnorm();
    $x->round( $r[0], $r[1], $r[2], $y );
    $x->_dng() if $x->is_int();
    return $x;
}

sub binv {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    return $x if $x->modify('binv');

    my $dng = Math::BigFloat->downgrade();
    Math::BigFloat->downgrade(undef);

    my $inv = $class->bone()->bdiv( $x, @r );

    Math::BigFloat->downgrade($dng);

    %$x = %$inv;

    $x->round(@r);
    $x->_dng() if ( $x->is_int()
        || $x->is_inf()
        || $x->is_nan() );
    return $x;
}

sub bsqrt {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    return $x if $x->modify('bsqrt');

    return $x->bnan(@r)        if $x->is_nan();
    return $x->binf( "+", @r ) if $x->is_inf("+");
    return $x->round(@r)       if $x->is_zero() || $x->is_one();

    if ( $x->is_neg() ) {
        return $x->_upg()->bsqrt(@r) if $class->upgrade();
        return $x->bnan(@r);
    }

    my $fallback = 0;
    my ( @params, $scale );
    ( $x, @params ) = $x->_find_round_parameters(@r);

    return $x->bnan(@r) if $x->is_nan();

    if ( scalar @params == 0 ) {
        $params[0] = $class->div_scale();
        $scale     = $params[0] + 4;
        $params[2] = $r[2];
        $fallback  = 1;
    }
    else {
        $scale = abs( $params[0] || $params[1] ) + 4;
    }

    my $l = $LIB->_len( $x->{_m} );
    my $n = 2 * $scale - $l;
    $n++ if ( $l % 2 xor $LIB->_is_odd( $x->{_e} ) );
    my ( $na, $ns ) = $n < 0 ? ( abs($n), "-" ) : ( $n, "+" );
    $na = $LIB->_new($na);

    $x->{_m} =
        $ns eq "+"
      ? $LIB->_lsft( $x->{_m}, $na, 10 )
      : $LIB->_rsft( $x->{_m}, $na, 10 );

    $x->{_m} = $LIB->_sqrt( $x->{_m} );

    ( $x->{_e}, $x->{_es} ) = $LIB->_ssub( $x->{_e}, $x->{_es}, $na, $ns );
    $x->{_e} = $LIB->_div( $x->{_e}, $LIB->_new("2") );

    $x->bnorm();

    if ( defined $params[0] ) {
        $x->bround( $params[0], $params[2] );
    }
    else {
        $x->bfround( $params[1], $params[2] );
    }

    if ($fallback) {
        $x->{accuracy}  = undef;
        $x->{precision} = undef;
    }

    $x->round(@r);
    $x->_dng() if $x->is_int();
    $x;
}

sub bpow {

    my ( $class, $x, $y, $a, $p, $r ) = ( ref( $_[0] ), @_ );
    if ( ( !ref( $_[0] ) ) || ( ref( $_[0] ) ne ref( $_[1] ) ) ) {
        ( $class, $x, $y, $a, $p, $r ) = objectify( 2, @_ );
    }

    return $x if $x->modify('bpow');

    return $x->bnan() if $x->is_nan() || $y->is_nan();

    if ( $x->is_inf("-") ) {
        return $x->bzero() if $y->is_negative();
        return $x->bnan()  if $y->is_zero();
        return $x          if $y->is_odd();
        return $x->bneg();
    }
    elsif ( $x->is_inf("+") ) {
        return $x->bzero() if $y->is_negative();
        return $x->bnan()  if $y->is_zero();
        return $x;
    }
    elsif ( $y->is_inf("-") ) {
        return $x->bnan()    if $x->is_one("-");
        return $x->binf("+") if $x > -1 && $x < 1;
        return $x->bone()    if $x->is_one("+");
        return $x->bzero();
    }
    elsif ( $y->is_inf("+") ) {
        return $x->bnan()  if $x->is_one("-");
        return $x->bzero() if $x > -1 && $x < 1;
        return $x->bone()  if $x->is_one("+");
        return $x->binf("+");
    }

    if ( $x->is_zero() ) {
        return $x->bone() if $y->is_zero();
        return $x->binf() if $y->is_negative();
        return $x;
    }

    if ( $x->is_negative() && !$y->is_int() ) {
        return $x->_upg()->bpow( $y, $a, $p, $r ) if $class->upgrade();
        return $x->bnan();
    }

    if ( $x->is_one("+") || $y->is_one() ) {
        return $x;
    }

    if ( $x->is_one("-") ) {
        return $x if $y->is_odd();
        return $x->bneg();
    }

    return $x->_pow( $y, $a, $p, $r ) if !$y->is_int();

    my $y1 = $y->as_int()->{value};

    my $new_sign = '+';
    $new_sign = $LIB->_is_odd($y1) ? '-' : '+' if $x->{sign} ne '+';

    $x->{_m} = $LIB->_pow( $x->{_m}, $y1 );
    $x->{_e} = $LIB->_mul( $x->{_e}, $y1 );

    $x->{sign} = $new_sign;
    $x->bnorm();

    if ( $y->{sign} eq '-' ) {
        my $z = $x->copy();
        $x->bone();
        return scalar $x->bdiv( $z, $a, $p, $r );
    }

    $x->round( $a, $p, $r, $y );

    $x->_dng() if ( $x->is_int()
        || $x->is_inf()
        || $x->is_nan() );
    return $x;
}

sub broot {

    my ( $class, $x, $y, @r ) =
      ref( $_[0] ) && ref( $_[0] ) eq ref( $_[1] )
      ? ( ref( $_[0] ), @_ )
      : objectify( 2, @_ );

    return $x if $x->modify('broot');

    return $x->bnan(@r) if $x->is_nan() || $y->is_nan();

    if ( $x->is_neg() ) {
        return $x->broot( $y->copy()->bneg(), @r )->bneg()
          if ( $x->is_int()
            && $y->is_int()
            && $y->is_neg()
            && $y->is_odd() );
        return $x->_upg->broot( $y, @r ) if $class->upgrade();
        return $x->bnan(@r);
    }

    return $x->bnan(@r)
      if ( $x->{sign} !~ /^\+/
        || $y->is_zero()
        || $y->{sign} !~ /^\+$/ );

    return $x
      if ( $x->is_zero()
        || $x->is_one()
        || $x->is_inf()
        || $y->is_one() );

    my $fallback = 0;
    my ( @params, $scale );
    ( $x, @params ) = $x->_find_round_parameters(@r);

    return $x if $x->is_nan();

    if ( scalar @params == 0 ) {
        $params[0] = $class->div_scale();
        $scale     = $params[0] + 4;
        $params[2] = $r[2];
        $fallback  = 1;
    }
    else {
        $scale = abs( $params[0] || $params[1] ) + 4;
    }

    my $ab = $class->accuracy();
    my $pb = $class->precision();
    $class->accuracy(undef);
    $class->precision(undef);

    my $upg = $class->upgrade();
    my $dng = $class->downgrade();
    $class->upgrade(undef);
    $class->downgrade(undef);

    $x->{accuracy}  = undef;
    $x->{precision} = undef;

    my $sign = 0;
    $sign = 1 if $x->{sign} eq '-';
    $x->{sign} = '+';

    my $is_two = 0;
    if ( $y->isa('Math::BigFloat') ) {
        $is_two =
             $y->{sign} eq '+'
          && $LIB->_is_two( $y->{_m} )
          && $LIB->_is_zero( $y->{_e} );
    }
    else {
        $is_two = $y == 2;
    }

    if ($is_two) {
        $x->bsqrt( $scale + 4 );
    }

    elsif ( $y->is_one('-') ) {
        $x->binv( $scale + 4 );
    }

    else {

        my $mbi_upg = Math::BigInt->upgrade();
        Math::BigInt->upgrade(undef);

        my $done = 0;
        if ( $y->is_int() && $x->is_int() ) {
            my $i = $LIB->_copy( $x->{_m} );
            $i = $LIB->_lsft( $i, $x->{_e}, 10 )
              unless $LIB->_is_zero( $x->{_e} );
            my $int = Math::BigInt->bzero();
            $int->{value} = $i;
            $int->broot( $y->as_int() );
            if ( $int->copy()->bpow( $y->as_int() ) == $x->as_int() ) {
                $x->{_m}  = $int->{value};
                $x->{_e}  = $LIB->_zero();
                $x->{_es} = '+';
                $x->bnorm();
                $done = 1;
            }
        }

        if ( $done == 0 ) {
            my $u = $class->bone()->bdiv( $y, $scale + 4 );
            $u->{accuracy}  = undef;
            $u->{precision} = undef;
            $x->bpow( $u, $scale + 4 );
        }

        Math::BigInt->upgrade($mbi_upg);
    }

    $x->bneg() if $sign == 1;

    if ( defined $params[0] ) {
        $x->bround( $params[0], $params[2] );
    }
    else {
        $x->bfround( $params[1], $params[2] );
    }
    if ($fallback) {
        $x->{accuracy}  = undef;
        $x->{precision} = undef;
    }

    if ( defined $ab ) {
        $class->accuracy($ab);
    }
    else {
        $class->precision($pb);
    }

    $class->upgrade($upg);
    $class->downgrade($dng);

    $x->round(@r);
    $x->_dng() if ( $x->is_int()
        || $x->is_inf()
        || $x->is_nan() );
    return $x;
}

sub bmuladd {

    my ( $class, $x, $y, $z, @r ) =
         ref( $_[0] )
      && ref( $_[0] ) eq ref( $_[1] )
      && ref( $_[1] ) eq ref( $_[2] )
      ? ( ref( $_[0] ), @_ )
      : objectify( 3, @_ );

    return $x if $x->modify('bmuladd');

    return $x->bnan(@r) if ( $x->is_nan()
        || $y->is_nan()
        || $z->is_nan() );

    if ( $x->is_inf("-") ) {

        if ( $y->is_neg() ) {
            if ( $z->is_inf("-") ) {
                return $x->bnan(@r);
            }
            else {
                return $x->binf( "+", @r );
            }
        }
        elsif ( $y->is_zero() ) {
            return $x->bnan(@r);
        }
        else {
            if ( $z->{sign} eq "+inf" ) {
                return $x->bnan(@r);
            }
            else {
                return $x->binf( "-", @r );
            }
        }

    }
    elsif ( $x->{sign} eq "+inf" ) {

        if ( $y->is_neg() ) {
            if ( $z->{sign} eq "+inf" ) {
                return $x->bnan(@r);
            }
            else {
                return $x->binf( "-", @r );
            }
        }
        elsif ( $y->is_zero() ) {
            return $x->bnan(@r);
        }
        else {
            if ( $z->is_inf("-") ) {
                return $x->bnan(@r);
            }
            else {
                return $x->binf( "+", @r );
            }
        }

    }
    elsif ( $x->is_neg() ) {

        if ( $y->is_inf("-") ) {
            if ( $z->is_inf("-") ) {
                return $x->bnan(@r);
            }
            else {
                return $x->binf( "+", @r );
            }
        }
        elsif ( $y->{sign} eq "+inf" ) {
            if ( $z->{sign} eq "+inf" ) {
                return $x->bnan(@r);
            }
            else {
                return $x->binf( "-", @r );
            }
        }
        else {
            if ( $z->is_inf("-") ) {
                return $x->binf( "-", @r );
            }
            elsif ( $z->{sign} eq "+inf" ) {
                return $x->binf( "+", @r );
            }
        }

    }
    elsif ( $x->is_zero() ) {

        if ( $y->is_inf("-") ) {
            return $x->bnan(@r);
        }
        elsif ( $y->{sign} eq "+inf" ) {
            return $x->bnan(@r);
        }
        else {
            if ( $z->is_inf("-") ) {
                return $x->binf( "-", @r );
            }
            elsif ( $z->{sign} eq "+inf" ) {
                return $x->binf( "+", @r );
            }
        }

    }
    elsif ( $x->is_pos() ) {

        if ( $y->is_inf("-") ) {
            if ( $z->{sign} eq "+inf" ) {
                return $x->bnan(@r);
            }
            else {
                return $x->binf( "-", @r );
            }
        }
        elsif ( $y->{sign} eq "+inf" ) {
            if ( $z->is_inf("-") ) {
                return $x->bnan(@r);
            }
            else {
                return $x->binf( "+", @r );
            }
        }
        else {
            if ( $z->is_inf("-") ) {
                return $x->binf( "-", @r );
            }
            elsif ( $z->{sign} eq "+inf" ) {
                return $x->binf( "+", @r );
            }
        }
    }

    $y = $y->copy() if refaddr($y) eq refaddr($x);
    $z = $z->copy() if refaddr($z) eq refaddr($x);

    $x->{_m} = $LIB->_mul( $x->{_m}, $y->{_m} );
    ( $x->{_e}, $x->{_es} ) =
      $LIB->_sadd( $x->{_e}, $x->{_es}, $y->{_e}, $y->{_es} );

    $r[3] = $y;

    $x->{sign} = $x->{sign} ne $y->{sign} ? '-' : '+';

    my $e = $z->{_e};
    $e = $LIB->_zero() if !defined $e;
    $e = $LIB->_copy($e);

    my $es;

    ( $e, $es ) = $LIB->_ssub( $e, $z->{_es} || '+', $x->{_e}, $x->{_es} );

    my $add = $LIB->_copy( $z->{_m} );

    if ( $es eq '-' ) {
        $x->{_m} = $LIB->_lsft( $x->{_m}, $e, 10 );
        ( $x->{_e}, $x->{_es} ) = $LIB->_sadd( $x->{_e}, $x->{_es}, $e, $es );
    }
    elsif ( !$LIB->_is_zero($e) ) {
        $add = $LIB->_lsft( $add, $e, 10 );
    }

    if ( $x->{sign} eq $z->{sign} ) {
        $x->{_m} = $LIB->_add( $x->{_m}, $add );
    }
    else {
        ( $x->{_m}, $x->{sign} ) =
          $LIB->_sadd( $x->{_m}, $x->{sign}, $add, $z->{sign} );
    }

    $x->bnorm()->round(@r);

    $x->_dng() if ( $x->is_int()
        || $x->is_inf()
        || $x->is_nan() );
    return $x;
}

sub bmodpow {
    my ( $class, $num, $exp, $mod, @r ) =
         ref( $_[0] )
      && ref( $_[0] ) eq ref( $_[1] )
      && ref( $_[1] ) eq ref( $_[2] )
      ? ( ref( $_[0] ), @_ )
      : objectify( 3, @_ );

    return $num if $num->modify('bmodpow');

    return $num->bnan(@r)
      if $mod->is_nan() || $exp->is_nan() || $mod->is_nan();

    return $num->bnan(@r) if $mod->{sign} ne '+' || $mod->is_zero();

    if ( $exp->{sign} =~ /\w/ ) {
        return $num->bnan(@r);
    }

    $num->bmodinv( $mod, @r ) if $exp->{sign} eq '-';

    return $num->bnan(@r) if $num->{sign} !~ /^[+-]$/;

    $num->bpow($exp)->bmod($mod);

    $num->round(@r);
    $num->_dng() if ( $num->is_int()
        || $num->is_inf()
        || $num->is_nan() );
    return $num;
}

sub blog {

    my ( $class, $x, $base, @r );

    if ( !ref( $_[0] ) && $_[0] =~ /^[A-Za-z]|::/ ) {
        ( $class, $x, $base, @r ) =
          defined $_[2] ? objectify( 2, @_ ) : objectify( 1, @_ );
    }
    else {
        ( $class, $x, $base, @r ) =
          defined $_[1] ? objectify( 2, @_ ) : objectify( 1, @_ );
    }

    return $x if $x->modify('blog');

    return $x->bnan(@r) if $x->is_nan();

    if ( defined $base ) {
        $base = $class->new($base)
          unless defined( blessed($base) ) && $base->isa(__PACKAGE__);
        if ( $base->is_nan() || $base->is_one() ) {
            return $x->bnan(@r);
        }
        elsif ( $base->is_inf() || $base->is_zero() ) {
            return $x->bnan(@r) if $x->is_inf() || $x->is_zero();
            return $x->bzero(@r);
        }
        elsif ( $base->is_negative() ) {
            return $x->bzero(@r)       if $x->is_one();
            return $x->bone( '+', @r ) if $x == $base;

            return $x->_upg()->blog( $base, @r ) if $class->upgrade();
            return $x->bnan(@r);
        }
        return $x->bone(@r) if $x == $base;
    }

    if ( $x->is_inf() ) {
        my $sign = defined($base) && $base < 1 ? '-' : '+';
        return $x->binf( $sign, @r );
    }
    elsif ( $x->is_neg() ) {
        return $x->_upg()->blog( $base, @r ) if $class->upgrade();
        return $x->bnan(@r);
    }
    elsif ( $x->is_one() ) {
        return $x->bzero(@r);
    }
    elsif ( $x->is_zero() ) {
        my $sign = defined($base) && $base < 1 ? '+' : '-';
        return $x->binf( $sign, @r );
    }

    my $fallback = 0;
    my ( $scale, @params );
    ( $x, @params ) = $x->_find_round_parameters(@r);

    if ( scalar @params == 0 ) {
        $params[0] = $class->div_scale();
        $params[1] = undef;
        $scale     = $params[0] + 4;
        $params[2] = $r[2];
        $fallback  = 1;
    }
    else {
        $scale = abs( $params[0] || $params[1] ) + 4;
    }

    my $ab = $class->accuracy();
    my $pb = $class->precision();
    $class->accuracy(undef);
    $class->precision(undef);

    my $upg = $class->upgrade();
    my $dng = $class->downgrade();
    $class->upgrade(undef);
    $class->downgrade(undef);

    $x->{accuracy}  = undef;
    $x->{precision} = undef;

    my $done = 0;

    if ( defined($base) && $base->is_int() && $x->is_int() ) {
        my $x_lib = $LIB->_new( $x->bdstr() );
        my $b_lib = $LIB->_new( $base->bdstr() );
        ( $x_lib, my $exact ) = $LIB->_log_int( $x_lib, $b_lib );
        if ($exact) {
            $x->{_m} = $x_lib;
            $x->{_e} = $LIB->_zero();
            $x->bnorm();
            $done = 1;
        }
    }

    unless ($done) {
        $x->_log_10($scale);
        if ( defined $base ) {
            my $base_log_e = $base->copy()->_log_10($scale);
            $x->bdiv( $base_log_e, $scale );
        }
    }

    if ( defined $params[0] ) {
        $x->bround( $params[0], $params[2] );
    }
    else {
        $x->bfround( $params[1], $params[2] );
    }
    if ($fallback) {
        $x->{accuracy}  = undef;
        $x->{precision} = undef;
    }

    if ( defined $ab ) {
        $class->accuracy($ab);
    }
    else {
        $class->precision($pb);
    }

    $class->upgrade($upg);
    $class->downgrade($dng);

    $x->round(@r);
    return $x->_dng() if $x->is_int();
    return $x;
}

sub bexp {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    return $x if $x->modify('bexp');

    return $x->bnan(@r)  if $x->is_nan();
    return $x->binf(@r)  if $x->is_inf("+");
    return $x->bzero(@r) if $x->is_inf("-");

    my $fallback = 0;
    my ( $scale, @params );
    ( $x, @params ) = $x->_find_round_parameters(@r);

    return $x->bnan(@r) if $x->is_nan();

    return $x->bone(@r) if $x->is_zero();

    if ( !@params ) {
        $params[0] = $class->div_scale();
        $params[1] = undef;
        $params[2] = $r[2];
        $scale     = $params[0];
        $fallback  = 1;
    }
    else {
        if ( defined( $params[0] ) ) {
            $scale = $params[0];
        }
        else {

            my $ndig = $x->numify() / log(10);
            $scale = 1 + int($ndig) - $params[1];
        }
    }

    $scale += 4;

    if ( !$x->isa('Math::BigFloat') ) {
        $x     = Math::BigFloat->new($x);
        $class = ref($x);
    }

    my $ab = $class->accuracy();
    my $pb = $class->precision();
    $class->accuracy(undef);
    $class->precision(undef);

    my $upg = $class->upgrade();
    my $dng = $class->downgrade();
    $class->upgrade(undef);
    $class->downgrade(undef);

    $x->{accuracy}  = undef;
    $x->{precision} = undef;

    my $x_orig = $x->copy();

    if ( $scale <= 75 ) {
        $x->{_m} = $LIB->_new( "2718281828459045235360287471352662497757"
              . "2470936999595749669676277240766303535476" );
        $x->{sign} = '+';
        $x->{_es}  = '-';
        $x->{_e}   = $LIB->_new(79);
    }
    else {

        my $A = $LIB->_new(
            "9093339520860578540197197" . "0164779391644753259799242" );
        my $F    = $LIB->_new(42);
        my $step = 42;

        my $steps = _len_to_steps( $scale - 4 );

        while ( $step++ <= $steps ) {
            $A = $LIB->_mul( $A, $F );
            $A = $LIB->_inc($A);
            $F = $LIB->_inc($F);
        }

        my $B = $LIB->_fac( $LIB->_new($steps) );

        $A = $LIB->_lsft( $A, $LIB->_new($scale), 10 );
        $A = $LIB->_div( $A, $B );

        $x->{_m}   = $A;
        $x->{sign} = '+';
        $x->{_es}  = '-';
        $x->{_e}   = $LIB->_new($scale);
    }

    if ( $x_orig->is_one() ) {

        $x->{accuracy}  = undef;
        $x->{precision} = undef;

        if ( defined $params[0] ) {
            $x->bround( $params[0], $params[2] );
        }
        else {
            $x->bfround( $params[1], $params[2] );
        }

    }
    else {

        my ( $m, $e ) = $x_orig->nparts();
        my $ms = $m->numify();
        my $es = $e->numify();

        my $mant = $x_orig->copy()->babs();
        my $expo;

        my $one  = $class->bone();
        my $two  = $class->new("2");
        my $half = $class->new("0.5");

        my $expo_est = ( log( abs($ms) ) / log(10) + $es ) * log(10) / log(2);
        $expo_est = int($expo_est);

        $expo = $class->new($expo_est);
        if ( $expo_est > 0 ) {
            $mant->bmul( $half->copy()->bpow($expo) );
        }
        elsif ( $expo_est < 0 ) {
            my $expo_abs = $expo->copy()->bneg();
            $mant->bmul( $two->copy()->bpow($expo_abs) );
        }

        while ( $mant->bcmp($two) >= 0 ) {
            $mant->bmul($half);
            $expo->binc();
        }

        while ( $mant->bcmp($one) < 0 ) {
            $mant->bmul($two);
            $expo->bdec();
        }

        my $rescale = int( $scale + abs($expo) * log(2) / log(10) + 1 );
        $rescale = 4 if $rescale < 4;

        $x->bpow( $mant, $rescale );
        my $pow2 = $two->bpow( $expo, $rescale );
        $pow2->bneg() if $x_orig->is_negative();

        croak "cannot compute bexp(); input value is too large"
          if $pow2->copy()->babs()->bcmp("1073741824") >= 0;

        $x->bpow( $pow2, $rescale );

        $x->{accuracy} = undef;
        $x->round(@params);
    }

    if ($fallback) {
        $x->{accuracy}  = undef;
        $x->{precision} = undef;
    }

    if ( defined $ab ) {
        $class->accuracy($ab);
    }
    else {
        $class->precision($pb);
    }

    $class->upgrade($upg);
    $class->downgrade($dng);

    $x->round(@r);
    $x->_dng() if $x->is_int();
    $x;
}

sub bilog2 {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    return $x if $x->modify('bilog2');

    return $x->bnan(@r) if $x->is_nan();
    return $x->binf( "+", @r ) if $x->is_inf("+");
    return $x->binf( "-", @r ) if $x->is_zero();

    if ( $x->is_neg() ) {
        return $x->_upg()->bilog2(@r) if $class->upgrade();
        return $x->bnan(@r);
    }

    if ( $x->{_es} eq '-' ) {
        $x->{_m} = $LIB->_rsft( $x->{_m}, $x->{_e}, 10 );
    }
    elsif ( !$LIB->_is_zero( $x->{_e} ) ) {
        $x->{_m} = $LIB->_lsft( $x->{_m}, $x->{_e}, 10 );
    }

    $x->{_m} = $LIB->_ilog2( $x->{_m} );
    $x->{_e} = $LIB->_zero();
    $x->bnorm()->round(@r);
    $x->_dng();
    return $x;
}

sub bilog10 {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    return $x if $x->modify('bilog10');

    return $x->bnan(@r) if $x->is_nan();
    return $x->binf( "+", @r ) if $x->is_inf("+");
    return $x->binf( "-", @r ) if $x->is_zero();

    if ( $x->is_neg() ) {
        return $x->_upg()->bilog10(@r) if $class->upgrade();
        return $x->bnan(@r);
    }

    if ( $x->{_es} eq '-' ) {
        $x->{_m} = $LIB->_rsft( $x->{_m}, $x->{_e}, 10 );
    }
    elsif ( !$LIB->_is_zero( $x->{_e} ) ) {
        $x->{_m} = $LIB->_lsft( $x->{_m}, $x->{_e}, 10 );
    }

    $x->{_m} = $LIB->_ilog10( $x->{_m} );
    $x->{_e} = $LIB->_zero();
    $x->bnorm()->round(@r);
    $x->_dng();
    return $x;
}

sub bclog2 {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    return $x if $x->modify('bclog2');

    return $x->bnan(@r) if $x->is_nan();
    return $x->binf( "+", @r ) if $x->is_inf("+");
    return $x->binf( "-", @r ) if $x->is_zero();

    if ( $x->is_neg() ) {
        return $x->_upg()->bclog2(@r) if $class->upgrade();
        return $x->bnan(@r);
    }

    if ( $x->{_es} eq '-' ) {
        $x->{_m} = $LIB->_rsft( $x->{_m}, $x->{_e}, 10 );
    }
    elsif ( !$LIB->_is_zero( $x->{_e} ) ) {
        $x->{_m} = $LIB->_lsft( $x->{_m}, $x->{_e}, 10 );
    }

    $x->{_m} = $LIB->_clog2( $x->{_m} );
    $x->{_e} = $LIB->_zero();
    $x->bnorm()->round(@r);
    $x->_dng();
    return $x;
}

sub bclog10 {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    return $x if $x->modify('bclog10');

    return $x->bnan(@r) if $x->is_nan();
    return $x->binf( "+", @r ) if $x->is_inf("+");
    return $x->binf( "-", @r ) if $x->is_zero();

    if ( $x->is_neg() ) {
        return $x->_upg()->bclog10(@r) if $class->upgrade();
        return $x->bnan(@r);
    }

    if ( $x->{_es} eq '-' ) {
        $x->{_m} = $LIB->_rsft( $x->{_m}, $x->{_e}, 10 );
    }
    elsif ( !$LIB->_is_zero( $x->{_e} ) ) {
        $x->{_m} = $LIB->_lsft( $x->{_m}, $x->{_e}, 10 );
    }

    $x->{_m} = $LIB->_clog10( $x->{_m} );
    $x->{_e} = $LIB->_zero();
    $x->bnorm()->round(@r);
    $x->_dng();
    return $x;
}

sub bnok {
    my ( $class, $x, $y, @r ) =
      ref( $_[0] ) && ref( $_[0] ) eq ref( $_[1] )
      ? ( ref( $_[0] ), @_ )
      : objectify( 2, @_ );

    carp "Rounding is not supported for ", ( caller(0) )[3], "()" if @r;

    return $x if $x->modify('bnok');

    return $x->bnan() if $x->is_nan() || $y->is_nan();
    return $x->bnan()
      if ( ( $x->is_finite() && !$x->is_int() )
        || ( $y->is_finite() && !$y->is_int() ) );

    my $xint = $x->as_int();
    my $yint = $y->as_int();

    $xint->bnok($yint);
    $xint->round(@r);

    my $xflt = $xint->as_float();
    $x->{sign} = $xflt->{sign};
    $x->{_m}   = $xflt->{_m};
    $x->{_es}  = $xflt->{_es};
    $x->{_e}   = $xflt->{_e};

    return $x->_dng();
    return $x;
}

sub bperm {
    my ( $class, $x, $y, @r ) =
      ref( $_[0] ) && ref( $_[0] ) eq ref( $_[1] )
      ? ( ref( $_[0] ), @_ )
      : objectify( 2, @_ );

    carp "Rounding is not supported for ", ( caller(0) )[3], "()" if @r;

    return $x if $x->modify('bperm');

    return $x->bnan() if $x->is_nan() || $y->is_nan();
    return $x->bnan()
      if ( ( $x->is_finite() && !$x->is_int() )
        || ( $y->is_finite() && !$y->is_int() ) );

    my $xint = $x->as_int();
    my $yint = $y->as_int();

    $xint->bperm($yint);
    $xint->round(@r);

    my $xflt = $xint->as_float();
    $x->{sign} = $xflt->{sign};
    $x->{_m}   = $xflt->{_m};
    $x->{_es}  = $xflt->{_es};
    $x->{_e}   = $xflt->{_e};

    return $x->_dng();
    return $x;
}

sub bsin {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    return $x if $x->modify('bsin');

    return $x->bzero(@r) if $x->is_zero();
    return $x->bnan(@r)  if $x->is_nan() || $x->is_inf();

    my $fallback = 0;
    my ( $scale, @params );
    ( $x, @params ) = $x->_find_round_parameters(@r);

    return $x->bnan(@r) if $x->is_nan();

    if ( !@params ) {
        $params[0] = $class->div_scale();
        $params[1] = undef;
        $params[2] = $r[2];
        $scale     = $params[0];
        $fallback  = 1;
    }
    else {
        if ( defined( $params[0] ) ) {
            $scale = $params[0];
        }
        else {
            $scale = 1 - $params[1];
        }
    }

    my ( $m, $e ) = $x->nparts();
    $scale += $e if $x >= 10;
    $scale = 4   if $scale < 4;

    my $ab = $class->accuracy();
    my $pb = $class->precision();
    $class->accuracy(undef);
    $class->precision(undef);

    my $upg = $class->upgrade();
    my $dng = $class->downgrade();
    $class->upgrade(undef);
    $class->downgrade(undef);

    $x->{accuracy}  = undef;
    $x->{precision} = undef;

    my $sin_prev;
    my $sin;

    while (1) {

        my $pi     = $class->bpi($scale);
        my $twopi  = $pi->copy()->bmul("2");
        my $halfpi = $pi->copy()->bmul("0.5");

        my $xsgn = $x < 0 ? -1 : 1;
        my $x    = $x->copy()->babs();

        $x->bmod( $twopi, $scale );

        if ( $x->bcmp($pi) > 0 ) {
            $xsgn = -$xsgn;
            $x->bsub($pi);
        }

        if ( $x->bcmp($halfpi) > 0 ) {
            $x->bsub($pi)->bneg();
        }

        my $tol = $class->new( "1E-" . ( $scale - 1 ) );

        my $xsq  = $x->copy()->bmul( $x, $scale )->bneg();
        my $term = $x->copy();
        my $fac  = $class->bone();
        my $n    = $class->bone();

        $sin = $x->copy();

        while (1) {
            $n->binc();
            $fac = $n->copy();
            $n->binc();
            $fac->bmul($n);

            $term->bmul( $xsq, $scale )->bdiv( $fac, $scale );

            $sin->badd( $term, $scale );
            last if $term->copy()->babs()->bcmp($tol) < 0;
        }

        $sin->bneg() if $xsgn < 0;

        $sin->{accuracy} = undef;
        $sin->round(@params);

        if ( defined $sin_prev ) {
            last if $sin->bcmp($sin_prev) == 0;
        }

        $sin_prev = $sin;
        $scale *= 2;
    }

    %$x = %$sin;

    if ($fallback) {
        $x->{accuracy}  = undef;
        $x->{precision} = undef;
    }

    if ( defined $ab ) {
        $class->accuracy($ab);
    }
    else {
        $class->precision($pb);
    }

    $class->upgrade($upg);
    $class->downgrade($dng);

    $x->_dng() if $x->is_int();
    $x;
}

sub bcos {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    return $x if $x->modify('bcos');

    my $fallback = 0;
    my ( $scale, @params );
    ( $x, @params ) = $x->_find_round_parameters(@r);

    return $x           if $x->is_nan();
    return $x->bnan()   if $x->is_inf();
    return $x->bone(@r) if $x->is_zero();

    if ( scalar @params == 0 ) {
        $params[0] = $class->div_scale();
        $params[1] = undef;
        $scale     = $params[0] + 4;
        $params[2] = $r[2];
        $fallback  = 1;
    }
    else {
        $scale = abs( $params[0] || $params[1] ) + 4;
    }

    my $ab = $class->accuracy();
    my $pb = $class->precision();
    $class->accuracy(undef);
    $class->precision(undef);

    my $upg = $class->upgrade();
    my $dng = $class->downgrade();
    $class->upgrade(undef);
    $class->downgrade(undef);

    $x->{accuracy}  = undef;
    $x->{precision} = undef;

    my $over      = $x * $x;
    my $x2        = $over->copy();
    my $sign      = 1;
    my $below     = $class->new(2);
    my $factorial = $class->new(3);
    $x->bone();
    $x->{accuracy}  = undef;
    $x->{precision} = undef;

    my $limit = $class->new( "1E-" . ( $scale - 1 ) );
    while ( 3 < 5 ) {
        my $next = $over->copy()->bdiv( $below, $scale );
        last if $next->bacmp($limit) <= 0;

        if ( $sign == 0 ) {
            $x->badd($next);
        }
        else {
            $x->bsub($next);
        }
        $sign = 1 - $sign;

        $over->bmul($x2);
        $below->bmul($factorial);
        $factorial->binc();
        $below->bmul($factorial);
        $factorial->binc();
    }

    if ( defined $params[0] ) {
        $x->bround( $params[0], $params[2] );
    }
    else {
        $x->bfround( $params[1], $params[2] );
    }
    if ($fallback) {
        $x->{accuracy}  = undef;
        $x->{precision} = undef;
    }

    if ( defined $ab ) {
        $class->accuracy($ab);
    }
    else {
        $class->precision($pb);
    }

    $class->upgrade($upg);
    $class->downgrade($dng);

    $x->round(@r);
    $x->_dng() if $x->is_int();
    $x;
}

sub batan {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    return $x if $x->modify('batan');

    return $x->bnan(@r) if $x->is_nan();

    my $fallback = 0;
    my ( $scale, @params );
    ( $x, @params ) = $x->_find_round_parameters(@r);

    return $x->bnan(@r) if $x->is_nan();

    if ( $x->{sign} =~ /^[+-]inf\z/ ) {
        my $pi = $class->bpi(@r);
        $x->{_m}  = $pi->{_m};
        $x->{_e}  = $pi->{_e};
        $x->{_es} = $pi->{_es};
        $x->{sign} = substr( $x->{sign}, 0, 1 );
        $x->{_m}   = $LIB->_div( $x->{_m}, $LIB->_new(2) );
        return $x;
    }

    return $x->bzero(@r) if $x->is_zero();

    if ( scalar @params == 0 ) {
        $params[0] = $class->div_scale();
        $params[1] = undef;
        $scale     = $params[0] + 4;
        $params[2] = $r[2];
        $fallback  = 1;
    }
    else {
        $scale = abs( $params[0] || $params[1] ) + 4;
    }

    if ( $LIB->_is_one( $x->{_m} ) && $LIB->_is_zero( $x->{_e} ) ) {
        my $pi = $class->bpi( $scale - 3 );
        $x->{_m}  = $pi->{_m};
        $x->{_e}  = $pi->{_e};
        $x->{_es} = $pi->{_es};
        $x->{_m} = $LIB->_div( $x->{_m}, $LIB->_new(4) );
        return $x;
    }

    my $ab = $class->accuracy();
    my $pb = $class->precision();
    $class->accuracy(undef);
    $class->precision(undef);

    my $upg = $class->upgrade();
    my $dng = $class->downgrade();
    $class->upgrade(undef);
    $class->downgrade(undef);

    $x->{accuracy}  = undef;
    $x->{precision} = undef;

    my $pi = undef;
    if ( $x->bacmp( $x->copy()->bone ) >= 0 ) {
        $pi = $class->bpi( $scale - 3 );
        $pi->{_m} = $LIB->_div( $pi->{_m}, $LIB->_new(2) );
        my $x_copy = $x->copy();
        $x->bone();
        $x->bdiv( $x_copy, $scale );
    }

    my $fmul = 1;
    foreach ( 0 .. int( $scale / 20 ) ) {
        $fmul *= 2;
        $x->bdiv( $x->copy()->bmul($x)->binc()->bsqrt( $scale + 4 )->binc(),
            $scale + 4 );
    }

    my $over = $x * $x;
    my $x2   = $over->copy();
    $over->bmul($x);
    my $sign  = 1;
    my $below = $class->new(3);
    my $two   = $class->new(2);
    $x->{accuracy}  = undef;
    $x->{precision} = undef;

    my $limit = $class->new( "1E-" . ( $scale - 1 ) );
    while (1) {
        my $next = $over->copy()->bdiv( $below, $scale );
        last if $next->bacmp($limit) <= 0;

        if ( $sign == 0 ) {
            $x->badd($next);
        }
        else {
            $x->bsub($next);
        }
        $sign = 1 - $sign;

        $over->bmul($x2);
        $below->badd($two);
    }
    $x->bmul($fmul);

    if ( defined $pi ) {
        my $x_copy = $x->copy();
        $x->{_m}  = $pi->{_m};
        $x->{_e}  = $pi->{_e};
        $x->{_es} = $pi->{_es};
        $x->bsub($x_copy);
    }

    if ( defined $params[0] ) {
        $x->bround( $params[0], $params[2] );
    }
    else {
        $x->bfround( $params[1], $params[2] );
    }
    if ($fallback) {
        $x->{accuracy}  = undef;
        $x->{precision} = undef;
    }

    if ( defined $ab ) {
        $class->accuracy($ab);
    }
    else {
        $class->precision($pb);
    }

    $class->upgrade($upg);
    $class->downgrade($dng);

    return $x->_dng() if ( $x->is_int()
        || $x->is_inf() );
    $x;
}

sub batan2 {

    my ( $class, $y, $x, @r ) =
      ref( $_[0] ) && ref( $_[0] ) eq ref( $_[1] )
      ? ( ref( $_[0] ), @_ )
      : objectify( 2, @_ );

    return $y if $y->modify('batan2');

    return $y->bnan() if $x->is_nan() || $y->is_nan();

    my $fallback = 0;
    my ( $scale, @params );
    ( $y, @params ) = $y->_find_round_parameters(@r);

    return $y if $y->is_nan();

    if ( scalar @params == 0 ) {
        $params[0] = $class->div_scale();
        $params[1] = undef;
        $scale     = $params[0] + 4;
        $params[2] = $r[2];
        $fallback  = 1;
    }
    else {
        $scale = abs( $params[0] || $params[1] ) + 4;
    }

    if ( $x->is_inf("+") ) {
        if ( $y->is_inf("+") ) {
            $y->bpi($scale)->bmul("0.25");
        }
        elsif ( $y->is_inf("-") ) {
            $y->bpi($scale)->bmul("-0.25");
        }
        else {
            return $y->bzero(@r);
        }
    }
    elsif ( $x->is_inf("-") ) {
        if ( $y->is_inf("+") ) {
            $y->bpi($scale)->bmul("0.75");
        }
        elsif ( $y->is_inf("-") ) {
            $y->bpi($scale)->bmul("-0.75");
        }
        elsif ( $y >= 0 ) {
            $y->bpi($scale);
        }
        else {
            $y->bpi($scale)->bneg();
        }
    }
    elsif ( $x > 0 ) {
        if ( $y->is_inf("+") ) {
            $y->bpi($scale)->bmul("0.5");
        }
        elsif ( $y->is_inf("-") ) {
            $y->bpi($scale)->bmul("-0.5");
        }
        else {
            $y->bdiv( $x, $scale )->batan($scale);
        }
    }
    elsif ( $x < 0 ) {
        my $pi = $class->bpi($scale);
        if ( $y >= 0 ) {
            $y->bdiv( $x, $scale )->batan() ->badd($pi);
        }
        else {
            $y->bdiv( $x, $scale )->batan() ->bsub($pi);
        }
    }
    else {
        if ( $y > 0 ) {
            $y->bpi($scale)->bmul("0.5");
        }
        elsif ( $y < 0 ) {
            $y->bpi($scale)->bmul("-0.5");
        }
        else {
            return $y->bzero(@r);
        }
    }

    $y->round(@r);

    if ($fallback) {
        $y->{accuracy}  = undef;
        $y->{precision} = undef;
    }

    return $y;
}

sub bfac {

    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    return $x if $x->modify('bfac');

    return $x->bnan(@r)        if $x->is_nan() || $x->is_inf("-");
    return $x->binf( "+", @r ) if $x->is_inf("+");
    return $x->bnan(@r)        if $x->is_neg()  || !$x->is_int();
    return $x->bone(@r)        if $x->is_zero() || $x->is_one();

    if ( $x->is_neg() || !$x->is_int() ) {
        return $x->_upg()->bfac(@r) if $class->upgrade();
        return $x->bnan(@r);
    }

    if ( !$LIB->_is_zero( $x->{_e} ) ) {
        $x->{_m}  = $LIB->_lsft( $x->{_m}, $x->{_e}, 10 );
        $x->{_e}  = $LIB->_zero();
        $x->{_es} = '+';
    }
    $x->{_m} = $LIB->_fac( $x->{_m} );

    $x->bnorm();
    $x->round(@r);
    $x->_dng();
    return $x;
}

sub bdfac {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    return $x if $x->modify('bdfac');

    return $x->bnan(@r)        if $x->is_nan() || $x->is_inf("-");
    return $x->binf( "+", @r ) if $x->is_inf("+");
    return $x->bnan(@r)        if $x <= -2 || !$x->is_int();
    return $x->bone(@r)        if $x <= 1;

    croak("bdfac() requires a newer version of the $LIB library.")
      unless $LIB->can('_dfac');

    if ( !$LIB->_is_zero( $x->{_e} ) ) {
        $x->{_m}  = $LIB->_lsft( $x->{_m}, $x->{_e}, 10 );
        $x->{_e}  = $LIB->_zero();
        $x->{_es} = '+';
    }
    $x->{_m} = $LIB->_dfac( $x->{_m} );

    $x->bnorm();
    $x->round(@r);
    $x->_dng();
    return $x;
}

sub btfac {

    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    return $x if $x->modify('btfac');

    return $x->bnan(@r)        if $x->is_nan() || $x->is_inf("-");
    return $x->binf( "+", @r ) if $x->is_inf("+");

    if ( $x <= -3 || !$x->is_int() ) {
        return $x->_upg()->btfac(@r) if $class->upgrade();
        return $x->bnan(@r);
    }

    my $k = $class->new("3");
    return $x->bnan(@r) if $x <= -$k;

    my $one = $class->bone();
    return $x->bone(@r) if $x <= $one;

    my $f = $x->copy();
    while ( $f->bsub($k) > $one ) {
        $x = $x->bmul($f);
    }

    $x->round(@r);
    $x->_dng();
    return $x;
}

sub bmfac {
    my ( $class, $x, $k, @r ) =
      ref( $_[0] ) && ref( $_[0] ) eq ref( $_[1] )
      ? ( ref( $_[0] ), @_ )
      : objectify( 2, @_ );

    return $x if $x->modify('bmfac');

    return $x->bnan(@r)
      if $x->is_nan()
      || $x->is_inf("-")
      || !$k->is_pos();
    return $x->binf( "+", @r ) if $x->is_inf("+");
    return $x->bround(@r)      if $k->is_inf("+");
    return $x->bnan(@r)        if !$x->is_int() || !$k->is_int();
    return $x->bnan(@r)        if $k < 1        || $x <= -$k;

    my $one = $class->bone();
    return $x->bone(@r) if $x <= $one;

    my $f = $x->copy();
    while ( $f->bsub($k) > $one ) {
        $x->bmul($f);
    }

    $x->round(@r);
    $x->_dng();
    return $x;
}

sub bfib {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    croak("bfib() requires a newer version of the $LIB library.")
      unless $LIB->can('_fib');

    return $x if $x->modify('bfib');

    if (wantarray) {
        croak("bfib() can't return an infinitely long list of numbers")
          if $x->is_inf();

        return if $x->is_nan() || !$x->is_int();

        my $n = $x->numify();

        my @y;
        {
            $y[0]     = $x->copy()->babs();
            $y[0]{_m} = $LIB->_zero();
            $y[0]{_e} = $LIB->_zero();
            last if $n == 0;

            $y[1]     = $y[0]->copy();
            $y[1]{_m} = $LIB->_one();
            $y[1]{_e} = $LIB->_zero();
            last if $n == 1;

            for ( my $i = 2 ; $i <= abs($n) ; $i++ ) {
                $y[$i] = $y[ $i - 1 ]->copy();
                $y[$i]{_m} = $LIB->_add( $LIB->_copy( $y[ $i - 1 ]{_m} ),
                    $y[ $i - 2 ]{_m} );
            }

            if ( $x->is_neg() ) {
                for ( my $i = 2 ; $i <= $#y ; $i += 2 ) {
                    $y[$i]{sign} = '-';
                }
            }

            $x->{sign} = $y[-1]{sign};
            $x->{_m}   = $y[-1]{_m};
            $x->{_es}  = $y[-1]{_es};
            $x->{_e}   = $y[-1]{_e};
            $y[-1]     = $x;
        }

        for (@y) {
            $_->bnorm();
            $_->round(@r);
        }

        return @y;
    }

    else {
        return $x         if $x->is_inf('+');
        return $x->bnan() if $x->is_nan() || $x->is_inf('-');

        if ( $x->is_int() ) {

            $x->{sign} = $x->is_neg() && $x->is_even() ? '-' : '+';
            $x->{_m}   = $LIB->_lsft( $x->{_m}, $x->{_e}, 10 );
            $x->{_e}   = $LIB->_zero();
            $x->{_m}   = $LIB->_fib( $x->{_m} );
            $x->bnorm();
        }

        return $x->round(@r);
    }
}

sub blucas {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    croak("blucas() requires a newer version of the $LIB library.")
      unless $LIB->can('_lucas');

    return $x if $x->modify('blucas');

    if (wantarray) {
        croak("blucas() can't return an infinitely long list of numbers")
          if $x->is_inf();

        return if $x->is_nan() || !$x->is_int();

        my $n = $x->numify();

        my @y;
        {
            $y[0]     = $x->copy()->babs();
            $y[0]{_m} = $LIB->_two();
            $y[0]{_e} = $LIB->_zero();
            last if $n == 0;

            $y[1]     = $y[0]->copy();
            $y[1]{_m} = $LIB->_one();
            $y[1]{_e} = $LIB->_zero();
            last if $n == 1;

            for ( my $i = 2 ; $i <= abs($n) ; $i++ ) {
                $y[$i] = $y[ $i - 1 ]->copy();
                $y[$i]{_m} = $LIB->_add( $LIB->_copy( $y[ $i - 1 ]{_m} ),
                    $y[ $i - 2 ]{_m} );
            }

            if ( $x->is_neg() ) {
                for ( my $i = 2 ; $i <= $#y ; $i += 2 ) {
                    $y[$i]{sign} = '-';
                }
            }

            $x->{sign} = $y[-1]{sign};
            $x->{_m}   = $y[-1]{_m};
            $x->{_es}  = $y[-1]{_es};
            $x->{_e}   = $y[-1]{_e};
            $y[-1]     = $x;
        }

        for (@y) {
            $_->bnorm();
            $_->round(@r);
        }

        return @y;
    }

    else {
        return $x         if $x->is_inf('+');
        return $x->bnan() if $x->is_nan() || $x->is_inf('-');

        if ( $x->is_int() ) {

            $x->{sign} = $x->is_neg() && $x->is_even() ? '-' : '+';
            $x->{_m}   = $LIB->_lsft( $x->{_m}, $x->{_e}, 10 );
            $x->{_e}   = $LIB->_zero();
            $x->{_m}   = $LIB->_lucas( $x->{_m} );
            $x->bnorm();
        }

        return $x->round(@r);
    }
}

sub blsft {

    my ( $class, $x, $y, $b, @r ) =
         ref( $_[0] )
      && ref( $_[0] ) eq ref( $_[1] )
      && ref( $_[1] ) eq ref( $_[2] )
      ? ( ref( $_[0] ), @_ )
      : objectify( 2, @_ );

    return $x if $x->modify('blsft');

    return $x->bnan(@r) if $x->is_nan() || $y->is_nan();

    $b = 2 if !defined $b;
    $b = $class->new($b)
      unless defined( blessed($b) ) && $b->isa(__PACKAGE__);
    return $x->bnan(@r) if $b->is_nan();

    return $x->brsft( $y->copy()->babs(), $b ) if $y->{sign} =~ /^-/;

    $x = $x->bmul( $b->bpow($y), $r[0], $r[1], $r[2], $y );

    $x->round(@r);
    $x->_dng() if ( $x->is_int()
        || $x->is_inf()
        || $x->is_nan() );
    return $x;
}

sub brsft {

    my ( $class, $x, $y, $b, @r ) =
         ref( $_[0] )
      && ref( $_[0] ) eq ref( $_[1] )
      && ref( $_[1] ) eq ref( $_[2] )
      ? ( ref( $_[0] ), @_ )
      : objectify( 2, @_ );

    return $x if $x->modify('brsft');

    return $x->bnan(@r) if $x->is_nan() || $y->is_nan();

    $b = 2 if !defined $b;
    $b = $class->new($b)
      unless defined( blessed($b) ) && $b->isa(__PACKAGE__);
    return $x->bnan(@r) if $b->is_nan();

    return $x->blsft( $y->copy()->babs(), $b ) if $y->{sign} =~ /^-/;

    $x = $x->bdiv( $b->bpow($y), $r[0], $r[1], $r[2], $y );

    $x->round(@r);
    $x->_dng() if ( $x->is_int()
        || $x->is_inf()
        || $x->is_nan() );
    return $x;
}

sub bblsft {

    my ( $class, $x, $y, @r ) = ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : @_;

    return $x if $x->modify('bblsft');

    my $xint = Math::BigInt->bblsft( $x, $y, @r );

    my $dng = $class->downgrade();
    $class->downgrade(undef);

    my $xflt = $class->new($xint);

    $class->downgrade($dng);

    if ( defined( blessed($x) ) && $x->isa(__PACKAGE__) ) {
        $x->{sign} = $xflt->{sign};
        $x->{_m}   = $xflt->{_m};
        $x->{_es}  = $xflt->{_es};
        $x->{_e}   = $xflt->{_e};
    }
    else {
        $x = $xflt;
    }

    $x->round(@r);
    $x->_dng();
    return $x;
}

sub bbrsft {

    my ( $class, $x, $y, @r ) = ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : @_;

    return $x if $x->modify('bbrsft');

    my $xint = Math::BigInt->bbrsft( $x, $y, @r );

    my $dng = $class->downgrade();
    $class->downgrade(undef);

    my $xflt = $class->new($xint);

    $class->downgrade($dng);

    if ( defined( blessed($x) ) && $x->isa(__PACKAGE__) ) {
        $x->{sign} = $xflt->{sign};
        $x->{_m}   = $xflt->{_m};
        $x->{_es}  = $xflt->{_es};
        $x->{_e}   = $xflt->{_e};
    }
    else {
        $x = $xflt;
    }

    $x->round(@r);
    $x->_dng();
    return $x;
}

sub band {
    my ( $class, $x, $y, @r ) =
      ref( $_[0] ) && ref( $_[0] ) eq ref( $_[1] )
      ? ( ref( $_[0] ), @_ )
      : objectify( 2, @_ );

    return if $x->modify('band');

    return $x->bnan(@r)
      if ( $x->is_nan()
        || $x->is_inf()
        || $y->is_nan()
        || $y->is_inf() );

    my $xint = $x->as_int();
    my $yint = $y->as_int();

    $xint->band($yint);
    $xint->round(@r);

    my $xflt = $xint->as_float();
    $x->{sign} = $xflt->{sign};
    $x->{_m}   = $xflt->{_m};
    $x->{_es}  = $xflt->{_es};
    $x->{_e}   = $xflt->{_e};

    return $x->_dng();
    return $x;
}

sub bior {
    my ( $class, $x, $y, @r ) =
      ref( $_[0] ) && ref( $_[0] ) eq ref( $_[1] )
      ? ( ref( $_[0] ), @_ )
      : objectify( 2, @_ );

    return if $x->modify('bior');

    return $x->bnan(@r)
      if ( $x->is_nan()
        || $x->is_inf()
        || $y->is_nan()
        || $y->is_inf() );

    my $xint = $x->as_int();
    my $yint = $y->as_int();

    $xint->bior($yint);
    $xint->round(@r);

    my $xflt = $xint->as_float();
    $x->{sign} = $xflt->{sign};
    $x->{_m}   = $xflt->{_m};
    $x->{_es}  = $xflt->{_es};
    $x->{_e}   = $xflt->{_e};

    return $x->_dng();
    return $x;
}

sub bxor {
    my ( $class, $x, $y, @r ) =
      ref( $_[0] ) && ref( $_[0] ) eq ref( $_[1] )
      ? ( ref( $_[0] ), @_ )
      : objectify( 2, @_ );

    return if $x->modify('bxor');

    return $x->bnan(@r)
      if ( $x->is_nan()
        || $x->is_inf()
        || $y->is_nan()
        || $y->is_inf() );

    my $xint = $x->as_int();
    my $yint = $y->as_int();

    $xint->bxor($yint);
    $xint->round(@r);

    my $xflt = $xint->as_float();
    $x->{sign} = $xflt->{sign};
    $x->{_m}   = $xflt->{_m};
    $x->{_es}  = $xflt->{_es};
    $x->{_e}   = $xflt->{_e};

    return $x->_dng();
    return $x;
}

sub bnot {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    return if $x->modify('bnot');

    return $x->bnan(@r) if $x->is_nan();

    my $xint = $x->as_int();

    $xint->bnot();
    $xint->round(@r);

    my $xflt = $xint->as_float();
    $x->{sign} = $xflt->{sign};
    $x->{_m}   = $xflt->{_m};
    $x->{_es}  = $xflt->{_es};
    $x->{_e}   = $xflt->{_e};

    return $x->_dng();
    return $x;
}

sub bround {

    my ( $class, $x, @a ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    if ( ( $a[0] || 0 ) < 0 ) {
        croak('bround() needs positive accuracy');
    }

    return $x if $x->modify('bround');

    my ( $scale, $mode ) = $x->_scale_a(@a);
    if ( !defined $scale ) {
        $x->_dng() if ( $x->is_int()
            || $x->is_inf()
            || $x->is_nan() );
        return $x;
    }

    if ( defined $x->{accuracy} && $x->{accuracy} < $scale ) {
        $x->_dng() if ( $x->is_int()
            || $x->is_inf()
            || $x->is_nan() );
        return $x;
    }

    if ( $scale <= 0 || $x->{sign} !~ /^[+-]$/ ) {
        $x->_dng() if ( $x->is_int()
            || $x->is_inf()
            || $x->is_nan() );
        return $x;
    }

    if ( $x->is_zero() || $LIB->_len( $x->{_m} ) <= $scale ) {
        $x->{accuracy} = $scale
          if !defined $x->{accuracy} || $x->{accuracy} > $scale;
        $x->_dng() if $x->is_int();
        return $x;
    }

    my $m = bless { sign => $x->{sign}, value => $x->{_m} }, 'Math::BigInt';

    $m              = $m->bround( $scale, $mode );
    $x->{_m}        = $m->{value};
    $x->{accuracy}  = $scale;
    $x->{precision} = undef;

    $x->bnorm();
}

sub bfround {

    my ( $class, $x, @p ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    return $x if $x->modify('bfround');

    my ( $scale, $mode ) = $x->_scale_p(@p);
    if ( !defined $scale ) {
        $x->_dng() if ( $x->is_int()
            || $x->is_inf()
            || $x->is_nan() );
        return $x;
    }

    if ( $x->is_zero() ) {
        $x->{precision} = $scale
          if !defined $x->{precision} || $x->{precision} < $scale;
        $x->_dng() if ( $x->is_int()
            || $x->is_inf()
            || $x->is_nan() );
        return $x;
    }

    if ( $x->{sign} !~ /^[+-]$/ ) {
        $x->_dng() if ( $x->is_int()
            || $x->is_inf()
            || $x->is_nan() );
        return $x;
    }

    if (   defined $x->{precision}
        && $x->{precision} < 0
        && $scale < $x->{precision} )
    {
        $x->_dng() if ( $x->is_int()
            || $x->is_inf()
            || $x->is_nan() );
        return $x;
    }

    $x->{precision} = $scale;
    $x->{accuracy}  = undef;
    if ( $scale < 0 ) {

        if ( $x->{_es} eq '+' ) {
            $x->_dng() if ( $x->is_int()
                || $x->is_inf()
                || $x->is_nan() );
            return $x;
        }

        $scale = -$scale;
        my $len = $LIB->_len( $x->{_m} );

        my $dad = -( 0 + ( $x->{_es} . $LIB->_num( $x->{_e} ) ) );
        my $zad = 0;
        $zad = $dad - $len if ( -$dad < -$len );

        if ( $scale > $dad ) {
            $x->_dng() if ( $x->is_int()
                || $x->is_inf()
                || $x->is_nan() );
            return $x;
        }

        if ( $scale < $zad ) {
            $x->_dng() if ( $x->is_int()
                || $x->is_inf()
                || $x->is_nan() );
            return $x->bzero();
        }

        if ( $scale == $zad ) {
            $scale = -$len;
        }
        else {
            if ( $zad != 0 ) {
                $scale = $scale - $zad;
            }
            else {
                my $dbd = $len - $dad;
                $dbd   = 0 if $dbd < 0;
                $scale = $dbd + $scale;
            }
        }
    }
    else {

        my $dbt = $LIB->_len( $x->{_m} );
        my $dbd = $dbt + ( $x->{_es} . $LIB->_num( $x->{_e} ) );
        $scale = 1 if $scale == 0;
        if ( $scale == 1 && $dbt <= $dbd ) {
            $x->_dng() if ( $x->is_int()
                || $x->is_inf()
                || $x->is_nan() );
            return $x;
        }
        ++$dbd;

        if ( $scale > $dbd ) {
            return $x->bzero;
        }
        elsif ( $scale == $dbd ) {
            $scale = -$dbt;
        }
        else {
            $scale = $dbd - $scale;
        }
    }

    my $m = bless { sign => $x->{sign}, value => $x->{_m} }, 'Math::BigInt';
    $m = $m->bround( $scale, $mode );
    $x->{_m} = $m->{value};

    $x->bnorm();
}

sub bfloor {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    return $x if $x->modify('bfloor');

    return $x->bnan(@r) if $x->is_nan();

    if ( $x->is_finite() ) {
        if ( $x->{_es} eq '-' ) {
            $x->{_m}  = $LIB->_rsft( $x->{_m}, $x->{_e}, 10 );
            $x->{_e}  = $LIB->_zero();
            $x->{_es} = '+';
            $x->{_m} = $LIB->_inc( $x->{_m} ) if $x->{sign} eq '-';
        }
    }

    $x->round(@r);
    $x->_dng();
    return $x;
}

sub bceil {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    return $x if $x->modify('bceil');

    return $x->bnan(@r) if $x->is_nan();

    if ( $x->is_finite() ) {
        if ( $x->{_es} eq '-' ) {
            $x->{_m}  = $LIB->_rsft( $x->{_m}, $x->{_e}, 10 );
            $x->{_e}  = $LIB->_zero();
            $x->{_es} = '+';
            if ( $x->{sign} eq '+' ) {
                $x->{_m} = $LIB->_inc( $x->{_m} );
            }
            else {
                $x->{sign} = '+' if $LIB->_is_zero( $x->{_m} );
            }
        }
    }

    $x->round(@r);
    $x->_dng();
    return $x;
}

sub bint {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    return $x if $x->modify('bint');

    return $x->bnan(@r) if $x->is_nan();

    if ( $x->is_finite() ) {
        if ( $x->{_es} eq '-' ) {
            $x->{_m}   = $LIB->_rsft( $x->{_m}, $x->{_e}, 10 );
            $x->{_e}   = $LIB->_zero();
            $x->{_es}  = '+';
            $x->{sign} = '+' if $LIB->_is_zero( $x->{_m} );
        }
    }

    $x->round(@r);
    $x->_dng();
    return $x;
}

sub bgcd {

    unless (
        @_
        && (
            defined( blessed( $_[0] ) ) && $_[0]->isa(__PACKAGE__)
            || (   $_[0] =~ /^[a-z]\w*(?:::[a-z]\w*)*$/i
                && $_[0] !~ /^(inf|nan)/i )
        )
      )
    {
        unshift @_, __PACKAGE__;
    }

    my ( $class, @args ) = objectify( 0, @_ );

    for my $arg (@args) {
        return $class->bnan() unless $arg->is_finite();
    }

    my $dng = $class->downgrade();
    $class->downgrade(undef);

    my $x = shift @args;
    $x = $x->copy();

    while (@args) {
        my $y = shift @args;

        while ( !$y->is_zero() ) {
            ( $x, $y ) = ( $y->copy(), $x->copy()->bmod($y) );
        }

        last if $x->is_one();
    }
    $x->babs();

    $class->downgrade($dng);

    $x->_dng() if $x->is_int();
    return $x;
}

sub blcm {

    unless (
        @_
        && (
            defined( blessed( $_[0] ) ) && $_[0]->isa(__PACKAGE__)
            || (   $_[0] =~ /^[a-z]\w*(?:::[a-z]\w*)*$/i
                && $_[0] !~ /^(inf|nan)/i )
        )
      )
    {
        unshift @_, __PACKAGE__;
    }

    my ( $class, @args ) = objectify( 0, @_ );

    for my $arg (@args) {
        return $class->bnan() unless $arg->is_finite();
    }

    for my $arg (@args) {
        return $class->bzero() if $arg->is_zero();
    }

    my $x = shift @args;
    $x = $x->copy();

    while (@args) {
        my $y   = shift @args;
        my $gcd = $x->copy()->bgcd($y);
        $x->bdiv($gcd)->bmul($y);
    }

    $x->babs();
    return $x;
}

sub length {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    carp "Rounding is not supported for ", ( caller(0) )[3], "()" if @r;

    return 1 if $LIB->_is_zero( $x->{_m} );

    my $len = $LIB->_len( $x->{_m} );
    $len += $LIB->_num( $x->{_e} ) if $x->{_es} eq '+';
    if ( wantarray() ) {
        my $t = 0;
        $t = $LIB->_num( $x->{_e} ) if $x->{_es} eq '-';
        return $len, $t;
    }
    $len;
}

sub mantissa {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    return $x->bnan(@r) if $x->is_nan();

    if ( $x->{sign} !~ /^[+-]$/ ) {
        my $s = $x->{sign};
        $s =~ s/^\+//;
        return Math::BigInt->new( $s, undef, undef );
    }
    my $m = Math::BigInt->new( $LIB->_str( $x->{_m} ), undef, undef );
    $m = $m->bneg() if $x->{sign} eq '-';
    $m;
}

sub exponent {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    return $x->bnan(@r) if $x->is_nan();

    if ( $x->{sign} !~ /^[+-]$/ ) {
        my $s = $x->{sign};
        $s =~ s/^[+-]//;
        return Math::BigInt->new( $s, undef, undef );
    }
    Math::BigInt->new( $x->{_es} . $LIB->_str( $x->{_e} ), undef, undef );
}

sub parts {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    carp "Rounding is not supported for ", ( caller(0) )[3], "()" if @r;

    if ( $x->{sign} !~ /^[+-]$/ ) {
        my $s = $x->{sign};
        $s =~ s/^\+//;
        my $se = $s;
        $se =~ s/^-//;
        return $class->new($s), $class->new($se);
    }
    my $m = Math::BigInt->bzero();
    $m->{value} = $LIB->_copy( $x->{_m} );
    $m = $m->bneg() if $x->{sign} eq '-';
    ( $m, Math::BigInt->new( $x->{_es} . $LIB->_num( $x->{_e} ) ) );
}

sub sparts {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    carp "Rounding is not supported for ", ( caller(0) )[3], "()" if @r;

    if ( $x->is_nan() ) {
        my $mant = $class->bnan();
        return $mant unless wantarray;
        my $expo = $class->bnan();
        return $mant, $expo;
    }

    if ( $x->is_inf() ) {
        my $mant = $class->binf( $x->{sign} );
        return $mant unless wantarray;
        my $expo = $class->binf('+');
        return $mant, $expo;
    }

    my $mant = $class->new($x);
    $mant->{_es} = '+';
    $mant->{_e}  = $LIB->_zero();
    $mant->_dng();
    return $mant unless wantarray;

    my $expo = $class->new( $x->{_es} . $LIB->_str( $x->{_e} ) );
    $expo->_dng();
    return $mant, $expo;
}

sub nparts {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    carp "Rounding is not supported for ", ( caller(0) )[3], "()" if @r;

    return $x->sparts() if $x->is_nan() || $x->is_inf();

    my ( $mant, $expo ) = $x->sparts();

    if ( $mant->bcmp(0) ) {
        my ( $ndigtot, $ndigfrac ) = $mant->length();
        my $expo10adj = $ndigtot - $ndigfrac - 1;

        if ( $expo10adj > 0 ) {
            $mant = $mant->brsft( $expo10adj, 10 );
            return $mant unless wantarray;
            $expo = $expo->badd($expo10adj);
            return $mant, $expo;
        }
    }

    return $mant unless wantarray;
    return $mant, $expo;
}

sub eparts {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    carp "Rounding is not supported for ", ( caller(0) )[3], "()" if @r;

    return $x->sparts() if $x->is_nan() || $x->is_inf();

    my ( $mant, $expo ) = $x->nparts();

    my $c = $expo->copy()->bmod(3);
    $mant = $mant->blsft( $c, 10 );
    return $mant unless wantarray;

    $expo = $expo->bsub($c);
    return $mant, $expo;
}

sub dparts {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    carp "Rounding is not supported for ", ( caller(0) )[3], "()" if @r;

    if ( $x->is_nan() ) {
        my $int = $class->bnan();
        return $int unless wantarray;
        my $frc = $class->bzero();
        return $int, $frc;
    }

    if ( $x->is_inf() ) {
        my $int = $class->binf( $x->{sign} );
        return $int unless wantarray;
        my $frc = $class->bzero();
        return $int, $frc;
    }

    my $int = $x->copy();
    my $frc;

    if ( $int->{_es} eq '+' ) {
        $frc = $class->bzero();
    }

    else {
        $int->{_m}   = $LIB->_rsft( $int->{_m}, $int->{_e}, 10 );
        $int->{_e}   = $LIB->_zero();
        $int->{_es}  = '+';
        $int->{sign} = '+' if $LIB->_is_zero( $int->{_m} );
        return $int unless wantarray;
        $frc = $x->copy()->bsub($int);
        return $int, $frc;
    }

    $int->_dng();
    return $int unless wantarray;
    return $int, $frc;
}

sub fparts {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    carp "Rounding is not supported for ", ( caller(0) )[3], "()" if @r;

    if ( $x->is_nan() ) {
        return $class->bnan() unless wantarray;
        return $class->bnan(), $class->bnan();
    }

    if ( $x->is_inf() ) {
        my $numer = $class->binf( $x->{sign} );
        return $numer unless wantarray;
        my $denom = $class->bone();
        return $numer, $denom;
    }

    $class = $downgrade if $class->downgrade();

    my @flt_parts = ( $x->{sign}, $x->{_m}, $x->{_es}, $x->{_e} );
    my @rat_parts = $class->_flt_lib_parts_to_rat_lib_parts(@flt_parts);
    my $num       = $class->new( $LIB->_str( $rat_parts[1] ) );
    my $den       = $class->new( $LIB->_str( $rat_parts[2] ) );
    $num = $num->bneg() if $rat_parts[0] eq "-";
    return $num unless wantarray;
    return $num, $den;
}

sub numerator {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    carp "Rounding is not supported for ", ( caller(0) )[3], "()" if @r;

    return $class->bnan()             if $x->is_nan();
    return $class->binf( $x->sign() ) if $x->is_inf();
    return $class->bzero()            if $x->is_zero();

    $class = $downgrade if $class->downgrade();

    if ( $x->{_es} eq '-' ) {
        my $numer_lib = $LIB->_copy( $x->{_m} );
        my $denom_lib = $LIB->_1ex( $x->{_e} );
        my $gcd_lib   = $LIB->_gcd( $LIB->_copy($numer_lib), $denom_lib );
        $numer_lib = $LIB->_div( $numer_lib, $gcd_lib );
        return $class->new( $x->{sign} . $LIB->_str($numer_lib) );
    }

    elsif ( !$LIB->_is_zero( $x->{_e} ) ) {
        my $numer_lib = $LIB->_copy( $x->{_m} );
        $numer_lib = $LIB->_lsft( $numer_lib, $x->{_e}, 10 );
        return $class->new( $x->{sign} . $LIB->_str($numer_lib) );
    }

    else {
        return $class->new( $x->{sign} . $LIB->_str( $x->{_m} ) );
    }
}

sub denominator {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    carp "Rounding is not supported for ", ( caller(0) )[3], "()" if @r;

    return $class->bnan() if $x->is_nan();

    $class = $downgrade if $class->downgrade();

    if ( $x->{_es} eq '-' ) {
        my $numer_lib = $LIB->_copy( $x->{_m} );
        my $denom_lib = $LIB->_1ex( $x->{_e} );
        my $gcd_lib   = $LIB->_gcd( $LIB->_copy($numer_lib), $denom_lib );
        $denom_lib = $LIB->_div( $denom_lib, $gcd_lib );
        return $class->new( $LIB->_str($denom_lib) );
    }

    else {
        return $class->bone();
    }
}

sub bstr {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    carp "Rounding is not supported for ", ( caller(0) )[3], "()" if @r;

    if ( $x->{sign} ne '+' && $x->{sign} ne '-' ) {
        return $x->{sign} unless $x->is_inf("+");
        return 'inf';
    }

    my $es  = '0';
    my $len = 1;
    my $cad = 0;
    my $dot = '.';

    my $not_zero = !( $x->{sign} eq '+' && $LIB->_is_zero( $x->{_m} ) );
    if ($not_zero) {
        $es  = $LIB->_str( $x->{_m} );
        $len = CORE::length($es);
        my $e = $LIB->_num( $x->{_e} );
        $e = -$e if $x->{_es} eq '-';
        if ( $e < 0 ) {
            $dot = '';
            if ( $e <= -$len ) {
                my $r = abs($e) - $len;
                $es  = '0.' . ( '0' x $r ) . $es;
                $cad = -( $len + $r );
            }
            else {
                substr( $es, $e, 0 ) = '.';
                $cad = $LIB->_num( $x->{_e} );
                $cad = -$cad if $x->{_es} eq '-';
            }
        }
        elsif ( $e > 0 ) {
            $es .= '0' x $e;
            $len += $e;
            $cad = 0;
        }
    }

    $es = '-' . $es if $x->{sign} eq '-';
    if ( ( defined $x->{accuracy} ) && ($not_zero) ) {
        my $zeros = $x->{accuracy} - $cad;
        $zeros = $x->{accuracy} - $len if $cad != $len;
        $es .= $dot . '0' x $zeros if $zeros > 0;
    }
    elsif ( ( ( $x->{precision} || 0 ) < 0 ) ) {
        my $zeros = -$x->{precision} + $cad;
        $es .= $dot . '0' x $zeros if $zeros > 0;
    }
    $es;
}

sub bsstr {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    if ( $x->{sign} ne '+' && $x->{sign} ne '-' ) {
        return $x->{sign} unless $x->is_inf("+");
        return 'inf';
    }

    return $x->_upg()->bsstr(@r)
      if $class->upgrade() && !$x->isa(__PACKAGE__);

    $x = $x->copy()->round(@r);

    ( $x->{sign} eq '-' ? '-' : '' )
      . $LIB->_str( $x->{_m} ) . 'e'
      . $x->{_es}
      . $LIB->_str( $x->{_e} );
}

sub bnstr {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    if ( $x->{sign} ne '+' && $x->{sign} ne '-' ) {
        return $x->{sign} unless $x->is_inf("+");
        return 'inf';
    }

    return $x->_upg()->bnstr(@r)
      if $class->upgrade() && !$x->isa(__PACKAGE__);

    my $str = $x->{sign} eq '-' ? '-' : '';

    $x = $x->copy()->round(@r);

    my $mant    = $LIB->_str( $x->{_m} );
    my $mantlen = CORE::length($mant);

    if ( $mantlen == 1 ) {

        $str .= $mant . 'e' . $x->{_es} . $LIB->_str( $x->{_e} );

    }
    else {

        my ( $eabs, $esgn ) = $LIB->_sadd(
            $LIB->_copy( $x->{_e} ),    $x->{_es},
            $LIB->_new( $mantlen - 1 ), "+"
        );
        substr $mant, 1, 0, ".";
        $str .= $mant . 'e' . $esgn . $LIB->_str($eabs);

    }

    return $str;
}

sub bestr {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    if ( $x->{sign} ne '+' && $x->{sign} ne '-' ) {
        return $x->{sign} unless $x->is_inf("+");
        return 'inf';
    }

    return $x->_upg()->bestr(@r)
      if $class->upgrade() && !$x->isa(__PACKAGE__);

    $x = $x->copy()->round(@r);

    my $str = $x->{sign} eq '-' ? '-' : '';

    my $mant    = $LIB->_str( $x->{_m} );
    my $mantlen = CORE::length($mant);
    my ( $eabs, $esgn ) = $LIB->_sadd( $LIB->_copy( $x->{_e} ),
        $x->{_es}, $LIB->_new( $mantlen - 1 ), "+" );

    my $dotpos = 1;
    my $mod    = $LIB->_mod( $LIB->_copy($eabs), $LIB->_new("3") );
    unless ( $LIB->_is_zero($mod) ) {
        if ( $esgn eq '+' ) {
            $eabs = $LIB->_sub( $eabs, $mod );
            $dotpos += $LIB->_num($mod);
        }
        else {
            my $delta = $LIB->_sub( $LIB->_new("3"), $mod );
            $eabs = $LIB->_add( $eabs, $delta );
            $dotpos += $LIB->_num($delta);
        }
    }

    if ( $dotpos < $mantlen ) {
        substr $mant, $dotpos, 0, ".";
    }
    elsif ( $dotpos > $mantlen ) {
        $mant .= "0" x ( $dotpos - $mantlen );
    }

    $str .= $mant . 'e' . $esgn . $LIB->_str($eabs);

    return $str;
}

sub bdstr {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    if ( $x->{sign} ne '+' && $x->{sign} ne '-' ) {
        return $x->{sign} unless $x->is_inf("+");
        return 'inf';
    }

    return $x->_upg()->bdstr(@r)
      if $class->upgrade() && !$x->isa(__PACKAGE__);

    $x = $x->copy()->round(@r);

    my $mant = $LIB->_str( $x->{_m} );
    my $esgn = $x->{_es};
    my $eabs = $LIB->_num( $x->{_e} );

    my $uintmax = ~0;

    my $str = $mant;
    if ( $esgn eq '+' ) {

        croak("The absolute value of the exponent is too large")
          if $eabs > $uintmax;

        $str .= "0" x $eabs;

    }
    else {
        my $mlen = CORE::length($mant);
        my $c    = $mlen - $eabs;

        my $intmax = ( $uintmax - 1 ) / 2;
        croak("The absolute value of the exponent is too large")
          if ( 1 - $c ) > $intmax;

        $str = "0" x ( 1 - $c ) . $str if $c <= 0;
        substr( $str, -$eabs, 0 ) = '.';
    }

    return $x->{sign} eq '-' ? '-' . $str : $str;
}

sub bfstr {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), $_[0] ) : objectify( 1, @_ );

    carp "Rounding is not supported for ", ( caller(0) )[3], "()" if @r;

    if ( $x->{sign} ne '+' && $x->{sign} ne '-' ) {
        return $x->{sign} unless $x->is_inf("+");
        return 'inf';
    }

    return $x->_upg()->bfstr(@r)
      if $class->upgrade() && !$x->isa(__PACKAGE__);

    my $str = $x->{sign} eq '-' ? '-' : '';

    if ( $x->{_es} eq '+' ) {
        $str .= $LIB->_str( $x->{_m} ) . ( "0" x $LIB->_num( $x->{_e} ) );
    }
    else {
        my @flt_parts = ( $x->{sign}, $x->{_m}, $x->{_es}, $x->{_e} );
        my @rat_parts = $class->_flt_lib_parts_to_rat_lib_parts(@flt_parts);
        $str = $LIB->_str( $rat_parts[1] ) . "/" . $LIB->_str( $rat_parts[2] );
        $str = "-" . $str if $rat_parts[0] eq "-";
    }

    return $str;
}

sub to_hex {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), $_[0] ) : objectify( 1, @_ );

    carp "Rounding is not supported for ", ( caller(0) )[3], "()" if @r;

    if ( $x->{sign} ne '+' && $x->{sign} ne '-' ) {
        return $x->{sign} unless $x->is_inf("+");
        return 'inf';
    }

    return $x->_upg()->to_hex(@r)
      if $class->upgrade() && !$x->isa(__PACKAGE__);

    return '0' if $x->is_zero();

    return $nan if $x->{_es} ne '+';

    my $z = $LIB->_copy( $x->{_m} );
    if ( !$LIB->_is_zero( $x->{_e} ) ) {
        $z = $LIB->_lsft( $z, $x->{_e}, 10 );
    }
    my $str = $LIB->_to_hex($z);
    return $x->{sign} eq '-' ? "-$str" : $str;
}

sub to_oct {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), $_[0] ) : objectify( 1, @_ );

    carp "Rounding is not supported for ", ( caller(0) )[3], "()" if @r;

    if ( $x->{sign} ne '+' && $x->{sign} ne '-' ) {
        return $x->{sign} unless $x->is_inf("+");
        return 'inf';
    }

    return $x->_upg()->to_oct(@r)
      if $class->upgrade() && !$x->isa(__PACKAGE__);

    return '0' if $x->is_zero();

    return $nan if $x->{_es} ne '+';

    my $z = $LIB->_copy( $x->{_m} );
    if ( !$LIB->_is_zero( $x->{_e} ) ) {
        $z = $LIB->_lsft( $z, $x->{_e}, 10 );
    }
    my $str = $LIB->_to_oct($z);
    return $x->{sign} eq '-' ? "-$str" : $str;
}

sub to_bin {
    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), $_[0] ) : objectify( 1, @_ );

    carp "Rounding is not supported for ", ( caller(0) )[3], "()" if @r;

    if ( $x->{sign} ne '+' && $x->{sign} ne '-' ) {
        return $x->{sign} unless $x->is_inf("+");
        return 'inf';
    }

    return $x->_upg()->to_bin(@r)
      if $class->upgrade() && !$x->isa(__PACKAGE__);

    return '0' if $x->is_zero();

    return $nan if $x->{_es} ne '+';

    my $z = $LIB->_copy( $x->{_m} );
    if ( !$LIB->_is_zero( $x->{_e} ) ) {
        $z = $LIB->_lsft( $z, $x->{_e}, 10 );
    }
    my $str = $LIB->_to_bin($z);
    return $x->{sign} eq '-' ? "-$str" : $str;
}

sub to_bytes {

    my ( $class, $x, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    carp "Rounding is not supported for ", ( caller(0) )[3], "()" if @r;

    croak("to_bytes() requires a finite, non-negative integer")
      if $x->is_neg() || !$x->is_int();

    return $x->_upg()->to_bytes(@r)
      if $class->upgrade() && !$x->isa(__PACKAGE__);

    croak("to_bytes() requires a newer version of the $LIB library.")
      unless $LIB->can('_to_bytes');

    return $LIB->_to_bytes( $LIB->_lsft( $x->{_m}, $x->{_e}, 10 ) );
}

sub to_ieee754 {
    my ( $class, $x, $format, @r ) =
      ref( $_[0] ) ? ( ref( $_[0] ), @_ ) : objectify( 1, @_ );

    carp "Rounding is not supported for ", ( caller(0) )[3], "()" if @r;

    my $enc;
    my $k;
    my $b;

    if ( $format =~ /^binary(\d+)\z/ ) {
        $k = $1;
        $b = 2;
    }
    elsif ( $format =~ /^decimal(\d+)(dpd|bcd)?\z/ ) {
        $k   = $1;
        $b   = 10;
        $enc = $2 || 'dpd';
    }
    elsif ( $format eq 'half' ) {
        $k = 16;
        $b = 2;
    }
    elsif ( $format eq 'single' ) {
        $k = 32;
        $b = 2;
    }
    elsif ( $format eq 'double' ) {
        $k = 64;
        $b = 2;
    }
    elsif ( $format eq 'quadruple' ) {
        $k = 128;
        $b = 2;
    }
    elsif ( $format eq 'octuple' ) {
        $k = 256;
        $b = 2;
    }
    elsif ( $format eq 'sexdecuple' ) {
        $k = 512;
        $b = 2;
    }

    if ( $b == 2 ) {

        my $p;
        my $t;
        my $w;

        if ( $k == 16 ) {
            $p = 11;
            $t = 10;
            $w = 5;
        }
        elsif ( $k == 32 ) {
            $p = 24;
            $t = 23;
            $w = 8;
        }
        elsif ( $k == 64 ) {
            $p = 53;
            $t = 52;
            $w = 11;
        }
        else {
            if ( $k < 128 || $k != 32 * sprintf( '%.0f', $k / 32 ) ) {
                croak "Number of bits must be 16, 32, 64, or >= 128 and",
                  " a multiple of 32";
            }
            $p = $k - sprintf( '%.0f', 4 * log($k) / log(2) ) + 13;
            $t = $p - 1;
            $w = $k - $t - 1;
        }

        my $emax = $class->new(2)->bpow( $w - 1 )->bdec();
        my $emin = 1 - $emax;
        my $bias = $emax;

        my $sign = 0;
        my $expo;
        my $mant;

        if ( $x->is_nan() ) {
            $sign = 1;
            $expo = $emax->copy()->binc();
            $mant = $class->new(2)->bpow( $t - 1 );
        }
        elsif ( $x->is_inf() ) {
            $sign = 1 if $x->is_neg();
            $expo = $emax->copy()->binc();
            $mant = $class->bzero();
        }
        elsif ( $x->is_zero() ) {
            $expo = $emin->copy()->bdec();
            $mant = $class->bzero();
        }
        else {

            $sign = 1 if $x->is_neg();

            my $binv = $class->new("0.5");
            my $b    = $class->new(2);
            my $one  = $class->bone();

            $mant = $x->copy()->babs();

            my ( $m, $e ) = $x->nparts();
            my $ms = $m->numify();
            my $es = $e->numify();

            my $expo_est =
              ( log( abs($ms) ) / log(10) + $es ) * log(10) / log(2);
            $expo_est = int($expo_est);

            if ( $expo_est > $emax ) {
                $expo_est = $emax;
            }
            elsif ( $expo_est < $emin ) {
                $expo_est = $emin;
            }

            $expo = $class->new($expo_est);
            if ( $expo_est > 0 ) {
                $mant = $mant->bmul( $binv->copy()->bpow($expo) );
            }
            elsif ( $expo_est < 0 ) {
                my $expo_abs = $expo->copy()->bneg();
                $mant = $mant->bmul( $b->copy()->bpow($expo_abs) );
            }

            while ( $mant >= $b && $expo <= $emax ) {
                $mant = $mant->bmul($binv);
                $expo = $expo->binc();
            }

            while ( $mant < $one && $expo >= $emin ) {
                $mant = $mant->bmul($b);
                $expo = $expo->bdec();
            }

            if ( $expo > $emax ) {
                $mant = $class->bzero();
                $expo = $emax->copy()->binc();
            }

            elsif ( $expo < $emin ) {

                my $const = $class->new($b)->bpow( $t - 1 );
                $mant = $mant->bmul($const);
                $mant = $mant->bfround(0);

                if ( $mant == $const->bmul($b) ) {
                    $mant = $mant->bzero();
                    $expo = $expo->binc();
                }
            }

            else {

                $mant = $mant->bdec();
                my $const = $class->new($b)->bpow($t);
                $mant = $mant->bmul($const)->bfround(0);

                if ( $mant == $const ) {
                    $mant = $mant->bzero();
                    $expo = $expo->binc();
                }
            }
        }

        $expo = $expo->badd($bias);

        my $signbit = "$sign";

        my $mantbits = $mant->to_bin();
        $mantbits = ( "0" x ( $t - CORE::length($mantbits) ) ) . $mantbits;

        my $expobits = $expo->to_bin();
        $expobits = ( "0" x ( $w - CORE::length($expobits) ) ) . $expobits;

        my $bin = $signbit . $expobits . $mantbits;
        return pack "B*", $bin;
    }

    croak("The format '$format' is not yet supported.");
}

sub as_hex {

    my ( undef, $x, @r ) = ref( $_[0] ) ? ( undef, @_ ) : objectify( 1, @_ );

    carp "Rounding is not supported for ", ( caller(0) )[3], "()" if @r;

    return $x->bstr() if $x->{sign} !~ /^[+-]$/;
    return '0x0'      if $x->is_zero();

    return $nan if $x->{_es} ne '+';

    my $z = $LIB->_copy( $x->{_m} );
    if ( !$LIB->_is_zero( $x->{_e} ) ) {
        $z = $LIB->_lsft( $z, $x->{_e}, 10 );
    }
    my $str = $LIB->_as_hex($z);
    return $x->{sign} eq '-' ? "-$str" : $str;
}

sub as_oct {

    my ( undef, $x, @r ) = ref( $_[0] ) ? ( undef, @_ ) : objectify( 1, @_ );

    carp "Rounding is not supported for ", ( caller(0) )[3], "()" if @r;

    return $x->bstr() if $x->{sign} !~ /^[+-]$/;
    return '00'       if $x->is_zero();

    return $nan if $x->{_es} ne '+';

    my $z = $LIB->_copy( $x->{_m} );
    if ( !$LIB->_is_zero( $x->{_e} ) ) {
        $z = $LIB->_lsft( $z, $x->{_e}, 10 );
    }
    my $str = $LIB->_as_oct($z);
    return $x->{sign} eq '-' ? "-$str" : $str;
}

sub as_bin {

    my ( undef, $x, @r ) = ref( $_[0] ) ? ( undef, @_ ) : objectify( 1, @_ );

    carp "Rounding is not supported for ", ( caller(0) )[3], "()" if @r;

    return $x->bstr() if $x->{sign} !~ /^[+-]$/;
    return '0b0'      if $x->is_zero();

    return $nan if $x->{_es} ne '+';

    my $z = $LIB->_copy( $x->{_m} );
    if ( !$LIB->_is_zero( $x->{_e} ) ) {
        $z = $LIB->_lsft( $z, $x->{_e}, 10 );
    }
    my $str = $LIB->_as_bin($z);
    return $x->{sign} eq '-' ? "-$str" : $str;
}

sub numify {

    my ( undef, $x, @r ) = ref( $_[0] ) ? ( undef, @_ ) : objectify( 1, @_ );

    carp "Rounding is not supported for ", ( caller(0) )[3], "()" if @r;

    if ( $x->is_nan() ) {
        require Math::Complex;
        my $inf = $Math::Complex::Inf;
        return $inf - $inf;
    }

    if ( $x->is_inf() ) {
        require Math::Complex;
        my $inf = $Math::Complex::Inf;
        return $x->is_negative() ? -$inf : $inf;
    }

    return 0 + $x->bnstr();
}

sub import {
    my $class = shift;
    $IMPORT++;
    my @a;

    my @import = ();

    while (@_) {
        my $param = shift;

        if ( $param eq ':constant' ) {
            overload::constant

              integer => sub {
                $class->new(shift);
              },

              float => sub {
                $class->new(shift);
              },

              binary => sub {
                return $class->from_oct( $_[0] ) if $_[0] =~ /^0_*[0-7]/;
                $class->new(shift);
              };
            next;
        }

        if ( $param eq 'upgrade' ) {
            $class->upgrade(shift);
            next;
        }

        if ( $param eq 'downgrade' ) {
            $class->downgrade(shift);
            next;
        }

        if ( $param eq 'accuracy' ) {
            $class->accuracy(shift);
            next;
        }

        if ( $param eq 'precision' ) {
            $class->precision(shift);
            next;
        }

        if ( $param eq 'round_mode' ) {
            $class->round_mode(shift);
            next;
        }

        if ( $param eq 'div_scale' ) {
            $class->div_scale(shift);
            next;
        }

        if ( $param =~ /^(lib|try|only)\z/ ) {
            push @import, $param;
            push @import, shift() if @_;
            next;
        }

        if ( $param eq 'with' ) {
            shift;
            next;
        }

        push @a, $param;
    }

    Math::BigInt->import(@import);

    $LIB = Math::BigInt->config('lib');

    $class->SUPER::import(@a);
    $class->export_to_level( 1, $class, @a ) if @a;
}

sub _len_to_steps {
    my $d = shift;

    my $lg2  = log( 2 * 3.14159265 ) / 2;
    my $lg10 = log(10);

    my $l = 40;
    my $r = $d;

    $l    = $l->numify    if ref($l);
    $r    = $r->numify    if ref($r);
    $lg2  = $lg2->numify  if ref($lg2);
    $lg10 = $lg10->numify if ref($lg10);

    while ( $r - $l > 1 ) {
        my $n         = int( ( $r - $l ) / 2 ) + $l;
        my $ramanujan = int(
            (
                $n * log($n) -
                  $n +
                  log( $n * ( 1 + 4 * $n * ( 1 + 2 * $n ) ) ) / 6 +
                  $lg2
            ) / $lg10
        );
        $ramanujan > $d ? $r = $n : $l = $n;
    }
    $l;
}

sub _log {
    my ( $x, $scale ) = @_;
    my $class = ref $x;

    return $x->bzero() if $x->is_one();

    my $scaleup = $scale + 4;

    my ( $v, $u, $numer, $denom, $factor, $f );

    $v = $x->copy();
    $v = $v->binc();
    $x = $x->bdec();
    $u = $x->copy();

    $x = $x->bdiv( $v, $scaleup );

    $numer = $u->copy();
    $denom = $v->copy();

    $u = $u->bmul($u);
    $v = $v->bmul($v);

    $numer = $numer->bmul($u);
    $denom = $denom->bmul($v);

    $factor = $class->new(3);
    $f      = $class->new(2);

    while (1) {
        my $next =
          $numer->copy()
          ->bround($scaleup)
          ->bdiv( $denom->copy()->bmul($factor)->bround($scaleup), $scaleup );

        $next->{accuracy}  = undef;
        $next->{precision} = undef;
        my $x_prev = $x->copy();
        $x = $x->badd($next);

        last if $x->bacmp($x_prev) == 0;

        $numer  = $numer->bmul($u);
        $denom  = $denom->bmul($v);
        $factor = $factor->badd($f);
    }

    $x = $x->bmul($f);
    $x = $x->bround($scale);
}

sub _log_10 {
    my ( $x, $scale ) = @_;
    my $class = ref $x;

    my $dbd = $LIB->_num( $x->{_e} );
    $dbd = -$dbd if $x->{_es} eq '-';
    $dbd += $LIB->_len( $x->{_m} );

    my $calc = 1;

    my $upg = $class->upgrade();
    my $dng = $class->downgrade();
    $class->upgrade(undef);
    $class->downgrade(undef);

    if (
        $x->{_es} eq '+'
        && (   $LIB->_is_one( $x->{_e} )
            && $LIB->_is_one( $x->{_m} ) )
      )
    {
        $dbd = 0;

        if ( $scale <= $LOG_10_A ) {
            $x    = $x->bzero();
            $x    = $x->badd($LOG_10);
            $calc = 0;
        }
    }
    else {
        if ( ( $LIB->_is_zero( $x->{_e} ) && $LIB->_is_two( $x->{_m} ) ) ) {
            $dbd = 0;

            if ( $scale <= $LOG_2_A ) {
                $x    = $x->bzero();
                $x    = $x->badd($LOG_2);
                $calc = 0;
            }
        }
    }

    if (
        $calc != 0
        && (
            $x->{_es} eq '-'
            && (   $LIB->_is_one( $x->{_e} )
                && $LIB->_is_one( $x->{_m} ) )
        )
      )
    {
        $dbd = 0;

        if ( $scale <= $LOG_10_A ) {
            $x    = $x->bzero();
            $x    = $x->bsub($LOG_10);
            $calc = 0;
        }
    }

    return $x if $calc == 0;

    my $l_10;
    my $l_2;

    my $two = $class->new(2);

    if ( ( $dbd > 1 ) || ( $dbd < 0 ) ) {
        $LOG_10 = $class->new( $LOG_10, undef, undef ) unless ref $LOG_10;

        if ( $scale <= $LOG_10_A ) {
            $l_10 = $LOG_10->copy();
        }
        else {

            $LOG_2 = $class->new( $LOG_2, undef, undef ) unless ref $LOG_2;
            if ( $scale <= $LOG_2_A ) {
                $l_2 = $LOG_2->copy();
            }
            else {
                $l_2   = $two->copy();
                $l_2   = $l_2->_log($scale);
                $LOG_2 = $l_2->copy();

                $LOG_2_A = $scale;
            }

            $l_10 = $class->new('1.25');
            $l_10 = $l_10->_log($scale);

            $l_10   = $l_10->badd($l_2);
            $l_10   = $l_10->badd($l_2);
            $l_10   = $l_10->badd($l_2);
            $LOG_10 = $l_10->copy();

            $LOG_10_A = $scale;
        }
        $dbd-- if ( $dbd > 1 );
        $l_10 = $l_10->bmul( $class->new($dbd) );
        my $dbd_sign = '+';
        if ( $dbd < 0 ) {
            $dbd      = -$dbd;
            $dbd_sign = '-';
        }
        ( $x->{_e}, $x->{_es} ) =
          $LIB->_ssub( $x->{_e}, $x->{_es}, $LIB->_new($dbd), $dbd_sign );
    }

    $HALF = $class->new($HALF) unless ref($HALF);

    my $twos = 0;
    while ( $x->bacmp($HALF) <= 0 ) {
        $twos--;
        $x = $x->bmul($two);
    }
    while ( $x->bacmp($two) >= 0 ) {
        $twos++;
        $x = $x->bdiv( $two, $scale + 4 );
    }
    $x = $x->bround( $scale + 4 );
    if ( $twos != 0 ) {
        $LOG_2 = $class->new( $LOG_2, undef, undef ) unless ref $LOG_2;
        if ( $scale <= $LOG_2_A ) {
            $l_2 = $LOG_2->copy();
        }
        else {
            $l_2   = $two->copy();
            $l_2   = $l_2->_log($scale);
            $LOG_2 = $l_2->copy();

            $LOG_2_A = $scale;
        }
        $l_2 = $l_2->bmul($twos);
    }
    else {
        undef $l_2;
    }

    $x = $x->_log($scale);
    $x = $x->badd($l_10) if defined $l_10;
    $x = $x->badd($l_2)  if defined $l_2;

    $class->upgrade($upg);
    $class->downgrade($dng);

    $x;
}

sub _pow {
    my ( $x, $y, @r ) = @_;
    my $class = ref($x);

    $HALF = $class->new($HALF) unless ref($HALF);
    return $x->bsqrt( @r, $y ) if $y->bcmp($HALF) == 0;

    my $fallback = 0;
    my ( $scale, @params );
    ( $x, @params ) = $x->_find_round_parameters(@r);

    return $x if $x->is_nan();

    if ( scalar @params == 0 ) {
        $params[0] = $class->div_scale();
        $params[1] = undef;
        $scale     = $params[0] + 4;
        $params[2] = $r[2];
        $fallback  = 1;
    }
    else {
        $scale = abs( $params[0] || $params[1] ) + 4;
    }

    my $ab = $class->accuracy();
    my $pb = $class->precision();
    $class->accuracy(undef);
    $class->precision(undef);

    my $upg = $class->upgrade();
    my $dng = $class->downgrade();
    $class->upgrade(undef);
    $class->downgrade(undef);

    $x->{accuracy}  = undef;
    $x->{precision} = undef;

    my ( $limit, $v, $u, $below, $factor, $next, $over );

    $u = $x->copy()->blog( undef, $scale )->bmul($y);
    my $do_invert = ( $u->{sign} eq '-' );
    $u      = $u->bneg() if $do_invert;
    $v      = $class->bone();
    $factor = $class->new(2);
    $x      = $x->bone();

    $below = $v->copy();
    $over  = $u->copy();

    $limit = $class->new( "1E-" . ( $scale - 1 ) );
    while ( 3 < 5 ) {
        $next = $over->copy()->bdiv( $below, $scale );
        last if $next->bacmp($limit) <= 0;
        $x = $x->badd($next);
        $over  *= $u;
        $below *= $factor;
        $factor = $factor->binc();

        last if $x->{sign} !~ /^[-+]$/;
    }

    if ($do_invert) {
        my $x_copy = $x->copy();
        $x = $x->bone->bdiv( $x_copy, $scale );
    }

    if ( defined $params[0] ) {
        $x = $x->bround( $params[0], $params[2] );
    }
    else {
        $x = $x->bfround( $params[1], $params[2] );
    }
    if ($fallback) {
        $x->{accuracy}  = undef;
        $x->{precision} = undef;
    }

    if ( defined $ab ) {
        $class->accuracy($ab);
    }
    else {
        $class->precision($pb);
    }

    $class->upgrade($upg);
    $class->downgrade($dng);

    $x;
}

sub _e_add {
    my ( $x, $y, $xs, $ys ) = @_;
    return $LIB->_sadd( $x, $xs, $y, $ys );
}

sub _e_sub {
    my ( $x, $y, $xs, $ys ) = @_;
    return $LIB->_ssub( $x, $xs, $y, $ys );
}

1;

__END__

