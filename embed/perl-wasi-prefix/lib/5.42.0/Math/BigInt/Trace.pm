
package Math::BigInt::Trace;

use strict;
use warnings;

use Exporter;
use Math::BigInt;

our @ISA = qw(Exporter Math::BigInt);

our $VERSION = '0.67';

use overload;

our $accuracy   = undef;
our $precision  = undef;
our $round_mode = 'even';
our $div_scale  = 40;

sub new {
    my $proto = shift;
    my $class = ref($proto) || $proto;

    my $value = shift;

    my $a = $accuracy;
    $a = $_[0] if defined $_[0];

    my $p = $precision;
    $p = $_[1] if defined $_[1];

    my $self = $class->SUPER::new( $value, $a, $p, $round_mode );

    printf "Math::BigInt new '%s' => '%s' (%s)\n", $value, $self, ref($self);

    return $self;
}

sub import {
    my $class = shift;

    printf "%s -> import(%s)\n", $class, join( ", ", @_ );

    my $constant = grep { $_ eq ':constant' } @_;
    my @a        = grep { $_ ne ':constant' } @_;

    if ($constant) {
        overload::constant

          integer => sub {
            $class->new(shift);
          },

          float => sub {
            $class->new(shift);
          },

          binary => sub {
            return $class->from_oct( $_[0] ) if $_[0] =~ /^0_*[0-7]/;
            $class->new(shift);
          };
    }

    $class->SUPER::import(@a);

}

1;
