package Math::BigInt::Calc;

use 5.006001;
use strict;
use warnings;

use Carp qw< carp croak >;
use Math::BigInt::Lib;

our $VERSION = '2.005002';
$VERSION =~ tr/_//d;

our @ISA = ('Math::BigInt::Lib');

my $MAX_EXP_F;
my $MAX_EXP_I;

my $MAX_BITS;

my $BASE_LEN;
my $USE_INT;

my $BASE;
my $MAX_VAL;

my $AND_BITS;
my $OR_BITS;
my $XOR_BITS;

my $AND_MASK;
my $OR_MASK;
my $XOR_MASK;

sub config {
    my $self = shift;

    croak "Missing input argument" unless @_;

    if ( @_ == 1 ) {
        my $param = shift;
        croak "Parameter name must be a non-empty string"
          unless defined $param && length $param;
        return $BASE_LEN if $param eq 'base_len';
        return $USE_INT  if $param eq 'use_int';
        croak "Unknown parameter '$param'";
    }

    my $opts;
    while (@_) {
        my $param = shift;
        croak "Parameter name must be a non-empty string"
          unless defined $param && length $param;
        croak "Missing value for parameter '$param'"
          unless @_;
        my $value = shift;

        if ( $param eq 'base_len' || $param eq 'use_int' ) {
            $opts->{$param} = $value;
            next;
        }

        croak "Unknown parameter '$param'";
    }

    $BASE_LEN = $opts->{base_len} if exists $opts->{base_len};
    $USE_INT  = $opts->{use_int}  if exists $opts->{use_int};
    __PACKAGE__->_base_len( $BASE_LEN, $USE_INT );

    return $self;
}

sub _base_len {
    shift;

    if (@_) {
        my ( $base_len, $use_int ) = @_;

        croak "The base length must be a positive integer"
          unless defined($base_len)
          && $base_len == int($base_len)
          && $base_len > 0;

        if ( $use_int && ( $base_len > $MAX_EXP_I )
            || !$use_int && ( $base_len > $MAX_EXP_F ) )
        {
            croak "The maximum base length (exponent) is $MAX_EXP_I with",
              " 'use integer' and $MAX_EXP_F without 'use integer'. The",
              " requested settings, a base length of $base_len ",
              $use_int ? "with" : "without", " 'use integer', is invalid.";
        }

        $BASE_LEN = $base_len;
        $BASE     = 0 + ( "1" . ( "0" x $BASE_LEN ) );
        $MAX_VAL  = $BASE - 1;
        $USE_INT  = $use_int ? 1 : 0;

        {
            no warnings "redefine";
            if ($use_int) {
                *_mul = \&_mul_use_int;
                *_div = \&_div_use_int;
            }
            else {
                *_mul = \&_mul_no_int;
                *_div = \&_div_no_int;
            }
        }
    }

    my $umax = ~0;
    my $tmp  = $umax < $BASE ? $umax : $BASE;

    $MAX_BITS = 0;
    while ( $tmp >>= 1 ) {
        $MAX_BITS++;
    }

    $MAX_BITS = 32 if $MAX_BITS > 32;

    for ( $AND_BITS = $MAX_BITS ; $AND_BITS > 0 ; $AND_BITS-- ) {
        my $x = CORE::oct( '0b' . '1' x $AND_BITS );
        my $y = $x & $x;
        my $z = 2 * ( 2**( $AND_BITS - 1 ) ) + 1;
        last unless $AND_BITS < $MAX_BITS && $x == $z && $y == $x;
    }

    for ( $XOR_BITS = $MAX_BITS ; $XOR_BITS > 0 ; $XOR_BITS-- ) {
        my $x = CORE::oct( '0b' . '1' x $XOR_BITS );
        my $y = $x ^ $x;
        my $z = 2 * ( 2**( $XOR_BITS - 1 ) ) + 1;
        last unless $XOR_BITS < $MAX_BITS && $x == $z && $y == $x;
    }

    for ( $OR_BITS = $MAX_BITS ; $OR_BITS > 0 ; $OR_BITS-- ) {
        my $x = CORE::oct( '0b' . '1' x $OR_BITS );
        my $y = $x | $x;
        my $z = 2 * ( 2**( $OR_BITS - 1 ) ) + 1;
        last unless $OR_BITS < $MAX_BITS && $x == $z && $y == $x;
    }

    $AND_MASK = __PACKAGE__->_new( ( 2**$AND_BITS ) );
    $XOR_MASK = __PACKAGE__->_new( ( 2**$XOR_BITS ) );
    $OR_MASK  = __PACKAGE__->_new( ( 2**$OR_BITS ) );

    return $BASE_LEN unless wantarray;
    return (
        $BASE_LEN,  $BASE,      $AND_BITS, $XOR_BITS,
        $OR_BITS,   $BASE_LEN,  $MAX_VAL,  $MAX_BITS,
        $MAX_EXP_F, $MAX_EXP_I, $USE_INT
    );
}

sub _new {

    my ( $class, $str ) = @_;

    my $input_len = length($str) - 1;

    return bless [$str], $class if $input_len < $BASE_LEN;

    my $format = "a" . ( ( $input_len % $BASE_LEN ) + 1 );
    $format .=
      $] < 5.008
      ? "a$BASE_LEN" x int( $input_len / $BASE_LEN )
      : "(a$BASE_LEN)*";

    my $self = [ reverse( map { 0 + $_ } unpack( $format, $str ) ) ];
    return bless $self, $class;
}

BEGIN {

    for ( $MAX_EXP_F = 1 ; ; $MAX_EXP_F++ ) {
        my $MAX_EXP_FM1 = $MAX_EXP_F - 1;
        my $bs          = "1" . ( "0" x $MAX_EXP_F );
        my $xs          = "9" x $MAX_EXP_F;
        my $cs          = ( "9" x $MAX_EXP_FM1 ) . "8";
        my $ys          = $cs . ( "0" x $MAX_EXP_FM1 ) . "1";

        my $yn = $xs * $xs;
        last if $yn != $ys;

        my $rn = $yn % $bs;
        last if $rn != 1;

        my $cn = ( $yn - $rn ) / $bs;
        last if $cn != $cs;

        my $zs = $cs . ( "9" x $MAX_EXP_F );
        my $zn = $yn + $cn;
        last if $zn != $zs;
        last if $zn - ( $zn - 1 ) != 1;
    }
    $MAX_EXP_F--;

    my $umax = ~0;
    for (
        $MAX_EXP_I = int( 0.5 * log($umax) / log(10) ) ;
        $MAX_EXP_I > 0 ;
        $MAX_EXP_I--
      )
    {
        my $MAX_EXP_IM1 = $MAX_EXP_I - 1;
        my $bs          = "1" . ( "0" x $MAX_EXP_I );
        my $xs          = "9" x $MAX_EXP_I;
        my $cs          = ( "9" x $MAX_EXP_IM1 ) . "8";
        my $ys          = $cs . ( "0" x $MAX_EXP_IM1 ) . "1";

        my $yn = $xs * $xs;
        next if $yn != $ys;

        my $rn = $yn % $bs;
        next if $rn != 1;

        my $cn = ( $yn - $rn ) / $bs;
        next if $cn != $cs;

        my $zs = $cs . ( "9" x $MAX_EXP_I );
        my $zn = $yn + $cn;
        next if $zn != $zs;
        next if $zn - ( $zn - 1 ) != 1;
        last;
    }

    ( $BASE_LEN, $USE_INT ) =
      $MAX_EXP_F > $MAX_EXP_I ? ( $MAX_EXP_F, 0 ) : ( $MAX_EXP_I, 1 );

    __PACKAGE__->_base_len( $BASE_LEN, $USE_INT );
}

sub _zero {
    my $class = shift;
    return bless [0], $class;
}

sub _one {
    my $class = shift;
    return bless [1], $class;
}

sub _two {
    my $class = shift;
    return bless [2], $class;
}

sub _ten {
    my $class = shift;
    my $self  = $BASE_LEN == 1 ? [ 0, 1 ] : [10];
    bless $self, $class;
}

sub _1ex {
    my $class = shift;

    my $rem = $_[0] % $BASE_LEN;
    my $div = ( $_[0] - $rem ) / $BASE_LEN;

    bless [ (0) x $div, 0 + ( "1" . ( "0" x $rem ) ) ], $class;
}

sub _copy {
    my $class = shift;
    return bless [ @{ $_[0] } ], $class;
}

sub import {
    my $self = shift;

    my $opts;
    my ( $base_len, $use_int );
    while (@_) {
        my $param = shift;
        croak "Parameter name must be a non-empty string"
          unless defined $param && length $param;
        croak "Missing value for parameter '$param'"
          unless @_;
        my $value = shift;

        if ( $param eq 'base_len' || $param eq 'use_int' ) {
            $opts->{$param} = $value;
            next;
        }

        croak "Unknown parameter '$param'";
    }

    $base_len = exists $opts->{base_len} ? $opts->{base_len} : $BASE_LEN;
    $use_int  = exists $opts->{use_int}  ? $opts->{use_int}  : $USE_INT;
    __PACKAGE__->_base_len( $base_len, $use_int );

    return $self;
}

sub _str {

    my $ary = $_[1];
    my $idx = $#$ary;

    if ( $idx < 0 ) {
        croak("$_[1] has no elements");
    }

    my $ret = int( $ary->[$idx] );
    if ( $idx > 0 ) {
        my $z = '0' x ( $BASE_LEN - 1 );
        while ( --$idx >= 0 ) {
            $ret .= substr( $z . $ary->[$idx], -$BASE_LEN );
        }
    }
    $ret;
}

sub _num {
    my $x = $_[1];

    return $x->[0] if @$x == 1;

    my $num = 0;
    for ( my $i = $#$x ; $i >= 0 ; --$i ) {
        $num *= $BASE;
        $num += $x->[$i];
    }
    return $num;
}

sub _add {

    my ( $c, $x, $y ) = @_;

    return $x if @$y == 1 && $y->[0] == 0;

    if ( @$x == 1 && $x->[0] == 0 ) {
        @$x = @$y;
        return $x;
    }

    my $car = 0;
    my $j   = 0;
    for my $i (@$y) {
        $x->[$j] -= $BASE
          if $car = ( ( $x->[$j] += $i + $car ) >= $BASE ) ? 1 : 0;
        $j++;
    }
    while ( $car != 0 ) {
        $x->[$j] -= $BASE if $car = ( ( $x->[$j] += $car ) >= $BASE ) ? 1 : 0;
        $j++;
    }
    $x;
}

sub _inc {
    my ( $c, $x ) = @_;

    for my $i (@$x) {
        return $x if ( $i += 1 ) < $BASE;
        $i = 0;
    }
    push @$x, 1 if $x->[-1] == 0;
    $x;
}

sub _dec {
    my ( $c, $x ) = @_;

    my $MAX = $BASE - 1;
    for my $i (@$x) {
        last if ( $i -= 1 ) >= 0;
        $i = $MAX;
    }
    pop @$x if $x->[-1] == 0 && @$x > 1;
    $x;
}

sub _sub {
    my ( $c, $sx, $sy, $s ) = @_;

    my $car = 0;
    my $j   = 0;
    if ( !$s ) {
        for my $i (@$sx) {
            last unless defined $sy->[$j] || $car;
            $i += $BASE if $car = ( ( $i -= ( $sy->[$j] || 0 ) + $car ) < 0 );
            $j++;
        }
        return __strip_zeros($sx);
    }
    for my $i (@$sx) {
        $sy->[$j] += $BASE
          if $car = ( $sy->[$j] = $i - ( $sy->[$j] || 0 ) - $car ) < 0;
        $j++;
    }
    __strip_zeros($sy);
}

sub _mul_use_int {
    my ( $c, $xv, $yv ) = @_;
    use integer;

    if ( @$yv == 1 ) {
        if ( @$xv == 1 ) {
            if ( ( $xv->[0] *= $yv->[0] ) >= $BASE ) {
                $xv->[0] =
                  $xv->[0] - ( $xv->[1] = $xv->[0] / $BASE ) * $BASE;
            }
            return $xv;
        }
        if ( $yv->[0] == 0 ) {
            @$xv = (0);
            return $xv;
        }

        my $y   = $yv->[0];
        my $car = 0;
        foreach my $i (@$xv) {
            $i = $i * $y + $car;
            $i -= ( $car = $i / $BASE ) * $BASE;
        }
        push @$xv, $car if $car != 0;
        return $xv;
    }

    return $xv if @$xv == 1 && $xv->[0] == 0;

    $yv = $c->_copy($xv) if $xv == $yv;

    my @prod = ();
    my ( $prod, $car, $cty );
    for my $xi (@$xv) {
        $car = 0;
        $cty = 0;
        $xi = ( shift(@prod) || 0 ), next if $xi == 0;
        for my $yi (@$yv) {
            $prod = $xi * $yi + ( $prod[$cty] || 0 ) + $car;
            $prod[ $cty++ ] = $prod - ( $car = $prod / $BASE ) * $BASE;
        }
        $prod[$cty] += $car if $car;
        $xi = shift(@prod) || 0;
    }
    push @$xv, @prod;
    $xv;
}

sub _mul_no_int {
    my ( $c, $xv, $yv ) = @_;

    if ( @$yv == 1 ) {
        if ( @$xv == 1 ) {
            if ( ( $xv->[0] *= $yv->[0] ) >= $BASE ) {
                my $rem = $xv->[0] % $BASE;
                $xv->[1] = ( $xv->[0] - $rem ) / $BASE;
                $xv->[0] = $rem;
            }
            return $xv;
        }
        if ( $yv->[0] == 0 ) {
            @$xv = (0);
            return $xv;
        }

        my $y   = $yv->[0];
        my $car = 0;
        my $rem;
        foreach my $i (@$xv) {
            $i   = $i * $y + $car;
            $rem = $i % $BASE;
            $car = ( $i - $rem ) / $BASE;
            $i   = $rem;
        }
        push @$xv, $car if $car != 0;
        return $xv;
    }

    return $xv if @$xv == 1 && $xv->[0] == 0;

    $yv = $c->_copy($xv) if $xv == $yv;

    my @prod = ();
    my ( $prod, $rem, $car, $cty );
    for my $xi (@$xv) {
        $car = 0;
        $cty = 0;
        $xi = ( shift(@prod) || 0 ), next if $xi == 0;
        for my $yi (@$yv) {
            $prod           = $xi * $yi + ( $prod[$cty] || 0 ) + $car;
            $rem            = $prod % $BASE;
            $car            = ( $prod - $rem ) / $BASE;
            $prod[ $cty++ ] = $rem;
        }
        $prod[$cty] += $car if $car;
        $xi = shift(@prod) || 0;
    }
    push @$xv, @prod;
    $xv;
}

sub _div_use_int {

    use integer;

    my ( $c, $x, $yorg ) = @_;

    if ( @$x == 1 && @$yorg == 1 ) {
        if (wantarray) {
            my $rem = [ $x->[0] % $yorg->[0] ];
            bless $rem, $c;
            $x->[0] = $x->[0] / $yorg->[0];
            return ( $x, $rem );
        }
        else {
            $x->[0] = $x->[0] / $yorg->[0];
            return $x;
        }
    }

    if ( @$yorg == 1 ) {
        my $rem;
        $rem = $c->_mod( $c->_copy($x), $yorg ) if wantarray;

        my $j = @$x;
        my $r = 0;
        my $y = $yorg->[0];
        my $b;
        while ( $j-- > 0 ) {
            $b       = $r * $BASE + $x->[$j];
            $r       = $b % $y;
            $x->[$j] = $b / $y;
        }
        pop(@$x)            if @$x > 1 && $x->[-1] == 0;
        return ( $x, $rem ) if wantarray;
        return $x;
    }

    if ( @$yorg > @$x ) {
        my $rem;
        $rem = $c->_copy($x) if wantarray;
        @$x  = 0;
        return ( $x, $rem ) if wantarray;
        return $x;
    }

    if ( @$yorg == @$x ) {
        my $cmp = 0;
        for ( my $j = $#$x ; $j >= 0 ; --$j ) {
            last if $cmp = $x->[$j] - $yorg->[$j];
        }

        if ( $cmp == 0 ) {
            @$x = 1;
            return $x, $c->_zero() if wantarray;
            return $x;
        }

        if ( $cmp < 0 ) {
            if (wantarray) {
                my $rem = $c->_copy($x);
                @$x = 0;
                return $x, $rem;
            }
            @$x = 0;
            return $x;
        }
    }

    my $y = $c->_copy($yorg);

    my $tmp;
    my $dd = $BASE / ( $y->[-1] + 1 );
    if ( $dd != 1 ) {
        my $car = 0;
        for my $xi (@$x) {
            $xi = $xi * $dd + $car;
            $xi -= ( $car = $xi / $BASE ) * $BASE;
        }
        push( @$x, $car );
        $car = 0;
        for my $yi (@$y) {
            $yi = $yi * $dd + $car;
            $yi -= ( $car = $yi / $BASE ) * $BASE;
        }
    }
    else {
        push( @$x, 0 );
    }

    my @q = ();
    my ( $v2, $v1 ) = @$y[ -2, -1 ];
    $v2 = 0 unless $v2;
    while ( $#$x > $#$y ) {
        my ( $u2, $u1, $u0 ) = @$x[ -3 .. -1 ];
        $u2 = 0 unless $u2;
        my $tmp = $u0 * $BASE + $u1;
        my $rem = $tmp % $v1;
        my $q   = $u0 == $v1 ? $MAX_VAL : ( ( $tmp - $rem ) / $v1 );
        --$q while $v2 * $q > ( $u0 * $BASE + $u1 - $q * $v1 ) * $BASE + $u2;
        if ($q) {
            my $prd;
            my ( $car, $bar ) = ( 0, 0 );
            for (
                my $yi = 0, my $xi = $#$x - $#$y - 1 ;
                $yi <= $#$y ;
                ++$yi, ++$xi
              )
            {
                $prd = $q * $y->[$yi] + $car;
                $prd -= ( $car = int( $prd / $BASE ) ) * $BASE;
                $x->[$xi] += $BASE
                  if $bar = ( ( $x->[$xi] -= $prd + $bar ) < 0 );
            }
            if ( $x->[-1] < $car + $bar ) {
                $car = 0;
                --$q;
                for (
                    my $yi = 0, my $xi = $#$x - $#$y - 1 ;
                    $yi <= $#$y ;
                    ++$yi, ++$xi
                  )
                {
                    $x->[$xi] -= $BASE
                      if $car = ( ( $x->[$xi] += $y->[$yi] + $car ) >= $BASE );
                }
            }
        }
        pop(@$x);
        unshift( @q, $q );
    }

    if (wantarray) {
        my $d = bless [], $c;
        if ( $dd != 1 ) {
            my $car = 0;
            my $prd;
            for my $xi ( reverse @$x ) {
                $prd = $car * $BASE + $xi;
                $car = $prd - ( $tmp = $prd / $dd ) * $dd;
                unshift @$d, $tmp;
            }
        }
        else {
            @$d = @$x;
        }
        @$x = @q;
        __strip_zeros($x);
        __strip_zeros($d);
        return ( $x, $d );
    }
    @$x = @q;
    __strip_zeros($x);
    $x;
}

sub _div_no_int {

    my ( $c, $x, $yorg ) = @_;

    if ( @$x == 1 && @$yorg == 1 ) {
        my $rem = [ $x->[0] % $yorg->[0] ];
        bless $rem, $c;
        $x->[0] = ( $x->[0] - $rem->[0] ) / $yorg->[0];
        return ( $x, $rem ) if wantarray;
        return $x;
    }

    if ( @$yorg == 1 ) {
        my $rem;
        $rem = $c->_mod( $c->_copy($x), $yorg ) if wantarray;

        my $j = @$x;
        my $r = 0;
        my $y = $yorg->[0];
        my $b;
        while ( $j-- > 0 ) {
            $b       = $r * $BASE + $x->[$j];
            $r       = $b % $y;
            $x->[$j] = ( $b - $r ) / $y;
        }
        pop(@$x)            if @$x > 1 && $x->[-1] == 0;
        return ( $x, $rem ) if wantarray;
        return $x;
    }

    if ( @$yorg > @$x ) {
        my $rem;
        $rem = $c->_copy($x) if wantarray;
        @$x  = 0;
        return ( $x, $rem ) if wantarray;
        return $x;
    }

    if ( @$yorg == @$x ) {
        my $cmp = 0;
        for ( my $j = $#$x ; $j >= 0 ; --$j ) {
            last if $cmp = $x->[$j] - $yorg->[$j];
        }

        if ( $cmp == 0 ) {
            @$x = 1;
            return $x, $c->_zero() if wantarray;
            return $x;
        }

        if ( $cmp < 0 ) {
            if (wantarray) {
                my $rem = $c->_copy($x);
                @$x = 0;
                return $x, $rem;
            }
            @$x = 0;
            return $x;
        }
    }

    my $y = $c->_copy($yorg);

    my $tmp = $y->[-1] + 1;
    my $rem = $BASE % $tmp;
    my $dd  = ( $BASE - $rem ) / $tmp;
    if ( $dd != 1 ) {
        my $car = 0;
        for my $xi (@$x) {
            $xi  = $xi * $dd + $car;
            $rem = $xi % $BASE;
            $car = ( $xi - $rem ) / $BASE;
            $xi  = $rem;
        }
        push( @$x, $car );
        $car = 0;
        for my $yi (@$y) {
            $yi  = $yi * $dd + $car;
            $rem = $yi % $BASE;
            $car = ( $yi - $rem ) / $BASE;
            $yi  = $rem;
        }
    }
    else {
        push( @$x, 0 );
    }

    my @q = ();
    my ( $v2, $v1 ) = @$y[ -2, -1 ];
    $v2 = 0 unless $v2;
    while ( $#$x > $#$y ) {
        my ( $u2, $u1, $u0 ) = @$x[ -3 .. -1 ];
        $u2 = 0 unless $u2;
        my $tmp = $u0 * $BASE + $u1;
        my $rem = $tmp % $v1;
        my $q   = $u0 == $v1 ? $MAX_VAL : ( ( $tmp - $rem ) / $v1 );
        --$q while $v2 * $q > ( $u0 * $BASE + $u1 - $q * $v1 ) * $BASE + $u2;
        if ($q) {
            my $prd;
            my ( $car, $bar ) = ( 0, 0 );
            for (
                my $yi = 0, my $xi = $#$x - $#$y - 1 ;
                $yi <= $#$y ;
                ++$yi, ++$xi
              )
            {
                $prd = $q * $y->[$yi] + $car;
                $rem = $prd % $BASE;
                $car = ( $prd - $rem ) / $BASE;
                $prd -= $car * $BASE;
                $x->[$xi] += $BASE
                  if $bar = ( ( $x->[$xi] -= $prd + $bar ) < 0 );
            }
            if ( $x->[-1] < $car + $bar ) {
                $car = 0;
                --$q;
                for (
                    my $yi = 0, my $xi = $#$x - $#$y - 1 ;
                    $yi <= $#$y ;
                    ++$yi, ++$xi
                  )
                {
                    $x->[$xi] -= $BASE
                      if $car = ( ( $x->[$xi] += $y->[$yi] + $car ) >= $BASE );
                }
            }
        }
        pop(@$x);
        unshift( @q, $q );
    }

    if (wantarray) {
        my $d = bless [], $c;
        if ( $dd != 1 ) {
            my $car = 0;
            my ( $prd, $rem );
            for my $xi ( reverse @$x ) {
                $prd = $car * $BASE + $xi;
                $rem = $prd % $dd;
                $tmp = ( $prd - $rem ) / $dd;
                $car = $rem;
                unshift @$d, $tmp;
            }
        }
        else {
            @$d = @$x;
        }
        @$x = @q;
        __strip_zeros($x);
        __strip_zeros($d);
        return ( $x, $d );
    }
    @$x = @q;
    __strip_zeros($x);
    $x;
}

sub _acmp {
    my ( $c, $cx, $cy ) = @_;

    return ( ( $cx->[0] <=> $cy->[0] ) <=> 0 )
      if @$cx == 1 && @$cy == 1;

    my $lxy = ( @$cx - @$cy )
      ||
      ( length( int( $cx->[-1] ) ) - length( int( $cy->[-1] ) ) );

    return -1 if $lxy < 0;
    return 1  if $lxy > 0;

    my $a;
    my $j = @$cx;
    while ( --$j >= 0 ) {
        last if $a = $cx->[$j] - $cy->[$j];
    }
    $a <=> 0;
}

sub _len {

    my $cx = $_[1];

    ( @$cx - 1 ) * $BASE_LEN + length( int( $cx->[-1] ) );
}

sub _digit {
    my ( $c, $x, $n ) = @_;

    my $len = _len( '', $x );

    $n += $len if $n < 0;

    return "0" if $n < 0 || $n >= $len;

    my $elem  = int( $n / $BASE_LEN );
    my $digit = $n % $BASE_LEN;
    substr( "0" x $BASE_LEN . "$x->[$elem]", -1 - $digit, 1 );
}

sub _zeros {

    my $x = $_[1];

    return 0 if @$x == 1 && $x->[0] == 0;

    my $zeros = 0;
    foreach my $elem (@$x) {
        if ( $elem != 0 ) {
            $elem =~ /[^0](0*)\z/;
            $zeros += length($1);
            last;
        }
        $zeros += $BASE_LEN;
    }
    $zeros;
}

sub _is_zero {
    @{ $_[1] } == 1 && $_[1]->[0] == 0 ? 1 : 0;
}

sub _is_even {
    $_[1]->[0] % 2 ? 0 : 1;
}

sub _is_odd {
    $_[1]->[0] % 2 ? 1 : 0;
}

sub _is_one {
    @{ $_[1] } == 1 && $_[1]->[0] == 1 ? 1 : 0;
}

sub _is_two {
    @{ $_[1] } == 1 && $_[1]->[0] == 2 ? 1 : 0;
}

sub _is_ten {
    if ( $BASE_LEN == 1 ) {
        @{ $_[1] } == 2 && $_[1]->[0] == 0 && $_[1]->[1] == 1 ? 1 : 0;
    }
    else {
        @{ $_[1] } == 1 && $_[1]->[0] == 10 ? 1 : 0;
    }
}

sub __strip_zeros {
    my $x = shift;

    push @$x, 0 if @$x == 0;
    return $x if @$x == 1;

    my $i = $#$x;
    while ( $i > 0 ) {
        last if $x->[$i] != 0;
        $i--;
    }
    $i++;
    splice( @$x, $i ) if $i < @$x;
    $x;
}

sub _check {
    my ( $class, $x ) = @_;

    my $msg = $class->SUPER::_check($x);
    return $msg if $msg;

    my $n;
    eval { $n = @$x };
    return "Not an array reference" unless $@ eq '';

    return "Reference to an empty array" unless $n > 0;

    for ( my $i = 0 ; $i <= $#$x ; ++$i ) {
        my $e = $x->[$i];

        return "Element at index $i is undefined"
          unless defined $e;

        return
            "Element at index $i is a '"
          . ref($e)
          . "', which is not a scalar"
          unless ref($e) eq "";

        return "Element at index $i is '$e', which does not look like an"
          . " normal integer"
          unless $e =~ /^\d+\z/;

        return "Element at index $i is '$e', which is not smaller than"
          . " the base '$BASE'"
          if $e >= $BASE;

        return "Element at index $i (last element) is zero"
          if $#$x > 0 && $i == $#$x && $e == 0;
    }

    return 0;
}

sub _mod {
    my ( $c, $x, $yo ) = @_;

    if ( @$yo > 1 ) {
        my ( $xo, $rem ) = $c->_div( $x, $yo );
        @$x = @$rem;
        return $x;
    }

    my $y = $yo->[0];

    if ( @$x == 1 ) {
        $x->[0] %= $y;
        return $x;
    }

    my $b = $BASE % $y;
    if ( $b == 0 ) {
        $x->[0] %= $y;
    }
    elsif ( $b == 1 ) {
        my $r = 0;
        foreach (@$x) {
            $r = ( $r + $_ ) % $y;

        }
        $r = 0 if $r == $y;
        $x->[0] = $r;
    }
    else {
        my $r  = 0;
        my $bm = 1;
        foreach (@$x) {
            $r  = ( $_ * $bm + $r ) % $y;
            $bm = ( $bm * $b ) % $y;

        }
        $r = 0 if $r == $y;
        $x->[0] = $r;
    }
    @$x = $x->[0];
    return $x;
}

sub _rsft {
    my ( $c, $x, $n, $b ) = @_;
    return $x if $c->_is_zero($x) || $c->_is_zero($n);

    $b = $c->_new($b) unless ref $b;

    if ( $c->_acmp( $b, $c->_ten() ) ) {
        return scalar $c->_div( $x, $c->_pow( $c->_copy($b), $n ) );
    }

    my $dst  = 0;
    my $src  = $c->_num($n);
    my $xlen = ( @$x - 1 ) * $BASE_LEN + length( int( $x->[-1] ) );
    if ( $src >= $xlen or ( $src == $xlen and !defined $x->[1] ) ) {
        splice( @$x, 1 );
        $x->[0] = 0;
        return $x;
    }
    my $rem = $src % $BASE_LEN;
    $src = int( $src / $BASE_LEN );
    if ( $rem == 0 ) {
        splice( @$x, 0, $src );
    }
    else {
        my $len = @$x - $src;
        my $vd;
        my $z = '0' x $BASE_LEN;
        $x->[@$x] = 0;
        while ( $dst < $len ) {
            $vd = $z . $x->[$src];
            $vd = substr( $vd, -$BASE_LEN, $BASE_LEN - $rem );
            $src++;
            $vd = substr( $z . $x->[$src], -$rem,      $rem ) . $vd;
            $vd = substr( $vd,             -$BASE_LEN, $BASE_LEN )
              if length($vd) > $BASE_LEN;
            $x->[$dst] = int($vd);
            $dst++;
        }
        splice( @$x, $dst ) if $dst > 0;
        pop(@$x)            if $x->[-1] == 0 && @$x > 1;
    }
    $x;
}

sub _lsft {
    my ( $c, $x, $n, $b ) = @_;

    return $x if $c->_is_zero($x) || $c->_is_zero($n);

    $b = $c->_new($b) unless ref $b;

    my $bstr = $c->_str($b);
    if ( $bstr =~ /^1(0+)\z/ ) {

        my $log10b = length($1);
        $n = $c->_mul( $c->_new($log10b), $n );
        $n = $c->_num($n);

        my $r = $n % $BASE_LEN;
        my $q = ( $n - $r ) / $BASE_LEN;

        if ($r) {
            my $i = @$x;
            $x->[$i] = 0;
            my $z = '0' x $BASE_LEN;
            my $vd;
            while ( $i >= 0 ) {
                $vd = $x->[$i];
                $vd = $z . $vd;
                $vd = substr( $vd, $r - $BASE_LEN, $BASE_LEN - $r );
                $vd .=
                  $i > 0
                  ? substr( $z . $x->[ $i - 1 ], -$BASE_LEN, $r )
                  : '0' x $r;
                $vd = substr( $vd, -$BASE_LEN, $BASE_LEN )
                  if length($vd) > $BASE_LEN;
                $x->[$i] = int($vd);
                $i--;
            }

            pop(@$x) if $x->[-1] == 0;
        }

        if ($q) {
            unshift @$x, (0) x $q;
        }

    }
    else {
        $x = $c->_mul( $x, $c->_pow( $b, $n ) );
    }

    return $x;
}

sub _pow {
    my ( $c, $cx, $cy ) = @_;

    if ( @$cy == 1 && $cy->[0] == 0 ) {
        splice( @$cx, 1 );
        $cx->[0] = 1;
        return $cx;
    }

    if (   ( @$cx == 1 && $cx->[0] == 1 )
        || ( @$cy == 1 && $cy->[0] == 1 ) )
    {
        return $cx;
    }

    if ( @$cx == 1 && $cx->[0] == 0 ) {
        splice( @$cx, 1 );
        $cx->[0] = 0;
        return $cx;
    }

    my $pow2 = $c->_one();

    my $y_bin = $c->_as_bin($cy);
    $y_bin =~ s/^0b//;
    my $len = length($y_bin);
    while ( --$len > 0 ) {
        $c->_mul( $pow2, $cx ) if substr( $y_bin, $len, 1 ) eq '1';
        $c->_mul( $cx,   $cx );
    }

    $c->_mul( $cx, $pow2 );
    $cx;
}

sub _nok {

    my ( $c, $n, $k ) = @_;

    {
        my $twok = $c->_mul( $c->_two(), $c->_copy($k) );
        if ( $c->_acmp( $twok, $n ) > 0 ) {
            $k = $c->_sub( $c->_copy($n), $k );
        }
    }

    if ( $c->_is_zero($k) ) {
        @$n = 1;
    }
    else {

        my $n_orig = $c->_copy($n);

        $c->_sub( $n, $k );
        $c->_inc($n);

        my $f = $c->_copy($n);
        $c->_inc($f);

        my $d = $c->_two();

        while ( $c->_acmp( $f, $n_orig ) <= 0 ) {

            $c->_mul( $n, $f );
            $c->_div( $n, $d );

            $c->_inc($f);
            $c->_inc($d);
        }

    }

    return $n;
}

sub _fac {
    my ( $c, $cx ) = @_;

    if ( @$cx == 1 ) {
        my @factorials = (
            '1',   '1',    '2',     '6', '24', '120',
            '720', '5040', '40320', '362880',
        );
        if ( $cx->[0] <= $#factorials ) {
            my $tmp = $c->_new( $factorials[ $cx->[0] ] );
            @$cx = @$tmp;
            return $cx;
        }
    }

    if ( $BASE_LEN <= 2 ) {
        my $tmp = $c->SUPER::_fac($cx);
        @$cx = @$tmp;
        return $cx;
    }

    if (   ( @$cx == 1 )
        && ( $cx->[0] >= 12 && $cx->[0] < 7000 ) )
    {

        my $zero_elements = 0;

        my $k    = $c->_num($cx);
        my $even = 1;
        if ( ( $k & 1 ) == 0 ) {
            $even = $k;
            $k--;
        }
        $k = ( $k + 1 ) / 2;
        my $k2  = $k * $k;
        my $odd = 1;
        my $sum = 1;
        my $i   = $k - 1;
        my $new_x = $c->_new( $k * $even );
        @$cx = @$new_x;

        if ( $cx->[0] == 0 ) {
            $zero_elements++;
            shift @$cx;
        }
        my $BASE2 = int( sqrt($BASE) ) - 1;
        my $j     = 1;
        while ( $j <= $i ) {
            my $m = ( $k2 - $sum );
            $odd += 2;
            $sum += $odd;
            $j++;
            while ( $j <= $i && ( $m < $BASE2 ) && ( ( $k2 - $sum ) < $BASE2 ) )
            {
                $m   *= ( $k2 - $sum );
                $odd += 2;
                $sum += $odd;
                $j++;
            }
            if ( $m < $BASE ) {
                $c->_mul( $cx, [$m] );
            }
            else {
                $c->_mul( $cx, $c->_new($m) );
            }
            if ( $cx->[0] == 0 ) {
                $zero_elements++;
                shift @$cx;
            }
        }
        unshift @$cx, (0) x $zero_elements;
        return $cx;
    }

    my $steps = 100;
    $steps = $cx->[0] if @$cx == 1;
    my $r    = 2;
    my $cf   = 3;
    my $step = 2;
    my $last = $r;
    while ( $r * $cf < $BASE && $step < $steps ) {
        $last = $r;
        $r *= $cf++;
        $step++;
    }
    if ( ( @$cx == 1 ) && $step == $cx->[0] ) {
        $cx->[0] = $r;
        return $cx;
    }

    my $n;
    if ( @$cx == 1 ) {
        $n = $cx->[0];
    }
    else {
        $n = $c->_copy($cx);
    }

    $cx->[0] = $last;
    splice( @$cx, 1 );
    my $zero_elements = 0;

    if ( ref $n eq 'ARRAY' ) {
        my $base_2 = int( sqrt($BASE) ) - 1;
        while ( $step < $base_2 ) {
            if ( $cx->[0] == 0 ) {
                $zero_elements++;
                shift @$cx;
            }
            my $b = $step * ( $step + 1 );
            $step += 2;
            $c->_mul( $cx, [$b] );
        }
        $step = [$step];
        while ( $c->_acmp( $step, $n ) <= 0 ) {
            if ( $cx->[0] == 0 ) {
                $zero_elements++;
                shift @$cx;
            }
            $c->_mul( $cx, $step );
            $c->_inc($step);
        }
    }
    else {

        my $base_4 = int( sqrt( sqrt($BASE) ) ) - 2;
        my $n4 = $n - 4;
        while ( $step < $n4 && $step < $base_4 ) {
            if ( $cx->[0] == 0 ) {
                $zero_elements++;
                shift @$cx;
            }
            my $b = $step * ( $step + 1 );
            $step += 2;
            $b    *= $step * ( $step + 1 );
            $step += 2;
            $c->_mul( $cx, [$b] );
        }
        my $base_2 = int( sqrt($BASE) ) - 1;
        my $n2     = $n - 2;
        while ( $step < $n2 && $step < $base_2 ) {
            if ( $cx->[0] == 0 ) {
                $zero_elements++;
                shift @$cx;
            }
            my $b = $step * ( $step + 1 );
            $step += 2;
            $c->_mul( $cx, [$b] );
        }
        while ( $step <= $n ) {
            $c->_mul( $cx, [$step] );
            $step++;
            if ( $cx->[0] == 0 ) {
                $zero_elements++;
                shift @$cx;
            }
        }
    }
    unshift @$cx, (0) x $zero_elements;
    $cx;
}

sub _log_int {
    my ( $c, $x, $base ) = @_;

    return if @$x == 1 && $x->[0] == 0;

    return if @$base == 1 && $base->[0] < 2;

    if ( @$x == 1 && $x->[0] == 1 ) {
        @$x = 0;
        return $x, 1;
    }

    my $cmp = $c->_acmp( $x, $base );

    if ( $cmp == 0 ) {
        @$x = 1;
        return $x, 1;
    }

    if ( $cmp < 0 ) {
        @$x = 0;
        return $x, 0;
    }

    my $x_org = $c->_copy($x);

    my $len = $c->_len($x_org);
    my $log = log( $base->[-1] ) / log(10);

    $log += ( @$base - 1 ) * $BASE_LEN;

    my $res = $c->_new( int( $len / $log ) );

    @$x = @$res;
    my $trial = $c->_pow( $c->_copy($base), $x );
    my $acmp  = $c->_acmp( $trial, $x_org );

    return $x, 1 if $acmp == 0;

    while ( $acmp < 0 ) {
        $c->_mul( $trial, $base );
        $c->_inc($x);
        $acmp = $c->_acmp( $trial, $x_org );
    }

    while ( $acmp > 0 ) {
        $c->_div( $trial, $base );
        $c->_dec($x);
        $acmp = $c->_acmp( $trial, $x_org );
    }

    return $x, 1 if $acmp == 0;
    return $x, 0;
}

sub _ilog2 {

    my ( $c, $x ) = @_;
    ( $x, my $is_exact ) = $c->_log_int( $x, $c->_two() );
    return wantarray ? ( $x, $is_exact ) : $x;
}

sub _ilog10 {

    my ( $c, $x ) = @_;

    return if @$x == 1 && $x->[0] == 0;

    if ( @$x == 1 && $x->[0] == 1 ) {
        @$x = 0;
        return wantarray ? ( $x, 1 ) : $x;
    }

    my $x_orig = $c->_copy($x);
    my $nm1    = $c->_len($x) - 1;

    my $xtmp = $c->_new($nm1);
    @$x = @$xtmp;

    return $x unless wantarray;

    my $is_pow10 = 1;
    for my $i ( 0 .. $#$x_orig - 1 ) {
        last unless $is_pow10 = $x_orig->[$i] == 0;
    }
    $is_pow10 &&=
      $x_orig->[-1] == 10**int( 0.5 + log( $x_orig->[-1] ) / log(10) );

    return wantarray ? ( $x, 1 ) : $x if $is_pow10;
    return wantarray ? ( $x, 0 ) : $x;
}

sub _clog2 {

    my ( $c, $x ) = @_;

    return if @$x == 1 && $x->[0] == 0;

    if ( @$x == 1 && $x->[0] == 1 ) {
        @$x = 0;
        return wantarray ? ( $x, 1 ) : $x;
    }

    my $base = $c->_two();
    my $acmp = $c->_acmp( $x, $base );

    if ( $acmp == 0 ) {
        @$x = 1;
        return wantarray ? ( $x, 1 ) : $x;
    }

    if ( $acmp < 0 ) {
        @$x = 0;
        return wantarray ? ( $x, 0 ) : $x;
    }

    my $len    = $c->_len($x);
    my $log    = log(2) / log(10);
    my $guess  = $c->_new( int( $len / $log ) );
    my $x_orig = $c->_copy($x);
    @$x = @$guess;

    my $trial = $c->_pow( $c->_copy($base), $x );
    $acmp = $c->_acmp( $trial, $x_orig );

    while ( $acmp > 0 ) {
        $c->_div( $trial, $base );
        $c->_dec($x);
        $acmp = $c->_acmp( $trial, $x_orig );
    }

    while ( $acmp < 0 ) {
        $c->_mul( $trial, $base );
        $c->_inc($x);
        $acmp = $c->_acmp( $trial, $x_orig );
    }

    return wantarray ? ( $x, 1 ) : $x if $acmp == 0;
    return wantarray ? ( $x, 0 ) : $x;
}

sub _clog10 {
    my ( $c, $x ) = @_;

    return if @$x == 1 && $x->[0] == 0;

    if ( @$x == 1 && $x->[0] == 1 ) {
        @$x = 0;
        return wantarray ? ( $x, 1 ) : $x;
    }

    my $n = $c->_len($x);

    my $is_pow10 = 1;
    for my $i ( 0 .. $#$x - 1 ) {
        last unless $is_pow10 = $x->[$i] == 0;
    }
    $is_pow10 &&= $x->[-1] == 10**int( 0.5 + log( $x->[-1] ) / log(10) );

    $n-- if $is_pow10;

    my $xtmp = $c->_new($n);
    @$x = @$xtmp;

    return wantarray ? ( $x, 1 ) : $x if $is_pow10;
    return wantarray ? ( $x, 0 ) : $x;
}

use constant DEBUG => 0;
my $steps = 0;
sub steps { $steps }

sub _sqrt {

    my ( $c, $x ) = @_;

    if ( @$x == 1 ) {
        $x->[0] = int( sqrt( $x->[0] ) );
        return $x;
    }

    my $s;
    if ( @$x % 2 ) {
        $s = [ (0) x ( ( @$x - 1 ) / 2 ), int( sqrt( $x->[-1] ) ) ];
    }
    else {
        $s = [
            (0) x ( ( @$x - 2 ) / 2 ),
            int( sqrt( $x->[-2] + $x->[-1] * $BASE ) )
        ];
    }

    my $cmp;
    while (1) {
        my $sq = $c->_mul( $c->_copy($s), $s );
        $cmp = $c->_acmp( $sq, $x );

        if ( $cmp > 0 ) {
            my $num   = $c->_sub( $c->_copy($sq), $x );
            my $den   = $c->_mul( $c->_two(), $s );
            my $delta = $c->_div( $num, $den );
            last if $c->_is_zero($delta);
            $s = $c->_sub( $s, $delta );
        }

        elsif ( $cmp < 0 ) {
            my $num   = $c->_sub( $c->_copy($x), $sq );
            my $den   = $c->_mul( $c->_two(), $s );
            my $delta = $c->_div( $num, $den );
            last if $c->_is_zero($delta);
            $s = $c->_add( $s, $delta );
        }

        else {
            last;
        }
    }

    $s  = $c->_dec($s) if $cmp > 0;
    @$x = @$s;
    return $x;
}

sub _root {

    my ( $c, $x, $n ) = @_;

    if ( @$x == 1 ) {
        return $x if $x->[0] == 0 || $x->[0] == 1;

        if ( @$n == 1 ) {
            my $y   = int( $x->[0]**( 1 / $n->[0] ) );
            my $yp1 = $y + 1;
            $y = $yp1 if $yp1**$n->[0] == $x->[0];
            $x->[0] = $y;
            return $x;
        }
    }

    if ( ( @$x > 1 || $x->[0] > 0 )
        && $c->_acmp( $x, $n ) <= 0 )
    {
        my $one = $c->_one();
        @$x = @$one;
        return $x;
    }

    my $b = $c->_as_bin($n);
    if ( $b =~ /0b1(0+)$/ ) {
        my $count = length($1);
        my $cnt   = $count;
        unshift @$x, 0;

        while ( $cnt-- > 0 ) {
            unshift @$x, 0;
            $c->_sqrt($x);
        }

        shift @$x;

        return $x;
    }

    my $DEBUG = 0;

    my $x_str = $c->_str($x);
    my $xm    = "." . $x_str;
    my $xe    = length($x_str);

    my $log10x = log($xm) / log(10) + $xe;
    my $log10y = $log10x / $c->_num($n);

    my $ye = int $log10y;
    my $ym = 10**( $log10y - $ye );

    if ($DEBUG) {
        print "\n";
        print "xm     = $xm\n";
        print "xe     = $xe\n";
        print "log10x = $log10x\n";
        print "log10y = $log10y\n";
        print "ym     = $ym\n";
        print "ye     = $ye\n";
        print "\n";
    }

    my $d = $ye < 15 ? $ye : 15;
    $ym *= 10**$d;
    $ye -= $d;

    my $y_str = sprintf( '%.0f', $ym ) . "0" x $ye;
    my $y     = $c->_new($y_str);

    if ($DEBUG) {
        print "ym     = $ym\n";
        print "ye     = $ye\n";
        print "\n";
        print "y_str  = $y_str (initial guess)\n";
        print "\n";
    }

    my $trial = $c->_pow( $c->_copy($y), $n );
    my $acmp  = $c->_acmp( $trial, $x );

    if ( $acmp == 0 ) {
        @$x = @$y;
        return $x;
    }

    my $lower;
    my $upper;

    my $delta = $c->_new( "1" . ( "0" x $ye ) );
    my $two   = $c->_two();

    if ( $acmp < 0 ) {
        $lower = $y;
        while ( $acmp < 0 ) {
            $upper = $c->_add( $c->_copy($lower), $delta );

            if ($DEBUG) {
                print "lower  = $lower\n";
                print "upper  = $upper\n";
                print "delta  = $delta\n";
                print "\n";
            }
            $acmp = $c->_acmp( $c->_pow( $c->_copy($upper), $n ), $x );
            if ( $acmp == 0 ) {
                @$x = @$upper;
                return $x;
            }
            $delta = $c->_mul( $delta, $two );
        }
    }

    elsif ( $acmp > 0 ) {
        $upper = $y;
        while ( $acmp > 0 ) {
            if ( $c->_acmp( $upper, $delta ) <= 0 ) {
                $lower = $c->_zero();
                last;
            }
            $lower = $c->_sub( $c->_copy($upper), $delta );

            if ($DEBUG) {
                print "lower  = $lower\n";
                print "upper  = $upper\n";
                print "delta  = $delta\n";
                print "\n";
            }
            $acmp = $c->_acmp( $c->_pow( $c->_copy($lower), $n ), $x );
            if ( $acmp == 0 ) {
                @$x = @$lower;
                return $x;
            }
            $delta = $c->_mul( $delta, $two );
        }
    }

    my $one = $c->_one();
    {

        $delta = $c->_sub( $c->_copy($upper), $lower );
        if ( $c->_acmp( $delta, $one ) <= 0 ) {
            @$x = @$lower;
            return $x;
        }

        if ($DEBUG) {
            print "lower  = $lower\n";
            print "upper  = $upper\n";
            print "delta   = $delta\n";
            print "\n";
        }

        $delta = $c->_div( $delta, $two );
        my $middle = $c->_add( $c->_copy($lower), $delta );

        $acmp = $c->_acmp( $c->_pow( $c->_copy($middle), $n ), $x );
        if ( $acmp < 0 ) {
            $lower = $middle;
        }
        elsif ( $acmp > 0 ) {
            $upper = $middle;
        }
        else {
            @$x = @$middle;
            return $x;
        }

        redo;
    }

    $x;
}

sub _and {
    my ( $c, $x, $y ) = @_;

    return $x if $c->_acmp( $x, $y ) == 0;

    my $m = $c->_one();
    my ( $xr, $yr );
    my $mask = $AND_MASK;

    my $x1 = $c->_copy($x);
    my $y1 = $c->_copy($y);
    my $z  = $c->_zero();

    use integer;
    until ( $c->_is_zero($x1) || $c->_is_zero($y1) ) {
        ( $x1, $xr ) = $c->_div( $x1, $mask );
        ( $y1, $yr ) = $c->_div( $y1, $mask );

        $c->_add( $z, $c->_mul( [ 0 + $xr->[0] & 0 + $yr->[0] ], $m ) );
        $c->_mul( $m, $mask );
    }

    @$x = @$z;
    return $x;
}

sub _xor {
    my ( $c, $x, $y ) = @_;

    return $c->_zero() if $c->_acmp( $x, $y ) == 0;

    my $m = $c->_one();
    my ( $xr, $yr );
    my $mask = $XOR_MASK;

    my $x1 = $c->_copy($x);
    my $y1 = $c->_copy($y);
    my $z  = $c->_zero();

    use integer;
    until ( $c->_is_zero($x1) || $c->_is_zero($y1) ) {
        ( $x1, $xr ) = $c->_div( $x1, $mask );
        ( $y1, $yr ) = $c->_div( $y1, $mask );

        $c->_add( $z, $c->_mul( [ 0 + $xr->[0] ^ 0 + $yr->[0] ], $m ) );
        $c->_mul( $m, $mask );
    }
    $c->_add( $z, $c->_mul( $x1, $m ) ) if !$c->_is_zero($x1);
    $c->_add( $z, $c->_mul( $y1, $m ) ) if !$c->_is_zero($y1);

    @$x = @$z;
    return $x;
}

sub _or {
    my ( $c, $x, $y ) = @_;

    return $x if $c->_acmp( $x, $y ) == 0;

    my $m = $c->_one();
    my ( $xr, $yr );
    my $mask = $OR_MASK;

    my $x1 = $c->_copy($x);
    my $y1 = $c->_copy($y);
    my $z  = $c->_zero();

    use integer;
    until ( $c->_is_zero($x1) || $c->_is_zero($y1) ) {
        ( $x1, $xr ) = $c->_div( $x1, $mask );
        ( $y1, $yr ) = $c->_div( $y1, $mask );
        $c->_add( $z, $c->_mul( [ 0 + $xr->[0] | 0 + $yr->[0] ], $m ) );
        $c->_mul( $m, $mask );
    }
    $c->_add( $z, $c->_mul( $x1, $m ) ) if !$c->_is_zero($x1);
    $c->_add( $z, $c->_mul( $y1, $m ) ) if !$c->_is_zero($y1);

    @$x = @$z;
    return $x;
}

sub _as_hex {
    my ( $c, $x ) = @_;

    return "0x0" if @$x == 1 && $x->[0] == 0;

    my $x1 = $c->_copy($x);

    my $x10000 = [0x10000];

    my $es = '';
    my $xr;
    until ( @$x1 == 1 && $x1->[0] == 0 ) {
        ( $x1, $xr ) = $c->_div( $x1, $x10000 );
        $es = sprintf( '%04x', $xr->[0] ) . $es;
    }
    $es =~ s/^0*/0x/;
    return $es;
}

sub _as_bin {
    my ( $c, $x ) = @_;

    return "0b0" if @$x == 1 && $x->[0] == 0;

    my $x1 = $c->_copy($x);

    my $x10000 = [0x10000];

    my $es = '';
    my $xr;

    until ( @$x1 == 1 && $x1->[0] == 0 ) {
        ( $x1, $xr ) = $c->_div( $x1, $x10000 );
        $es = sprintf( '%016b', $xr->[0] ) . $es;
    }
    $es =~ s/^0*/0b/;
    return $es;
}

sub _as_oct {
    my ( $c, $x ) = @_;

    return "00" if @$x == 1 && $x->[0] == 0;

    my $x1 = $c->_copy($x);

    my $x1000 = [ 1 << 15 ];

    my $es = '';
    my $xr;
    until ( @$x1 == 1 && $x1->[0] == 0 ) {
        ( $x1, $xr ) = $c->_div( $x1, $x1000 );
        $es = sprintf( "%05o", $xr->[0] ) . $es;
    }
    $es =~ s/^0*/0/;
    return $es;
}

sub _from_oct {
    my ( $c, $os ) = @_;

    my $m = $c->_new( 1 << 30 );
    my $d = 10;

    my $mul = $c->_one();
    my $x   = $c->_zero();

    my $len = int( ( length($os) - 1 ) / $d );
    my $val;
    my $i = -$d;
    while ( $len >= 0 ) {
        $val = substr( $os, $i, $d );
        $val = CORE::oct($val);
        $i -= $d;
        $len--;
        my $adder = $c->_new($val);
        $c->_add( $x, $c->_mul( $adder, $mul ) ) if $val != 0;
        $c->_mul( $mul, $m )                     if $len >= 0;
    }
    $x;
}

sub _from_hex {
    my ( $c, $hs ) = @_;

    my $m   = $c->_new(0x10000000);
    my $d   = 7;
    my $mul = $c->_one();
    my $x   = $c->_zero();

    my $len = int( ( length($hs) - 2 ) / $d );
    my $val;
    my $i = -$d;
    while ( $len >= 0 ) {
        $val = substr( $hs, $i, $d );
        $val =~ s/^0x// if $len == 0;
        $val = CORE::hex($val);
        $i -= $d;
        $len--;
        my $adder = $c->_new($val);
        if ( CORE::length($val) > $BASE_LEN ) {
            $adder = $c->_new($val);
        }
        $c->_add( $x, $c->_mul( $adder, $mul ) ) if $val != 0;
        $c->_mul( $mul, $m )                     if $len >= 0;
    }
    $x;
}

sub _from_bin {
    my ( $c, $bs ) = @_;

    my $hs = $bs;
    $hs =~ s/^[+-]?0b//;
    my $l = length($hs);
    $hs = '0' x ( 8 - ( $l % 8 ) ) . $hs if ( $l % 8 ) != 0;
    my $h = '0x' . unpack( 'H*', pack( 'B*', $hs ) );

    $c->_from_hex($h);
}

sub _modinv {

    my ( $c, $x, $y ) = @_;

    if ( $c->_is_zero($y) ) {
        return;
    }

    if ( $c->_is_one($y) ) {
        return $c->_zero(), '+';
    }

    my $u = $c->_zero();
    my $v = $c->_one();
    my $a = $c->_copy($y);
    my $b = $c->_copy($x);

    my $q;
    my $sign = 1;
    {
        ( $a, $q, $b ) = ( $b, $c->_div( $a, $b ) );
        last if $c->_is_zero($b);

        my $t = $c->_add( $c->_mul( $c->_copy($v), $q ), $u );
        $u    = $v;
        $v    = $t;
        $sign = -$sign;
        redo;
    }

    return unless $c->_is_one($a);

    ( $v, $sign == 1 ? '+' : '-' );
}

sub _modpow {
    my ( $c, $num, $exp, $mod ) = @_;

    if ( $c->_is_one($mod) ) {
        @$num = 0;
        return $num;
    }

    if ( $c->_is_zero($num) ) {
        if ( $c->_is_zero($exp) ) {
            @$num = 1;
        }
        else {
            @$num = 0;
        }
        return $num;
    }

    my $acc = $c->_copy($num);
    my $t   = $c->_one();

    my $expbin = $c->_to_bin($exp);
    my $len    = length($expbin);
    while ( $len-- ) {
        if ( substr( $expbin, $len, 1 ) eq '1' ) {
            $t = $c->_mul( $t, $acc );
            $t = $c->_mod( $t, $mod );
        }
        $acc = $c->_mul( $acc, $acc );
        $acc = $c->_mod( $acc, $mod );
    }
    @$num = @$t;
    $num;
}

sub _gcd {

    my ( $c, $x, $y ) = @_;

    if ( @$x == 1 && $x->[0] == 0 ) {
        if ( @$y == 1 && $y->[0] == 0 ) {
            @$x = 0;
        }
        else {
            @$x = @$y;
        }
        return $x;
    }

    until ( @$y == 1 && $y->[0] == 0 ) {

        $c->_mod( $x, $y );

        my $tmp = $c->_copy($x);
        @$x = @$y;
        $y  = $tmp;
    }

    return $x;
}

1;

