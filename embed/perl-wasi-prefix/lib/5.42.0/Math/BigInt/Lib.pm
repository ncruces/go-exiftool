package Math::BigInt::Lib;

use 5.006001;
use strict;
use warnings;

our $VERSION = '2.005002';
$VERSION =~ tr/_//d;

use Carp;

use overload

  '+' => sub {
    my $class = ref $_[0];
    my $x     = $class->_copy( $_[0] );
    my $y     = ref( $_[1] ) ? $_[1] : $class->_new( $_[1] );
    return $class->_add( $x, $y );
  },

  '-' => sub {
    my $class = ref $_[0];
    my ( $x, $y );
    if ( $_[2] ) {
        $y = $_[0];
        $x = ref( $_[1] ) ? $_[1] : $class->_new( $_[1] );
    }
    else {
        $x = $class->_copy( $_[0] );
        $y = ref( $_[1] ) ? $_[1] : $class->_new( $_[1] );
    }
    return $class->_sub( $x, $y );
  },

  '*' => sub {
    my $class = ref $_[0];
    my $x     = $class->_copy( $_[0] );
    my $y     = ref( $_[1] ) ? $_[1] : $class->_new( $_[1] );
    return $class->_mul( $x, $y );
  },

  '/' => sub {
    my $class = ref $_[0];
    my ( $x, $y );
    if ( $_[2] ) {
        $y = $_[0];
        $x = ref( $_[1] ) ? $_[1] : $class->_new( $_[1] );
    }
    else {
        $x = $class->_copy( $_[0] );
        $y = ref( $_[1] ) ? $_[1] : $class->_new( $_[1] );
    }
    return $class->_div( $x, $y );
  },

  '%' => sub {
    my $class = ref $_[0];
    my ( $x, $y );
    if ( $_[2] ) {
        $y = $_[0];
        $x = ref( $_[1] ) ? $_[1] : $class->_new( $_[1] );
    }
    else {
        $x = $class->_copy( $_[0] );
        $y = ref( $_[1] ) ? $_[1] : $class->_new( $_[1] );
    }
    return $class->_mod( $x, $y );
  },

  '**' => sub {
    my $class = ref $_[0];
    my ( $x, $y );
    if ( $_[2] ) {
        $y = $_[0];
        $x = ref( $_[1] ) ? $_[1] : $class->_new( $_[1] );
    }
    else {
        $x = $class->_copy( $_[0] );
        $y = ref( $_[1] ) ? $_[1] : $class->_new( $_[1] );
    }
    return $class->_pow( $x, $y );
  },

  '<<' => sub {
    my $class = ref $_[0];
    my ( $x, $y );
    if ( $_[2] ) {
        $y = $class->_num( $_[0] );
        $x = ref( $_[1] ) ? $_[1] : $class->_new( $_[1] );
    }
    else {
        $x = $_[0];
        $y = ref( $_[1] ) ? $class->_num( $_[1] ) : $_[1];
    }
    return $class->_lsft( $x, $y );
  },

  '>>' => sub {
    my $class = ref $_[0];
    my ( $x, $y );
    if ( $_[2] ) {
        $y = $_[0];
        $x = ref( $_[1] ) ? $_[1] : $class->_new( $_[1] );
    }
    else {
        $x = $class->_copy( $_[0] );
        $y = ref( $_[1] ) ? $_[1] : $class->_new( $_[1] );
    }
    return $class->_rsft( $x, $y );
  },

  '<' => sub {
    my $class = ref $_[0];
    my ( $x, $y );
    if ( $_[2] ) {
        $y = $_[0];
        $x = ref( $_[1] ) ? $_[1] : $class->_new( $_[1] );
    }
    else {
        $x = $class->_copy( $_[0] );
        $y = ref( $_[1] ) ? $_[1] : $class->_new( $_[1] );
    }
    return $class->_acmp( $x, $y ) < 0;
  },

  '<=' => sub {
    my $class = ref $_[0];
    my ( $x, $y );
    if ( $_[2] ) {
        $y = $_[0];
        $x = ref( $_[1] ) ? $_[1] : $class->_new( $_[1] );
    }
    else {
        $x = $class->_copy( $_[0] );
        $y = ref( $_[1] ) ? $_[1] : $class->_new( $_[1] );
    }
    return $class->_acmp( $x, $y ) <= 0;
  },

  '>' => sub {
    my $class = ref $_[0];
    my ( $x, $y );
    if ( $_[2] ) {
        $y = $_[0];
        $x = ref( $_[1] ) ? $_[1] : $class->_new( $_[1] );
    }
    else {
        $x = $class->_copy( $_[0] );
        $y = ref( $_[1] ) ? $_[1] : $class->_new( $_[1] );
    }
    return $class->_acmp( $x, $y ) > 0;
  },

  '>=' => sub {
    my $class = ref $_[0];
    my ( $x, $y );
    if ( $_[2] ) {
        $y = $_[0];
        $x = ref( $_[1] ) ? $_[1] : $class->_new( $_[1] );
    }
    else {
        $x = $class->_copy( $_[0] );
        $y = ref( $_[1] ) ? $_[1] : $class->_new( $_[1] );
    }
    return $class->_acmp( $x, $y ) >= 0;
  },

  '==' => sub {
    my $class = ref $_[0];
    my $x     = $class->_copy( $_[0] );
    my $y     = ref( $_[1] ) ? $_[1] : $class->_new( $_[1] );
    return $class->_acmp( $x, $y ) == 0;
  },

  '!=' => sub {
    my $class = ref $_[0];
    my $x     = $class->_copy( $_[0] );
    my $y     = ref( $_[1] ) ? $_[1] : $class->_new( $_[1] );
    return $class->_acmp( $x, $y ) != 0;
  },

  '<=>' => sub {
    my $class = ref $_[0];
    my ( $x, $y );
    if ( $_[2] ) {
        $y = $_[0];
        $x = ref( $_[1] ) ? $_[1] : $class->_new( $_[1] );
    }
    else {
        $x = $class->_copy( $_[0] );
        $y = ref( $_[1] ) ? $_[1] : $class->_new( $_[1] );
    }
    return $class->_acmp( $x, $y );
  },

  '&' => sub {
    my $class = ref $_[0];
    my ( $x, $y );
    if ( $_[2] ) {
        $y = $_[0];
        $x = ref( $_[1] ) ? $_[1] : $class->_new( $_[1] );
    }
    else {
        $x = $class->_copy( $_[0] );
        $y = ref( $_[1] ) ? $_[1] : $class->_new( $_[1] );
    }
    return $class->_and( $x, $y );
  },

  '|' => sub {
    my $class = ref $_[0];
    my ( $x, $y );
    if ( $_[2] ) {
        $y = $_[0];
        $x = ref( $_[1] ) ? $_[1] : $class->_new( $_[1] );
    }
    else {
        $x = $class->_copy( $_[0] );
        $y = ref( $_[1] ) ? $_[1] : $class->_new( $_[1] );
    }
    return $class->_or( $x, $y );
  },

  '^' => sub {
    my $class = ref $_[0];
    my ( $x, $y );
    if ( $_[2] ) {
        $y = $_[0];
        $x = ref( $_[1] ) ? $_[1] : $class->_new( $_[1] );
    }
    else {
        $x = $class->_copy( $_[0] );
        $y = ref( $_[1] ) ? $_[1] : $class->_new( $_[1] );
    }
    return $class->_xor( $x, $y );
  },

  'abs' => sub { $_[0] },

  'sqrt' => sub {
    my $class = ref $_[0];
    return $class->_sqrt( $class->_copy( $_[0] ) );
  },

  'int' => sub { $_[0] },

  'bool' => sub { ref( $_[0] )->_is_zero( $_[0] ) ? '' : 1; },

  '""' => sub { ref( $_[0] )->_str( $_[0] ); },

  '0+' => sub { ref( $_[0] )->_num( $_[0] ); },

  '=' => sub { ref( $_[0] )->_copy( $_[0] ); },

  ;

sub _new {
    croak "@{[(caller 0)[3]]} method not implemented";
}

sub _zero {
    my $class = shift;
    return $class->_new("0");
}

sub _one {
    my $class = shift;
    return $class->_new("1");
}

sub _two {
    my $class = shift;
    return $class->_new("2");

}

sub _ten {
    my $class = shift;
    return $class->_new("10");
}

sub _1ex {
    my ( $class, $exp ) = @_;
    $exp = $class->_num($exp) if ref($exp);
    return $class->_new( "1" . ( "0" x $exp ) );
}

sub _copy {
    my ( $class, $x ) = @_;
    return $class->_new( $class->_str($x) );
}

sub import { }

sub _str {
    croak "@{[(caller 0)[3]]} method not implemented";
}

sub _num {
    my ( $class, $x ) = @_;
    0 + $class->_str($x);
}

sub _add {
    croak "@{[(caller 0)[3]]} method not implemented";
}

sub _sub {
    croak "@{[(caller 0)[3]]} method not implemented";
}

sub _mul {
    my ( $class, $x, $y ) = @_;
    my $sum = $class->_zero();
    my $i   = $class->_zero();
    while ( $class->_acmp( $i, $y ) < 0 ) {
        $sum = $class->_add( $sum, $x );
        $i   = $class->_inc($i);
    }
    return $sum;
}

sub _div {
    my ( $class, $x, $y ) = @_;

    croak "@{[(caller 0)[3]]} requires non-zero divisor"
      if $class->_is_zero($y);

    my $r = $class->_copy($x);
    my $q = $class->_zero();
    while ( $class->_acmp( $r, $y ) >= 0 ) {
        $q = $class->_inc($q);
        $r = $class->_sub( $r, $y );
    }

    return $q, $r if wantarray;
    return $q;
}

sub _inc {
    my ( $class, $x ) = @_;
    $class->_add( $x, $class->_one() );
}

sub _dec {
    my ( $class, $x ) = @_;
    $class->_sub( $x, $class->_one() );
}

sub _sadd {
    my $class = shift;
    my ( $xa, $xs, $ya, $ys, $flag ) = @_;
    my ( $za, $zs );

    if ( $xs eq $ys ) {
        if ($flag) {
            $za = $class->_add( $ya, $xa );
        }
        else {
            $za = $class->_add( $xa, $ya );
        }
        $zs = $class->_is_zero($za) ? '+' : $xs;
        return $za, $zs;
    }

    my $acmp = $class->_acmp( $xa, $ya );

    if ( $acmp == 0 ) {
        $za = $class->_zero();
        $zs = '+';
        return $za, $zs;
    }

    if ( $acmp > 0 ) {
        $za = $class->_sub( $xa, $ya, $flag );
        $zs = $xs;
    }
    else {
        $za = $class->_sub( $ya, $xa, !$flag );
        $zs = $ys;
    }
    return $za, $zs;
}

sub _ssub {
    my $class = shift;
    my ( $xa, $xs, $ya, $ys, $flag ) = @_;

    $ys = $ys eq '+' ? '-' : '+';
    $class->_sadd( $xa, $xs, $ya, $ys, $flag );
}

sub _acmp {
    my ( $class, $x, $y ) = @_;
    my $xstr = $class->_str($x);
    my $ystr = $class->_str($y);

    length($xstr) <=> length($ystr) || $xstr cmp $ystr;
}

sub _scmp {
    my ( $class, $xa, $xs, $ya, $ys ) = @_;
    if ( $xs eq '+' ) {
        if ( $ys eq '+' ) {
            return $class->_acmp( $xa, $ya );
        }
        else {
            return 1;
        }
    }
    else {
        if ( $ys eq '+' ) {
            return -1;
        }
        else {
            return $class->_acmp( $ya, $xa );
        }
    }
}

sub _len {
    my ( $class, $x ) = @_;
    CORE::length( $class->_str($x) );
}

sub _alen {
    my ( $class, $x ) = @_;
    $class->_len($x);
}

sub _digit {
    my ( $class, $x, $n ) = @_;
    substr( $class->_str($x), -( $n + 1 ), 1 );
}

sub _digitsum {
    my ( $class, $x ) = @_;

    my $len = $class->_len($x);
    my $sum = $class->_zero();
    for ( my $i = 0 ; $i < $len ; ++$i ) {
        my $digit = $class->_digit( $x, $i );
        $digit = $class->_new($digit);
        $sum   = $class->_add( $sum, $digit );
    }

    return $sum;
}

sub _zeros {
    my ( $class, $x ) = @_;
    my $str = $class->_str($x);
    $str =~ /[^0](0*)\z/ ? CORE::length($1) : 0;
}

sub _is_zero {
    my ( $class, $x ) = @_;
    $class->_str($x) == 0;
}

sub _is_even {
    my ( $class, $x ) = @_;
    substr( $class->_str($x), -1, 1 ) % 2 == 0;
}

sub _is_odd {
    my ( $class, $x ) = @_;
    substr( $class->_str($x), -1, 1 ) % 2 != 0;
}

sub _is_one {
    my ( $class, $x ) = @_;
    $class->_str($x) == 1;
}

sub _is_two {
    my ( $class, $x ) = @_;
    $class->_str($x) == 2;
}

sub _is_ten {
    my ( $class, $x ) = @_;
    $class->_str($x) == 10;
}

sub _check {
    my ( $class, $x ) = @_;
    return "Input is undefined"    unless defined $x;
    return "$x is not a reference" unless ref($x);
    return 0;
}

sub _mod {
    my ( $class, $x, $y ) = @_;

    croak "@{[(caller 0)[3]]} requires non-zero second operand"
      if $class->_is_zero($y);

    if ( $class->can('_div') ) {
        $x = $class->_copy($x);
        my ( $q, $r ) = $class->_div( $x, $y );
        return $r;
    }
    else {
        my $r = $class->_copy($x);
        while ( $class->_acmp( $r, $y ) >= 0 ) {
            $r = $class->_sub( $r, $y );
        }
        return $r;
    }
}

sub _rsft {
    my ( $class, $x, $n, $b ) = @_;
    $b = $class->_new($b) unless ref $b;
    return scalar $class->_div( $x, $class->_pow( $class->_copy($b), $n ) );
}

sub _lsft {
    my ( $class, $x, $n, $b ) = @_;
    $b = $class->_new($b) unless ref $b;
    return $class->_mul( $x, $class->_pow( $class->_copy($b), $n ) );
}

sub _pow {
    my ( $class, $x, $y ) = @_;

    if ( $class->_is_zero($y) ) {
        return $class->_one();
    }

    if (   ( $class->_is_one($x) )
        || ( $class->_is_one($y) ) )
    {
        return $x;
    }

    if ( $class->_is_zero($x) ) {
        return $class->_zero();
    }

    my $pow2 = $class->_one();

    my $y_bin = $class->_as_bin($y);
    $y_bin =~ s/^0b//;
    my $len = length($y_bin);

    while ( --$len > 0 ) {
        $pow2 = $class->_mul( $pow2, $x ) if substr( $y_bin, $len, 1 ) eq '1';
        $x    = $class->_mul( $x,    $x );
    }

    $x = $class->_mul( $x, $pow2 );
    return $x;
}

sub _nok {
    my ( $class, $n, $k ) = @_;

    {
        my $twok = $class->_mul( $class->_two(), $class->_copy($k) );
        if ( $class->_acmp( $twok, $n ) > 0 ) {
            $k = $class->_sub( $class->_copy($n), $k );
        }
    }

    if ( $class->_is_zero($k) ) {
        return $class->_one();
    }

    my $n_orig = $class->_copy($n);

    $n = $class->_sub( $n, $k );
    $n = $class->_inc($n);

    my $f = $class->_copy($n);
    $f = $class->_inc($f);

    my $d = $class->_two();

    while ( $class->_acmp( $f, $n_orig ) <= 0 ) {
        $n = $class->_mul( $n, $f );
        $n = $class->_div( $n, $d );
        $f = $class->_inc($f);
        $d = $class->_inc($d);
    }

    return $n;
}

sub _fac {
    my ( $class, $x ) = @_;

    my $p   = $class->_one();
    my $r   = $class->_one();
    my $two = $class->_two();

    my ($log2n) = $class->_log_int( $class->_copy($x), $two );
    my $h       = $class->_zero();
    my $shift   = $class->_zero();
    my $k       = $class->_one();

    while ( $class->_acmp( $h, $x ) ) {
        $shift = $class->_add( $shift, $h );
        $h     = $class->_rsft( $class->_copy($x), $log2n, $two );
        $log2n = $class->_dec($log2n) if !$class->_is_zero($log2n);
        my $high = $class->_copy($h);
        $high = $class->_dec($high) if $class->_is_even($h);
        while ( $class->_acmp( $k, $high ) ) {
            $k = $class->_add( $k, $two );
            $p = $class->_mul( $p, $k );
        }
        $r = $class->_mul( $r, $p );
    }
    return $class->_lsft( $r, $shift, $two );
}

sub _dfac {
    my ( $class, $x ) = @_;

    my $two = $class->_two();

    if ( $class->_acmp( $x, $two ) < 0 ) {
        return $class->_one();
    }

    my $i = $class->_copy($x);
    while ( $class->_acmp( $i, $two ) > 0 ) {
        $i = $class->_sub( $i, $two );
        $x = $class->_mul( $x, $i );
    }

    return $x;
}

sub _log_int {
    my ( $class, $x, $base ) = @_;

    return if $class->_is_zero($x);

    $base = $class->_new(2)     unless defined($base);
    $base = $class->_new($base) unless ref($base);

    return if $class->_is_zero($base) || $class->_is_one($base);

    if ( $class->_is_one($x) ) {
        return $class->_zero(), 1 if wantarray;
        return $class->_zero();
    }

    my $cmp = $class->_acmp( $x, $base );

    if ( $cmp == 0 ) {
        return $class->_one(), 1 if wantarray;
        return $class->_one();
    }

    if ( $cmp < 0 ) {
        return $class->_zero(), 0 if wantarray;
        return $class->_zero();
    }

    my $y;

    {
        my $x_str = $class->_str($x);
        my $b_str = $class->_str($base);
        my $xm    = "." . $x_str;
        my $bm    = "." . $b_str;
        my $xe    = length($x_str);
        my $be    = length($b_str);
        my $log10 = log(10);
        my $guess =
          int( ( log($xm) + $xe * $log10 ) / ( log($bm) + $be * $log10 ) );
        $y = $class->_new($guess);
    }

    my $trial = $class->_pow( $class->_copy($base), $y );
    my $acmp  = $class->_acmp( $trial, $x );

    while ( $acmp < 0 ) {
        $trial = $class->_mul( $trial, $base );
        $y     = $class->_inc($y);
        $acmp  = $class->_acmp( $trial, $x );
    }

    while ( $acmp > 0 ) {
        $trial = $class->_div( $trial, $base );
        $y     = $class->_dec($y);
        $acmp  = $class->_acmp( $trial, $x );
    }

    return wantarray ? ( $y, 1 ) : $y if $acmp == 0;
    return wantarray ? ( $y, 0 ) : $y;
}

sub _ilog2 {
    my ( $class, $x ) = @_;

    return if $class->_is_zero($x);

    my $str = $class->_to_hex($x);

    my $y = $class->_new( length($str) - 1 );
    $y = $class->_mul( $y, $class->_new(4) );

    my $n = int log( hex( substr( $str, 0, 1 ) ) ) / log(2);
    $y = $class->_add( $y, $class->_new($n) );
    return $y unless wantarray;

    my $pow2     = $class->_lsft( $class->_one(), $y, 2 );
    my $is_exact = $class->_acmp( $x, $pow2 ) == 0 ? 1 : 0;
    return $y, $is_exact;
}

sub _ilog10 {
    my ( $class, $x ) = @_;

    return if $class->_is_zero($x);

    my $str = $class->_str($x);
    my $len = length($str);
    my $y   = $class->_new( $len - 1 );
    return $y unless wantarray;

    my $is_exact = $str =~ /^10*$/ ? 1 : 0;
    return $y, $is_exact;
}

sub _clog2 {
    my ( $class, $x ) = @_;

    return if $class->_is_zero($x);

    my $str = $class->_to_hex($x);

    my $y = $class->_new( length($str) - 1 );
    $y = $class->_mul( $y, $class->_new(4) );

    my $n = int log( hex( substr( $str, 0, 1 ) ) ) / log(2);
    $y = $class->_add( $y, $class->_new($n) );

    my $pow2     = $class->_lsft( $class->_one(), $y, 2 );
    my $is_exact = $class->_acmp( $x, $pow2 ) == 0 ? 1 : 0;
    $y = $class->_inc($y) if $is_exact == 0;
    return $y, $is_exact if wantarray;
    return $y;
}

sub _clog10 {
    my ( $class, $x ) = @_;

    return if $class->_is_zero($x);

    my $str = $class->_str($x);
    my $len = length($str);

    if ( $str =~ /^10*$/ ) {
        my $y = $class->_new( $len - 1 );
        return $y, 1 if wantarray;
        return $y;
    }

    my $y = $class->_new($len);
    return $y, 0 if wantarray;
    return $y;
}

sub _sqrt {
    my ( $class, $y ) = @_;

    return $y if $class->_is_zero($y);

    my $y_str = $class->_str($y);
    my $y_len = length($y_str);

    my $xm;
    my $xe;
    if ( $y_len % 2 == 0 ) {
        $xm = sqrt( "." . $y_str );
        $xe = $y_len / 2;
        $xm = sprintf "%.0f", int( $xm * 1e15 );
        $xe -= 15;
    }
    else {
        $xm = sqrt( ".0" . $y_str );
        $xe = ( $y_len + 1 ) / 2;
        $xm = sprintf "%.0f", int( $xm * 1e16 );
        $xe -= 16;
    }

    my $x;
    if ( $xe < 0 ) {
        $x = substr $xm, 0, length($xm) + $xe;
    }
    else {
        $x = $xm . ( "0" x $xe );
    }

    $x = $class->_new($x);

    my $xsq  = $class->_mul( $class->_copy($x), $x );
    my $acmp = $class->_acmp( $xsq, $y );

    my $two;
    $two = $class->_two() if $acmp != 0;

    if ( $acmp < 0 ) {

        my $numer = $class->_sub( $class->_copy($y), $xsq );
        my $denom = $class->_mul( $class->_copy($two), $x );
        my $delta = $class->_div( $numer, $denom );

        unless ( $class->_is_zero($delta) ) {
            $x    = $class->_add( $x, $delta );
            $xsq  = $class->_mul( $class->_copy($x), $x );
            $acmp = $class->_acmp( $xsq, $y );
        }
    }

    while ( $acmp > 0 ) {

        my $numer = $class->_sub( $xsq, $y );
        my $denom = $class->_mul( $class->_copy($two), $x );
        my $delta = $class->_div( $numer, $denom );
        last if $class->_is_zero($delta);

        $x    = $class->_sub( $x, $delta );
        $xsq  = $class->_mul( $class->_copy($x), $x );
        $acmp = $class->_acmp( $xsq, $y );
    }

    while ( $acmp > 0 ) {
        $x    = $class->_dec($x);
        $xsq  = $class->_mul( $class->_copy($x), $x );
        $acmp = $class->_acmp( $xsq, $y );
    }

    return $x;
}

sub _root {
    my ( $class, $y, $n ) = @_;

    return $y
      if $class->_is_zero($y)
      || $class->_is_one($y)
      || $class->_is_one($n);

    return $class->_one() if $class->_acmp( $y, $n ) <= 0;

    my $DEBUG = 0;

    my $y_str = $class->_str($y);
    my $ym    = "." . $y_str;
    my $ye    = length($y_str);

    my $log10y = log($ym) / log(10) + $ye;

    my $log10x = $log10y / $class->_num($n);

    my $xe = int $log10x;
    my $xm = 10**( $log10x - $xe );

    if ($DEBUG) {
        print "\n";
        print "y_str  = $y_str\n";
        print "ym     = $ym\n";
        print "ye     = $ye\n";
        print "log10y = $log10y\n";
        print "log10x = $log10x\n";
        print "xm     = $xm\n";
        print "xe     = $xe\n";
    }

    my $d = $xe < 15 ? $xe : 15;
    $xm *= 10**$d;
    $xe -= $d;

    if ($DEBUG) {
        print "\n";
        print "xm     = $xm\n";
        print "xe     = $xe\n";
    }

    my $xm_int = int($xm);
    my $x_str  = sprintf '%.0f', $xm > $xm_int ? $xm_int + 1 : $xm_int;
    $x_str .= "0" x $xe;

    my $x = $class->_new($x_str);

    if ($DEBUG) {
        print "xm     = $xm\n";
        print "xe     = $xe\n";
        print "\n";
        print "x_str  = $x_str (initial guess)\n";
        print "\n";
    }

    my $nm1     = $class->_dec( $class->_copy($n) );
    my $xpownm1 = $class->_pow( $class->_copy($x), $nm1 );
    my $xpown   = $class->_mul( $class->_copy($xpownm1), $x );
    my $acmp    = $class->_acmp( $xpown, $y );

    if ($DEBUG) {
        print "\n";
        print "x      = ", $class->_str($x),     "\n";
        print "x^n    = ", $class->_str($xpown), "\n";
        print "y      = ", $class->_str($y),     "\n";
        print "acmp   = $acmp\n";
    }

    if ( $acmp < 0 ) {

        my $numer = $class->_sub( $class->_copy($y), $xpown );
        my $denom = $class->_mul( $class->_copy($n), $xpownm1 );
        my $delta = $class->_div( $numer, $denom );

        if ($DEBUG) {
            print "\n";
            print "numer  = ", $class->_str($numer), "\n";
            print "denom  = ", $class->_str($denom), "\n";
            print "delta  = ", $class->_str($delta), "\n";
        }

        unless ( $class->_is_zero($delta) ) {
            $x       = $class->_add( $x, $delta );
            $xpownm1 = $class->_pow( $class->_copy($x), $nm1 );
            $xpown   = $class->_mul( $class->_copy($xpownm1), $x );
            $acmp    = $class->_acmp( $xpown, $y );

            if ($DEBUG) {
                print "\n";
                print "x      = ", $class->_str($x),     "\n";
                print "x^n    = ", $class->_str($xpown), "\n";
                print "y      = ", $class->_str($y),     "\n";
                print "acmp   = $acmp\n";
            }
        }
    }

    while ( $acmp > 0 ) {

        my $numer = $class->_sub( $class->_copy($xpown), $y );
        my $denom = $class->_mul( $class->_copy($n), $xpownm1 );

        if ($DEBUG) {
            print "numer  = ", $class->_str($numer), "\n";
            print "denom  = ", $class->_str($denom), "\n";
        }

        my $delta = $class->_div( $numer, $denom );

        if ($DEBUG) {
            print "delta  = ", $class->_str($delta), "\n";
        }

        last if $class->_is_zero($delta);

        $x       = $class->_sub( $x, $delta );
        $xpownm1 = $class->_pow( $class->_copy($x), $nm1 );
        $xpown   = $class->_mul( $class->_copy($xpownm1), $x );
        $acmp    = $class->_acmp( $xpown, $y );

        if ($DEBUG) {
            print "\n";
            print "x      = ", $class->_str($x),     "\n";
            print "x^n    = ", $class->_str($xpown), "\n";
            print "y      = ", $class->_str($y),     "\n";
            print "acmp   = $acmp\n";
        }
    }

    while ( $acmp > 0 ) {
        $x     = $class->_dec($x);
        $xpown = $class->_pow( $class->_copy($x), $n );
        $acmp  = $class->_acmp( $xpown, $y );
    }

    return $x;
}

sub _and {
    my ( $class, $x, $y ) = @_;

    return $x if $class->_acmp( $x, $y ) == 0;

    my $m    = $class->_one();
    my $mask = $class->_new("32768");

    my ( $xr, $yr );

    my $xc = $class->_copy($x);
    my $yc = $class->_copy($y);
    my $z  = $class->_zero();

    until ( $class->_is_zero($xc) || $class->_is_zero($yc) ) {
        ( $xc, $xr ) = $class->_div( $xc, $mask );
        ( $yc, $yr ) = $class->_div( $yc, $mask );
        my $bits = $class->_new( $class->_num($xr) & $class->_num($yr) );
        $z = $class->_add( $z, $class->_mul( $bits, $m ) );
        $m = $class->_mul( $m, $mask );
    }

    return $z;
}

sub _xor {
    my ( $class, $x, $y ) = @_;

    return $class->_zero() if $class->_acmp( $x, $y ) == 0;

    my $m    = $class->_one();
    my $mask = $class->_new("32768");

    my ( $xr, $yr );

    my $xc = $class->_copy($x);
    my $yc = $class->_copy($y);
    my $z  = $class->_zero();

    until ( $class->_is_zero($xc) || $class->_is_zero($yc) ) {
        ( $xc, $xr ) = $class->_div( $xc, $mask );
        ( $yc, $yr ) = $class->_div( $yc, $mask );
        my $bits = $class->_new( $class->_num($xr) ^ $class->_num($yr) );
        $z = $class->_add( $z, $class->_mul( $bits, $m ) );
        $m = $class->_mul( $m, $mask );
    }

    $z = $class->_add( $z, $class->_mul( $xc, $m ) )
      unless $class->_is_zero($xc);
    $z = $class->_add( $z, $class->_mul( $yc, $m ) )
      unless $class->_is_zero($yc);

    return $z;
}

sub _or {
    my ( $class, $x, $y ) = @_;

    return $x if $class->_acmp( $x, $y ) == 0;

    my $m    = $class->_one();
    my $mask = $class->_new("32768");

    my ( $xr, $yr );

    my $xc = $class->_copy($x);
    my $yc = $class->_copy($y);
    my $z  = $class->_zero();

    until ( $class->_is_zero($xc) || $class->_is_zero($yc) ) {
        ( $xc, $xr ) = $class->_div( $xc, $mask );
        ( $yc, $yr ) = $class->_div( $yc, $mask );
        my $bits = $class->_new( $class->_num($xr) | $class->_num($yr) );
        $z = $class->_add( $z, $class->_mul( $bits, $m ) );
        $m = $class->_mul( $m, $mask );
    }

    $z = $class->_add( $z, $class->_mul( $xc, $m ) )
      unless $class->_is_zero($xc);
    $z = $class->_add( $z, $class->_mul( $yc, $m ) )
      unless $class->_is_zero($yc);

    return $z;
}

sub _sand {
    my ( $class, $x, $sx, $y, $sy ) = @_;

    return ( $class->_zero(), '+' )
      if $class->_is_zero($x) || $class->_is_zero($y);

    my $sign = $sx eq '-' && $sy eq '-' ? '-' : '+';

    my ( $bx, $by );

    if ( $sx eq '-' ) {

        $bx = $class->_copy($x);
        $bx = $class->_dec($bx);
        $bx = $class->_as_hex($bx);
        $bx =~ s/^-?0x//;
        $bx =~ tr<0123456789abcdef>
                <\x0f\x0e\x0d\x0c\x0b\x0a\x09\x08\x07\x06\x05\x04\x03\x02\x01\x00>;
    }
    else {
        $bx = $class->_as_hex($x);
        $bx =~ s/^-?0x//;
        $bx =~ tr<fedcba9876543210>
                 <\x0f\x0e\x0d\x0c\x0b\x0a\x09\x08\x07\x06\x05\x04\x03\x02\x01\x00>;
    }

    if ( $sy eq '-' ) {

        $by = $class->_copy($y);
        $by = $class->_dec($by);
        $by = $class->_as_hex($by);
        $by =~ s/^-?0x//;
        $by =~ tr<0123456789abcdef>
                <\x0f\x0e\x0d\x0c\x0b\x0a\x09\x08\x07\x06\x05\x04\x03\x02\x01\x00>;
    }
    else {
        $by = $class->_as_hex($y);
        $by =~ s/^-?0x//;
        $by =~ tr<fedcba9876543210>
                <\x0f\x0e\x0d\x0c\x0b\x0a\x09\x08\x07\x06\x05\x04\x03\x02\x01\x00>;
    }

    $bx = reverse $bx;
    $by = reverse $by;

    my $xx = "\x00";
    $xx = "\x0f" if $sx eq '-';
    my $yy = "\x00";
    $yy = "\x0f" if $sy eq '-';
    my $diff = CORE::length($bx) - CORE::length($by);
    if ( $diff > 0 ) {
        $by .= $yy x $diff;
    }
    elsif ( $diff < 0 ) {
        $bx .= $xx x abs($diff);
    }

    my $r = $bx & $by;

    $bx = reverse $r;

    if ( $sign eq '-' ) {
        $bx =~
          tr<\x0f\x0e\x0d\x0c\x0b\x0a\x09\x08\x07\x06\x05\x04\x03\x02\x01\x00>
                 <0123456789abcdef>;
    }
    else {
        $bx =~
          tr<\x0f\x0e\x0d\x0c\x0b\x0a\x09\x08\x07\x06\x05\x04\x03\x02\x01\x00>
                 <fedcba9876543210>;
    }

    $bx = '0x' . $bx;
    $bx = $class->_from_hex($bx);

    $bx = $class->_inc($bx) if $sign eq '-';

    $sign = '+' if $class->_is_zero($bx);

    return $bx, $sign;
}

sub _sxor {
    my ( $class, $x, $sx, $y, $sy ) = @_;

    return ( $class->_zero(), '+' )
      if $class->_is_zero($x) && $class->_is_zero($y);

    my $sign = $sx ne $sy ? '-' : '+';

    my ( $bx, $by );

    if ( $sx eq '-' ) {

        $bx = $class->_copy($x);
        $bx = $class->_dec($bx);
        $bx = $class->_as_hex($bx);
        $bx =~ s/^-?0x//;
        $bx =~ tr<0123456789abcdef>
                <\x0f\x0e\x0d\x0c\x0b\x0a\x09\x08\x07\x06\x05\x04\x03\x02\x01\x00>;
    }
    else {
        $bx = $class->_as_hex($x);
        $bx =~ s/^-?0x//;
        $bx =~ tr<fedcba9876543210>
                 <\x0f\x0e\x0d\x0c\x0b\x0a\x09\x08\x07\x06\x05\x04\x03\x02\x01\x00>;
    }

    if ( $sy eq '-' ) {

        $by = $class->_copy($y);
        $by = $class->_dec($by);
        $by = $class->_as_hex($by);
        $by =~ s/^-?0x//;
        $by =~ tr<0123456789abcdef>
                <\x0f\x0e\x0d\x0c\x0b\x0a\x09\x08\x07\x06\x05\x04\x03\x02\x01\x00>;
    }
    else {
        $by = $class->_as_hex($y);
        $by =~ s/^-?0x//;
        $by =~ tr<fedcba9876543210>
                <\x0f\x0e\x0d\x0c\x0b\x0a\x09\x08\x07\x06\x05\x04\x03\x02\x01\x00>;
    }

    $bx = reverse $bx;
    $by = reverse $by;

    my $xx = "\x00";
    $xx = "\x0f" if $sx eq '-';
    my $yy = "\x00";
    $yy = "\x0f" if $sy eq '-';
    my $diff = CORE::length($bx) - CORE::length($by);
    if ( $diff > 0 ) {
        $by .= $yy x $diff;
    }
    elsif ( $diff < 0 ) {
        $bx .= $xx x abs($diff);
    }

    my $r = $bx ^ $by;

    $bx = reverse $r;

    if ( $sign eq '-' ) {
        $bx =~
          tr<\x0f\x0e\x0d\x0c\x0b\x0a\x09\x08\x07\x06\x05\x04\x03\x02\x01\x00>
                 <0123456789abcdef>;
    }
    else {
        $bx =~
          tr<\x0f\x0e\x0d\x0c\x0b\x0a\x09\x08\x07\x06\x05\x04\x03\x02\x01\x00>
                 <fedcba9876543210>;
    }

    $bx = '0x' . $bx;
    $bx = $class->_from_hex($bx);

    $bx = $class->_inc($bx) if $sign eq '-';

    $sign = '+' if $class->_is_zero($bx);

    return $bx, $sign;
}

sub _sor {
    my ( $class, $x, $sx, $y, $sy ) = @_;

    return ( $class->_zero(), '+' )
      if $class->_is_zero($x) && $class->_is_zero($y);

    my $sign = $sx eq '-' || $sy eq '-' ? '-' : '+';

    my ( $bx, $by );

    if ( $sx eq '-' ) {

        $bx = $class->_copy($x);
        $bx = $class->_dec($bx);
        $bx = $class->_as_hex($bx);
        $bx =~ s/^-?0x//;
        $bx =~ tr<0123456789abcdef>
                <\x0f\x0e\x0d\x0c\x0b\x0a\x09\x08\x07\x06\x05\x04\x03\x02\x01\x00>;
    }
    else {
        $bx = $class->_as_hex($x);
        $bx =~ s/^-?0x//;
        $bx =~ tr<fedcba9876543210>
                 <\x0f\x0e\x0d\x0c\x0b\x0a\x09\x08\x07\x06\x05\x04\x03\x02\x01\x00>;
    }

    if ( $sy eq '-' ) {

        $by = $class->_copy($y);
        $by = $class->_dec($by);
        $by = $class->_as_hex($by);
        $by =~ s/^-?0x//;
        $by =~ tr<0123456789abcdef>
                <\x0f\x0e\x0d\x0c\x0b\x0a\x09\x08\x07\x06\x05\x04\x03\x02\x01\x00>;
    }
    else {
        $by = $class->_as_hex($y);
        $by =~ s/^-?0x//;
        $by =~ tr<fedcba9876543210>
                <\x0f\x0e\x0d\x0c\x0b\x0a\x09\x08\x07\x06\x05\x04\x03\x02\x01\x00>;
    }

    $bx = reverse $bx;
    $by = reverse $by;

    my $xx = "\x00";
    $xx = "\x0f" if $sx eq '-';
    my $yy = "\x00";
    $yy = "\x0f" if $sy eq '-';
    my $diff = CORE::length($bx) - CORE::length($by);
    if ( $diff > 0 ) {
        $by .= $yy x $diff;
    }
    elsif ( $diff < 0 ) {
        $bx .= $xx x abs($diff);
    }

    my $r = $bx | $by;

    $bx = reverse $r;

    if ( $sign eq '-' ) {
        $bx =~
          tr<\x0f\x0e\x0d\x0c\x0b\x0a\x09\x08\x07\x06\x05\x04\x03\x02\x01\x00>
                 <0123456789abcdef>;
    }
    else {
        $bx =~
          tr<\x0f\x0e\x0d\x0c\x0b\x0a\x09\x08\x07\x06\x05\x04\x03\x02\x01\x00>
                 <fedcba9876543210>;
    }

    $bx = '0x' . $bx;
    $bx = $class->_from_hex($bx);

    $bx = $class->_inc($bx) if $sign eq '-';

    $sign = '+' if $class->_is_zero($bx);

    return $bx, $sign;
}

sub _to_bin {
    my ( $class, $x ) = @_;
    my $str   = '';
    my $tmp   = $class->_copy($x);
    my $chunk = $class->_new("16777216");
    my $rem;
    until ( $class->_acmp( $tmp, $chunk ) < 0 ) {
        ( $tmp, $rem ) = $class->_div( $tmp, $chunk );
        $str = sprintf( "%024b", $class->_num($rem) ) . $str;
    }
    unless ( $class->_is_zero($tmp) ) {
        $str = sprintf( "%b", $class->_num($tmp) ) . $str;
    }
    return length($str) ? $str : '0';
}

sub _to_oct {
    my ( $class, $x ) = @_;
    my $str   = '';
    my $tmp   = $class->_copy($x);
    my $chunk = $class->_new("16777216");
    my $rem;
    until ( $class->_acmp( $tmp, $chunk ) < 0 ) {
        ( $tmp, $rem ) = $class->_div( $tmp, $chunk );
        $str = sprintf( "%08o", $class->_num($rem) ) . $str;
    }
    unless ( $class->_is_zero($tmp) ) {
        $str = sprintf( "%o", $class->_num($tmp) ) . $str;
    }
    return length($str) ? $str : '0';
}

sub _to_hex {
    my ( $class, $x ) = @_;
    my $str   = '';
    my $tmp   = $class->_copy($x);
    my $chunk = $class->_new("16777216");
    my $rem;
    until ( $class->_acmp( $tmp, $chunk ) < 0 ) {
        ( $tmp, $rem ) = $class->_div( $tmp, $chunk );
        $str = sprintf( "%06x", $class->_num($rem) ) . $str;
    }
    unless ( $class->_is_zero($tmp) ) {
        $str = sprintf( "%x", $class->_num($tmp) ) . $str;
    }
    return length($str) ? $str : '0';
}

sub _as_bin {
    my ( $class, $x ) = @_;
    return '0b' . $class->_to_bin($x);
}

sub _as_oct {
    my ( $class, $x ) = @_;
    return '0' . $class->_to_oct($x);
}

sub _as_hex {
    my ( $class, $x ) = @_;
    return '0x' . $class->_to_hex($x);
}

sub _to_bytes {
    my ( $class, $x ) = @_;
    my $str   = '';
    my $tmp   = $class->_copy($x);
    my $chunk = $class->_new("65536");
    my $rem;
    until ( $class->_is_zero($tmp) ) {
        ( $tmp, $rem ) = $class->_div( $tmp, $chunk );
        $str = pack( 'n', $class->_num($rem) ) . $str;
    }
    $str =~ s/^\0+//;
    return length($str) ? $str : "\x00";
}

*_as_bytes = \&_to_bytes;

sub _to_base {
    my $class = shift;
    my $x     = shift;
    my $base  = shift;
    $base = $class->_new($base) unless ref($base);

    my $collseq;
    if (@_) {
        $collseq = shift;
        croak "The collation sequence must be a non-empty string"
          unless defined($collseq) && length($collseq);
    }
    else {
        if ( $class->_acmp( $base, $class->_new("94") ) <= 0 ) {
            $collseq =
                '0123456789'
              . 'ABCDEFGHIJKLMNOPQRSTUVWXYZ'
              . 'abcdefghijklmnopqrstuvwxyz'
              . '!"#$%&\'()*+,-./'
              . ':;<=>?@'
              . '[\\]^_`' . '{|}~';
        }
        else {
            croak "When base > 94, a collation sequence must be given";
        }
    }

    my @collseq = split '', $collseq;

    my $str = '';
    my $tmp = $class->_copy($x);
    my $rem;
    until ( $class->_is_zero($tmp) ) {
        ( $tmp, $rem ) = $class->_div( $tmp, $base );
        my $num = $class->_num($rem);
        croak "no character to represent '$num' in collation sequence",
          " (collation sequence is too short)"
          if $num > $#collseq;
        my $chr = $collseq[$num];
        $str = $chr . $str;
    }
    return $collseq[0] unless length $str;
    return $str;
}

sub _to_base_num {
    my ( $class, $x, $base ) = @_;

    $base = $class->_new($base) unless ref($base);
    my $two = $class->_two();
    croak "base must be >= 2" unless $class->_acmp( $base, $two ) >= 0;

    my $out   = [];
    my $xcopy = $class->_copy($x);
    my $rem;

    until ( $class->_acmp( $xcopy, $base ) < 0 ) {
        ( $xcopy, $rem ) = $class->_div( $xcopy, $base );
        unshift @$out, $rem;
    }

    unless ( $class->_is_zero($xcopy) ) {
        unshift @$out, $xcopy;
    }

    unshift @$out, $class->_zero() unless @$out;

    return $out;
}

sub _from_hex {

    my ( $class, $hex ) = @_;
    $hex =~ s/^0[xX]//;

    my $len = length $hex;
    my $rem = 1 + ( $len - 1 ) % 7;

    my $ret = $class->_new( int hex substr $hex, 0, $rem );
    return $ret if $rem == $len;

    my $shift = $class->_new( 1 << ( 4 * 7 ) );
    for ( my $offset = $rem ; $offset < $len ; $offset += 7 ) {
        my $part = int hex substr $hex, $offset, 7;
        $ret = $class->_mul( $ret, $shift );
        $ret = $class->_add( $ret, $class->_new($part) );
    }

    return $ret;
}

sub _from_oct {

    my ( $class, $oct ) = @_;

    my $len = length $oct;
    my $rem = 1 + ( $len - 1 ) % 10;

    my $ret = $class->_new( int oct substr $oct, 0, $rem );
    return $ret if $rem == $len;

    my $shift = $class->_new( 1 << ( 3 * 10 ) );
    for ( my $offset = $rem ; $offset < $len ; $offset += 10 ) {
        my $part = int oct substr $oct, $offset, 10;
        $ret = $class->_mul( $ret, $shift );
        $ret = $class->_add( $ret, $class->_new($part) );
    }

    return $ret;
}

sub _from_bin {

    my ( $class, $bin ) = @_;
    $bin =~ s/^0[bB]//;

    my $len = length $bin;
    my $rem = 1 + ( $len - 1 ) % 31;

    my $ret = $class->_new( int oct '0b' . substr $bin, 0, $rem );
    return $ret if $rem == $len;

    my $shift = $class->_new( 1 << 31 );
    for ( my $offset = $rem ; $offset < $len ; $offset += 31 ) {
        my $part = int oct '0b' . substr $bin, $offset, 31;
        $ret = $class->_mul( $ret, $shift );
        $ret = $class->_add( $ret, $class->_new($part) );
    }

    return $ret;
}

sub _from_bytes {
    my ( $class, $str ) = @_;
    my $x    = $class->_zero();
    my $base = $class->_new("256");
    my $n    = length($str);
    for ( my $i = 0 ; $i < $n ; ++$i ) {
        $x = $class->_mul( $x, $base );
        my $byteval = $class->_new( unpack 'C', substr( $str, $i, 1 ) );
        $x = $class->_add( $x, $byteval );
    }
    return $x;
}

sub _from_base {
    my $class = shift;
    my $str   = shift;
    my $base  = shift;
    $base = $class->_new($base) unless ref($base);

    my $n = length($str);
    my $x = $class->_zero();

    my $collseq;
    if (@_) {
        $collseq = shift();
    }
    else {
        if ( $class->_acmp( $base, $class->_new("36") ) <= 0 ) {
            $str     = uc $str;
            $collseq = '0123456789' . 'ABCDEFGHIJKLMNOPQRSTUVWXYZ';
        }
        elsif ( $class->_acmp( $base, $class->_new("94") ) <= 0 ) {
            $collseq =
                '0123456789'
              . 'ABCDEFGHIJKLMNOPQRSTUVWXYZ'
              . 'abcdefghijklmnopqrstuvwxyz'
              . '!"#$%&\'()*+,-./'
              . ':;<=>?@'
              . '[\\]^_`' . '{|}~';
        }
        else {
            croak "When base > 94, a collation sequence must be given";
        }
        $collseq = substr $collseq, 0, $class->_num($base);
    }

    my @collseq = split '', $collseq;
    my %collseq;
    for my $num ( 0 .. $#collseq ) {
        my $chr = $collseq[$num];
        die "duplicate character '$chr' in collation sequence"
          if exists $collseq{$chr};
        $collseq{$chr} = $num;
    }

    for ( my $i = 0 ; $i < $n ; ++$i ) {
        my $chr = substr( $str, $i, 1 );
        die "input character '$chr' does not exist in collation sequence"
          unless exists $collseq{$chr};
        $x = $class->_mul( $x, $base );
        my $num = $class->_new( $collseq{$chr} );
        $x = $class->_add( $x, $num );
    }

    return $x;
}

sub _from_base_num {
    my ( $class, $in, $base ) = @_;

    $base = $class->_new($base) unless ref($base);
    my $two = $class->_two();
    croak "base must be >= 2" unless $class->_acmp( $base, $two ) >= 0;

    my $ele = $in->[0];

    $ele = $class->_new($ele) unless ref($ele);
    my $x = $class->_copy($ele);

    for my $i ( 1 .. $#$in ) {
        $x   = $class->_mul( $x, $base );
        $ele = $in->[$i];
        $ele = $class->_new($ele) unless ref($ele);
        $x   = $class->_add( $x, $ele );
    }

    return $x;
}

sub _modinv {
    my ( $class, $x, $y ) = @_;

    if ( $class->_is_zero($y) ) {
        return;
    }

    if ( $class->_is_one($y) ) {
        return ( $class->_zero(), '+' );
    }

    my $u = $class->_zero();
    my $v = $class->_one();
    my $a = $class->_copy($y);
    my $b = $class->_copy($x);

    my $q;
    my $sign = 1;
    {
        ( $a, $q, $b ) = ( $b, $class->_div( $a, $b ) );
        last if $class->_is_zero($b);

        my $vq = $class->_mul( $class->_copy($v), $q );
        my $t  = $class->_add( $vq, $u );
        $u    = $v;
        $v    = $t;
        $sign = -$sign;
        redo;
    }

    return unless $class->_is_one($a);

    ( $v, $sign == 1 ? '+' : '-' );
}

sub _modpow {
    my ( $class, $num, $exp, $mod ) = @_;

    if ( $class->_is_one($mod) ) {
        return $class->_zero();
    }

    if ( $class->_is_zero($num) ) {
        return $class->_is_zero($exp)
          ? $class->_one()
          : $class->_zero();
    }

    $num = $class->_mod( $class->_copy($num), $mod );

    my $acc = $class->_copy($num);
    my $t   = $class->_one();

    my $expbin = $class->_to_bin($exp);
    my $len    = length($expbin);

    while ( $len-- ) {
        if ( substr( $expbin, $len, 1 ) eq '1' ) {
            $t = $class->_mul( $t, $acc );
            $t = $class->_mod( $t, $mod );
        }
        $acc = $class->_mul( $acc, $acc );
        $acc = $class->_mod( $acc, $mod );
    }
    return $t;
}

sub _gcd {

    my ( $class, $x, $y ) = @_;

    if ( $class->_acmp( $x, $y ) == 0 ) {
        return $class->_copy($x);
    }

    if ( $class->_is_zero($x) ) {
        if ( $class->_is_zero($y) ) {
            return $class->_zero();
        }
        else {
            return $class->_copy($y);
        }
    }
    else {
        if ( $class->_is_zero($y) ) {
            return $class->_copy($x);
        }
        else {

            $x = $class->_copy($x);
            until ( $class->_is_zero($y) ) {

                $x = $class->_mod( $x, $y );

                my $tmp = $x;
                $x = $class->_copy($y);
                $y = $tmp;
            }

            return $x;
        }
    }
}

sub _lcm {

    my ( $class, $x, $y ) = @_;

    return $class->_zero()
      if ( $class->_is_zero($x)
        || $class->_is_zero($y) );

    my $gcd = $class->_gcd( $class->_copy($x), $y );
    $x = $class->_div( $x, $gcd );
    $x = $class->_mul( $x, $y );
    return $x;
}

sub _lucas {
    my ( $class, $n ) = @_;

    $n = $class->_num($n) if ref $n;

    if (wantarray) {
        my @y;

        push @y, $class->_two();
        return @y if $n == 0;

        push @y, $class->_one();
        return @y if $n == 1;

        for ( my $i = 2 ; $i <= $n ; ++$i ) {
            $y[$i] =
              $class->_add( $class->_copy( $y[ $i - 1 ] ), $y[ $i - 2 ] );
        }

        return @y;
    }

    return $class->_two() if $n == 0;

    return $class->_add( scalar( $class->_fib( $n - 1 ) ),
        scalar( $class->_fib( $n + 1 ) ) );
}

sub _fib {
    my ( $class, $n ) = @_;

    $n = $class->_num($n) if ref $n;

    if (wantarray) {
        my @y;

        push @y, $class->_zero();
        return @y if $n == 0;

        push @y, $class->_one();
        return @y if $n == 1;

        for ( my $i = 2 ; $i <= $n ; ++$i ) {
            $y[$i] =
              $class->_add( $class->_copy( $y[ $i - 1 ] ), $y[ $i - 2 ] );
        }

        return @y;
    }

    my $cache = {};
    my $two   = $class->_two();
    my $fib;

    $fib = sub {
        my $n = shift;
        return $class->_zero() if $n <= 0;
        return $class->_one()  if $n <= 2;
        return $cache->{$n}    if exists $cache->{$n};

        my $k = int( $n / 2 );
        my $a = $fib->( $k + 1 );
        my $b = $fib->($k);
        my $y;

        if ( $n % 2 == 1 ) {
            $y = $class->_add(
                $class->_mul( $class->_copy($a), $a ),
                $class->_mul( $class->_copy($b), $b )
            );
        }
        else {
            $y = $class->_mul(
                $class->_sub( $class->_mul( $class->_copy($two), $a ), $b ),
                $b );
        }

        $cache->{$n} = $y;
        return $y;
    };

    return $fib->($n);
}

1;

__END__

