#! perl

use strict;
use warnings;

package Getopt::Long::Parser;

our $VERSION = 2.58;


use Getopt::Long ();
no warnings 'redefine';

sub new {
    my $that  = shift;
    my $class = ref($that) || $that;
    my %atts  = @_;

    my $self = { caller_pkg => (caller)[0] };

    bless( $self, $class );

    my $default_config = Getopt::Long::_default_config();

    if ( defined $atts{config} ) {
        my $save =
          Getopt::Long::Configure( $default_config, @{ $atts{config} } );
        $self->{settings} = Getopt::Long::Configure($save);
        delete( $atts{config} );
    }
    else {
        $self->{settings} = $default_config;
    }

    if (%atts) {
        die(    __PACKAGE__
              . ": unhandled attributes: "
              . join( " ", sort( keys(%atts) ) )
              . "\n" );
    }

    $self;
}

use warnings 'redefine';


sub configure {
    my ($self) = shift;

    my $save = Getopt::Long::Configure( $self->{settings}, @_ );

    $self->{settings} = Getopt::Long::Configure($save);
}


sub getoptions {
    my ($self) = shift;

    return $self->getoptionsfromarray( \@ARGV, @_ );
}

sub getoptionsfromarray {
    my ($self) = shift;

    my $save = Getopt::Long::Configure( $self->{settings} );

    my $ret = 0;
    $Getopt::Long::caller = $self->{caller_pkg};

    eval {
        local ( $SIG{__DIE__} ) = 'DEFAULT';
        $ret = Getopt::Long::GetOptionsFromArray(@_);
    };

    Getopt::Long::Configure($save);

    die($@) if $@;
    return $ret;
}


1;
