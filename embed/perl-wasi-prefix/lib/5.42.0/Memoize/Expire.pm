use strict;
use warnings;

package Memoize::Expire;
our $VERSION = '1.17';

use Carp;
our $DEBUG;

BEGIN {
    eval { require Time::HiRes };
    unless ($@) {
        Time::HiRes->import('time');
    }
}

sub TIEHASH {
    my ( $package, %args ) = @_;
    my %cache;
    if ( $args{TIE} ) {
        my ( $module, @opts ) = @{ $args{TIE} };
        my $modulefile = $module . '.pm';
        $modulefile =~ s{::}{/}g;
        eval { require $modulefile };
        if ($@) {
            croak
"Memoize::Expire: Couldn't load hash tie module `$module': $@; aborting";
        }
        my $rc = ( tie %cache => $module, @opts );
        unless ($rc) {
            croak
              "Memoize::Expire: Couldn't tie hash to `$module': $@; aborting";
        }
    }
    $args{LIFETIME} ||= 0;
    $args{NUM_USES} ||= 0;
    $args{C} = delete $args{HASH} || \%cache;
    bless \%args => $package;
}

sub STORE {
    $DEBUG and print STDERR " >> Store $_[1] $_[2]\n";
    my ( $self, $key, $value ) = @_;
    my $expire_time = $self->{LIFETIME} > 0 ? $self->{LIFETIME} + time : 0;
    my $header = _make_header( time, $expire_time, $self->{NUM_USES} - 1 );
    @{ $self->{C} }{ "H$key", "V$key" } = ( $header, $value );
    $value;
}

sub FETCH {
    $DEBUG and print STDERR " >> Fetch cached value for $_[1]\n";
    my ( $last_access, $expire_time, $num_uses_left ) =
      _get_header( $_[0]{C}{"H$_[1]"} );
    $DEBUG
      and print STDERR " >>   (ttl: ", ( $expire_time - time() ),
      ", nuses: $num_uses_left)\n";
    $_[0]{C}{"H$_[1]"} = _make_header( time, $expire_time, --$num_uses_left );
    $_[0]{C}{"V$_[1]"};
}

sub EXISTS {
    $DEBUG and print STDERR " >> Exists $_[1]\n";
    unless ( exists $_[0]{C}{"V$_[1]"} ) {
        $DEBUG and print STDERR "    Not in underlying hash at all.\n";
        return 0;
    }
    my $item = $_[0]{C}{"H$_[1]"};
    my ( $last_access, $expire_time, $num_uses_left ) = _get_header($item);
    my $ttl = $expire_time - time;
    if ($DEBUG) {
        $_[0]{LIFETIME}
          and print STDERR "    Time to live for this item: $ttl\n";
        $_[0]{NUM_USES} and print STDERR "    Uses remaining: $num_uses_left\n";
    }
    if (   ( !$_[0]{LIFETIME} || $expire_time > time )
        && ( !$_[0]{NUM_USES} || $num_uses_left > 0 ) )
    {
        $DEBUG and print STDERR "    (Still good)\n";
        return 1;
    }
    else {
        $DEBUG and print STDERR "    (Expired)\n";
        return 0;
    }
}

sub FIRSTKEY {
    scalar keys %{ $_[0]{C} };
    &NEXTKEY;
}

sub NEXTKEY {
    while ( defined( my $key = each %{ $_[0]{C} } ) ) {
        return substr $key, 1 if 'V' eq substr $key, 0, 1;
    }
    undef;
}

sub _make_header {
    pack "N N n", @_;
}

sub _get_header {
    unpack "N N n", substr( $_[0], 0, 10 );
}

1;

__END__

