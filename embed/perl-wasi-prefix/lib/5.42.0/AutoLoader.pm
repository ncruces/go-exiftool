package AutoLoader;

use strict;
use 5.006_001;

our ( $VERSION, $AUTOLOAD );

my $is_dosish;
my $is_epoc;
my $is_vms;
my $is_macos;

BEGIN {
    $is_dosish =
      $^O eq 'dos' || $^O eq 'os2' || $^O eq 'MSWin32' || $^O eq 'NetWare';
    $is_epoc  = $^O eq 'epoc';
    $is_vms   = $^O eq 'VMS';
    $is_macos = $^O eq 'MacOS';
    $VERSION  = '5.74';
}

AUTOLOAD {
    my $sub = $AUTOLOAD;
    autoload_sub($sub);
    goto &$sub;
}

sub autoload_sub {
    my $sub = shift;

    my $filename = AutoLoader::find_filename($sub);

    my $save = $@;
    local $!;
    eval { local $SIG{__DIE__}; require $filename };
    if ($@) {
        if ( substr( $sub, -9 ) eq '::DESTROY' ) {
            no strict 'refs';
            *$sub = sub { };
            $@    = undef;
        }
        elsif ( $@ =~ /^Can't locate/ ) {
            if ( $filename =~ s/(\w{12,})\.al$/substr($1,0,11).".al"/e ) {
                eval { local $SIG{__DIE__}; require $filename };
            }
        }
        if ($@) {
            $@ =~ s/ at .*\n//;
            my $error = $@;
            require Carp;
            Carp::croak($error);
        }
    }
    $@ = $save;

    return 1;
}

sub find_filename {
    my $sub = shift;
    my $filename;
    {

        my ( $pkg, $func ) = ( $sub =~ /(.*)::([^:]+)$/ );
        $pkg =~ s#::#/#g;
        if ( defined( $filename = $INC{"$pkg.pm"} ) ) {
            if ($is_macos) {
                $pkg =~ tr#/#:#;
                $filename = undef
                  unless $filename =~ s#^(.*)$pkg\.pm\z#$1auto:$pkg:$func.al#s;
            }
            else {
                $filename = undef
                  unless $filename =~ s#^(.*)$pkg\.pm\z#$1auto/$pkg/$func.al#s;
            }

            if ( defined $filename and -r $filename ) {
                unless ( $filename =~ m|^/|s ) {
                    if ($is_dosish) {
                        unless ( $filename =~ m{^([a-z]:)?[\\/]}is ) {
                            if ( $^O ne 'NetWare' ) {
                                $filename = "./$filename";
                            }
                            else {
                                $filename = "$filename";
                            }
                        }
                    }
                    elsif ($is_epoc) {
                        unless ( $filename =~ m{^([a-z?]:)?[\\/]}is ) {
                            $filename = "./$filename";
                        }
                    }
                    elsif ($is_vms) {
                        $filename = "./$filename";
                    }
                    elsif ( !$is_macos ) {
                        $filename = "./$filename";
                    }
                }
            }
            else {
                $filename = undef;
            }
        }
        unless ( defined $filename ) {
            $filename = "auto/$sub.al";
            $filename =~ s#::#/#g;
        }
    }
    return $filename;
}

sub import {
    my $pkg     = shift;
    my $callpkg = caller;

    if ( $pkg eq 'AutoLoader' ) {
        if ( @_ and $_[0] =~ /^&?AUTOLOAD$/ ) {
            no strict 'refs';
            *{ $callpkg . '::AUTOLOAD' } = \&AUTOLOAD;
        }
    }

    ( my $calldir = $callpkg ) =~ s#::#/#g;
    my $path = $INC{ $calldir . '.pm' };
    if ( defined($path) ) {
        my $replaced_okay;
        if ($is_macos) {
            ( my $malldir = $calldir ) =~ tr#/#:#;
            $replaced_okay =
              ( $path =~ s#^(.*)$malldir\.pm\z#$1auto:$malldir:autosplit.ix#s );
        }
        else {
            $replaced_okay =
              ( $path =~ s#^(.*)$calldir\.pm\z#$1auto/$calldir/autosplit.ix# );
        }

        eval { require $path; } if $replaced_okay;
        if ( !$replaced_okay or $@ ) {
            $path = "auto/$calldir/autosplit.ix";
            eval { require $path; };
        }
        if ($@) {
            my $error = $@;
            require Carp;
            Carp::carp($error);
        }
    }
}

sub unimport {
    my $callpkg = caller;

    no strict 'refs';

    for my $exported (qw( AUTOLOAD )) {
        my $symname = $callpkg . '::' . $exported;
        undef *{$symname} if \&{$symname} == \&{$exported};
        *{$symname} = \&{$symname};
    }
}

1;

__END__

