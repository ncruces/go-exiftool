

package FindBin;
use strict;
use warnings;

use Carp;
require Exporter;
use Cwd qw(getcwd cwd abs_path);
use File::Basename;
use File::Spec;

our ( $Bin, $Script, $RealBin, $RealScript, $Dir, $RealDir );
our @EXPORT_OK = qw($Bin $Script $RealBin $RealScript $Dir $RealDir);
our %EXPORT_TAGS =
  ( ALL => [qw($Bin $Script $RealBin $RealScript $Dir $RealDir)] );
our @ISA = qw(Exporter);

our $VERSION = "1.54";

if ( $^O eq 'VMS' ) {
    require VMS::Filespec;
    VMS::Filespec->import;
}

sub cwd2 {
    my $cwd = getcwd();
    defined $cwd or $cwd = cwd();
    $cwd;
}

sub init {
    *Dir     = \$Bin;
    *RealDir = \$RealBin;

    if ( $0 eq '-e' || $0 eq '-' ) {
        $Script = $RealScript = $0;
        $Bin    = $RealBin    = cwd2();
        $Bin    = VMS::Filespec::unixify($Bin) if $^O eq 'VMS';
    }
    else {
        my $script = $0;

        if ( $^O eq 'VMS' ) {
            ( $Bin, $Script ) =
              VMS::Filespec::rmsexpand($0) =~ /(.*[\]>\/]+)(.*)/s;
            ( $Bin = VMS::Filespec::unixify($Bin) ) =~ s/\/\z//;
            ( $RealBin, $RealScript ) = ( $Bin, $Script );
        }
        else {
            croak("Cannot find current script '$0'") unless ( -f $script );

            $script = File::Spec->catfile( cwd2(), $script )
              unless File::Spec->file_name_is_absolute($script);

            ( $Script, $Bin ) = fileparse($script);

            while (1) {
                my $linktext = readlink($script);

                ( $RealScript, $RealBin ) = fileparse($script);
                last unless defined $linktext;

                $script =
                  ( File::Spec->file_name_is_absolute($linktext) )
                  ? $linktext
                  : File::Spec->catfile( $RealBin, $linktext );
            }

            if ($Bin) {
                my $BinOld = $Bin;
                $Bin = abs_path($Bin);
                defined $Bin or $Bin = File::Spec->canonpath($BinOld);
            }
            $RealBin = abs_path($RealBin) if ($RealBin);
        }
    }
}

BEGIN { init }

*again = \&init;

1;
