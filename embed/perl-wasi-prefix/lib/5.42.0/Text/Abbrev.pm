package Text::Abbrev;
require 5.005;
require Exporter;

our $VERSION = '1.02';


@ISA    = qw(Exporter);
@EXPORT = qw(abbrev);

sub abbrev {
    my ( $word, $hashref, $glob, %table, $returnvoid );

    @_ or return;
    if ( ref( $_[0] ) ) {
        $hashref    = shift;
        $returnvoid = 1;
    }
    elsif ( ref \$_[0] eq 'GLOB' ) {
        $hashref    = \%{ shift() };
        $returnvoid = 1;
    }
    %{$hashref} = ();

  WORD: foreach $word (@_) {
        for ( my $len = ( length $word ) - 1 ; $len > 0 ; --$len ) {
            my $abbrev = substr( $word, 0, $len );
            my $seen   = ++$table{$abbrev};
            if ( $seen == 1 ) {

                $hashref->{$abbrev} = $word;
            }
            elsif ( $seen == 2 ) {

                delete $hashref->{$abbrev};
            }
            else {

                next WORD;
            }
        }
    }
    foreach $word (@_) {
        $hashref->{$word} = $word;
    }
    return if $returnvoid;
    if (wantarray) {
        %{$hashref};
    }
    else {
        $hashref;
    }
}

1;
